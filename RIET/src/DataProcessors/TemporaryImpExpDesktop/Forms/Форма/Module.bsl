
/////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		Список.Отбор,
		"QtyInTemporaryImpExp",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
		
	ImportExportСервер.ДобавитьОтборПоProcessLevel(Параметры.Отбор);
		
	ResponsibleChange = Перечисления.TypesOfTemporaryImpExpTransaction.ResponsibleChange;	
		
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаTemporaryImportTransaction" 
		ИЛИ ИмяСобытия = "UpdateExpiryDateOfPayments" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ

&НаКлиенте
Процедура CreateTransactionWithSelectedItems(Команда)
	
	ТаблицаФормы = Элементы.Список;
	ВыделенныеСтроки = ТаблицаФормы.ВыделенныеСтроки;
	
	// Если не выделено ни одной строки - выходим
	Если ТаблицаФормы.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверим, что во всех строках совпадает Customs file no и во всех положительный Qty in temporary import
	ДанныеПервойВыделеннойСтроки = ТаблицаФормы.ДанныеСтроки(ВыделенныеСтроки[0]);	
	CustomsFile = ДанныеПервойВыделеннойСтроки.CustomsFile;
	CustomsFileNo = ДанныеПервойВыделеннойСтроки.CustomsFileNo;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = ТаблицаФормы.ДанныеСтроки(ВыделеннаяСтрока);
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.QtyInTemporaryImpExp) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Item """ + СокрЛП(ВыделеннаяСтрока) + """ is not in temporary import!");	
			Возврат;
			
		КонецЕсли;
		
		Если CustomsFile <> ДанныеСтроки.CustomsFile 
			ИЛИ CustomsFileNo <> ДанныеСтроки.CustomsFileNo Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Selected items are in different Customs files!");
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Сформируем параметры и откроем форму нового документа
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("CustomsFile", CustomsFile);
	ЗначенияЗаполнения.Вставить("CustomsFileNo", CustomsFileNo);
	ЗначенияЗаполнения.Вставить("Items", ВыделенныеСтроки);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Документ.TemporaryImpExpTransactions.ФормаОбъекта", СтруктураПараметров);
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ChangeResponsibleOfSelectedItems(Команда)
	
	ТаблицаФормы = Элементы.Список;
	ВыделенныеСтроки = ТаблицаФормы.ВыделенныеСтроки;
	
	// Если не выделено ни одной строки - выходим
	Если ТаблицаФормы.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = ТаблицаФормы.ДанныеСтроки(ВыделеннаяСтрока);
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.QtyInTemporaryImpExp) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Item """ + СокрЛП(ВыделеннаяСтрока) + """ is not in temporary import/export!");	
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Сформируем параметры и откроем форму нового документа
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("TypeOfTransaction", ResponsibleChange);
	ЗначенияЗаполнения.Вставить("Items", ВыделенныеСтроки);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Документ.TemporaryImpExpTransactions.ФормаОбъекта", СтруктураПараметров);
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ShowHistory(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("Item", ТекущаяСтрока);
	ОткрытьФорму("Обработка.TemporaryImpExpDesktop.Форма.History", СтруктураПараметров, ЭтаФорма, ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура UpdateExpiryDateOfPaymentsForSelectedItems(Команда)
	
	ТаблицаФормы = Элементы.Список;
	ВыделенныеСтроки = ТаблицаФормы.ВыделенныеСтроки;
	
	// Если не выделено ни одной строки - выходим
	Если ТаблицаФормы.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = ТаблицаФормы.ДанныеСтроки(ВыделеннаяСтрока);
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.QtyInTemporaryImpExp) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Item """ + СокрЛП(ВыделеннаяСтрока) + """ is not in temporary import/export!");	
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Сформируем параметры и откроем форму
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Items", ВыделенныеСтроки);
	ОткрытьФорму("Обработка.TemporaryImpExpDesktop.Форма.UpdateExpiryDateOfPayments", СтруктураПараметров);
	
КонецПроцедуры
