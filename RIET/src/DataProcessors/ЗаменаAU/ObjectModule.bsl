// Процедуры заполнения табличных частей по выбранному документу
Процедура ЗаполнитьТабличныеЧасти()  Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Документ);
	СтрокиРТиУ.Очистить();
	СтрокиПередачаОС.Очистить();
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	""Товары"" КАК ВидНоменклатуры,
		|	РеализацияТоваровУслугТовары.Номенклатура,
		|	РеализацияТоваровУслугТовары.ЕдиницаИзмерения,
		|	РеализацияТоваровУслугТовары.Количество,
		|	РеализацияТоваровУслугТовары.КоличествоМест,
		|	РеализацияТоваровУслугТовары.Коэффициент,
		|	РеализацияТоваровУслугТовары.Цена,
		|	РеализацияТоваровУслугТовары.Сумма,
		|	РеализацияТоваровУслугТовары.СтавкаНДС,
		|	РеализацияТоваровУслугТовары.СуммаНДС,
		|	РеализацияТоваровУслугТовары.НомерГТД,
		|	РеализацияТоваровУслугТовары.СтранаПроисхождения,
		|	РеализацияТоваровУслугТовары.Ticket,
		|	РеализацияТоваровУслугТовары.Well,
		|	РеализацияТоваровУслугТовары.Oilfield,
		|	РеализацияТоваровУслугТовары.ProductLine,
		|	РеализацияТоваровУслугТовары.КостЦентр,
		|	РеализацияТоваровУслугТовары.TicketNumber,
		|	РеализацияТоваровУслугТовары.WO,
		|	РеализацияТоваровУслугТовары.СуммаБезНДСРуб,
		|	РеализацияТоваровУслугТовары.СуммаНДСРуб,
		|	"""" КАК Содержание,
		|	"""" КАК СодержаниеEng,
		|	РеализацияТоваровУслугТовары.НомерСтроки КАК НомерСтрокиТЧ
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|ГДЕ
		|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""Услуги"",
		|	РеализацияТоваровУслугУслуги.Номенклатура,
		|	РеализацияТоваровУслугУслуги.ЕдиницаИзмерения,
		|	РеализацияТоваровУслугУслуги.Количество,
		|	0,
		|	РеализацияТоваровУслугУслуги.Коэффициент,
		|	РеализацияТоваровУслугУслуги.Цена,
		|	РеализацияТоваровУслугУслуги.Сумма,
		|	РеализацияТоваровУслугУслуги.СтавкаНДС,
		|	РеализацияТоваровУслугУслуги.СуммаНДС,
		|	"""",
		|	ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка),
		|	РеализацияТоваровУслугУслуги.Ticket,
		|	РеализацияТоваровУслугУслуги.Well,
		|	РеализацияТоваровУслугУслуги.Oilfield,
		|	РеализацияТоваровУслугУслуги.ProductLine,
		|	РеализацияТоваровУслугУслуги.КостЦентр,
		|	РеализацияТоваровУслугУслуги.TicketNumber,
		|	РеализацияТоваровУслугУслуги.WO,
		|	РеализацияТоваровУслугУслуги.СуммаБезНДСРуб,
		|	РеализацияТоваровУслугУслуги.СуммаНДСРуб,
		|	РеализацияТоваровУслугУслуги.Содержание,
		|	РеализацияТоваровУслугУслуги.СодержаниеEng,
		|	РеализацияТоваровУслугУслуги.НомерСтроки
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
		|ГДЕ
		|	РеализацияТоваровУслугУслуги.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидНоменклатуры,
		|	НомерСтрокиТЧ";
		
		Результат = Запрос.Выполнить().Выгрузить();
		СтрокиРТиУ.Загрузить(Результат);
		
	ИначеЕсли  ТипЗнч(Документ) = Тип("ДокументСсылка.ПередачаОС") Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПередачаОСОС.ОсновноеСредство,
		|	ПередачаОСОС.СтоимостьБУ,
		|	ПередачаОСОС.АмортизацияБУ,
		|	ПередачаОСОС.АмортизацияЗаМесяцБУ,
		|	ПередачаОСОС.СтоимостьНУ,
		|	ПередачаОСОС.АмортизацияНУ,
		|	ПередачаОСОС.АмортизацияЗаМесяцНУ,
		|	ПередачаОСОС.Сумма,
		|	ПередачаОСОС.СтавкаНДС,
		|	ПередачаОСОС.СуммаНДС,
		|	ПередачаОСОС.СуммаКапитальныхВложенийВключаемыхВРасходыНУ,
		|	ПередачаОСОС.КостЦентр,
		|	ПередачаОСОС.РБП,
		|	ПередачаОСОС.НомерСтроки КАК НомерСтрокиТЧ
		|ИЗ
		|	Документ.ПередачаОС.ОС КАК ПередачаОСОС
		|ГДЕ
		|	ПередачаОСОС.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПередачаОСОС.НомерСтроки";
		
		Результат = Запрос.Выполнить().Выгрузить();
		СтрокиПередачаОС.Загрузить(Результат);
	КонецЕсли;
	

	

КонецПроцедуры

// Процедура замены AU в строках
// Параметры: СтруктураВыбранныхСтрок - структура, ключи: НомераСтрокТовар - массив номеров строк ТЧ Товары  
// AU - СправочникСсылка.КостЦентры - новое значение кост центра для выбранных строк
//                                             		
// СтруктураВыбранныхСтрок		- структура  				- Содержит массивы номеров выбранных строк табличных частей документа - источника.
//		НомераСтрокТовары - массив		- номера выбранных строк табличной части  Товары документа РеализацияТоваровУслуг
//		НомераСтрокУслуги - массив		- номера выбранных строк табличной части  Услуги документа РеализацияТоваровУслуг
//		НомераСтрокОС	  - массив		- номера выбранных строк табличной части  Услуги документа ПередачаОС
Процедура ЗаменаAU(AU,СтруктураВыбранныхСтрок )  Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиРТиУ.ВидНоменклатуры,
	|	СтрокиРТиУ.Содержание,
	|	СтрокиРТиУ.Количество,
	|	СтрокиРТиУ.Цена,
	|	СтрокиРТиУ.Сумма,
	|	СтрокиРТиУ.СтавкаНДС,
	|	СтрокиРТиУ.КоличествоМест,
	|	СтрокиРТиУ.СуммаНДС,
	|	СтрокиРТиУ.Номенклатура,
	|	СтрокиРТиУ.Ticket,
	|	СтрокиРТиУ.Well,
	|	СтрокиРТиУ.Oilfield,
	|	СтрокиРТиУ.ProductLine,
	|	СтрокиРТиУ.КостЦентр,
	|	СтрокиРТиУ.TicketNumber,
	|	СтрокиРТиУ.ЕдиницаИзмерения,
	|	СтрокиРТиУ.СодержаниеEng,
	|	СтрокиРТиУ.Коэффициент,
	|	СтрокиРТиУ.WO,
	|	СтрокиРТиУ.СуммаБезНДСРуб,
	|	СтрокиРТиУ.СуммаНДСРуб,
	|	СтрокиРТиУ.НомерГДТ,
	|	СтрокиРТиУ.СтранаПроисхождения,
	|	СтрокиРТиУ.НомерСтрокиТЧ
	|ПОМЕСТИТЬ СтрокиРТиУ
	|ИЗ
	|	&СтрокиРТиУ КАК СтрокиРТиУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиРТиУ.ВидНоменклатуры,
	|	СтрокиРТиУ.Содержание,
	|	СтрокиРТиУ.Количество,
	|	СтрокиРТиУ.Цена,
	|	СтрокиРТиУ.Сумма,
	|	СтрокиРТиУ.СтавкаНДС,
	|	СтрокиРТиУ.КоличествоМест,
	|	СтрокиРТиУ.СуммаНДС,
	|	СтрокиРТиУ.Номенклатура,
	|	СтрокиРТиУ.Ticket,
	|	СтрокиРТиУ.Well,
	|	СтрокиРТиУ.Oilfield,
	|	СтрокиРТиУ.ProductLine,
	|	СтрокиРТиУ.КостЦентр,
	|	СтрокиРТиУ.TicketNumber,
	|	СтрокиРТиУ.ЕдиницаИзмерения,
	|	СтрокиРТиУ.СодержаниеEng,
	|	СтрокиРТиУ.Коэффициент,
	|	СтрокиРТиУ.WO,
	|	СтрокиРТиУ.СуммаБезНДСРуб,
	|	СтрокиРТиУ.СуммаНДСРуб,
	|	СтрокиРТиУ.НомерГДТ,
	|	СтрокиРТиУ.СтранаПроисхождения,
	|	СтрокиРТиУ.НомерСтрокиТЧ,
	|	ВЫБОР
	|		КОГДА СтрокиРТиУ.НомерСтрокиТЧ В (&МассивНомеровТовары)
	|				И СтрокиРТиУ.ВидНоменклатуры = ""Товары""
	|			ТОГДА ИСТИНА
	|		КОГДА СтрокиРТиУ.НомерСтрокиТЧ В (&МассивНомеровУслуги)
	|				И СтрокиРТиУ.ВидНоменклатуры = ""Услуги""
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Изменять
	|ИЗ
	|	СтрокиРТиУ КАК СтрокиРТиУ";
	
	
	Запрос.УстановитьПараметр("СтрокиРТиУ",СтрокиРТиУ.Выгрузить());
	Запрос.УстановитьПараметр("МассивНомеровТовары",СтруктураВыбранныхСтрок.НомераСтрокТовары);
	Запрос.УстановитьПараметр("МассивНомеровУслуги",СтруктураВыбранныхСтрок.НомераСтрокУслуги);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ТЗ = Результат.Выгрузить();
		Для Каждого Строка Из ТЗ Цикл
			Если Строка.Изменять Тогда
				Строка.КостЦентр = AU;
			КонецЕсли;
		КонецЦикла;
		СтрокиРТиУ.Загрузить(ТЗ);
	КонецЕсли;
	
	//
	//Для Каждого НомерСтроки Из СтруктураВыбранныхСтрок.НомераСтрокТовары Цикл
	//	Отбор = Новый Структура("НомерСтрокиТЧ, ВидНоменклатуры", НомерСтроки, "Товары");
	//	ТекСтрока = СтрокиРТиУ.НайтиСтроки(Отбор);
	//	ТекСтрока[0].КостЦентр = AU; 
	//КонецЦикла;
	//
	//Для Каждого НомерСтроки Из СтруктураВыбранныхСтрок.НомераСтрокУслуги Цикл
	//    Отбор = Новый Структура("НомерСтрокиТЧ, ВидНоменклатуры", НомерСтроки, "Услуги");
	//	ТекСтрока = СтрокиРТиУ.НайтиСтроки(Отбор);
	//	ТекСтрока[0].КостЦентр = AU;  
	//КонецЦикла;
	
	Для Каждого НомерСтроки Из СтруктураВыбранныхСтрок.НомераСтрокОС Цикл
	    ТекСтрока = СтрокиПередачаОС.Найти(НомерСтроки,"НомерСтрокиТЧ");
		ТекСтрока.КостЦентр = AU; 
	КонецЦикла;

КонецПроцедуры


// Процедура распределения выбранных строк табличных частей по коэффициентам
// Параметры: СтруктураВыбранныхСтрок - структура, ключи: НомераСтрокТовар - массив номеров строк ТЧ Товары                                             		
// СтруктураВыбранныхСтрок		- структура  				- Содержит массивы номеров выбранных строк табличных частей документа - источника.
//		НомераСтрокТовары - массив		- номера выбранных строк табличной части  Товары документа РеализацияТоваровУслуг
//		НомераСтрокУслуги - массив		- номера выбранных строк табличной части  Услуги документа РеализацияТоваровУслуг
//		НомераСтрокОС	  - массив		- номера выбранных строк табличной части  Услуги документа ПередачаОС
Процедура Распределить(СтруктураВыбранныхСтрок) Экспорт
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтрокиРТиУ.ВидНоменклатуры,
		|	СтрокиРТиУ.Содержание,
		|	СтрокиРТиУ.Количество,
		|	СтрокиРТиУ.Цена,
		|	СтрокиРТиУ.Сумма,
		|	СтрокиРТиУ.СтавкаНДС,
		|	СтрокиРТиУ.КоличествоМест,
		|	СтрокиРТиУ.СуммаНДС,
		|	СтрокиРТиУ.Номенклатура,
		|	СтрокиРТиУ.Ticket,
		|	СтрокиРТиУ.Well,
		|	СтрокиРТиУ.Oilfield,
		|	СтрокиРТиУ.ProductLine,
		|	СтрокиРТиУ.КостЦентр,
		|	СтрокиРТиУ.TicketNumber,
		|	СтрокиРТиУ.ЕдиницаИзмерения,
		|	СтрокиРТиУ.СодержаниеEng,
		|	СтрокиРТиУ.Коэффициент,
		|	СтрокиРТиУ.WO,
		|	СтрокиРТиУ.СуммаБезНДСРуб,
		|	СтрокиРТиУ.СуммаНДСРуб,
		|	СтрокиРТиУ.НомерГДТ,
		|	СтрокиРТиУ.СтранаПроисхождения,
		|	СтрокиРТиУ.НомерСтрокиТЧ
		|ПОМЕСТИТЬ СтрокиРТиУ
		|ИЗ
		|	&СтрокиРТиУ КАК СтрокиРТиУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаРаспределения.Коэффициент,
		|	ТаблицаРаспределения.AU
		|ПОМЕСТИТЬ РаспределениеВТ
		|ИЗ
		|	&ТаблицаРаспределения КАК ТаблицаРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтрокиРТиУ.ВидНоменклатуры,
		|	СтрокиРТиУ.Содержание,
		|	СтрокиРТиУ.Количество,
		|	СтрокиРТиУ.Цена,
		|	СтрокиРТиУ.Сумма,
		|	СтрокиРТиУ.СтавкаНДС,
		|	СтрокиРТиУ.КоличествоМест,
		|	СтрокиРТиУ.СуммаНДС,
		|	СтрокиРТиУ.Номенклатура,
		|	СтрокиРТиУ.Ticket,
		|	СтрокиРТиУ.Well,
		|	СтрокиРТиУ.Oilfield,
		|	СтрокиРТиУ.ProductLine,
		|	СтрокиРТиУ.КостЦентр,
		|	СтрокиРТиУ.TicketNumber,
		|	СтрокиРТиУ.ЕдиницаИзмерения,
		|	СтрокиРТиУ.СодержаниеEng,
		|	СтрокиРТиУ.Коэффициент,
		|	СтрокиРТиУ.WO,
		|	СтрокиРТиУ.СуммаБезНДСРуб,
		|	СтрокиРТиУ.СуммаНДСРуб,
		|	СтрокиРТиУ.НомерГДТ,
		|	СтрокиРТиУ.СтранаПроисхождения,
		|	СтрокиРТиУ.НомерСтрокиТЧ,
		|	ВЫБОР
		|		КОГДА СтрокиРТиУ.НомерСтрокиТЧ В (&МассивНомеровТовары)
		|				И СтрокиРТиУ.ВидНоменклатуры = ""Товары""
		|			ТОГДА ИСТИНА
		|		КОГДА СтрокиРТиУ.НомерСтрокиТЧ В (&МассивНомеровУслуги)
		|				И СтрокиРТиУ.ВидНоменклатуры = ""Услуги""
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Изменять
		|ПОМЕСТИТЬ СтрокиТЧсВыбранными
		|ИЗ
		|	СтрокиРТиУ КАК СтрокиРТиУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтрокиТЧсВыбранными.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СтрокиТЧсВыбранными.Содержание,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА СтрокиТЧсВыбранными.Количество
		|			ИНАЧЕ СтрокиТЧсВыбранными.Количество * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 3)) КАК Количество,
		|	СтрокиТЧсВыбранными.Цена,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА СтрокиТЧсВыбранными.Сумма
		|			ИНАЧЕ СтрокиТЧсВыбранными.Сумма * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	СтрокиТЧсВыбранными.СтавкаНДС,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА СтрокиТЧсВыбранными.КоличествоМест
		|			ИНАЧЕ СтрокиТЧсВыбранными.КоличествоМест * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(10, 3)) КАК КоличествоМест,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА СтрокиТЧсВыбранными.СуммаНДС
		|			ИНАЧЕ СтрокиТЧсВыбранными.СуммаНДС * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
		|	СтрокиТЧсВыбранными.Номенклатура,
		|	СтрокиТЧсВыбранными.Ticket,
		|	СтрокиТЧсВыбранными.Well,
		|	СтрокиТЧсВыбранными.Oilfield,
		|	СтрокиТЧсВыбранными.ProductLine,
		|	ЕСТЬNULL(РаспределениеВТ.AU, СтрокиТЧсВыбранными.КостЦентр) КАК КостЦентр,
		|	СтрокиТЧсВыбранными.TicketNumber,
		|	СтрокиТЧсВыбранными.ЕдиницаИзмерения,
		|	СтрокиТЧсВыбранными.СодержаниеEng,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА СтрокиТЧсВыбранными.Коэффициент
		|			ИНАЧЕ СтрокиТЧсВыбранными.Коэффициент * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(10, 3)) КАК Коэффициент,
		|	СтрокиТЧсВыбранными.WO,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА СтрокиТЧсВыбранными.СуммаБезНДСРуб
		|			ИНАЧЕ СтрокиТЧсВыбранными.СуммаБезНДСРуб * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаБезНДСРуб,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА СтрокиТЧсВыбранными.СуммаНДСРуб
		|			ИНАЧЕ СтрокиТЧсВыбранными.СуммаНДСРуб * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСРуб,
		|	СтрокиТЧсВыбранными.НомерГДТ,
		|	СтрокиТЧсВыбранными.СтранаПроисхождения,
		|	СтрокиТЧсВыбранными.НомерСтрокиТЧ КАК НомерСтрокиТЧ,
		|	СтрокиТЧсВыбранными.Изменять,
		|	СтрокиТЧсВыбранными.Количество КАК КоличествоИтог,
		|	СтрокиТЧсВыбранными.Сумма КАК СуммаИтог,
		|	СтрокиТЧсВыбранными.КоличествоМест КАК КоличествоМестИтог,
		|	СтрокиТЧсВыбранными.СуммаНДС КАК СуммаНДСИтог,
		|	СтрокиТЧсВыбранными.Коэффициент КАК КоэффициентИтог,
		|	СтрокиТЧсВыбранными.СуммаБезНДСРуб КАК СуммаБезНДСРубИтог,
		|	СтрокиТЧсВыбранными.СуммаНДСРуб КАК СуммаНДСРубИтог
		|ИЗ
		|	СтрокиТЧсВыбранными КАК СтрокиТЧсВыбранными
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределениеВТ КАК РаспределениеВТ
		|		ПО (СтрокиТЧсВыбранными.Изменять = ИСТИНА)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидНоменклатуры,
		|	НомерСтрокиТЧ
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Сумма),
		|	СУММА(КоличествоМест),
		|	СУММА(СуммаНДС),
		|	СУММА(Коэффициент),
		|	СУММА(СуммаБезНДСРуб),
		|	СУММА(СуммаНДСРуб),
		|	МАКСИМУМ(КоличествоИтог),
		|	МАКСИМУМ(СуммаИтог),
		|	СУММА(КоличествоМестИтог),
		|	МАКСИМУМ(СуммаНДСИтог),
		|	МАКСИМУМ(КоэффициентИтог),
		|	СУММА(СуммаБезНДСРубИтог),
		|	МАКСИМУМ(СуммаНДСРубИтог)
		|ПО
		|	ВидНоменклатуры,
		|	НомерСтрокиТЧ";
		
		Запрос.УстановитьПараметр("СтрокиРТиУ",СтрокиРТиУ.Выгрузить());
		Запрос.УстановитьПараметр("МассивНомеровТовары",СтруктураВыбранныхСтрок.НомераСтрокТовары);
		Запрос.УстановитьПараметр("МассивНомеровУслуги",СтруктураВыбранныхСтрок.НомераСтрокУслуги);	
		
		ТЗРаспределения = РаспределениеAU.Выгрузить();
		СуммаКоэффициентов = РаспределениеAU.Итог("Коэффициент");
		//Для Каждого Строка ИЗ ТЗраспределения Цикл
		//	Строка.Коэффициент = Строка.Коэффициент/СуммаКоэффициентов;
		//КонецЦикла;
		Запрос.УстановитьПараметр("ТаблицаРаспределения", ТЗРаспределения );
		Запрос.УстановитьПараметр("СуммаКоэффициентов", СуммаКоэффициентов);
		
		
		Результаты = Запрос.ВыполнитьПакет();
		
		// Будем перезаполнять
		СтрокиРТиУ.Очистить();
		СтрокиПередачаОС.Очистить();
		
		ВыборкаМаксимальныхНомеровСтрок = Результаты[1].Выбрать();
			
		МассивПроверяемыхРеквизитов  = Новый Массив(7,2);
		МассивПроверяемыхРеквизитов[0][0] = "Количество";
		МассивПроверяемыхРеквизитов[1][0] = "Сумма";
		МассивПроверяемыхРеквизитов[2][0] = "КоличествоМест";
		МассивПроверяемыхРеквизитов[3][0] = "СуммаНДС";
		МассивПроверяемыхРеквизитов[4][0] = "Коэффициент";
		МассивПроверяемыхРеквизитов[5][0] = "СуммаБезНДСРуб";
		МассивПроверяемыхРеквизитов[6][0] = "СуммаНДСРуб";
		
		МаксНомерТовары = 0;
		МаксНомерУслуги = 0;
		
		ВыборкаИтогТаблица = Результаты[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаИтогТаблица.Следующий() Цикл
			ВыборкаИтогТаблицаНомер = ВыборкаИтогТаблица.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаИтогТаблицаНомер.Следующий() Цикл
				Для  Эл = 0 По МассивПроверяемыхРеквизитов.Количество()-1 Цикл
					МассивПроверяемыхРеквизитов[Эл][1] = ВыборкаИтогТаблицаНомер[МассивПроверяемыхРеквизитов[Эл][0]+"Итог"] - ВыборкаИтогТаблицаНомер[МассивПроверяемыхРеквизитов[Эл][0]]; // "последняя копейка" 
				КонецЦикла;
				ВыборкаДетальныеЗаписи = ВыборкаИтогТаблицаНомер.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					
					НовСтр = СтрокиРТиУ.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтр,ВыборкаДетальныеЗаписи);
					Если  ВыборкаДетальныеЗаписи.ВидНоменклатуры = "Товары" Тогда
						МаксНомерТовары = МаксНомерТовары+1;
						НовСтр.НомерСтрокиТЧ = МаксНомерТовары;
						
					Иначе
						МаксНомерУслуги = МаксНомерУслуги+1;
						НовСтр.НомерСтрокиТЧ = МаксНомерУслуги;
					КонецЕсли;
				КонецЦикла;
				Для  Эл = 0 По МассивПроверяемыхРеквизитов.Количество()-1 Цикл
					НовСтр[МассивПроверяемыхРеквизитов[Эл][0]] = НовСтр[МассивПроверяемыхРеквизитов[Эл][0]]+ МассивПроверяемыхРеквизитов[Эл][1]  // "последняя копейка" 
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;	
		
	ИначеЕсли  ТипЗнч(Документ) = Тип("ДокументСсылка.ПередачаОС") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗОС.ОсновноеСредство,
		|	ТЗОС.СтоимостьБУ,
		|	ТЗОС.АмортизацияБУ,
		|	ТЗОС.АмортизацияЗаМесяцБУ,
		|	ТЗОС.СтоимостьНУ,
		|	ТЗОС.АмортизацияНУ,
		|	ТЗОС.АмортизацияЗаМесяцНУ,
		|	ТЗОС.Сумма,
		|	ТЗОС.СтавкаНДС,
		|	ТЗОС.СуммаНДС,
		|	ТЗОС.СуммаКапитальныхВложенийВключаемыхВрасходыНУ,
		|	ТЗОС.КостЦентр,
		|	ТЗОС.РБП,
		|	ТЗОС.НомерСтрокиТЧ
		|ПОМЕСТИТЬ ОСВТ
		|ИЗ
		|	&ТЗОС КАК ТЗОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОСВТ.ОсновноеСредство,
		|	ОСВТ.СтоимостьБУ,
		|	ОСВТ.АмортизацияБУ,
		|	ОСВТ.АмортизацияЗаМесяцБУ,
		|	ОСВТ.СтоимостьНУ,
		|	ОСВТ.АмортизацияНУ,
		|	ОСВТ.АмортизацияЗаМесяцНУ,
		|	ОСВТ.Сумма,
		|	ОСВТ.СтавкаНДС,
		|	ОСВТ.СуммаНДС,
		|	ОСВТ.СуммаКапитальныхВложенийВключаемыхВрасходыНУ,
		|	ОСВТ.КостЦентр,
		|	ОСВТ.РБП,
		|	ОСВТ.НомерСтрокиТЧ,
		|	ВЫБОР
		|		КОГДА ОСВТ.НомерСтрокиТЧ В (&МассивВыбранныхСтрок)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Изменять
		|ПОМЕСТИТЬ ОСсВыбранными
		|ИЗ
		|	ОСВТ КАК ОСВТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспределениеAU.AU,
		|	РаспределениеAU.Коэффициент
		|ПОМЕСТИТЬ РаспределениеВТ
		|ИЗ
		|	&РаспределениеAU КАК РаспределениеAU
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОСсВыбранными.ОсновноеСредство,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА ОСсВыбранными.СтоимостьБУ
		|			ИНАЧЕ ОСсВыбранными.СтоимостьБУ * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СтоимостьБУ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА ОСсВыбранными.АмортизацияБУ
		|			ИНАЧЕ ОСсВыбранными.АмортизацияБУ * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК АмортизацияБУ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА ОСсВыбранными.АмортизацияЗаМесяцБУ
		|			ИНАЧЕ ОСсВыбранными.АмортизацияЗаМесяцБУ * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК АмортизацияЗаМесяцБУ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА ОСсВыбранными.СтоимостьНУ
		|			ИНАЧЕ ОСсВыбранными.СтоимостьНУ * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СтоимостьНУ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА ОСсВыбранными.АмортизацияНУ
		|			ИНАЧЕ ОСсВыбранными.АмортизацияНУ * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК АмортизацияНУ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА ОСсВыбранными.АмортизацияЗаМесяцНУ
		|			ИНАЧЕ ОСсВыбранными.АмортизацияЗаМесяцНУ * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК АмортизацияЗаМесяцНУ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА ОСсВыбранными.Сумма
		|			ИНАЧЕ ОСсВыбранными.Сумма * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	ОСсВыбранными.СтавкаНДС,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА ОСсВыбранными.СуммаНДС
		|			ИНАЧЕ ОСсВыбранными.СуммаНДС * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА РаспределениеВТ.Коэффициент ЕСТЬ NULL 
		|				ТОГДА ОСсВыбранными.СуммаКапитальныхВложенийВключаемыхВрасходыНУ
		|			ИНАЧЕ ОСсВыбранными.СуммаКапитальныхВложенийВключаемыхВрасходыНУ * РаспределениеВТ.Коэффициент / &СуммаКоэффициентов
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаКапитальныхВложенийВключаемыхВрасходыНУ,
		|	ЕСТЬNULL(РаспределениеВТ.AU, ОСсВыбранными.КостЦентр) КАК КостЦентр,
		|	ОСсВыбранными.РБП,
		|	ОСсВыбранными.НомерСтрокиТЧ КАК НомерСтрокиТЧ,
		|	ОСсВыбранными.СтоимостьБУ КАК СтоимостьБУИтог,
		|	ОСсВыбранными.АмортизацияБУ КАК АмортизацияБУИтог,
		|	ОСсВыбранными.АмортизацияЗаМесяцБУ КАК АмортизацияЗаМесяцБУИтог,
		|	ОСсВыбранными.СтоимостьНУ КАК СтоимостьНУИтог,
		|	ОСсВыбранными.АмортизацияНУ КАК АмортизацияНУИтог,
		|	ОСсВыбранными.АмортизацияЗаМесяцНУ КАК АмортизацияЗаМесяцНУИтог,
		|	ОСсВыбранными.Сумма КАК СуммаИтог,
		|	ОСсВыбранными.СуммаНДС КАК СуммаНДСИтог,
		|	ОСсВыбранными.СуммаКапитальныхВложенийВключаемыхВрасходыНУ КАК СуммаКапитальныхВложенийВключаемыхВрасходыНУИтог
		|ИЗ
		|	ОСсВыбранными КАК ОСсВыбранными
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределениеВТ КАК РаспределениеВТ
		|		ПО (ОСсВыбранными.Изменять = ИСТИНА)
		|ИТОГИ
		|	СУММА(СтоимостьБУ),
		|	СУММА(АмортизацияБУ),
		|	СУММА(АмортизацияЗаМесяцБУ),
		|	СУММА(СтоимостьНУ),
		|	СУММА(АмортизацияНУ),
		|	СУММА(АмортизацияЗаМесяцНУ),
		|	СУММА(Сумма),
		|	СУММА(СуммаНДС),
		|	СУММА(СуммаКапитальныхВложенийВключаемыхВрасходыНУ),
		|	МАКСИМУМ(СтоимостьБУИтог),
		|	МАКСИМУМ(АмортизацияБУИтог),
		|	МАКСИМУМ(АмортизацияЗаМесяцБУИтог),
		|	МАКСИМУМ(СтоимостьНУИтог),
		|	МАКСИМУМ(АмортизацияНУИтог),
		|	МАКСИМУМ(АмортизацияЗаМесяцНУИтог),
		|	МАКСИМУМ(СуммаИтог),
		|	МАКСИМУМ(СуммаНДСИтог),
		|	МАКСИМУМ(СуммаКапитальныхВложенийВключаемыхВрасходыНУИтог)
		|ПО
		|	НомерСтрокиТЧ";
		
		Запрос.УстановитьПараметр("ТЗОС",СтрокиПередачаОС.Выгрузить());
		Запрос.УстановитьПараметр("МассивВыбранныхСтрок", СтруктураВыбранныхСтрок.НомераСтрокОС);
		ТЗРаспределения = РаспределениеAU.Выгрузить();
		СуммаКоэффициентов = РаспределениеAU.Итог("Коэффициент");
		//Для Каждого Строка ИЗ ТЗраспределения Цикл
		//	Строка.Коэффициент = Строка.Коэффициент/СуммаКоэффициентов;
		//КонецЦикла;
		Запрос.УстановитьПараметр("РаспределениеAU", ТЗРаспределения );
		Запрос.УстановитьПараметр("СуммаКоэффициентов", СуммаКоэффициентов);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СтрокиРТиУ.Очистить();
		СтрокиПередачаОС.Очистить();
		
		МассивПроверяемыхРеквизитов  = Новый Массив(9,2);
		МассивПроверяемыхРеквизитов[0][0] = "СтоимостьБУ";
		МассивПроверяемыхРеквизитов[1][0] = "АмортизацияБУ";
		МассивПроверяемыхРеквизитов[2][0] = "АмортизацияЗаМесяцБУ";
		МассивПроверяемыхРеквизитов[3][0] = "СтоимостьНУ";
		МассивПроверяемыхРеквизитов[4][0] = "АмортизацияНУ";
		МассивПроверяемыхРеквизитов[5][0] = "АмортизацияЗаМесяцНУ";
		МассивПроверяемыхРеквизитов[6][0] = "Сумма";
		МассивПроверяемыхРеквизитов[7][0] = "СуммаНДС";
		МассивПроверяемыхРеквизитов[8][0] = "СуммаКапитальныхВложенийВключаемыхВрасходыНУ";
		
		МаксНомер = 0;
		
		ВыборкаИтогНомерСтроки = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаИтогНомерСтроки.Следующий() Цикл
			Для  Эл = 0 По МассивПроверяемыхРеквизитов.Количество()-1 Цикл
				МассивПроверяемыхРеквизитов[Эл][1] = ВыборкаИтогНомерСтроки[МассивПроверяемыхРеквизитов[Эл][0]+"Итог"] - ВыборкаИтогНомерСтроки[МассивПроверяемыхРеквизитов[Эл][0]]; // "последняя копейка" 
			КонецЦикла;
			ВыборкаДетальныеЗаписи = ВыборкаИтогНомерСтроки.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				НовСтр = СтрокиПередачаОС.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр,ВыборкаДетальныеЗаписи);
				МаксНомер = МаксНомер+1;
				НовСтр.НомерСтрокиТЧ = МаксНомер;
				
			КонецЦикла;
			Для  Эл = 0 По МассивПроверяемыхРеквизитов.Количество()-1 Цикл
				НовСтр[МассивПроверяемыхРеквизитов[Эл][0]] = НовСтр[МассивПроверяемыхРеквизитов[Эл][0]]+ МассивПроверяемыхРеквизитов[Эл][1]  // "последняя копейка" 
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


