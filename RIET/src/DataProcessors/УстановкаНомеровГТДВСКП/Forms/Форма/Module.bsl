//Обрабочики формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ПериодС  = Дата(2015,1,1);
	Объект.ПериодПо = Дата(2015,6,30);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсеФлажки(Команда)
	
	Для каждого Строка из Объект.ТаблицаСоответствий Цикл
		Строка.Выбор = Истина;
		ПроверитьВыборСтроки(Строка, Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеФлажки(Команда)
	
	Для каждого Строка из Объект.ТаблицаСоответствий Цикл
		Строка.Выбор = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиСоответствия(Команда)
	
	Если НЕ (ЗначениеЗаполнено(Объект.ПериодС) И ЗначениеЗаполнено(Объект.ПериодПо)) Тогда
		Сообщить("Необходимо установить период отбора!");
		Возврат;
	КонецЕсли;
	
	НайтиСоответствияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийВыборПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаСоответствий.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Выбор Тогда
		ПроверитьВыборСтроки(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийПриИзменении(Элемент)
	
	ПроверитьВыборСтроки(Элементы.ТаблицаСоответствий.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СинхронизироватьНомераИДаты(Команда)
	СинхронизироватьНомераИДатыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийГТДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Элементы.ТаблицаСоответствийГТД.СписокВыбора.Очистить();	
	ТекущиеДанные 					 = Элементы.ТаблицаСоответствий.ТекущиеДанные;	
	ЭлементСтрокиСКП 				 = ТекущиеДанные.СчетКнигиПокупок; 	
	НеобходимоУстановитьСписокВыбора = ТекущиеДанные.КоличествоСоответствий > 1 И Элементы.ТаблицаСоответствийИспользоватьСписокВыбораГТД.Пометка;
	
	Элементы.ТаблицаСоответствийГТД.РежимВыбораИзСписка = НеобходимоУстановитьСписокВыбора;
	
	Если НеобходимоУстановитьСписокВыбора Тогда				
		НайденныеСтроки = CоответствияСКП_ГТД.НайтиСтроки(Новый Структура("СКП", ЭлементСтрокиСКП));		
		МассивВыбора 	= Новый Массив;		
		Для каждого Строка из НайденныеСтроки Цикл
			МассивВыбора.Добавить(Строка.ГТД);
		КонецЦикла;                           		
		Элементы.ТаблицаСоответствийГТД.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);		
	КонецЕсли;   	
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответствийГТДПриИзменении(Элемент)
	
	ТД = Элементы.ТаблицаСоответствий.ТекущиеДанные;
	
	ДанныеПоГТД = ПолучитьДанныеПоГТД(ТД.ГТД);
	
	ТД.НомерГТД 	  = ДанныеПоГТД.Номер;
	ТД.ДатаГТД  	  = ДанныеПоГТД.Дата;
	ТД.ОбщаяСуммаГТД  = ДанныеПоГТД.ОбщаяСумма;
	ТД.СуммаНДС_ГТД   = ДанныеПоГТД.СуммаНДС;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРежимВыбораИзСписка(Команда)
	
	Элементы.ТаблицаСоответствийИспользоватьСписокВыбораГТД.Пометка = НЕ Элементы.ТаблицаСоответствийИспользоватьСписокВыбораГТД.Пометка; 	
	
КонецПроцедуры

//Основные процедуры и функции

&НаСервере
Процедура НайтиСоответствияНаСервере()
	
	ТаблицаСоответствий = Объект.ТаблицаСоответствий;	
	ТаблицаСоответствий.Очистить();
	
	ТаблицаКорректных = Объект.ТаблицаКорректных;	
	ТаблицаКорректных.Очистить();
	
	CоответствияСКП_ГТД.Очистить();
	
	//|ПОМЕСТИТЬ ВТ_СчетаКнигиПокупок     						- таможенные СКП
	//|ПОМЕСТИТЬ ВТ_Корректные_СКП                              - таможенные СКП, с найденными для них ГТД по соответствию номера и даты
	//|ПОМЕСТИТЬ ВТ_НекорректныеСКП     						- таможенные СКП, для которых не найденны ГТД по соответствию номера и даты
	
	//Алгоритм по связям
	//|ПОМЕСТИТЬ ВТ_ПроводкиДеталейСКП 							- блок временных таблиц для нахождения ГТД по связям: проводки деталей, привязанные к СКП
	//|ПОМЕСТИТЬ ВТ_ShipmentСlosingСКП							- блок временных таблиц для нахождения ГТД по связям: ShipmentClosing через связь по проводке ДСС
	//|ПОМЕСТИТЬ ВТ_ПоставкиСКП                                 - блок временных таблиц для нахождения ГТД по связям: закрытие поставки через ShipmentClosing
	//|ПОМЕСТИТЬ ВТ_НайденныеГТД_ПоСвязям						- блок временных таблиц для нахождения ГТД по связям: ГТД, найденные в соответствие СКП по поставке
	
	//Алгоритм по суммам и датам
	//|ПОМЕСТИТЬ ВТ_ГТДСуммы									- суммы по ГТД
	//|ПОМЕСТИТЬ ВТ_НайденныеГТД_ПоСуммамИДатам					- ГТД, найденные в соответствие СКП по суммам и датам (гтд)
		
	//|ПОМЕСТИТЬ ВТ_Соответствие_СКП_ГТД				        - объединение результатов поиска ГТД для СКП по связям и суммам+датам
	//|ПОМЕСТИТЬ ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП			- сворачивание по ГТД с целью выявления количества совпадений ГТД
	//|ПОМЕСТИТЬ ВТ_ИтогСоответствий_СКП_И_ГТД                  - составление итога по найденным совпадениям + не имеющие совпадений
	
	//Дополнить список совпадения ГТД по лайт, только 1 к 1
	//|ПОМЕСТИТЬ ВТ_СКП_Без_Соответствий						- некорректные СКП, по которым не нашлись соответствия перечисленными способами
	//|ПОМЕСТИТЬ ВТ_СКП_И_ГТД_Light								- найденные 1 к 1 соответствия ГТД лайт для СКП (только 1 к 1)  по дате и сумме
	
	//Запрос 15                                                 - результат поиска сооветствий ГТД и ГТД лайт для СКП с подтягивание различных вспомогательных данных. Соответсвия ГТД проставляются только для совпадений 1 к 1
	//Запрос 16													- корректные СКП список
	//Запрос 17													- соответствие ГТД для СКП, в случаях, когда нашлось более 1-го совпадения
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("НачалоПериода", 	 Объект.ПериодС);
	Запрос.Параметры.Вставить("КонецПериода",  	 Объект.ПериодПо);
	Запрос.Параметры.Вставить("СинонимГТД",      Метаданные.Документы.ГТД.Синоним);
	Запрос.Параметры.Вставить("СинонимГТД_Лайт", Метаданные.Документы.CustomsFilesLight.Синоним);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетКнигиПокупок.Ссылка КАК СКП,
		|	НАЧАЛОПЕРИОДА(СчетКнигиПокупок.ДатаВходящегоДокумента, ДЕНЬ) КАК ДатаГТД,
		|	СчетКнигиПокупок.НомерВходящегоДокумента КАК НомерГТД,
		|	СчетКнигиПокупок.СуммаДокументаСНДСРуб КАК СуммаДокументаСКП
		|ПОМЕСТИТЬ ВТ_СчетаКнигиПокупок
		|ИЗ
		|	Документ.СчетКнигиПокупок КАК СчетКнигиПокупок
		|ГДЕ
		|	СчетКнигиПокупок.ТаможеннаяДекларация
		|	И НЕ СчетКнигиПокупок.ПометкаУдаления
		|	И СчетКнигиПокупок.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СчетаКнигиПокупок.СКП,
		|	ВТ_СчетаКнигиПокупок.ДатаГТД,
		|	ВТ_СчетаКнигиПокупок.НомерГТД,
		|	ГТД.Ссылка КАК ГТД
		|ПОМЕСТИТЬ ВТ_Корректные_СКП
		|ИЗ
		|	ВТ_СчетаКнигиПокупок КАК ВТ_СчетаКнигиПокупок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ГТД КАК ГТД
		|		ПО ВТ_СчетаКнигиПокупок.НомерГТД = ГТД.Номер
		|			И (ВТ_СчетаКнигиПокупок.ДатаГТД = НАЧАЛОПЕРИОДА(ГТД.Дата, ДЕНЬ))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_СчетаКнигиПокупок.СКП,
		|	ВТ_СчетаКнигиПокупок.ДатаГТД,
		|	ВТ_СчетаКнигиПокупок.НомерГТД,
		|	ГТД.Ссылка
		|
		|ИМЕЮЩИЕ
		|	СУММА(1) = 1
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_СчетаКнигиПокупок.СКП,
		|	ВТ_СчетаКнигиПокупок.ДатаГТД,
		|	ВТ_СчетаКнигиПокупок.НомерГТД,
		|	CustomsFilesLight.Ссылка
		|ИЗ
		|	ВТ_СчетаКнигиПокупок КАК ВТ_СчетаКнигиПокупок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.CustomsFilesLight КАК CustomsFilesLight
		|		ПО ВТ_СчетаКнигиПокупок.НомерГТД = CustomsFilesLight.Номер
		|			И (ВТ_СчетаКнигиПокупок.ДатаГТД = НАЧАЛОПЕРИОДА(CustomsFilesLight.Дата, ДЕНЬ))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_СчетаКнигиПокупок.СКП,
		|	ВТ_СчетаКнигиПокупок.ДатаГТД,
		|	ВТ_СчетаКнигиПокупок.НомерГТД,
		|	CustomsFilesLight.Ссылка
		|
		|ИМЕЮЩИЕ
		|	СУММА(1) = 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СчетаКнигиПокупок.СКП,
		|	ВТ_СчетаКнигиПокупок.ДатаГТД,
		|	ВТ_СчетаКнигиПокупок.НомерГТД,
		|	ВТ_СчетаКнигиПокупок.СуммаДокументаСКП
		|ПОМЕСТИТЬ ВТ_НекорректныеСКП
		|ИЗ
		|	ВТ_СчетаКнигиПокупок КАК ВТ_СчетаКнигиПокупок
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Корректные_СКП КАК ВТ_Корректные_СКП
		|		ПО ВТ_СчетаКнигиПокупок.СКП = ВТ_Корректные_СКП.СКП
		|ГДЕ
		|	ВТ_Корректные_СКП.СКП ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НекорректныеСКП.СКП,
		|	ПроводкаDSS.Ссылка КАК ПроводкаDSS
		|ПОМЕСТИТЬ ВТ_ПроводкиДеталейСКП
		|ИЗ
		|	Документ.ПроводкаDSS КАК ПроводкаDSS
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НекорректныеСКП КАК ВТ_НекорректныеСКП
		|		ПО ПроводкаDSS.Документ = ВТ_НекорректныеСКП.СКП
		|ГДЕ
		|	НЕ ПроводкаDSS.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_ПроводкиДеталейСКП.СКП,
		|	РаспределениеИмпортаПоЗакрытиюПоставкиDSS.Ссылка.ShipmentСlosing
		|ПОМЕСТИТЬ ВТ_ShipmentСlosingСКП
		|ИЗ
		|	ВТ_ПроводкиДеталейСКП КАК ВТ_ПроводкиДеталейСКП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеИмпортаПоЗакрытиюПоставки.DSS КАК РаспределениеИмпортаПоЗакрытиюПоставкиDSS
		|		ПО ВТ_ПроводкиДеталейСКП.ПроводкаDSS = РаспределениеИмпортаПоЗакрытиюПоставкиDSS.ПроводкаDSSСКП
		|ГДЕ
		|	НЕ РаспределениеИмпортаПоЗакрытиюПоставкиDSS.Ссылка.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_ShipmentСlosingСКП.СКП,
		|	ЗакрытиеПоставки.Поставка
		|ПОМЕСТИТЬ ВТ_ПоставкиСКП
		|ИЗ
		|	ВТ_ShipmentСlosingСКП КАК ВТ_ShipmentСlosingСКП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗакрытиеПоставки КАК ЗакрытиеПоставки
		|		ПО ВТ_ShipmentСlosingСКП.ShipmentСlosing = ЗакрытиеПоставки.Ссылка
		|ГДЕ
		|	НЕ ЗакрытиеПоставки.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_ПоставкиСКП.СКП,
		|	ГТД.Ссылка КАК ГТД
		|ПОМЕСТИТЬ ВТ_НайденныеГТД_ПоСвязям
		|ИЗ
		|	ВТ_ПоставкиСКП КАК ВТ_ПоставкиСКП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ГТД КАК ГТД
		|		ПО ВТ_ПоставкиСКП.Поставка = ГТД.Shipment
		|ГДЕ
		|	НЕ ГТД.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГТДПодробностиПодсчета.Ссылка КАК ГТД,
		|	НАЧАЛОПЕРИОДА(ГТДПодробностиПодсчета.Ссылка.Дата, ДЕНЬ) КАК Дата,
		|	СУММА(ГТДПодробностиПодсчета.СуммаПлатежа) КАК СуммаПлатежа,
		|	СУММА(ВЫБОР
		|			КОГДА ГТДПодробностиПодсчета.КодВидаПлатежа = ""5010""
		|				ТОГДА ГТДПодробностиПодсчета.СуммаПлатежа
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНДС
		|ПОМЕСТИТЬ ВТ_ГТДСуммы
		|ИЗ
		|	Документ.ГТД.ПодробностиПодсчета КАК ГТДПодробностиПодсчета
		|ГДЕ
		|	НЕ ГТДПодробностиПодсчета.Ссылка.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	ГТДПодробностиПодсчета.Ссылка,
		|	НАЧАЛОПЕРИОДА(ГТДПодробностиПодсчета.Ссылка.Дата, ДЕНЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НекорректныеСКП.СКП КАК СКП,
		|	ВТ_ГТДСуммы.ГТД КАК ГТД
		|ПОМЕСТИТЬ ВТ_НайденныеГТД_ПоСуммамИДатам
		|ИЗ
		|	ВТ_НекорректныеСКП КАК ВТ_НекорректныеСКП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ГТДСуммы КАК ВТ_ГТДСуммы
		|		ПО ВТ_НекорректныеСКП.СуммаДокументаСКП = ВТ_ГТДСуммы.СуммаПлатежа
		|			И ВТ_НекорректныеСКП.ДатаГТД = ВТ_ГТДСуммы.Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НайденныеГТД_ПоСвязям.СКП КАК СКП,
		|	ВТ_НайденныеГТД_ПоСвязям.ГТД КАК ГТД
		|ПОМЕСТИТЬ ВТ_Соответствие_СКП_ГТД
		|ИЗ
		|	ВТ_НайденныеГТД_ПоСвязям КАК ВТ_НайденныеГТД_ПоСвязям
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_НайденныеГТД_ПоСуммамИДатам.СКП,
		|	ВТ_НайденныеГТД_ПоСуммамИДатам.ГТД
		|ИЗ
		|	ВТ_НайденныеГТД_ПоСуммамИДатам КАК ВТ_НайденныеГТД_ПоСуммамИДатам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Соответствие_СКП_ГТД.СКП,
		|	МАКСИМУМ(ВТ_Соответствие_СКП_ГТД.ГТД) КАК ГТД,
		|	СУММА(1) КАК КоличествоГТД
		|ПОМЕСТИТЬ ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП
		|ИЗ
		|	ВТ_Соответствие_СКП_ГТД КАК ВТ_Соответствие_СКП_ГТД
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Соответствие_СКП_ГТД.СКП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НекорректныеСКП.СКП КАК СКП,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.СКП ЕСТЬ NULL 
		|				И ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.КоличествоГТД = 1
		|			ТОГДА ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.ГТД
		|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ГТД.ПустаяСсылка)
		|	КОНЕЦ КАК ГТД,
		|	ЕСТЬNULL(ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.КоличествоГТД, 0) КАК КоличествоСоответствийГТД,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.СКП ЕСТЬ NULL 
		|				И ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.КоличествоГТД = 1
		|			ТОГДА ЕСТЬNULL(ВТ_ГТДСуммы.СуммаПлатежа, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаПлатежа,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.СКП ЕСТЬ NULL 
		|				И ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.КоличествоГТД = 1
		|			ТОГДА ЕСТЬNULL(ВТ_ГТДСуммы.СуммаНДС, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНДС
		|ПОМЕСТИТЬ ВТ_ИтогСоответствий_СКП_И_ГТД
		|ИЗ
		|	ВТ_НекорректныеСКП КАК ВТ_НекорректныеСКП
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП КАК ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ГТДСуммы КАК ВТ_ГТДСуммы
		|			ПО ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.ГТД = ВТ_ГТДСуммы.ГТД
		|		ПО ВТ_НекорректныеСКП.СКП = ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.СКП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НекорректныеСКП.СКП,
		|	ВТ_НекорректныеСКП.ДатаГТД,
		|	ВТ_НекорректныеСКП.СуммаДокументаСКП
		|ПОМЕСТИТЬ ВТ_СКП_Без_Соответствий
		|ИЗ
		|	ВТ_ИтогСоответствий_СКП_И_ГТД КАК ВТ_ИтогСоответствий_СКП_И_ГТД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НекорректныеСКП КАК ВТ_НекорректныеСКП
		|		ПО ВТ_ИтогСоответствий_СКП_И_ГТД.СКП = ВТ_НекорректныеСКП.СКП
		|ГДЕ
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.КоличествоСоответствийГТД = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СКП_Без_Соответствий.СКП КАК СКП,
		|	CustomsFilesLightСуммы.ГТД_Лайт КАК ГТД_Light,
		|	СУММА(CustomsFilesLightСуммы.ОбщаяСумма) КАК ОбщаяСумма,
		|	СУММА(CustomsFilesLightСуммы.СуммаНДС) КАК СуммаНДС
		|ПОМЕСТИТЬ ВТ_СКП_И_ГТД_Light
		|ИЗ
		|	ВТ_СКП_Без_Соответствий КАК ВТ_СКП_Без_Соответствий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			CustomsFilesLightPayments.Ссылка КАК ГТД_Лайт,
		|			НАЧАЛОПЕРИОДА(CustomsFilesLightPayments.Ссылка.Дата, ДЕНЬ) КАК Дата,
		|			СУММА(CustomsFilesLightPayments.Sum) КАК ОбщаяСумма,
		|			СУММА(ВЫБОР
		|					КОГДА CustomsFilesLightPayments.PaymentKind = ""5010""
		|						ТОГДА CustomsFilesLightPayments.Sum
		|					ИНАЧЕ 0
		|				КОНЕЦ) КАК СуммаНДС
		|		ИЗ
		|			Документ.CustomsFilesLight.Payments КАК CustomsFilesLightPayments
		|		
		|		СГРУППИРОВАТЬ ПО
		|			CustomsFilesLightPayments.Ссылка,
		|			НАЧАЛОПЕРИОДА(CustomsFilesLightPayments.Ссылка.Дата, ДЕНЬ)) КАК CustomsFilesLightСуммы
		|		ПО (ВТ_СКП_Без_Соответствий.ДатаГТД = НАЧАЛОПЕРИОДА(CustomsFilesLightСуммы.Дата, ДЕНЬ))
		|			И ВТ_СКП_Без_Соответствий.СуммаДокументаСКП = CustomsFilesLightСуммы.ОбщаяСумма
		|ГДЕ
		|	НЕ ВТ_СКП_Без_Соответствий.СуммаДокументаСКП = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_СКП_Без_Соответствий.СКП,
		|	CustomsFilesLightСуммы.ГТД_Лайт
		|
		|ИМЕЮЩИЕ
		|	СУММА(1) = 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.СКП КАК СчетКнигиПокупок,
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.ГТД,
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.КоличествоСоответствийГТД КАК КоличествоСоответствий,
		|	&СинонимГТД КАК ИмяДокумента,
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.СКП.НомерВходящегоДокумента КАК НомерГТДИзСКП,
		|	НАЧАЛОПЕРИОДА(ВТ_ИтогСоответствий_СКП_И_ГТД.СКП.ДатаВходящегоДокумента, ДЕНЬ) КАК ДатаГТДИзСКП,
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.ГТД.Номер КАК НомерГТД,
		|	НАЧАЛОПЕРИОДА(ВТ_ИтогСоответствий_СКП_И_ГТД.ГТД.Дата, ДЕНЬ) КАК ДатаГТД,
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.СКП.СуммаДокументаСНДСРуб КАК ОбщаяСуммаСКП,
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.СКП.СуммаНДСДокументаРуб КАК СуммаНДС_СКП,
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.СуммаПлатежа КАК ОбщаяСуммаГТД,
		|	ВТ_ИтогСоответствий_СКП_И_ГТД.СуммаНДС КАК СуммаНДС_ГТД
		|ИЗ
		|	ВТ_ИтогСоответствий_СКП_И_ГТД КАК ВТ_ИтогСоответствий_СКП_И_ГТД
		|ГДЕ
		|	НЕ ВТ_ИтогСоответствий_СКП_И_ГТД.СКП В
		|				(ВЫБРАТЬ
		|					ВТ_СКП_И_ГТД_Light.СКП
		|				ИЗ
		|					ВТ_СКП_И_ГТД_Light КАК ВТ_СКП_И_ГТД_Light)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_СКП_И_ГТД_Light.СКП,
		|	ВТ_СКП_И_ГТД_Light.ГТД_Light,
		|	1,
		|	&СинонимГТД_Лайт,
		|	ВТ_СКП_И_ГТД_Light.СКП.НомерВходящегоДокумента,
		|	НАЧАЛОПЕРИОДА(ВТ_СКП_И_ГТД_Light.СКП.ДатаВходящегоДокумента, ДЕНЬ),
		|	ВТ_СКП_И_ГТД_Light.ГТД_Light.Номер,
		|	НАЧАЛОПЕРИОДА(ВТ_СКП_И_ГТД_Light.ГТД_Light.Дата, ДЕНЬ),
		|	ВТ_СКП_И_ГТД_Light.СКП.СуммаДокументаСНДСРуб,
		|	ВТ_СКП_И_ГТД_Light.СКП.СуммаНДСДокументаРуб,
		|	ВТ_СКП_И_ГТД_Light.ОбщаяСумма,
		|	ВТ_СКП_И_ГТД_Light.СуммаНДС
		|ИЗ
		|	ВТ_СКП_И_ГТД_Light КАК ВТ_СКП_И_ГТД_Light
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаГТДИзСКП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Корректные_СКП.СКП КАК СчетКнигиПокупок,
		|	ВТ_Корректные_СКП.ДатаГТД,
		|	ВТ_Корректные_СКП.НомерГТД,
		|	ВТ_Корректные_СКП.ГТД
		|ИЗ
		|	ВТ_Корректные_СКП КАК ВТ_Корректные_СКП
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_Корректные_СКП.СКП.Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Соответствие_СКП_ГТД.СКП,
		|	ВТ_Соответствие_СКП_ГТД.ГТД
		|ИЗ
		|	ВТ_Соответствие_СКП_ГТД КАК ВТ_Соответствие_СКП_ГТД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП КАК ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП
		|		ПО ВТ_Соответствие_СКП_ГТД.СКП = ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.СКП
		|			И (ВТ_Соответствие_СКП_ГТД_СвернутыеПоСКП.КоличествоГТД > 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_Соответствие_СКП_ГТД.СКП.Дата";
	
	Результат 				 	= Запрос.ВыполнитьПакет();	
	КоличествоЗапросов 		 	= Результат.Количество();	
	ВыборкаСоответствиеСКП_ГТД 	= Результат[КоличествоЗапросов-1].Выбрать();
	ВыборкаКорректныхСКП   	 	= Результат[КоличествоЗапросов-2].Выбрать();
	ВыборкаНекорректныхСКП   	= Результат[КоличествоЗапросов-3].Выбрать();
	
	Пока ВыборкаСоответствиеСКП_ГТД.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(CоответствияСКП_ГТД.Добавить(), ВыборкаСоответствиеСКП_ГТД);		
	КонецЦикла;	
	
	Пока ВыборкаКорректныхСКП.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаКорректных.Добавить(), ВыборкаКорректныхСКП);		
	КонецЦикла;	
	      	
	Пока ВыборкаНекорректныхСКП.Следующий() Цикл		
		ЗаполнитьЗначенияСвойств(ТаблицаСоответствий.Добавить(), ВыборкаНекорректныхСКП);		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СинхронизироватьНомераИДатыНаСервере()
	
	Перезаписано = 0;
	
	Для каждого Строка из Объект.ТаблицаСоответствий Цикл
		
		Если Строка.Выбор и ЗначениеЗаполнено(Строка.ГТД) Тогда
			
			СКПОбъект = Строка.СчетКнигиПокупок.ПолучитьОбъект();
			
			СКПОбъект.ОбменДанными.Загрузка = Истина;
			
			СКПОбъект.НомерВходящегоДокумента = Строка.НомерГТД;
			СКПОбъект.ДатаВходящегоДокумента  = Строка.ДатаГТД;
			
			Попытка
				СКПОбъект.Записать(РежимЗаписиДокумента.Запись);
				Перезаписано = Перезаписано + 1;
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;		
		
	КонецЦикла;
	
	Если Перезаписано Тогда
		Сообщить("Откорректированы номера и даты в " + Перезаписано + " документах СКП");
	КонецЕсли;
	
	Если Объект.ТаблицаСоответствий.Количество() Тогда
		НайтиСоответствияНаСервере();
	КонецЕсли;
	
КонецПроцедуры

//Вспомогательные процедуры и функции

&НаКлиенте
Процедура ПроверитьВыборСтроки(Строка, МассоваяПроверка = Ложь)
	
	Если Строка.Выбор И НЕ ЗначениеЗаполнено(Строка.ГТД) Тогда
		Строка.Выбор = Ложь;
		Если НЕ МассоваяПроверка Тогда
			Сообщить("Данная строка не может быть выбрана, т.к. не установлено соответствие ГТД!");
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры     

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоГТД(ТекГТД)
	                  	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГТДПодробностиПодсчета.Ссылка.Номер,
		|	ГТДПодробностиПодсчета.Ссылка.Дата,
		|	СУММА(ГТДПодробностиПодсчета.СуммаПлатежа) КАК ОбщаяСумма,
		|	СУММА(ВЫБОР
		|			КОГДА ГТДПодробностиПодсчета.КодВидаПлатежа = ""5010""
		|				ТОГДА ГТДПодробностиПодсчета.СуммаПлатежа
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНДС
		|ИЗ
		|	Документ.ГТД.ПодробностиПодсчета КАК ГТДПодробностиПодсчета
		|ГДЕ
		|	ГТДПодробностиПодсчета.Ссылка = &ГТД
		|
		|СГРУППИРОВАТЬ ПО
		|	ГТДПодробностиПодсчета.Ссылка.Номер,
		|	ГТДПодробностиПодсчета.Ссылка.Дата
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	CustomsFilesLight.Номер,
		|	CustomsFilesLight.Дата,
		|	СУММА(CustomsFilesLightPayments.Sum),
		|	СУММА(ВЫБОР
		|			КОГДА CustomsFilesLightPayments.PaymentKind = ""5010""
		|				ТОГДА CustomsFilesLightPayments.Sum
		|			ИНАЧЕ 0
		|		КОНЕЦ)
		|ИЗ
		|	Документ.CustomsFilesLight.Payments КАК CustomsFilesLightPayments
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.CustomsFilesLight КАК CustomsFilesLight
		|		ПО CustomsFilesLightPayments.Ссылка = CustomsFilesLight.Ссылка
		|ГДЕ
		|	CustomsFilesLight.Ссылка = &ГТД
		|
		|СГРУППИРОВАТЬ ПО
		|	CustomsFilesLight.Номер,
		|	CustomsFilesLight.Дата";
	
	Запрос.УстановитьПараметр("ГТД", ТекГТД);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	           	
	НомерГТД   = "";
	ДатаГТД    = "";	
	ОбщаяСумма = 0;
	СуммаНДС   = 0;
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		НомерГТД   = ВыборкаДетальныеЗаписи.Номер;
		ДатаГТД    = ВыборкаДетальныеЗаписи.Дата;
		ОбщаяСумма = ВыборкаДетальныеЗаписи.ОбщаяСумма;
		СуммаНДС   = ВыборкаДетальныеЗаписи.СуммаНДС;
	КонецЕсли;
	                       
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Номер",		 НомерГТД);
	СтруктураВозврата.Вставить("Дата",  	 ДатаГТД);
	СтруктураВозврата.Вставить("ОбщаяСумма", ОбщаяСумма);
	СтруктураВозврата.Вставить("СуммаНДС",   СуммаНДС);
	
	Возврат СтруктураВозврата;
	
КонецФункции    
