#Область ТранспортОбмена

Функция СоздатьWSПрокси(УзелОбмена, WSСсылка) Экспорт
	
	// Анализирует WSСсылку и возвращает настроенный WSПрокси, полученной из этой WSСсылки
	
	URIПространстваИмен = "http://www.1c.ru/SSL/EnterpriseDataExchange_1_0_1_1_Addition";
	ИмяСервиса = "ERMIntegration";
	
	Если WSСсылка = WSСсылки.rgsИнтеграцияСERM Тогда
		ИмяПорта = "ERMIntegrationSoap12";
	Иначе
		ВызватьИсключение "Failed to find port for WS reference '" + WSСсылка + "' to query ERM master data!";
	КонецЕсли;
	
	НастройкиТранспорта = РегистрыСведений.НастройкиТранспортаОбмена.НастройкиТранспорта(УзелОбмена, Перечисления.ВидыТранспортаСообщенийОбмена.WS);
	
	WSПрокси = WSСсылка.СоздатьWSПрокси(URIПространстваИмен, ИмяСервиса, ИмяПорта);
	WSПрокси.Пользователь = НастройкиТранспорта.WSИмяПользователя;
	WSПрокси.Пароль = НастройкиТранспорта.WSПароль;
	
	Возврат WSПрокси;
	
КонецФункции

Функция ПолучитьОбъектXDTO(ИспользуемаяФабрикаXDTO, URIПространстваИмен, Имя) Экспорт
	
	// Конструирует объект XDTO типа Имя с помощью ФабрикиXDTO и URIПространстваИмен
	ТипПоля = ИспользуемаяФабрикаXDTO.Тип(URIПространстваИмен, Имя);
	Возврат ИспользуемаяФабрикаXDTO.Создать(ТипПоля);
	
КонецФункции

Функция ПолучитьТипОбъектаXDTO(ИспользуемаяФабрикаXDTO, URIПространстваИмен, Имя) Экспорт
	
	Возврат ИспользуемаяФабрикаXDTO.Тип(URIПространстваИмен, Имя);
	
КонецФункции

#КонецОбласти

#Область ВспомогательныеМетоды

Процедура ОбработатьТекстОшибки(ТекстОшибки) Экспорт
	
	Если Найти(ТекстОшибки, "Аутентификация пользователя не выполнена") > 0 Тогда
		ТекстОшибкиДляСообщенияПользователю = "Аутентификация не выполнена. Сообщите, пожалуйста, администраторам.";
	ИначеЕсли Найти(ТекстОшибки, "Ошибка проверки данных XDTO") > 0 Тогда
		ТекстОшибкиДляСообщенияПользователю = "Ошибка проверки данных. Сообщите, пожалуйста, администраторам.";
	Иначе
		ТекстОшибкиДляСообщенияПользователю = "Ошибка работы с веб-сервисом. Сообщите, пожалуйста, администраторам.";
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиДляСообщенияПользователю);
	
КонецПроцедуры

#КонецОбласти