
///////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА ПИСЕМ

Функция ОбработатьПисьма(Письма) Экспорт
	
	// Обрабатывает переданные письма
	// Возвращает массив обработанных писем - как успешно, так и нет. 
	
	КаталогАттачей = Константы.RCATLMКаталогВложений.Получить();
	ОбработанныеПисьма = Новый Массив;
	
	Для Каждого Письмо Из Письма Цикл
		
		// Письма должны обрабатываться независимо
		// Если при обработке письма случилась ошибка - это не должно повлиять на другое письмо
		
		Попытка
			
			ОбработатьПисьмо(Письмо, КаталогАттачей);
			ОбработанныеПисьма.Добавить(Письмо);
			
		Исключение
								
			// Залоггируем ошибку, чтобы получить ее по е-mail
			РГСофт.СообщитьИЗалоггировать(
				"Failed to process incoming RCA TLM e-mail",
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Обработки.PullAttachmentsFromRCATLMEmails,
				Неопределено,
				СтрШаблон("Failed to process incoming RCA TLM e-mail %1: %2", Письмо.Идентификатор[0], ОписаниеОшибки()));
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат ОбработанныеПисьма;
	
КонецФункции

Процедура ОбработатьПисьмо(Письмо, КаталогАттачей)
	
	МассивПутей = ПолучитьПутиКФайлуВложения(Письмо.Вложения);
	
	Для каждого ПутьКВложению Из МассивПутей Цикл
		
		ФайлВложения = Новый Файл(ПутьКВложению);
		
		КопироватьФайл(ПутьКВложению, ПолучитьИмяФайлаПриемника(КаталогАттачей, ФайлВложения.ИмяБезРасширения, ФайлВложения.Расширение, Письмо.ДатаПолучения));
		
		УдалитьФайлы(ПутьКВложению);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЭтоПисьмоRCATLM(Письмо)

	Возврат Истина;

КонецФункции // ()


//////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ / ФУНКЦИИ

Функция ПолучитьИмяФайлаПриемника(Каталог, ИмяФайла, РасширениеФайла, ДатаПолучения)

	Возврат Каталог + ?(СтрЗаканчиваетсяНа(Каталог, "\"), "", "\") + ИмяФайла + "_" + Формат(ДатаПолучения, "ДФ=yyyy_MM_dd_HH_mm_ss") + РасширениеФайла;

КонецФункции // ()

Функция ПолучитьПутиКФайлуВложения(Вложения) Экспорт
	
	МассивПутей = Новый Массив;
	
	Для каждого Вложение Из Вложения Цикл
		
		ДД = Вложение.Данные;
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		ВремФайл = Новый Файл(ИмяВременногоФайла);
		
		ПутьККаталогу = ВремФайл.Путь + ?(РГСофтСерверПовтИспСеанс.ЭтоProductionБаза(), "RCATLMCatalog", "RCATLMCatalogTest");
		СоздатьКаталог(ПутьККаталогу);
		
		ПолныйПуть = ПутьККаталогу + "\" + Вложение.ИмяФайла;
		ДД.Записать(ПолныйПуть);
		
		МассивПутей.Добавить(ПолныйПуть);
		
	КонецЦикла;
	
	Возврат МассивПутей;
	
КонецФункции

Функция ПолучитьОписаниеИнформацииОбОшибке(ИнформацияОбОшибке)
	
	Описание = ИнформацияОбОшибке.Описание;
		
	Если ИнформацияОбОшибке.Причина <> Неопределено Тогда
		Описание = Описание + "
		|
		|by reason:
		|
		|" + ПолучитьОписаниеИнформацииОбОшибке(ИнформацияОбОшибке.Причина);
	КонецЕсли;
	
	Возврат СокрЛП(Описание);
	
КонецФункции
