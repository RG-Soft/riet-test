
&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Для каждого СтрокаТЧ Из Объект.Unprocessed Цикл
		Попытка
			УдалитьФайлы(СтрокаТЧ.Attachment);
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура UnprocessedВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Попытка
		ЗапуститьПриложение(Элементы.Unprocessed.ТекущиеДанные.Attachment);
	Исключение
	КонецПопытки;
	
КонецПроцедуры
            
//////////////////////////////////////////////////////////////////////////////////////
// RECEIVE EMAILS

&НаКлиенте
Процедура ReceiveEmails(Команда)
	
	ReceiveEmailsНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ReceiveEmailsНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИнтернетПочтовыйПрофиль = ImportExportСерверПовтИспСеанс.ПолучитьИнтернетПочтовыйПрофильRCATLM();
	ИнтернетПочта = ImportExportСервер.ПодключитьсяКИнтернетПочте(ИнтернетПочтовыйПрофиль);
	
	ОписаниеОшибки = Неопределено;
	Попытка
		UnprocessedEmails = ImportExportСервер.ПолучитьUnprocessedEmails(ИнтернетПочта, ИнтернетПочтовыйПрофиль.ПользовательIMAP);
	Исключение
		ОписаниеОшибки = "Failed to get unprocessed emails:
			|" + ОписаниеОшибки();
	КонецПопытки;
	
	ИнтернетПочта.Отключиться();
	
	Если ОписаниеОшибки <> Неопределено Тогда
		ВызватьИсключение ОписаниеОшибки;	
	КонецЕсли;
	
	FillUnprocessed(UnprocessedEmails);
	
КонецПроцедуры

&НаСервере
Процедура FillUnprocessed(UnprocessedEmails)
	
	Объект.Unprocessed.Очистить();
	Для Каждого Письмо Из UnprocessedEmails Цикл
		
		НоваяСтрока = Объект.Unprocessed.Добавить();
		
		НоваяСтрока.UID = Письмо.Идентификатор[0];
		
		НоваяСтрока.SentDate = Письмо.ДатаОтправления;
		
		НоваяСтрока.From = Письмо.Отправитель.Адрес;
		
		НоваяСтрока.Subject = Письмо.Тема;
		
		НоваяСтрока.Body = ImportExportСервер.ПолучитьПростойТекстПисьма(Письмо);
		 		
		НоваяСтрока.Attachment = СтрСоединить(Обработки.PullAttachmentsFromRCATLMEmails.ПолучитьПутиКФайлуВложения(Письмо.Вложения), ";" + Символы.ПС);
		
	КонецЦикла;
		
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////////
// LOAD

&НаКлиенте
Процедура Load(Команда)
	
	ИДВыделенныхСтрок = Элементы.Unprocessed.ВыделенныеСтроки;
	Если НЕ ИДВыделенныхСтрок.Количество() Тогда
		Сообщить("Select at least one not processed e-mail!");
		Возврат;
	КонецЕсли;
	
	МассивUIDs = Новый Массив;
	Для Каждого ИДВыделеннойСтроки Из ИДВыделенныхСтрок Цикл
		
		СтрокаТЧ = Объект.Unprocessed.НайтиПоИдентификатору(ИДВыделеннойСтроки);
		МассивUIDs.Добавить(СтрокаТЧ.UID);
		
	КонецЦикла;

	ЗагрузитьНаСервере(МассивUIDs);

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНаСервере(МассивUIDs)
	
	УстановитьПривилегированныйРежим(Истина);

	ИнтернетПочтовыйПрофиль = ImportExportСерверПовтИспСеанс.ПолучитьИнтернетПочтовыйПрофильRCATLM();
	ИнтернетПочта = ImportExportСервер.ПодключитьсяКИнтернетПочте(ИнтернетПочтовыйПрофиль);
	
	ОписаниеОшибки = Неопределено;
	Попытка
		Письма = ИнтернетПочта.Выбрать(Ложь, МассивUIDs);
		ОбработанныеПисьма = Обработки.PullAttachmentsFromRCATLMEmails.ОбработатьПисьма(Письма);
	Исключение
		ОписаниеОшибки = "Failed to load reports from selected e-mails:
			|" + ОписаниеОшибки();
	КонецПопытки;
	
	ИнтернетПочта.Отключиться();
	
	Если ОписаниеОшибки <> Неопределено Тогда
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	ReceiveEmailsНаСервере();
	Элементы.Processed.Обновить();
	
КонецПроцедуры


//////////////////////////////////////////////////////////////////
// MARK AS PROCESSED

&НаКлиенте
Процедура MarkAsProcessed(Команда)
	
	ИДВыделенныхСтрок = Элементы.Unprocessed.ВыделенныеСтроки;
	Если НЕ ИДВыделенныхСтрок.Количество() Тогда
		Сообщить("Select at least one not processed e-mail!");
		Возврат;
	КонецЕсли;
	
	МассивUIDs = Новый Массив;
	Для Каждого ИДВыделеннойСтроки Из ИДВыделенныхСтрок Цикл
		
		СтрокаТЧ = Объект.Unprocessed.НайтиПоИдентификатору(ИДВыделеннойСтроки);
		МассивUIDs.Добавить(СтрокаТЧ.UID);
		
	КонецЦикла;
	
	MarkAsProcessedНаСервере(МассивUIDs);
	
КонецПроцедуры

&НаСервере
Процедура MarkAsProcessedНаСервере(МассивUIDs)
	
	ИнтернетПочтовыйПрофиль = ImportExportСерверПовтИспСеанс.ПолучитьИнтернетПочтовыйПрофильRCATLM();
	ИнтернетПочта = ImportExportСервер.ПодключитьсяКИнтернетПочте(ИнтернетПочтовыйПрофиль);
	
	РегистрыСведений.UIDsOfProcessedEmails.ДобавитьНесколько(МассивUIDs, ИнтернетПочтовыйПрофиль.ПользовательIMAP);
	ReceiveEmailsНаСервере();
	Элементы.Processed.Обновить();
	
КонецПроцедуры


&НаКлиенте
Процедура LoadFile(Команда)
	
	СтруктураВозврата = ОткрытьФормуМодально("Обработка.PullReportsFromServiceProvidersEmails.Форма.ФормаУказанияФайла", Новый Структура("ИмяФайла", ИмяФайла), ЭтаФорма);
	Если СтруктураВозврата <> Неопределено Тогда
		ИмяФайла = СтруктураВозврата["ИмяФайла"];
		ServiceProvider = СтруктураВозврата["ServiceProvider"];
		LoadFileНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура LoadFileНаСервере()
	
	Обработки.PullReportsFromServiceProvidersEmails.ОбработатьФайл(ИмяФайла, ServiceProvider);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Processed, "Account", Константы.RCATLMEmailUser.Получить(), ВидСравненияКомпоновкиДанных.Равно, , Истина);
	
КонецПроцедуры


