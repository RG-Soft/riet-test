
Процедура ЗагрузитьТаблицу() Экспорт
	
	ТЗПоляТаблицы = Новый ТаблицаЗначений;
	ТЗПоляТаблицы.Колонки.Добавить("ИмяПоля");
	ТЗПоляТаблицы.Колонки.Добавить("ПолеПоиска");
	ТЗПоляТаблицы.Колонки.Добавить("НомерКолонки");
	НоваяСтрока = ТЗПоляТаблицы.Добавить();
	НоваяСтрока.ИмяПоля = "BUYER_PART_NUMBER";
	НоваяСтрока.ПолеПоиска = "BUYER PART NUMBER";
	НоваяСтрока = ТЗПоляТаблицы.Добавить();
	НоваяСтрока.ИмяПоля = "BUYER_PART_DESCRIPTION";
	НоваяСтрока.ПолеПоиска = "BUYER PART DESCRIPTION";
	НоваяСтрока = ТЗПоляТаблицы.Добавить();
	НоваяСтрока.ИмяПоля = "SUPPLIER_PART_NUMBER";
	НоваяСтрока.ПолеПоиска = "SUPPLIER PART NUMBER";
	НоваяСтрока = ТЗПоляТаблицы.Добавить();
	НоваяСтрока.ИмяПоля = "SUPPLIER_PART_DESCRIPTION";
	НоваяСтрока.ПолеПоиска = "SUPPLIER PART DESCRIPTION";
	НоваяСтрока = ТЗПоляТаблицы.Добавить();
	НоваяСтрока.ИмяПоля = "SUPPLIER_NAME";
	НоваяСтрока.ПолеПоиска = "SUPPLIER NAME";
	КоличествоСтрок = 0;
	
	ТаблицаДанных = ЗагрузитьВСтруктуруНаСервере(ВоВременномХранилище,ТЗПоляТаблицы, КоличествоСтрок);
	
	Если ТаблицаДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из ТаблицаДанных Цикл
		МенеджерЗаписи = РегистрыСведений.rgsBPNSPNMappingData.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.BPN = Элемент.BUYER_PART_NUMBER;
		МенеджерЗаписи.BuyerPartDescription = Элемент.BUYER_PART_DESCRIPTION;
		МенеджерЗаписи.SPN = Элемент.SUPPLIER_PART_NUMBER;
		МенеджерЗаписи.SuppllerPartDescription = Элемент.SUPPLIER_PART_DESCRIPTION;
		МенеджерЗаписи.SuppllerName = Элемент.SUPPLIER_NAME;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Загружено " + КоличествоСтрок);
КонецПроцедуры


Процедура ЗагрузитьТаблицуВ2() Экспорт
	ТаблицаОшибок = Новый Массив;
	Если НаКлиенте Тогда  //ЭтотОбъект.РаботаНаКлиенте
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ВоВременномХранилище);
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".xlsx");
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
	Иначе
		ИмяВременногоФайла = ПутьФайл;
	КонецЕсли;	
	
	ТабДок = Новый ТабличныйДокумент;
	Попытка
		ТабДок.Прочитать(ИмяВременногоФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	Исключение		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;	
	РегистрыСведений.rgsBPNSPNMappingData.СоздатьНаборЗаписей().Записать();
	КолСтр = ТабДок.ВысотаТаблицы;
	Для НомерСтроки = 2 по КолСтр Цикл		
			МенеджерЗаписи = РегистрыСведений.rgsBPNSPNMappingData.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.BPN = Строка(ТабДок.Область(НомерСтроки,2,НомерСтроки,2).Текст); //F2
			МенеджерЗаписи.BuyerPartDescription = Строка(ТабДок.Область(НомерСтроки,3,НомерСтроки,3).Текст); //F3
			МенеджерЗаписи.SPN = Строка(ТабДок.Область(НомерСтроки,12,НомерСтроки,12).Текст); //F12
			МенеджерЗаписи.SuppllerPartDescription = Строка(ТабДок.Область(НомерСтроки,13,НомерСтроки,13).Текст); //F13
			МенеджерЗаписи.SuppllerName = Строка(ТабДок.Область(НомерСтроки,14,НомерСтроки,14).Текст); //F14
			МенеджерЗаписи.Записать();
	КонецЦикла;
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Загружено "+КолСтр);
КонецПроцедуры



/////////////////////////// ////////////////////////////////////

&НаСервере
Функция ЗагрузитьВСтруктуруНаСервере(АдресФайла, ТЗПоляТаблицы, КоличествоСтрок)
	
	ПолноеИмяXLSФайла = ПолучитьИмяВременногоФайла(".xlsx");
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
	ДвоичныеДанные.Записать(ПолноеИмяXLSФайла);
	
	ТекстОшибок = "";
	
	ТаблицаExcel = ПолучитьТаблицуExcel(ТекстОшибок, ПолноеИмяXLSФайла, ТЗПоляТаблицы, КоличествоСтрок);
	
	УдалитьФайлы(ПолноеИмяXLSФайла);
	
	Если ПустаяСтрока(ТекстОшибок) Тогда
		Возврат ТаблицаExcel;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("" + ТекстОшибок);
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуExcel(ТекстОшибок, ПолноеИмяФайла, ТЗПоляТаблицы, НомерСтроки)
	
	Connection = Новый COMОбъект("ADODB.Connection");
	СтрокаПодключения = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source=" + ПолноеИмяФайла + ";Extended Properties=""Excel 12.0;HDR=No;IMEX=1""";	
	
	Попытка 
		Connection.Open(СтрокаПодключения);	
	Исключение
		Попытка
			СтрокаПодключения = "Provider=Microsoft.Jet.OLEDB.4.0;Data Source=" + ПолноеИмяФайла + ";Extended Properties=""Excel 8.0;HDR=No;IMEX=1""";
			Connection.Open(СтрокаПодключения);
		Исключение
			ТекстОшибок = ТекстОшибок + ОписаниеОшибки();
		КонецПопытки;
	КонецПопытки;
	
	rs = Новый COMObject("ADODB.RecordSet");
	rs.ActiveConnection = Connection;
	rs = Connection.OpenSchema(20);
	
	МассивЛистов = Новый Массив;
	Лист = Неопределено;
	
	Пока rs.EOF() = 0 Цикл
		
		Если ЗначениеЗаполнено(Лист) И СтрНайти(rs.Fields("TABLE_NAME").Value, Лист) > 0 Тогда
			rs.MoveNext();
			Продолжить;
		КонецЕсли;
		
		Лист = rs.Fields("TABLE_NAME").Value;
		МассивЛистов.Добавить(Лист);
		
		rs.MoveNext();
		
	КонецЦикла;  
	
	ТаблицаExcel = Новый ТаблицаЗначений();
	ТаблицаExcel.Колонки.Добавить("НомерСтрокиФайла", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0)),"НомерСтрокиФайла");
	
	Для Каждого ЛистЭксель из МассивЛистов Цикл 
		
		sqlString = "select * from [" + ЛистЭксель + "]";
		rs.Close();
		rs.Open(sqlString);
		
		rs.MoveFirst();
				
		НомерСтроки = 0;
		Пока rs.EOF = 0 Цикл
			
			НомерСтроки = НомерСтроки + 1;
			
			Если НомерСтроки = 1 Тогда 
				
				ПолучитьСтруктуруИменИНомеровКолонок(rs, ТЗПоляТаблицы, ТаблицаExcel, ТекстОшибок);
				
				Если Не ПустаяСтрока(ТекстОшибок) Тогда 
					Прервать;
				КонецЕсли;
				
				rs.MoveNext();
				Продолжить;
				
			КонецЕсли;
			
			СвойстваСтруктуры = "";
			Для Каждого ПолеКолонки Из ТЗПоляТаблицы Цикл
				СвойстваСтруктуры = ?(ЗначениеЗаполнено(СвойстваСтруктуры),СвойстваСтруктуры+",","")+ПолеКолонки.ИмяПоля;
			КонецЦикла;
			
			СтруктураЗначенийСтроки = Новый Структура(СвойстваСтруктуры);
			
			//добавляем значение каждой ячейки файла в структуру значений
			Для Каждого ПолеКолонки из ТЗПоляТаблицы Цикл 
				
				ЗначениеЯчейки = rs.Fields(ПолеКолонки.НомерКолонки-1).Value;
				СтруктураЗначенийСтроки[ПолеКолонки.ИмяПоля] = СокрЛП(ЗначениеЯчейки);
				
			КонецЦикла;
			
			//добавляем новую структуру и пытаемся заполнить	
			Попытка
				
				НоваяСтрокаТаблицы = ТаблицаExcel.Добавить();
				
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицы, СтруктураЗначенийСтроки, СвойстваСтруктуры);
				
				НоваяСтрокаТаблицы.НомерСтрокиФайла = НомерСтроки;
				
			Исключение
				ТекстОшибок = ТекстОшибок + "
				|не удалось прочитать данные в строке №" + НомерСтроки + "'!";
			КонецПопытки;
			
			rs.MoveNext();
			
		КонецЦикла;
		
		//Прервать;
		
	КонецЦикла;
	
	rs.Close();
	Connection.Close();
	
	Возврат ТаблицаExcel;
	
КонецФункции

&НаСервере
Функция ПолучитьСтруктуруИменИНомеровКолонок(rs, ТЗПоляТаблицы, ТаблицаExcel, ТекстОшибок)
	
	НомерКолонки = 1;
	Для Каждого Field из rs.Fields Цикл 
		
		ТекстЯчейки = СокрЛП(Field.Value);
		Если НЕ ЗначениеЗаполнено(ТекстЯчейки) Тогда
			Прервать;
		КонецЕсли;
		
		
		Для Каждого ПолеКолонки Из ТЗПоляТаблицы Цикл
		
			Если Найти(ТекстЯчейки, ПолеКолонки.ПолеПоиска) Тогда
				ПолеКолонки.НомерКолонки = НомерКолонки;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		НомерКолонки = НомерКолонки + 1;
		
	КонецЦикла; 
	
	Для Каждого ПолеКолонки Из ТЗПоляТаблицы Цикл
		
		Если ПолеКолонки.НомерКолонки = Неопределено Тогда
			ТекстОшибок = ТекстОшибок + "
			|необходимо проверить наличие колонки с данными '" + СтрЗаменить(ПолеКолонки.ИмяПоля, "_", " ") + "'!";
		Иначе
			Если ТаблицаExcel.Колонки.Найти(ПолеКолонки.ИмяПоля) = Неопределено Тогда
				ТаблицаExcel.Колонки.Добавить(ПолеКолонки.ИмяПоля, , ПолеКолонки.ИмяПоля);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

КонецФункции
