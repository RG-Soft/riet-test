
//////////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВыбратьФайл();
	
КонецПроцедуры


//////////////////////////////////////////////////////////////////////////////////////
// ВЫБОР ФАЙЛА

&НаКлиенте
Процедура FullPathНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьФайл();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайл()
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр						= "Файлы xlsb (*.xlsb)|*.xlsb|Файлы xls (*.xls)|*.xls|Файлы xlsx (*.xlsx)|*.xlsx";
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла	= Истина;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
		FullPath = ДиалогВыбораФайла.ПолноеИмяФайла;
				
	КонецЕсли;
		
КонецПроцедуры


//////////////////////////////////////////////////////////////////////////////////////
// ЗАГРУЗКА

#Если не ВебКлиент Тогда
	
&НаКлиенте
Процедура Load(Команда)
	
	Если НЕ ЗначениеЗаполнено(PeriodOfReport) Тогда
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Period of report is empty!",
			, , "PeriodOfReport");
			Возврат;
		
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(FullPath) Тогда
		
		ВыбратьФайл();
		
		Если НЕ ЗначениеЗаполнено(FullPath) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Select file!",
				, "Объект", "FullPath");
				Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	 
	Состояние("File loading...");
	
	Если НЕ РГСофтКлиентСервер.ФайлДоступенДляЗагрузки(FullPath) Тогда
		Возврат;
	КонецЕсли;
	   	
	Файл = Новый Файл(FullPath);
	ПолноеИмяXLSФайла = Неопределено;
	РасширениеФайла = НРег(Файл.Расширение);
	
	Если РасширениеФайла = ".xls" или РасширениеФайла = ".xlsx" или РасширениеФайла = ".xlsb" Тогда
		
		ПолноеИмяXLSФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);
		КопироватьФайл(FullPath, ПолноеИмяXLSФайла);
		
	КонецЕсли; 
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяXLSФайла);
	АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ЭтаФорма.УникальныйИдентификатор);
	  	
	ЗагрузитьДанныеНаСервере(АдресФайла, Файл.ИмяБезРасширения, РасширениеФайла);
	
	УдалитьФайлы(ПолноеИмяXLSФайла); 
	
		
КонецПроцедуры

#КонецЕсли

&НаСервере
Процедура ЗагрузитьДанныеНаСервере(АдресФайла, ИмяБезРасширения, РасширениеФайла)
	
	ПолноеИмяXLSФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);

	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
	ДвоичныеДанные.Записать(ПолноеИмяXLSФайла);
	
	//Обработки.GRReportLoading.ЗагрузитьДанныеИзФайла(ПолноеИмяXLSФайла);
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(ПолноеИмяXLSФайла);
	Задание = ФоновыеЗадания.Выполнить("ImportExportСервер.ЗагрузитьGRReportИзФайла", МассивПараметров, , "Загрузка GR Report за период "+PeriodOfReport);
	
	Попытка
		Задание.ОжидатьЗавершения();
	Исключение
		Сообщить(Задание.ИнформацияОбОшибке);
		Сообщить("Failed to load file.");
		Возврат;
	КонецПопытки;
	
	МассивСообщений = Задание.ПолучитьСообщенияПользователю();
	Для Каждого Сообщение из МассивСообщений Цикл 
		Сообщить(Сообщение.Текст);
	КонецЦикла;
	
	Если МассивСообщений.Количество() > 0 Тогда 
		Сообщить("Failed to load file.");
		Возврат;
	КонецЕсли;
	
	Если ДлительныеОперации.ЗаданиеВыполнено(Задание.УникальныйИдентификатор) Тогда 
		
		УстановитьПривилегированныйРежим(Истина);
		
		// добавим новую запись
		МенеджерЗаписи = РегистрыСведений.GRReportsLoadingProcess.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = PeriodOfReport;
		МенеджерЗаписи.GRReportName = ИмяБезРасширения;
		МенеджерЗаписи.UploadDate = ТекущаяДата(); 
		МенеджерЗаписи.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);

		Сообщить("File was successfully loaded.");
		
	иначе
		
		Сообщить("Failed to load file.");
		
	КонецЕсли;
	
КонецПроцедуры  

