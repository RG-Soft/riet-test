
&НаКлиенте
Процедура PullDataFromTMS(Команда)
	
	PullDataFromTMSНаСервере(Период.ДатаНачала, Период.ДатаОкончания);
	Элементы.OBQueue.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура PullDataFromTMSНаСервере(ДатаНачала, ДатаОкончания)
	
	Таймаут = 2;
	Обработки.PullWONoFromTMS.PullDataFromTMS(Таймаут, ДатаНачала, ДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура FillQueue(Команда)
	
	FillQueueНаСервере();
	Элементы.OBQueue.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура FillQueueНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	TripDomesticOB.OBNo
		|ИЗ
		|	Документ.Trip.DomesticOB КАК TripDomesticOB
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.OBNoQueue КАК OBNoQueue
		|		ПО TripDomesticOB.OBNo = OBNoQueue.OBNo
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.OBNoAndWONo КАК OBNoAndWONo
		|		ПО TripDomesticOB.OBNo = OBNoAndWONo.OBNo
		|ГДЕ
		|	OBNoQueue.OBNo ЕСТЬ NULL 
		|			И OBNoAndWONo.OBNo ЕСТЬ NULL ";

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	НЗ = РегистрыСведений.OBNoQueue.СоздатьНаборЗаписей();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НЗ.Очистить();
		НЗ.Отбор.OBNo.Установить(ВыборкаДетальныеЗаписи.OBNo);
		Запись = НЗ.Добавить();
		Запись.OBNo = ВыборкаДетальныеЗаписи.OBNo;
		НЗ.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ClearQueue(Команда)
	
	ClearQueueНаСервере();
	Элементы.OBQueue.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ClearQueueНаСервере()
	
	НЗ = РегистрыСведений.OBNoQueue.СоздатьНаборЗаписей();
	НЗ.Записать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура UpdateQueueStatistics(Команда)
	
	UpdateQueueStatisticsНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура UpdateQueueStatisticsНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(TripDomesticOB.Ссылка.Дата, МЕСЯЦ) КАК Месяц,
	|	TripDomesticOB.Ссылка.Дата КАК Дата,
	|	OBNoQueue.OBNo КАК OBNo,
	|	TripDomesticOB.Ссылка КАК Trip
	|ИЗ
	|	РегистрСведений.OBNoQueue КАК OBNoQueue
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Trip.DomesticOB КАК TripDomesticOB
	|		ПО OBNoQueue.OBNo = TripDomesticOB.OBNo
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ OBNo),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Trip)
	|ПО
	|	Месяц";
	
	ДЗ = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ЗначениеВРеквизитФормы(ДЗ, "QueueStatistics");
	
КонецПроцедуры

&НаКлиенте
Процедура UpdateResultStatistics(Команда)
	
	UpdateResultStatisticsНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура UpdateResultStatisticsНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(TripDomesticOB.Ссылка.Дата, МЕСЯЦ) КАК Месяц,
	|	TripDomesticOB.Ссылка.Дата КАК Дата,
	|	OBNoAndWONo.OBNo КАК OBNo,
	|	TripDomesticOB.Ссылка КАК Trip
	|ИЗ
	|	РегистрСведений.OBNoAndWONo КАК OBNoAndWONo
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Trip.DomesticOB КАК TripDomesticOB
	|		ПО OBNoAndWONo.OBNo = TripDomesticOB.OBNo
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ OBNo),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Trip)
	|ПО
	|	Месяц";
	
	ДЗ = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ЗначениеВРеквизитФормы(ДЗ, "ResultStatistics");
	
КонецПроцедуры



