//{ ПЕРЕМЕННЫЕ МОДУЛЯ
	
	&НаКлиенте
	Перем МассивСтруктурКонтент;
	&НаКлиенте
	Перем МаксимальноеКоличествоЗаданий;
	
//} ПЕРЕМЕННЫЕ МОДУЛЯ

#Область ПЕРМЕННЫЕ_ПЛАТФОРМЫ

&НаКлиенте
Перем Платформа Экспорт, Манифест Экспорт;

&НаКлиенте
Перем НомерИтерацииВызоваМодуля;

&НаСервере
Перем ОбработкаОбъект;

#КонецОбласти

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ПЛАТФОРМЫ

&НаКлиенте
Функция МетодКлиента(ИмяМодуля= "", ИмяМетода, 
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL)
	
	Возврат  Платформа.МетодКлиента(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4, 
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция МетодСервера(Знач ИмяМодуля= "", Знач ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL)
	
	Возврат ОбработкаОбъект().МетодСервера(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4, 
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект()
	
	Если ОбработкаОбъект = Неопределено Тогда
		ОбработкаОбъект= РеквизитФормыВЗначение("Объект");
	КонецЕсли;
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Процедура Инициализировать(ИмяМодуля) Экспорт
	
	Если НомерИтерацииВызоваМодуля = Неопределено Тогда
		НомерИтерацииВызоваМодуля= 0;
	КонецЕсли;
	
	НомерИтерацииВызоваМодуля= НомерИтерацииВызоваМодуля + 1;
	
	Если Манифест = Неопределено Тогда
		Платформа.ЗаполнитьМанифест(ЭтаФорма, ИмяМодуля);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПеременные() Экспорт
	
	// Разрывается циклическая ссылка, для того чтобы исключить утечку памяти.
	// Модуль может быть вызван повторно во вложенных методах,
	// поэтому очищаем переменные, ТОЛЬКО если это начальная итерация вызова модуля.
	
	НомерИтерацииВызоваМодуля= НомерИтерацииВызоваМодуля - 1;
	
	Если НомерИтерацииВызоваМодуля = 0 Тогда
		Платформа= 					  Неопределено;
		Объект.ПараметрыКлиентСервер= Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МАНИФЕСТ

// Все функции и процедуры тела модуля должны быть экспортными и добавлены в соответствующую структуру!

&НаКлиенте
Функция ФункцииМодуля() Экспорт
	
	СтруктураМетодов= Новый Структура;
	
	// Шаблон: Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, <Имя функции>, <Параметры строкой>, <Вариант кэширования>, <Переопределение>);
	//Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПримерФункции", "ОбязательныйПараметр, НеобязательныйПараметр= Неопределено", "НеИспользовать", Истина);
	//Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПримерФункции", "ОбязательныйПараметр, НеобязательныйПараметр= Неопределено", "НаВремяВыполнения", Истина);
	//Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПримерФункции", "ОбязательныйПараметр, НеобязательныйПараметр= Неопределено", "НаВремяСеанса", Истина);
	//...
	
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "СформироватьПредставлениеКнопкиСоздатьДокументВ1С", "Документ1С, ЭДОбъект, МассивСсылокРНК, ПодходящаяСФ", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеПродавца", "ЭДОбъект, НаименованиеЯщика=неопределено", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеПокупателя", "ЭДОбъект, НаименованиеЯщика=неопределено", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеПодразделения", "ЭДОбъект", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеЭД", "ЭДОбъект", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьСтруктуруПредставленияЭД", "док", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеТипаЭД", "ЭДОбъект", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредсталениеНомераЭД", "ЭДОбъект", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "РасшифровкаСтатуса", "СтатусСтрокой, СтатусАннулированияСтрокой = """"", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеСтатуса", "ЭДОбъект", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеСтатусаРоуминг", "ЭДОбъект", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеСуммы", "ЭДОбъект", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеСуммыНДС", "ЭДОбъект", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеПериодаДД", "ДатаНачала, Знач ДатаОкончания", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеТипаРезолюции", "ResolutionType", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "СформироватьHTMLПредставлениеОшибок", "Document, Организация, МассивОшибок", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "СформироватьHTMLПредставлениеРезолюций", "Document, Организация", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеAuthor", "ResolutionStatus", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеTargetUser", "ResolutionStatus", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПредставлениеСтатусаСогласования", "Document", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьФИОСИнициалами", "ФИО", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "НайтиСтрокиВМассивеСтруктур", "МассивСтруктур, ПараметрыОтбора", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "BoxID_2_Организация_Форма", "BoxID, DepartmentKpp = """"", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "CounteragentBoxID_2_Контрагент_Форма", "CounteragentID, BoxID = Неопределено", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "DocumentID_2_Документ_Форма", "DocumentID, BoxID", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьСправа", "Стр, Маркер", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "КраткоеСообщениеОбОшибке", "текстОшибки", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "СформироватьТекстОшибкиДиадок", "ТекстОшибки", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьОсновнуюФормуОбъекта", "Объект1С", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьОписаниеФормы", "Документ1С", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ВернутьМассивВыбранныхФайлов", "МножественныйВыбор =  Ложь", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьИмяФормыДокумента", "Document", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьDocumentID_2_Документ", "DocumentID, BoxID", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ЭтоНеформализованныйДокументБезМетаданных", "Document", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ЭтоФормализованныйДокумент", "Document", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "КлассификаторыДляДиадок", "", "НаВремяСеанса", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "НайтиОрганизациюВИерархииОрганизацийDiadoc", "BoxID, DepartmentId", "НаВремяСеанса", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "НайтиОрганизациюВИерархииОрганизацийDiadocРекурсия", "BoxID, СтруктураПодразделения", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ТекстИнформацииОНовомФормате", "", "НаВремяСеанса", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "СписокОбработокДереваКонфигурации", "", "НаВремяСеанса", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ДатаГоризонта", "", "НаВремяСеанса", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ИдентификаторСвойства", "ИмяСвойства", "НаВремяСеанса", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПроверитьФорматДатыПрото", "ЗначениеПоля, ПредставлениеПоля, ОбязательноеЗаполнение= Ложь", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПроверитьМаксимальнуюДлинуПрото", "ЗначениеПоля, ПредставлениеПоля, МаксимальнаяДлина, род=""м"", ОбязательноеЗаполнение = ложь", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПроверитьТитулПолучателяТорг12", "СтруктураДляТитула", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьСписокФильтровДляДокументовДиадок", "Направление, ФильтрПоСостояниюДокументооборота, ФильтрПотипамДокументов", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьФилиаловDepartmentId", "", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьDepartmentByKpp", "Organization, DepartmentKpp", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ВернутьМассивDepartmentId", "стрЯщик, Box, ПодразделениеID, ВключатьПодчиненныеПодазделения", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьСтруктуруЗадачи", "НаименованиеЯщика, Организация, AsyncTask", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьМассивАсинхронныхЗадач", "СписокФильтров, ДатаНачала, ДатаОкончания, СписокЯщиков, counterAgentBoxId, ПараметрыВыборки", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьРезультатВыполненияЗадач", "МассивЗадач, ПараметрыВыборки", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ВернутьВыборкуРНКИСчетовФактурДиадок", "ДатаНачала, ДатаОкончания, Организация = Неопределено, counterAgentBoxId = """", ПараметрыВыборки", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьЗначениеКонтентаКэш", "BoxID, DocumentId", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьЯщикДиадокДляОрганизации", "Организация, ОтпечатокСертификата=неопределено, BoxID=неопределено, Connection = неопределено", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "НайтиСтрокиКонтекстДиадокаПоОтбору", "СтруктураОтбор", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "РазвернутьGUID", "СтрНач", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьURLНаДокументДиадока", "BoxId, DocumentId", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьПутьКWEBСерверу", "", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ОбновитьДанныеДокумента", "ДокументСсылка, DocumentDate, DocumentNumber", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьПараметрыПодписанияТ12вДиадок", "ФИОПодписанта", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ОтправитьНаОбработку", "ЭДОбъект, ПараметрыОтправкиНаСогласование", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьКраткоеИмяФайла", "ПолноеИмяФайла", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ОтправитьПринятьПриглашениеКонтрагенту", "Organization, CounteragentId, ИНН, Комментарий, ИмяФайла = """", КонтрагентСсылка = Неопределено", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадокСервер", "МассивСсылок, OrganizationId", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадок", "ОбъектДиадок", "НеИспользовать", Истина);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "НайтиПодходящийСчетФактуруИзРНК", "Знач МассивСсылокРНК, Знач ИдентификаторДокументаВДиадок", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьПодходящуюСФ", "МассивСсылокРНК", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьМассивТиповДокументов", "ФильтрПоТипамДокументов, ВидТипаДокумента", "НеИспользовать", Ложь);
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ВариантыИспользованияПроксиСервера");
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ЗначениеРеквизитаОбъекта", "Ссылка, ИмяРеквизита");
	Платформа.ДобавитьФункциюВМанифест(СтруктураМетодов, "ПолучитьНастройкиОрганизации", "Организация");
	
	Возврат СтруктураМетодов;
	
КонецФункции

&НаКлиенте
Функция ПроцедурыМодуля() Экспорт
	
	СтруктураМетодов= Новый Структура;
	
	// Шаблон: Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, <Имя процедуры>, <Параметры строкой>, <Переопределение>);
	//Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ПримерПроцедуры", "ОбязательныйПараметр, НеобязательныйПараметр= Неопределено", Истина);
	//...
	
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "Инициализировать", "ИмяМодуля", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ОчиститьПеременные", "", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ЗаполнитьМассивСтруктурКонтент", "", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ЗаполнитьСтрокуТаблицыДокумента", "док, строкаДок, НаименованиеЯщика=неопределено, заполнятьПервичныйДокумент = истина, ПолучатьРезолюции =  Ложь", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ПоказатьДокументВДиадоке", "OrganizationId, DocumentId", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ОбработчикСозданиеДокумента", "ДокументССылка, ЭДОбъект", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ПодписатьВДиадоке", "Document, Подписать, BoxID, DocumentID, ТекстОтказа = """", СтруктураДляТитула = Неопределено", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ВыбратьПараметрыПолучателяПриОтправкеНаСогласование", "ОбъектВладелецОбработчика, ТекущиеДанные, ResolutionRequestType, Режим", Истина);
	// { RGS TChubarova 16.05.2016 1:52:21 - TCS-0002911
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ВыбратьПараметрыПолучателяПриОтправкеНаСогласованиеНеформализ", "ОбъектВладелецОбработчика, ОрганизацияОтправитель, ResolutionRequestType, Режим", Истина);
	// } RGS TChubarova 16.05.2016 1:53:29 - TCS-0002911
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ОтправитьЭДОбъектНаСогласование", "ОбъектВладелецОбработчика, Организация, ResolutionRequestType, Режим", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ПодтверждениеВыполненияОперации", "ИмяОбработчика, ВладелецОбработчика, ПараметрыОбработчика= Неопределено", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "СократитьСписокВыбораДоВыбранногоЗначения", "Элемент, ВыбранноеЗначение", Ложь);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ЭлементВРежимеВыбораИзСпискаПриИзменении", "Форма, Элемент, ВыбранноеЗначение", Ложь);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "УстановитьЗначениеПараметровСвойствОтправки", "Организация, ПараметрыСвойствОтправки", Истина);
	Платформа.ДобавитьПроцедуруВМанифест(СтруктураМетодов, "ЗаписатьВЛог", "ИдентификаторСобытия, Комментарий = "", ДокументСсылка = Неопределено", Ложь);
	
	Возврат СтруктураМетодов;
	
КонецФункции

#КонецОбласти

//{ УПРАВЛЕНИЕ ФОРМОЙ

	&НаКлиенте
	Функция СформироватьПредставлениеКнопкиСоздатьДокументВ1С(Документ1С, ЭДОбъект, МассивСсылокРНК, ПодходящаяСФ) Экспорт 
		
		ПредставлениеКнопки=	"";
		
		Если НЕ ЗначениеЗаполнено(Документ1С) Тогда
			
			Если ЭДОбъект.Direction = "Inbound" Тогда
				Если ЛЕВ(ЭДОбъект.Type, 7) = "Invoice"  Тогда
					Если ЗначениеЗаполнено(ПодходящаяСФ) Тогда
						ПредставлениеКнопки=	"Сопоставить с СФ в 1С";
					ИначеЕсли МассивСсылокРНК.Количество() > 0 И (ЭДОбъект.Type = "Invoice") Тогда
						ПредставлениеКнопки=	"Создать на основании накладной";
					Иначе
						ПредставлениеКнопки=	"Создать СФ в 1С";
					КонецЕсли;
				Иначе
					ПредставлениеКнопки=	"Создать документ в 1С";
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат ПредставлениеКнопки;
		
	КонецФункции
	
//} УПРАВЛЕНИЕ ФОРМОЙ

//{ ПРЕДСТАВЛЕНИЯ
	
	&НаКлиенте
	Функция ПредставлениеПродавца(ЭДОбъект, НаименованиеЯщика=неопределено) Экспорт
		Если ЭДОбъект.Direction = "Outbound"  тогда 
			Возврат ?(НаименованиеЯщика=неопределено, ЭДОбъект.Organization.name, НаименованиеЯщика);
		Иначе 
			Возврат ЭДОбъект.CounterAgent.name;
		КонецЕсли;
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеПокупателя(ЭДОбъект, НаименованиеЯщика=неопределено) Экспорт
		Если ЭДОбъект.Direction = "Outbound"  тогда 
			Возврат ЭДОбъект.CounterAgent.name;
		Иначе 
			Возврат ?(НаименованиеЯщика=неопределено, ЭДОбъект.Organization.name, НаименованиеЯщика);
		КонецЕсли;
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеПодразделения(ЭДОбъект) Экспорт
		
		Если ЭДОбъект.Department = Неопределено Тогда
			Возврат "";
		Иначе
			Возврат  ЭДОбъект.Department.Name;
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеЭД(ЭДОбъект) Экспорт
		
		Если ЭДОбъект.Type = "Invoice" Тогда
			ПредставлениеЭД=	"Счет-фактура №" + ЭДОбъект.DocumentNumber + " от " + Формат(ЭДОбъект.DocumentDate, "ДФ=dd.MM.yyyy");
		ИначеЕсли ЭДОбъект.Type = "InvoiceRevision" Тогда
			ПредставлениеЭД=	"Исправление №" + ЭДОбъект.DocumentNumber + " от " + ЭДОбъект.DocumentDate + " счета-фактуры №" + ЭДОбъект.OriginalDocumentNumber + " от " + Формат(ЭДОбъект.OriginalDocumentDate, "ДФ=dd.MM.yyyy");
		ИначеЕсли ЭДОбъект.Type = "InvoiceCorrection" Тогда
			ПредставлениеЭД=	"Корректировочный счет-фактура №" + ЭДОбъект.DocumentNumber + " от " + ЭДОбъект.DocumentDate;
		ИначеЕсли ЭДОбъект.Type = "InvoiceCorrectionRevision" Тогда
			ПредставлениеЭД=	"Исправление №" + ЭДОбъект.DocumentNumber + " от " + ЭДОбъект.DocumentDate 
			+ " корректировочного счета-фактуры №" + + ЭДОбъект.OriginalInvoiceCorrectionNumber + " от " + Формат(ЭДОбъект.OriginalInvoiceCorrectionDate, "ДФ=dd.MM.yyyy");
		ИначеЕсли Найти(ЭДОбъект.Type, "Torg12") > 0 Тогда
			ПредставлениеЭД=	"Торг-12 №" + ЭДОбъект.DocumentNumber + " от " + Формат(ЭДОбъект.DocumentDate, "ДФ=dd.MM.yyyy");
		ИначеЕсли Найти(ЭДОбъект.Type, "AcceptanceCertificate") > 0 Тогда
			ПредставлениеЭД=	"Акт №" + ЭДОбъект.DocumentNumber + " от " + Формат(ЭДОбъект.DocumentDate, "ДФ=dd.MM.yyyy");
		ИначеЕсли ЭДОбъект.Type = "NonformalizedProforma" Тогда
			ПредставлениеЭД=	"Счет на оплату №" + ЭДОбъект.DocumentNumber + " от " + Формат(ЭДОбъект.DocumentDate, "ДФ=dd.MM.yyyy");
		
		ИначеЕсли ЭДОбъект.Type="Nonformalized" ИЛИ ЭДОбъект.Type="ReconciliationAct" ИЛИ ЭДОбъект.Type="CertificateRegistry" ИЛИ ЭДОбъект.Type="ServiceDetails" ИЛИ ЭДОбъект.Type="Contract" 
					ИЛИ ЭДОбъект.Type="PriceListAgreement" ИЛИ ЭДОбъект.Type="TrustConnectionRequest" Тогда
			ПредставлениеЭД=	ЭДОбъект.FileName;
		Иначе
			Возврат "Тип документа не определен";
		КонецЕсли;
		
		Если ЭДОбъект.IsTest Тогда
			ПредставлениеЭД= ПредставлениеЭД + " (тестовый)";
		КонецЕсли;
		
		Возврат ПредставлениеЭД;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьСтруктуруПредставленияЭД(док) Экспорт
		
		Box = Док.Organization;
		
		СтруктураЭД = Новый Структура;
		
		СтруктураЭД.Вставить("ЭДОбъект", док);
		СтруктураЭД.Вставить("ТипДокумента", ПредставлениеТипаЭД(док));
		СтруктураЭД.Вставить("BoxID", док.OrganizationID);
		СтруктураЭД.Вставить("DocumentId", док.DocumentId);
		СтруктураЭД.Вставить("Организация", BoxID_2_Организация_Форма(док.OrganizationID));
		СтруктураЭД.Вставить("Контрагент", CounteragentBoxID_2_Контрагент_Форма(док.CounterAgent.Id, док.OrganizationID));
		СтруктураЭД.Вставить("Дата", док.DocumentDate);
		СтруктураЭД.Вставить("Номер", док.DocumentNumber);
		Если док.Direction = "Outbound"  тогда 
			СтруктураЭД.Вставить("Получатель", док.CounterAgent.name);
			СтруктураЭД.Вставить("Отправитель", Box.name);
		Иначе 
			СтруктураЭД.Вставить("Отправитель", док.CounterAgent.name);
			СтруктураЭД.Вставить("Получатель", Box.name);
		КонецЕсли;	
		СтруктураЭД.Вставить("Сумма", ПредставлениеСуммы(док));
		
		//СтруктураЭД.Вставить("ПервичныйДокумент", DocumentID_2_Документ_Форма(док.DocumentId, док.OrganizationID));
		
		Если док.Type = "Invoice" Тогда
			СтруктураЭД.Вставить("ДатаУчета", док.ConfirmationDate);
		Иначе
			СтруктураЭД.Вставить("ДатаУчета", док.Timestamp);
		КонецЕсли;
		
		Возврат СтруктураЭД;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеТипаЭД(ЭДОбъект) Экспорт
		тип = "";
		Если ЭДОбъект.Type = "Invoice" Тогда
			тип = "Счет-фактура";
		ИначеЕсли ЭДОбъект.Type = "InvoiceRevision" Тогда
			тип = "Исправление СФ";
		ИначеЕсли ЭДОбъект.Type = "InvoiceCorrection" Тогда
			тип = "Корректировочный СФ";
		ИначеЕсли ЭДОбъект.Type = "InvoiceCorrectionRevision" Тогда
			тип = "Исправление КСФ";
		ИначеЕсли найти(ЭДОбъект.Type , "AcceptanceCertificate")>0 тогда 
			тип = "Акт";
		ИначеЕсли найти(ЭДОбъект.Type , "NonformalizedProforma")>0 тогда 
			тип = "Счет на оплату";
		ИначеЕсли ЭДОбъект.Type = "Nonformalized" Тогда
			тип = ЭДОбъект.fileName;	
		ИначеЕсли ЭДОбъект.Type = "ReconciliationAct" Тогда
			тип = "Акт сверки";
		ИначеЕсли ЭДОбъект.Type = "CertificateRegistry" Тогда
			тип = "Реестр сертификатов";
		ИначеЕсли ЭДОбъект.Type = "ServiceDetails" Тогда
			тип = "Детализация";
		ИначеЕсли ЭДОбъект.Type = "Contract" Тогда
			тип = "Договор";
		ИначеЕсли ЭДОбъект.Type = "PriceListAgreement" Тогда
			тип = "Протокол согласования цены";
		ИначеЕсли ЭДОбъект.Type = "TrustConnectionRequest" Тогда
			тип = "Приглашение к ЭДО";
		Иначе 	
			тип = "Торг-12";
		КонецЕсли;
		
		Если ЭДОбъект.IsTest Тогда
			тип = тип + " (тестовый)";
		КонецЕсли;
		
		Возврат тип;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредсталениеНомераЭД(ЭДОбъект) Экспорт
		Возврат ЭДОбъект.DocumentNumber;
		
		//Если ЭДОбъект.Type = "InvoiceRevision" Тогда
		//	Возврат ЭДОбъект.DocumentNumber + " на СФ №" + ЭДОбъект.OriginalDocumentNumber + " от " + Формат(ЭДОбъект.OriginalDocumentDate, "ДЛФ=Д");
		//ИначеЕсли ЭДОбъект.Type = "InvoiceCorrection" Тогда
		//	Возврат ЭДОбъект.DocumentNumber;
		//ИначеЕсли ЭДОбъект.Type = "InvoiceCorrectionRevision" Тогда
		//	Возврат ЭДОбъект.DocumentNumber + " на КСФ №" + ЭДОбъект.OriginalInvoiceCorrectionNumber + " от " + Формат(ЭДОбъект.OriginalInvoiceCorrectionDate, "ДЛФ=Д");
		//Иначе
		//	Возврат ЭДОбъект.DocumentNumber;
		//КонецЕсли;
	КонецФункции
	
	&НаКлиенте
	функция РасшифровкаСтатуса(СтатусСтрокой, СтатусАннулированияСтрокой = "")
		
		Если СтатусАннулированияСтрокой = "RevocationAccepted" Тогда
			РасшифровкаСтатуса=	"Аннулирован";
		Иначе
			Если СтатусСтрокой = "OutboundWaitingForInvoiceReceipt" Тогда
				РасшифровкаСтатуса=	"Ожидается извещение от покупателя"
			ИначеЕсли СтатусСтрокой = "OutboundNotFinished" или статусСтрокой = "InboundNotFinished" Тогда
				РасшифровкаСтатуса=	"Документооборот не завершен"
			ИначеЕсли СтатусСтрокой = "OutboundFinished" или статусСтрокой = "InboundFinished" 
				ИЛИ СтатусСтрокой = "Outbound" ИЛИ СтатусСтрокой = "Inbound" Тогда
				РасшифровкаСтатуса=	"Документооборот завершен"
			ИначеЕсли СтатусСтрокой = "OutboundWaitingForRecipientSignature" ИЛИ СтатусСтрокой = "InboundWaitingForRecipientSignature" Тогда
				РасшифровкаСтатуса=	"Требуется подпись";
			ИначеЕсли СтатусСтрокой = "OutboundWithRecipientSignature" ИЛИ СтатусСтрокой = "InboundWithRecipientSignature " ИЛИ СтатусСтрокой = "InboundWithRecipientSignature" Тогда   
				РасшифровкаСтатуса=	"Подписан";                                                                                                 
			ИначеЕсли СтатусСтрокой = "OutboundRecipientSignatureRequestRejected" ИЛИ СтатусСтрокой = "InboundRecipientSignatureRequestRejected" Тогда
				РасшифровкаСтатуса=	"Отказано в подписи";
			ИначеЕсли СтатусСтрокой = "OutboundWaitingForSenderSignature" Тогда
				РасшифровкаСтатуса=	"Требуется подписать и отправить через сайт " + Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж;
			ИначеЕсли СтатусСтрокой = "InboundInvalidRecipientSignature" ИЛИ СтатусСтрокой = "OutboundInvalidSenderSignature" Тогда
				РасшифровкаСтатуса=	"Ошибка подписи";
			ИначеЕсли СтатусСтрокой = "InboundNoRecipientSignatureRequest" Тогда
				РасшифровкаСтатуса=	"Получен";
			ИначеЕсли СтатусСтрокой = "OutboundNoRecipientSignatureRequest" Тогда
				РасшифровкаСтатуса=	"Доставлен";
			Иначе
				РасшифровкаСтатуса=	"";
			КонецЕсли;
		КонецЕсли;
		
		Если СтатусАннулированияСтрокой = "RevocationIsRequestedByMe" Тогда
			РасшифровкаСтатусаАннулирования=	"Ожидается аннулирование";
		ИначеЕсли СтатусАннулированияСтрокой = "RequestsMyRevocation" Тогда
			РасшифровкаСтатусаАннулирования=	"Требуется аннулирование";
		ИначеЕсли СтатусАннулированияСтрокой = "RevocationRejected" Тогда
			РасшифровкаСтатусаАннулирования=	"Отказано в аннулировании";
		Иначе
			РасшифровкаСтатусаАннулирования=	"";
		КонецЕсли;
		
		Возврат РасшифровкаСтатуса + ?(ПустаяСтрока(РасшифровкаСтатусаАннулирования), "", ". " + РасшифровкаСтатусаАннулирования);
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСтатуса(ЭДОбъект) Экспорт
		
		Статус = РасшифровкаСтатуса(ЭДОбъект.Status, ЭДОбъект.RevocationStatus);
		
		Если Лев(ЭДОбъект.Type, 7) = "Invoice" Тогда
			Если ЭДОбъект.Corrected Тогда
				Статус=	Статус + ". Откорректирован";
			ИначеЕсли ЭДОбъект.Revised Тогда
				Статус=	Статус + ". Исправлен";
			ИначеЕсли ЭДОбъект.AmendmentRequested Тогда
				Статус=	Статус + ". Требует уточнения";
			КонецЕсли;
		КонецЕсли;
		
		Возврат Статус;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСтатусаРоуминг(ЭДОбъект) Экспорт
		
		RoamingNotificationStatus= ЭДОбъект.RoamingNotificationStatus;
		
		Если RoamingNotificationStatus= "RoamingNotificationStatusNone" Тогда
			Статус= "";
		ИначеЕсли RoamingNotificationStatus = "RoamingNotificationStatusError" Тогда
			Статус = Новый ФорматированнаяСтрока("Ошибка доставки документа через роуминг!",,WebЦвета.Красный,,RoamingNotificationStatus);
		ИначеЕсли RoamingNotificationStatus = "RoamingNotificationStatusSuccess" Тогда
			Статус = "Доставлен через роуминг.";
		ИначеЕсли RoamingNotificationStatus = "UnknownRoamingNotificationStatus" Тогда
			Статус = "Идет доставка через роуминг.";
		КонецЕсли;
		
		Возврат Статус;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСуммы(ЭДОбъект) Экспорт
		
		Если ЭДОбъект.Type = "InvoiceCorrection" ИЛИ ЭДОбъект.Type = "InvoiceCorrectionRevision" Тогда
			
			стрУв = ?(Окр(ЭДОбъект.TotalInc, 2) <> 0, "+" + Формат(Окр(ЭДОбъект.TotalInc, 2), "ЧДЦ=2"), "");
			стрУм = ?(Окр(ЭДОбъект.TotalDec, 2) <> 0, "-" + Формат(Окр(ЭДОбъект.TotalDec, 2), "ЧДЦ=2"), "");
			Если НЕ ПустаяСтрока(стрУв) И НЕ ПустаяСтрока(стрУм) Тогда
				Возврат стрУв + " " + СтрУм;
			ИначеЕсли НЕ ПустаяСтрока(стрУв) Тогда
				Возврат стрУв;
			Иначе
				Возврат стрУм;
			КонецЕсли;
			
		ИначеЕсли 
				ЭДОбъект.Type = "Nonformalized"
			ИЛИ ЭДОбъект.Type = "ReconciliationAct"
			ИЛИ ЭДОбъект.Type = "CertificateRegistry"
			ИЛИ ЭДОбъект.Type = "ServiceDetails" 
			ИЛИ ЭДОбъект.Type = "Contract"
			ИЛИ ЭДОбъект.Type = "PriceListAgreement"
			ИЛИ ЭДОбъект.Type = "TrustConnectionRequest"
			ИЛИ ЭДОбъект.Type = "PriceList"
		Тогда
			Возврат "";
		Иначе
			Возврат Формат(Окр(ЭДОбъект.Total, 2), "ЧДЦ=2");
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСуммыНДС(ЭДОбъект) Экспорт
		Если ЭДОбъект.Type = "InvoiceCorrection" ИЛИ ЭДОбъект.Type = "InvoiceCorrectionRevision" Тогда
			стрУв = ?(Окр(ЭДОбъект.VatInc, 2) <> 0, "+" + Формат(Окр(ЭДОбъект.VatInc, 2), "ЧДЦ=2"), "");
			стрУм = ?(Окр(ЭДОбъект.VatDec, 2) <> 0, "-" + Формат(Окр(ЭДОбъект.VatDec, 2), "ЧДЦ=2"), "");
			Если НЕ ПустаяСтрока(стрУв) И НЕ ПустаяСтрока(стрУм) Тогда
				Возврат стрУв + " " + СтрУм;
			ИначеЕсли НЕ ПустаяСтрока(стрУв) Тогда
				Возврат стрУв;
			Иначе
				Возврат стрУм;
			КонецЕсли;
		Иначе
			Возврат Формат(Окр(ЭДОбъект.Vat, 2), "ЧДЦ=2");
		КонецЕсли;
	КонецФункции
	
	&НаКлиенте 
	Функция ПредставлениеПериодаДД(ДатаНачала, Знач ДатаОкончания) Экспорт
		
		ДатаОкончания= ?(ДатаОкончания = МетодКлиента("Модуль_Клиент","ДатаГоризонта"), '00010101', ДатаОкончания);
		ПредставлениеПериодаДД= "";
		
		Если ЗначениеЗаполнено(ДатаОкончания) Тогда
			Если ЗначениеЗаполнено(ДатаНачала) Тогда
				ПредставлениеПериодаДД= ПредставлениеПериода(ДатаНачала, КонецДня(ДатаОкончания), "ФП = Истина");
			Иначе
				ПредставлениеПериодаДД= "по " + Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(ДатаНачала) Тогда
			ПредставлениеПериодаДД= "с " + Формат(Датаначала, "ДФ=dd.MM.yyyy");
		Иначе
			ПредставлениеПериодаДД= "Без ограничения";
		КонецЕсли;
		
		Возврат ПредставлениеПериодаДД;	
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеТипаРезолюции(ResolutionType) Экспорт
		
		Если ResolutionType = "ResolutionApprove" Тогда
			Действие=	"Согласовано";
		ИначеЕсли ResolutionType = "ResolutionDisapprove" Тогда
			Действие=	"Отказано в согласовании";
		ИначеЕсли ResolutionType = "ResolutionRequest" Тогда
			Действие=	"Отправлен запрос на согласование";
		ИначеЕсли ResolutionType = "SignatureApprove" Тогда
			Действие=	"Подписан";
		ИначеЕсли ResolutionType = "SignatureDisapprove" Тогда
			Действие=	"Отказано в подписи";
		ИначеЕсли ResolutionType = "SignatureRequest" Тогда
			Действие=	"Отправлен запрос на подпись";
		ИначеЕсли ResolutionType = "RevocationRequest" Тогда
			Действие=	"Отправлен запрос на аннулирование";
		ИначеЕсли ResolutionType = "RevocationApprove" Тогда
			Действие=	"Аннулирован";
		ИначеЕсли ResolutionType = "RevocationDisapprove" Тогда
			Действие=	"Отказано в аннулировании";
		ИначеЕсли ResolutionType = "InvoiceCorrectionRequest" Тогда
			Действие = "Отправлен запрос на уточнение";
		Иначе
			Действие=	"Тип согласования не определен";
		КонецЕсли;
		
		Возврат Действие;
		
	КонецФункции
	
	&НаКлиенте 
	Функция СформироватьHTMLПредставлениеОшибок(Document, Организация, МассивОшибок) Экспорт
		
		HTMLДокумент=	"";
		
		HTMLДокумент= 	"<style>
		|
		|p1 {font-family: 'Times New Roman', Times, serif; font-size: 11pt; }
		|p2 {font-family: 'Times New Roman', Times, serif; font-size: 10pt; }
		|p {font-family: 'Times New Roman', Times, serif; font-size: 9pt;}
		|
		|.left_block {
		|display: block;
		|float: left; 
		|}
		|
		|.right_block {
		|display: block;
		|float: right;
		|}
		|
		|.bottom_brd {
		|border-bottom-width: 1px;
		|border-bottom-style: solid;
		|border-bottom-color: black;
		|}
		|
		|.block_comment {
		|width: 98%;
		|float: right;
		|}
		|
		|</style>";
		
		
		HTMLДокумент=	HTMLДокумент + "<body>" +
		"<div>" +
		"<div>" +
		
		"<div>" + 
		
		"<div>" +
		"<div class=""bottom_brd"">" +
		"<h3>" + ПредставлениеЭД(Document) + "</h3>" +
		"<h4 style=""margin-bottom: -15px;"">" + ?(Document.Type = "Inbound", "Отправитель: ", "Получатель: ") + Document.Organization.Name + "</h4>" +
		"<h4>" + ?(Document.Type = "Inbound", "Получатель: ", "Отправитель: ") + Document.Counteragent.Name + "</h4>" +
		"</div>" +
		"</div>";
		
		НомерОшибки=				0;
		ПредыдущаяКатегорияОшибки=	"";
		
		Для каждого Ошибка Из МассивОшибок Цикл
			
			Если НЕ ПредыдущаяКатегорияОшибки = Ошибка.КатегорияОшибки Тогда
				
				НомерОшибки=				0;
				ПредыдущаяКатегорияОшибки=	Ошибка.КатегорияОшибки;
				
				HTMLДокумент=	HTMLДокумент +
				"<div>" +
				
				"<div>" +
				
				"<div style= ""color = red"">" +
				"<p2 align=""left"">" + Ошибка.КатегорияОшибки + "</p2>" + 
				"</div>"
				
				"</div>" +
				
				"</div>";
				
			КонецЕсли;
			
			НомерОшибки=	НомерОшибки + 1;
			
			HTMLДокумент=	HTMLДокумент +
			"<div>" + 
			"<div class=""block_comment"" style= ""white-space: normal; word-wrap: break-word"">" +
			"<p>" + НомерОшибки + ". " + Ошибка.ТекстОшибки + "</p>" +
			"</div>" + 
			"</div>";
			
		КонецЦикла;
		
		HTMLДокумент=	HTMLДокумент + "</div></div></div></<body>";
		
		Возврат HTMLДокумент;
		
	КонецФункции
	
	&НаКлиенте 
	Функция СформироватьHTMLПредставлениеРезолюций(Document, Организация) Экспорт
		
		//Верстальщик бы меня на рее повесил...
		
		HTMLДокумент=	"";
		
		HTMLДокумент= 	"<!DOCTYPE html PUBLIC """"-//W3C//DTD XHTML 1.0 Transitional//EN
		|http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"""">
		|
		|<html xmlns=""""http://www.w3.org/1999/xhtml"""">
		|
		|<head>
		|<meta http-equiv=""""Content-Type"""" content=""""text/html; charset=utf-8""""/>
		|
		|<style>	
		|p1 {font-family: 'Times New Roman', Times, serif; font-size: 11pt; }
		|p2 {font-family: 'Times New Roman', Times, serif; font-size: 10pt; }
		|p {font-family: 'Times New Roman', Times, serif; font-size: 9pt; color = #828282; 
		|}
		|
		|.background.block_creationdate {
		|height: auto !important;
		|padding-bottom: 0;
		|right: 0;
		|width: 50%;
		|}
		|
		|.block_creationdate {
		|width: 30%;
		|padding-bottom: 0px;
		|text-align: right; 
		|}
		|
		|.block_resolutiontype {
		|width: 70%;
		|display: block;
		|float: left;
		|}
		|
		|.block_author {
		|width: 98%;
		|display: block;
		|float: left;
		|}
		|
		|.left_block {
		|display: block;
		|float: left; 
		|}
		|
		|.right_block {
		|display: block;
		|float: right;
		|}
		|
		|.bottom_brd {
		|border-bottom-width: 1px;
		|border-bottom-style: solid;
		|border-bottom-color: black;
		|}
		|
		|.block_comment {
		|width: 96%;
		|float: right; 
		|}
		|
		|</style>
		|
		|</head>";
		
		
		HTMLДокумент=	HTMLДокумент + "<body>" +
		"<div>" +
		"<div>" +
		
		"<div>" + 
		
		"<div>" +
		"<div class=""bottom_brd"">" +
		"<h3>" + ПредставлениеЭД(Document) + "</h3>" +
		"<h4 style=""margin-bottom: -15px;"">" + ?(Document.Type = "Inbound", "Отправитель: ", "Получатель: ") + Document.Organization.Name + "</h4>" +
		"<h4>" + ?(Document.Type = "Inbound", "Получатель: ", "Отправитель: ") + Document.Counteragent.Name + "</h4>" +
		"</div>" +
		"</div>";
		
		МассивРезолюций=	Document.Resolutions;
		
		Для Индекс = 0 По МассивРезолюций.Count - 1 Цикл
			
			Резолюция=	МассивРезолюций.GetItem(Индекс);
			
			Color=	"#0000FF";
			Если Резолюция.ResolutionType = "ResolutionApprove"
				ИЛИ Резолюция.ResolutionType = "SignatureApprove" Тогда
				Color=	"#00D403";
			ИначеЕсли  Резолюция.ResolutionType = "ResolutionDisapprove"
				ИЛИ Резолюция.ResolutionType = "SignatureDisapprove" Тогда
				Color=	"#FF0000";
			КонецЕсли;
			
			ResolutionType=	ПредставлениеТипаРезолюции(Резолюция.ResolutionType);
			
			Если Резолюция.ResolutionType = "ResolutionApprove" Тогда
				Пояснение=		"Согласовал: " + Резолюция.Author;
			ИначеЕсли Резолюция.ResolutionType = "ResolutionDisapprove" Тогда
				Пояснение=		"Отклонил: " + Резолюция.Author;
			ИначеЕсли Резолюция.ResolutionType = "SignatureApprove" Тогда
				Пояснение=		"Подписал: " + Резолюция.Author;
			ИначеЕсли Резолюция.ResolutionType = "SignatureDisapprove" Тогда
				Пояснение=		"Отказал: " + Резолюция.Author;
			ИначеЕсли Резолюция.ResolutionType = "ResolutionRequest" ИЛИ 
				Резолюция.ResolutionType = "SignatureRequest" ИЛИ
				Резолюция.ResolutionType = "RevocationRequest" Тогда
				ПолучательРезолюции=	?(Резолюция.TargetDepartment = Неопределено, "", " " + Резолюция.TargetDepartment.Name) + 
				?(Резолюция.TargetUser = Неопределено, "", ?(Резолюция.TargetDepartment = Неопределено, " ",", ") + ПолучитьФИОСИнициалами(Резолюция.TargetUser.Name));
				
				Пояснение=				"" + ПолучитьФИОСИнициалами(Резолюция.Author) + ?(НЕ ПустаяСтрока(ПолучательРезолюции), " &rarr; " + ПолучательРезолюции, "");
			Иначе
				Пояснение=		"";
			КонецЕсли;
			
			HTMLДокумент=	HTMLДокумент +
			"<div class=""block_left"">" +
			
			"<div>" +
			
			"<div class= ""right_block"">" +
			
			"<div class=""block_resolutiontype"" style= ""color: " + Color + """>" +
			"<p2 align=""left"">" + ResolutionType + "</p2>" + 
			"</div>"
			
			"<div class=""right_block block_creationdate"" >" + 
			"<div>" +
			"<p2 align=""right""  style=""vertical-align: baseline"" > &nbsp; " + Формат(Резолюция.CreationDate, "ДФ='dd.MM.yy HH:mm'") + "</p2>" +
			"</div>" +
			"</div>" +
			
			"</div>" +
			
			"</div>" +
			
			"<div>" +
			"<div class=""block_author"">" +
			"<p1>" + Пояснение + "</p1>" +
			"</div>" +
			"</div>" +
			
			"<div>" + 
			"<div class=""block_comment"" style= ""background-color: #FFFBF0; white-space: normal; word-wrap: break-word"">" +
			"<p><pre>" + Резолюция.Comment + "</pre></p>" +
			"</div>" + 
			
			"<div class=""block_comment"">" + 
			"<p>&nbsp</p>" +
			"</div>" + 
			
			"</div>"+
			
			"</div>";
			
		КонецЦикла;
		
		HTMLДокумент=	HTMLДокумент + "</div></div></div></div></<body>";
		
		Возврат HTMLДокумент;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеAuthor(ResolutionStatus)
		
		Возврат ?(ResolutionStatus.Author = Неопределено, "", ": " + ПолучитьФИОСИнициалами(ResolutionStatus.Author.Name));
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеTargetUser(ResolutionStatus)
		
		Возврат ?(ResolutionStatus.TargetDepartment = Неопределено, ": ", ResolutionStatus.TargetDepartment.Name)
		+ ?(ResolutionStatus.TargetUser = Неопределено, "", ?(ResolutionStatus.TargetDepartment = Неопределено, "", ", ") + ПолучитьФИОСИнициалами(ResolutionStatus.TargetUser.Name));
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСтатусаСогласования(Document) Экспорт
		
		СтатусСогласования=	"";
		ResolutionStatus=	Document.ResolutionStatus;
		
		Если НЕ ResolutionStatus = Неопределено Тогда
			
			Если ResolutionStatus.Type = "Approved" Тогда
				СтатусСогласования=	"Согласовано" + ПредставлениеAuthor(ResolutionStatus);
			ИначеЕсли ResolutionStatus.Type = "Disapproved" Тогда
				СтатусСогласования=	"Отказано в согласовании" + ПредставлениеAuthor(ResolutionStatus);
			ИначеЕсли ResolutionStatus.Type = "ApprovementRequested" Тогда
				СтатусСогласования=	"На согласовании" + ПредставлениеTargetUser(ResolutionStatus);
			ИначеЕсли ResolutionStatus.Type = "SignatureRequested" Тогда
				СтатусСогласования=	"На подписании" + ПредставлениеTargetUser(ResolutionStatus);
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат СтатусСогласования;			
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьФИОСИнициалами(ФИО) Экспорт
		
		СтрокаФИОСИнициалами=	"";
		
		ИсходнаяСтрока=			ФИО;
		ПозицияПослПробела=		Найти(ИсходнаяСтрока, " ");
		Фамилия=				Лев(ИсходнаяСтрока, ПозицияПослПробела);
		
		ИсходнаяСтрока=			Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
		ПозицияПослПробела=		Найти(ИсходнаяСтрока, " ");
		Имя=					Лев(ИсходнаяСтрока, ПозицияПослПробела);
		
		ИсходнаяСтрока=			Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
		Отчество=				ИсходнаяСтрока;
		ИсходнаяСтрока=			Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
		
		Если НЕ ПустаяСтрока(Фамилия) Тогда
			СтрокаФИОСИнициалами=	Фамилия + 
			?(НЕ ПустаяСтрока(Имя), Лев(Имя, 1) + ".", "") +
			?(НЕ ПустаяСтрока(Отчество), Лев(Отчество, 1) + ".", "");
		КонецЕсли;
		
		Возврат СтрокаФИОСИнициалами;
		
	КонецФункции
	
//} ПРЕДСТАВЛЕНИЯ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

	&НаКлиентеНаСервереБезКонтекста
	Функция НайтиСтрокиВМассивеСтруктур(МассивСтруктур, ПараметрыОтбора) //вместо НайтиСтроки таблицы значений
		Результат = Новый Массив;
		Для Каждого Стр из МассивСтруктур Цикл
			БылоНесоответствие = Ложь;
			Для Каждого Стр1 из ПараметрыОтбора Цикл
				Если СокрЛП(ПараметрыОтбора[Стр1.Ключ]) <>  СокрЛП(Стр[Стр1.Ключ]) Тогда
					БылоНесоответствие = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если БылоНесоответствие = Ложь Тогда
				Результат.Добавить(Стр);
			КонецЕсли;
			
		КонецЦикла;
		Возврат Результат;
	КонецФункции
	
	&НаСервере
	Функция BoxID_2_Организация_Форма(BoxID, DepartmentKpp = "") Экспорт 
		Возврат МетодСервера(,"BoxID_2_Организация", BoxID, DepartmentKpp);
	КонецФункции
	
	&НаСервере
	Функция CounteragentBoxID_2_Контрагент_Форма(CounteragentID, BoxID = Неопределено) экспорт 
		Возврат МетодСервера(,"CounteragentBoxID_2_Контрагент", CounteragentID, BoxID);
	КонецФункции
	
	&НаСервере
	Функция DocumentID_2_Документ_Форма(DocumentID, BoxID) Экспорт 
		Возврат МетодСервера(,"DocumentID_2_Документ", DocumentID, BoxID);
	КонецФункции
	
	&НаКлиенте
	функция ПолучитьСправа(Стр, Маркер)
		
		Если Найти(Стр, Маркер) = 0 Тогда
			Возврат Стр 
		Иначе 	
			Возврат Прав(Стр, СтрДлина(Стр) - Найти(Стр, Маркер) - СтрДлина(Маркер) + 1);
		КонецЕсли;	
		
	КонецФункции	
	
	&НаКлиенте
	Функция  КраткоеСообщениеОбОшибке(текстОшибки);
		
		Подстрока=	ПолучитьСправа(ТекстОшибки, "##");
		Подстрока=	ПолучитьСправа(Подстрока, "[");
		Подстрока=	ПолучитьСправа(Подстрока, "]");
		
		Возврат Подстрока;
		
	КонецФункции	
	
	&НаКлиенте
	Функция СформироватьТекстОшибкиДиадок(ТекстОшибки) Экспорт
		
		КраткоеСообщение = КраткоеСообщениеОбОшибке(ТекстОшибки);
		Если (найти( КраткоеСообщение, "is not registered in FNS for box")>0)  тогда
			ИДЯщика = прав(КраткоеСообщение, стрДлина(КраткоеСообщение) - найти( КраткоеСообщение, "is not registered in FNS for box")-32);
			ИДЯщика = лев(ИдЯщика, найти(ИдЯщика, ".")-1);
			
			ИдСертификата = лев(КраткоеСообщение, найти( КраткоеСообщение, "is not registered in FNS for box")-1);
			ИДСертификата = прав(ИдСертификата, стрДлина(ИдСертификата) - найти( ИдСертификата, "Signer certificate")-18);
			ТекстОшибки = "Сертификат, которым подписано сообщение, не зарегистрирован в ФНС.
			|Для решения данной проблемы обратитесь в техподдержку.
			|Отладочная информация: 
			|отпечаток сертификата: "+ИдСертификата+ "
			|идентификатор ящика организации: "+ИДЯщика;
		Иначеесли найти(ТекстОшибки, "##3")>0 тогда
			текстОшибки = ("Произошла ошибка при работе с КриптоПро:
			|"+  КраткоеСообщениеОбОшибке(ТекстОшибки));
		ИначеЕсли найти(ТекстОшибки, "##1")>0 тогда 
			КраткоеСообщение = КраткоеСообщениеОбОшибке(ТекстОшибки);
			Если (найти(текстОшибки, "Код ошибки: 500")>0)	 и (найти(текстОшибки, "GetTokenByCertificate")>0) тогда 
				текстОшибки = ("При работе программы произошла ошибка:
				|Выбранный сертификат не имеет доступа в " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
			ИначеЕсли (найти( КраткоеСообщение, "407")>0) или  (найти( КраткоеСообщение, "401")>0) тогда 
				текстОшибки = ("Настройки прокси-сервера не позволяют установить соединение с сервером "+ Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж +"
				|("+КраткоеСообщение+")");
			ИначеЕсли 	(найти(ТекстОшибки, "for entity that already signed")> 0) или 
				(найти(ТекстОшибки, "Receipt for document which already has one")> 0) 
				тогда 
				текстОшибки = ("При работе программы произошла ошибка:
				|данный документ уже подписан");
			ИначеЕсли 	(найти(ТекстОшибки, "for entity that has requested signature rejection")> 0) или 
				(найти(ТекстОшибки, "RequestedSignatureRejection for document that already has one")> 0) или 
				(найти(ТекстОшибки, "for entity that already have requested signature rejection")> 0) тогда    
				текстОшибки = ("При работе программы произошла ошибка:
				|по данному документу уже сформирован отказ в подписи");
			ИначеЕсли (найти(ТекстОшибки, "Access to Box")> 0) и 
				(найти(ТекстОшибки, "is denied")> 0) тогда 
				текстОшибки = ("При работе программы произошла ошибка:
				|у пользователя отсутствует доступ к ящику " +Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж);
			Иначе                                                             
				текстОшибки = ("Произошла ошибка при работе с сервером "+ Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж +":
				|"+  КраткоеСообщение);
			КонецЕсли;	
		ИначеЕсли найти(ТекстОшибки, "##2")>0 тогда
			КраткоеСообщение = КраткоеСообщениеОбОшибке(ТекстОшибки);
			Если (найти( КраткоеСообщение, "407")>0) или  (найти( КраткоеСообщение, "401")>0) тогда 
				текстОшибки = ("Настройки прокси-сервера не позволяют установить соединение с сервером "+ Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж +"
				|("+  КраткоеСообщение+")");
			иначе 	
				текстОшибки = ("Невозможно установить соединение с сервером "+ Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж +" по причине:
				|"+  КраткоеСообщениеОбОшибке(ТекстОшибки));
			КонецЕсли;	
		ИначеЕсли найти(ТекстОшибки, "##9")>0 тогда
			текстОшибки = КраткоеСообщениеОбОшибке(ТекстОшибки);
		КонецЕсли;	
		
		Возврат ТекстОшибки;
		
	КонецФункции	
	
	&НаСервере
	Функция ПолучитьОсновнуюФормуОбъекта(Объект1С)
		
		МетаданныеОбъекта=	Объект1С.ПолучитьОбъект().Метаданные();
		ИмяОбъекта1С=		МетаданныеОбъекта.Имя;
		
		Если Метаданные.Документы.Содержит(МетаданныеОбъекта) Тогда
			КореньОбъекта=	"Документ";
		ИначеЕсли Метаданные.Справочники.Содержит(МетаданныеОбъекта) Тогда
			КореньОбъекта=	"Справочник";
		КонецЕсли;
		
		Попытка
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
				Если ТипЗнч(Объект1С) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
					ФормыСчетаФактуры=			Новый ФиксированноеСоответствие(Документы.СчетФактураПолученный.ПолучитьСоответствиеВидовСчетаФактурыФормам());
					ИмяФормыОбъекта=			ФормыСчетаФактуры[Объект1С.ВидСчетаФактуры];
				ИначеЕсли ТипЗнч(Объект1С) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
					ФормыСчетаФактуры= 			Новый ФиксированноеСоответствие(Документы.СчетФактураВыданный.ПолучитьСоответствиеВидовСчетаФактурыФормам());
					ИмяФормыОбъекта=			ФормыСчетаФактуры[Объект1С.ВидСчетаФактуры];
				ИначеЕсли ТипЗнч(Объект1С) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
					ФормыДокументаНакладной=	Новый ФиксированноеСоответствие(Документы.РеализацияТоваровУслуг.ПолучитьСоответствиеВидовОперацийФормам());
					ИмяФормыОбъекта=			ФормыДокументаНакладной[Объект1С.ВидОперации];
				ИначеЕсли ТипЗнч(Объект1С) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
					ФормыДокументаНакладной=	Новый ФиксированноеСоответствие(Документы.ПоступлениеТоваровУслуг.ПолучитьСоответствиеВидовОперацийФормам());
					ИмяФормыОбъекта=			ФормыДокументаНакладной[Объект1С.ВидОперации];
				ИначеЕсли ТипЗнч(Объект1С) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") тогда 
					ИмяФормыОбъекта = "ФормаДокументаОбщая"
				Иначе
					ИмяФормыОбъекта=			МетаданныеОбъекта.ОсновнаяФормаОбъекта.Имя;
				КонецЕсли;
			Иначе
				ИмяФормыОбъекта=			МетаданныеОбъекта.ОсновнаяФормаОбъекта.Имя;
			КонецЕсли;
		Исключение
			ИмяФормыОбъекта=			МетаданныеОбъекта.ОсновнаяФормаОбъекта.Имя;
		КонецПопытки;
		
		Возврат КореньОбъекта + "." + ИмяОбъекта1С + ".Форма." + ИмяФормыОбъекта;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьОписаниеФормы(Документ1С) Экспорт
		
		Возврат ПолучитьОсновнуюФормуОбъекта(Документ1С);
		
	КонецФункции
	
	&НаКлиенте
	Функция ВернутьМассивВыбранныхФайлов(МножественныйВыбор =  Ложь) Экспорт
		
		Попытка
			WshShell= 			Новый COMОбъект("WScript.Shell");
			КаталогФайлов=	WshShell.CurrentDirectory;
		Исключение
			КаталогФайлов=	"";
		КонецПопытки;
		
		ДиалогВыбора=						Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогВыбора.Заголовок=				?(МножественныйВыбор, "Выберите файлы", "Выберите файл");
		ДиалогВыбора.Каталог=				КаталогФайлов;
		ДиалогВыбора.Фильтр=				НСтр("ru = 'Все файлы(*.*)|*.*'"); 
		ДиалогВыбора.МножественныйВыбор=	МножественныйВыбор;
		
		Если ДиалогВыбора.Выбрать() Тогда
			Возврат ДиалогВыбора.ВыбранныеФайлы;
		Иначе
			Возврат Новый Массив();
		КонецЕсли;
		
	КонецФункции
	
	&НаСервере
	Процедура УстановитьРеквизитКонтрагента(ВыбКонтрагент, CounteragentId)
		
		МетодСервера(,"Установить_CounteragentBoxID_для_Контрагент", ВыбКонтрагент, CounteragentId);
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьИмяФормыДокумента(Document) Экспорт
		
		Если ЭтоФормализованныйДокумент(Document) = Истина И Document.HasCustomPrintForm = Ложь Тогда
			ИмяФормыПросмотра=	"ФормаДокумента";
		Иначе
			ИмяФормыПросмотра=	"ФормаДокументаБезВизуализации";
		КонецЕсли;
		
		Возврат ИмяФормыПросмотра;
		
	КонецФункции
	
	&НаСервере
	Функция ПолучитьDocumentID_2_Документ(DocumentID, BoxID)
		
		Возврат МетодСервера(,"DocumentID_2_Документ", DocumentID, BoxID);
		
	КонецФункции

	&НаКлиенте
	Функция ЭтоНеформализованныйДокументБезМетаданных(Document) Экспорт 
		
		Если Document.Type = "Nonformalized" ИЛИ
			Document.Type = "NonformalizedProforma" ИЛИ
			Document.Type = "NonformalizedAcceptanceCertificate" ИЛИ
			Document.Type = "Contract" ИЛИ
			Document.Type = "ReconciliationAct" ИЛИ
			Document.Type = "ServiceDetails" ИЛИ
			Document.Type = "PriceListAgreement" ИЛИ
			Document.Type = "TrustConnectionRequest" ИЛИ
			Document.Type = "CertificateRegistry" Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	КонецФункции

	&НаКлиенте
	Функция ЭтоФормализованныйДокумент(Document)
		
		Если ЛЕВ(Document.Type, 7) = "Invoice" ИЛИ 
			Document.Type = "XmlTorg12" ИЛИ 
			Document.Type = "XmlAcceptanceCertificate" Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;

	КонецФункции
	
	&НаСервере
	Функция КлассификаторыДляДиадок() Экспорт 
		
		Результат=	Новый Структура;
		Результат.Вставить("ОКЕИ", МетодСервера(,"ТаблицаЗначений_2_МассивСтруктур", МетодСервера(,"ПолучитьОКЕИ")));
		Результат.Вставить("ОКСМ", МетодСервера(,"ТаблицаЗначений_2_МассивСтруктур", МетодСервера(,"ПолучитьОКСМ")));
		Результат.Вставить("ОКВ",  МетодСервера(,"ТаблицаЗначений_2_МассивСтруктур", МетодСервера(,"ПолучитьОКВ")));
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НайтиОрганизациюВИерархииОрганизацийDiadoc(BoxID, DepartmentId) Экспорт
		
		Возврат НайтиОрганизациюВИерархииОрганизацийDiadocРекурсия(BoxID, Платформа.ПараметрыКлиент.ИерархияОрганизацийDiadoc[DepartmentId]);
		
	КонецФункции
	
	&НаКлиенте
	Функция НайтиОрганизациюВИерархииОрганизацийDiadocРекурсия(BoxID, СтруктураПодразделения)
		
		Если СтруктураПодразделения = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Организация= МетодСервера(,"BoxID_2_Организация", BoxID, СтруктураПодразделения.DepartmentKpp);
		
		Если ЗначениеЗаполнено(Организация) Тогда
			Возврат Организация;
		КонецЕсли;
		
		Возврат НайтиОрганизациюВИерархииОрганизацийDiadocРекурсия(BoxID, Платформа.ПараметрыКлиент.ИерархияОрганизацийDiadoc[СтруктураПодразделения.ParentId]);
		
	КонецФункции
	
	&НаСервере
	Функция ТекстИнформацииОНовомФормате() Экспорт
		
		ТекстИнформацииОНовомФормате = ОбработкаОбъект().ПолучитьМакет("МакетИнформацииОНовомФормате").ПолучитьТекст();
		
		СловарьWL= МетодСервера(,"ПолучитьСловарь");
		
		Если СловарьWL.НаименованиеСистемы <> "Диадок" Тогда
			ТекстИнформацииОНовомФормате = СтрЗаменить(ТекстИнформацииОНовомФормате, "Диадок", СловарьWL.НаименованиеСистемы); 	
		КонецЕсли;
		
		Возврат ТекстИнформацииОНовомФормате;
		
	КонецФункции
	
	&НаСервере
	Функция СписокОбработокДереваКонфигурации() Экспорт
		
		СписокОбработок= 	 Новый СписокЗначений;
		МетаданныеОбработки= Метаданные.Обработки;
		
		Для Каждого МетаданныеОбработка ИЗ МетаданныеОбработки Цикл
			СписокОбработок.Добавить(МетаданныеОбработка.Имя, МетаданныеОбработка.Представление());
		КонецЦикла;
		
		Возврат СписокОбработок;
		
	КонецФункции
	
	&НаКлиенте
	Функция ДатаГоризонта() Экспорт
		
		Возврат ДобавитьМесяц(ТекущаяДата(), 12);
		
	КонецФункции
	
	&НаКлиенте
	Функция ИдентификаторСвойства(ИмяСвойства) Экспорт
		
		Возврат МетодСервера(,"ИдентификаторСвойства"+ИмяСвойства);
		
	КонецФункции
	
	&НаКлиенте
	Функция ВариантыИспользованияПроксиСервера() Экспорт
		
		ВариантыИспользованияПроксиСервера= Новый СписокЗначений;
		
		ВариантыИспользованияПроксиСервера.Добавить("НеИспользовать"		  , "Не использовать");
		ВариантыИспользованияПроксиСервера.Добавить("ИспользоватьПроксиСервер", "Использовать прокси-сервер");
		ВариантыИспользованияПроксиСервера.Добавить("ИспользоватьНастройкиIE" , "Использовать настройки Internet Explorer");
		
		Возврат ВариантыИспользованияПроксиСервера;
		
	КонецФункции
	
	&НаСервере
	Процедура УстановитьЗначениеПараметровСвойствОтправки(Организация, ПараметрыСвойствОтправки) Экспорт
		
		МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваПечатнаяФормаДляТоваров"), , ПараметрыСвойствОтправки.НакладныеСТоварами);
		МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваПечатнаяФормаДляУслуг"), , ПараметрыСвойствОтправки.НакладныеСУслугамиБезТоваров);
		МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваОтправлятьНеПроведенные"), , ПараметрыСвойствОтправки.ОтправлятьНеПроведенные);
		
		Если ЗначениеЗаполнено(ПараметрыСвойствОтправки.ВнешняяПечатнаяФормаСчета) Тогда
			МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваВнешняяПечатнаяФормаСчета"), , ПараметрыСвойствОтправки.ВнешняяПечатнаяФормаСчета.УникальныйИдентификатор());
		Иначе
			МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваВнешняяПечатнаяФормаСчета"), , "");
		КонецЕсли;
		МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваФормироватьСчетНаОсновании"), , ПараметрыСвойствОтправки.ФормироватьСчетНаОсновании);
		
		Если ЗначениеЗаполнено(ПараметрыСвойствОтправки.ВнешняяПечатнаяФормаАкта) Тогда
			МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваВнешняяПечатнаяФормаАкта"), , ПараметрыСвойствОтправки.ВнешняяПечатнаяФормаАкта.УникальныйИдентификатор());
		Иначе
			МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваВнешняяПечатнаяФормаАкта"), , "");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыСвойствОтправки.ВнешняяПечатнаяФормаАктаСверки) Тогда
			МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваВнешняяПечатнаяФормаАктаСверки"), , ПараметрыСвойствОтправки.ВнешняяПечатнаяФормаАктаСверки.УникальныйИдентификатор());
		Иначе
			МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваВнешняяПечатнаяФормаАктаСверки"), , "");
		КонецЕсли;
		
		МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваСпособФормированияАкта"), , ПараметрыСвойствОтправки.СпособФормированияАкта);
		МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваСпособФормированияАктаСверки"), , ПараметрыСвойствОтправки.СпособФормированияАктаСверки);
		
		МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваПодставлятьКППГрузоотправителя"), , ПараметрыСвойствОтправки.ПодставлятьКППГрузоотправителя);
		МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификатрСвойстваТекстКомментарияДиадок"), , ПараметрыСвойствОтправки.ТекстКомментарияДиадок);
		
		
		// дополнительные внешние печатные формы
		МассивВнешнихПФСервер = ПолучитьМассивДополнительныхВнешнихПечатныхФорм(Организация);
		
		МассивВнешнихПФНастройка = ПараметрыСвойствОтправки.МассивДополнительныхВПФ;
		
		Для каждого ВнешняяПФ из МассивВнешнихПФНастройка Цикл
			Если МассивВнешнихПФСервер.Найти(ВнешняяПФ) = Неопределено Тогда
				ВключитьВнешнююПечатнуюФормуВСписок(Организация, ВнешняяПФ);	
			КонецЕсли;
		КонецЦикла;
		
		Для каждого ВнешняяПФ из МассивВнешнихПФСервер Цикл
			Если МассивВнешнихПФНастройка.Найти(ВнешняяПФ) = Неопределено Тогда
				ИсключитьВнешнююПечатнуюФормуИзСписка(Организация, ВнешняяПФ);	
			КонецЕсли;
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ВключитьВнешнююПечатнуюФормуВСписок(Организация, ВнешняяПечатнаяФорма)
		НомерВПФ = ПолучитьПорядковыйНомерВнешнейПечатнойФормы(ВнешняяПечатнаяФорма);
		УстановитьПризнакВнешнейПечатнойФормы(Организация, НомерВПФ, "1");	
	КонецПроцедуры
	
	&НаСервере
	Процедура ИсключитьВнешнююПечатнуюФормуИзСписка(Организация, ВнешняяПечатнаяФорма)
		НомерВПФ = ПолучитьПорядковыйНомерВнешнейПечатнойФормы(ВнешняяПечатнаяФорма) ;
		УстановитьПризнакВнешнейПечатнойФормы(Организация, номерВПФ, "0");
	КонецПроцедуры
	
	&НаСервере
	Функция ПолучитьПорядковыйНомерВнешнейПечатнойФормы(ВнешняяПечатнаяФорма)
		
		//получим список всех внешних печатных форм, которые исп в модуле 
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
			|	ДополнительныеСведения.Объект 	КАК Объект,
			|	ДополнительныеСведения.Значение КАК Значение
			|ИЗ
			|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
			|ГДЕ
			|	ДополнительныеСведения.Свойство.Наименование = &Наименование
			|	И НЕ ДополнительныеСведения.Значение = """"";
			
		Запрос.УстановитьПараметр("Наименование", МетодСервера(,"ИдентификаторСвойстваНомерВПФ"));
		Выборка = Запрос.Выполнить().Выбрать();
		МаксЗначение = 0 ;
		Пока Выборка.Следующий() Цикл 
			ТекЗначение = Число(Выборка.Значение);
			Если Выборка.Объект = ВнешняяПечатнаяФорма Тогда 
				Возврат ТекЗначение;
			Иначе 
				МаксЗначение = Макс(ТекЗначение, МаксЗначение);
			КонецЕсли;
		КонецЦикла;
		
		Результат = МаксЗначение+1;
		МетодСервера(,"УстановитьЗначениеСвойства", ВнешняяПечатнаяФорма, МетодСервера(,"ИдентификаторСвойстваНомерВПФ"), , формат(результат, "ЧГ=0"));
		
		Возврат Результат;
		
	КонецФункции
	
	&НаСервере
	Процедура УстановитьПризнакВнешнейПечатнойФормы(Организация, НомерВПФ, Симв)
		
		Если НЕ СтрДлина(симв) = 1 Тогда
			ВызватьИсключение("неверно задан параметр при вызове функции ""УстановитьПризнакВнешнейПечатнойФормы""");
		КонецЕсли;	
		
		ТекущийСписокФорм = МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваСписокДопВПФ"));
		Если ЗначениеЗаполнено(ТекущийСписокФорм) = Ложь Тогда
			ТекущийСписокФорм = "";
		КонецЕсли;		  
		
		Если СтрДлина(ТекущийСписокФорм) < НомерВПФ Тогда 
			
			Пока СтрДлина(ТекущийСписокФорм) < НомерВПФ-1 Цикл 
				ТекущийСписокФорм = ТекущийСписокФорм +" ";
			КонецЦикла;	  
			ИтоговыйСписокФорм = ТекущийСписокФорм + Симв;
		Иначе
			
			ИтоговыйСписокФорм = Лев(ТекущийСписокФорм, НомерВПФ-1) + Симв + Прав(ТекущийСписокФорм, СтрДлина(ТекущийСписокФорм) - НомерВПФ);
		КонецЕсли;	  
		
		МетодСервера(,"УстановитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваСписокДопВПФ"), , ИтоговыйСписокФорм);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЭлементВРежимеВыбораИзСпискаПриИзменении(Форма, Элемент, ВыбранноеЗначение) Экспорт
		
		СократитьСписокВыбораДоВыбранногоЗначения(Элемент, ВыбранноеЗначение);
		
		СброситьКартинкуСпискаВыбора(Элемент);
		
		СинхронизироватьСпискиВыбораЭлементовПовторителей(Форма, Элемент);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СократитьСписокВыбораДоВыбранногоЗначения(Элемент, ВыбранноеЗначение) Экспорт
		
		ОбратныйИндекс= Элемент.СписокВыбора.Количество() - 1;
		
		Пока ОбратныйИндекс > -1 Цикл
			Если Элемент.СписокВыбора[ОбратныйИндекс].Значение <> ВыбранноеЗначение Тогда
				Элемент.СписокВыбора.Удалить(ОбратныйИндекс);
			КонецЕсли;
			ОбратныйИндекс= ОбратныйИндекс - 1;
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СброситьКартинкуСпискаВыбора(Элемент)
		
		Если Элемент.СписокВыбора.Количество() = 1 Тогда
			Элемент.СписокВыбора[0].Картинка= Новый Картинка;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СинхронизироватьСпискиВыбораЭлементовПовторителей(Форма, Элемент)
		
		МаскаИмени= Лев(Элемент.Имя, Найти(Элемент.Имя, "_") - 1);
		
		Для НомерЭлемента=1 ПО 10 Цикл
			
			ЭлементПовторитель= Форма.Элементы.Найти(МаскаИмени + "_" + Формат(НомерЭлемента, "ЧГ="));
			
			Если ЭлементПовторитель = Неопределено Тогда
				
				Прервать;
				
			ИначеЕсли ЭлементПовторитель <> Элемент Тогда
				
				ЭлементПовторитель.СписокВыбора.Очистить();
				
				Для Каждого ЭлементСписка ИЗ Элемент.СписокВыбора Цикл
					ЭлементПовторитель.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ЗаписатьВЛог(ИдентификаторСобытия, Комментарий = "", ДокументСсылка = Неопределено) Экспорт
		
		ЗаписьЖурналаРегистрации(МетодСервера(,"ПолучитьСловарь").НаименованиеСистемы + "."+СтрЗаменить(ИдентификаторСобытия, " ", "_"),,,ДокументСсылка, Комментарий);
		
	КонецПроцедуры
	
//} СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ ИНТЕГРАЦИЯ

	&НаКлиенте
	функция ПроверитьФорматДатыПрото(ЗначениеПоля, ПредставлениеПоля, ОбязательноеЗаполнение= Ложь) Экспорт  
		
		если ОбязательноеЗаполнение и (ЗначениеЗаполнено(ЗначениеПоля)=Ложь) тогда 
			возврат  "<p>не указана "+ПредставлениеПоля+"</p>
			|"
		конецЕсли;
		
		Если ТипЗнч(ЗначениеПоля) = тип("Дата") тогда 
			Если (ЗначениеПоля < '18000101') или (ЗначениеПоля>='21000101') тогда 
				возврат "<p>"+ПредставлениеПоля+" ("+формат(ЗначениеПоля,"ДФ=dd.MM.yyyy")+") должна находиться в интервале 01.01.1800 - 31.12.2099</p>"
			КонецЕсли;
		ИначеЕсли ТипЗнч(ЗначениеПоля) = тип("Строка") тогда 	
			ДатаГод = прав(ЗначениеПоля, 4);
			Если (ДатаГод <"1800") или (ДатаГод >"2099") тогда 
				возврат "<p>"+ПредставлениеПоля+" ("+ЗначениеПоля+") должна находиться в интервале 01.01.1800 - 31.12.2099</p>"
			КонецЕсли;
		КонецЕсли;
		
		возврат ""
		
	КонецФункции

	&НаКлиенте
	функция ПроверитьМаксимальнуюДлинуПрото(ЗначениеПоля, ПредставлениеПоля, МаксимальнаяДлина, род="м", ОбязательноеЗаполнение = ложь) Экспорт
		если ОбязательноеЗаполнение и (ЗначениеЗаполнено(ЗначениеПоля)=Ложь) тогда 
			если род = "ж" тогда 
				глагол = "указана"
			ИначеЕсли род="с" тогда 
				глагол = "указано"
			Иначе 
				глагол = "указан"
			КонецЕсли;
			возврат  "<p>не "+глагол+" "+ПредставлениеПоля+"</p>
			|"
		конецЕсли;
		
		если род = "ж" тогда 
			глагол = "должна"
		ИначеЕсли род="с" тогда 
			глагол = "должно"
		Иначе 
			глагол = "должен"
		КонецЕсли;	
		
		Если стрДлина(сокрЛП(ЗначениеПоля))>МаксимальнаяДлина тогда 
			возврат "<p>"+ПредставлениеПоля+" ("+ЗначениеПоля+") не "+глагол+" быть длиннее "+МаксимальнаяДлина+" символов</p>"
		иначе 
			возврат ""
		КонецЕсли;	
	КонецФункции
	
	&НаКлиенте
	функция ПроверитьТитулПолучателяТорг12(СтруктураДляТитула) экспорт 
		
		ТекстОшибки=	ПроверитьФорматДатыПрото(СтруктураДляТитула.Получить("ShipmentReceiptDate"), "дата получения груза");
		
		Если НЕ ТекстОшибки = "" Тогда
			Если Найти(ТекстОшибки, "<p>")>0 Тогда
				ТекстОшибки = Прав(ТекстОшибки, СтрДлина(ТекстОшибки) - Найти(ТекстОшибки, "<p>")-2);
			КонецЕсли;
			Если Найти(ТекстОшибки, "</p>")>0 Тогда
				ТекстОшибки = Лев(ТекстОшибки, Найти(ТекстОшибки, "</p>")-1);
			КонецЕсли;
		КонецЕсли;
		
		Возврат ТекстОшибки;
		
	КонецФункции	

	&НаКлиенте
	Процедура ДобавитьВСписокФильтр(СписокФильтров, МассивТиповФильтруемыхДокументов, СтрокаФильтра)
		
		Для каждого ТипДокумента Из МассивТиповФильтруемыхДокументов Цикл
			
			СтруктураФильтра=	Новый Структура();
			СтруктураФильтра.Вставить("ТипДокумента", 	ТипДокумента);
			СтруктураФильтра.Вставить("Фильтр", 		СтрокаФильтра);
			
			СписокФильтров.Добавить(СтруктураФильтра);
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	функция ПолучитьСписокФильтровДляДокументовДиадок(Направление, ФильтрПоСостояниюДокументооборота, ФильтрПотипамДокументов) 
		
		//ФильтрПотипамДокументов =1 выводим РНК
		//ФильтрПотипамДокументов =2 выводим СФ
		//ФильтрПотипамДокументов =3 выводим Счета на оплату
		//ФильтрПотипамДокументов =4 выводим неформализованные 
		//ФильтрПотипамДокументов =5 выводим Акты сверки
		//ФильтрПотипамДокументов =0 выводим все 
		
		// ФильтрПоСостояниюДокументооборота = 0 все состояния
		// ФильтрПоСостояниюДокументооборота = 1 завершен
		// ФильтрПоСостояниюДокументооборота = 2 не завершен
		// ФильтрПоСостояниюДокументооборота = 3 прекращен
		
		фильтрDirection = направление;
		СписокФильтров = Новый Массив;
		
		Если направление = "OutboundWaitingForSenderSignature" тогда 
			Если ФильтрПотипамДокументов = "1" Тогда
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "XmlTorg12", фильтрDirection));
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "XmlAcceptanceCertificate", фильтрDirection));
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "NonformalizedAcceptanceCertificate", фильтрDirection));
			ИначеЕсли ФильтрПотипамДокументов = "2" Тогда
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "AnyInvoiceDocumentType", фильтрDirection));
			ИначеЕсли ФильтрПотипамДокументов = "3" Тогда
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "NonformalizedProforma", фильтрDirection));
			ИначеЕсли ФильтрПотипамДокументов = "4" Тогда
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "Nonformalized", фильтрDirection));
			ИначеЕсли ФильтрПотипамДокументов = "5" Тогда
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "ReconciliationAct", фильтрDirection));
			Иначе
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "AnyInvoiceDocumentType", фильтрDirection));
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "XmlTorg12", фильтрDirection));
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "XmlAcceptanceCertificate", фильтрDirection));
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "NonformalizedAcceptanceCertificate", фильтрDirection));
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "Nonformalized", фильтрDirection));
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "NonformalizedProforma", фильтрDirection));
				СписокФильтров.Добавить(Новый Структура("ТипДокумента, Фильтр", "ReconciliationAct", фильтрDirection));
			КонецЕсли;
			
			возврат СписокФильтров
		КонецЕсли;
		
		Если ФильтрПоСостояниюДокументооборота = 0  Тогда // все состояния
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), ФильтрDirection);
		ИначеЕсли  ФильтрПоСостояниюДокументооборота = 1 Тогда // завершен
			
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 1), ФильтрDirection+"Finished"); //сф
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 3), фильтрDirection+""); //строго односторонние
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 2), фильтрDirection+"WithRecipientSignature"); //строго двусторонние
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 4), фильтрDirection+"WithRecipientSignature"); //односторонние или двусторонние
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 4), фильтрDirection+"NoRecipientSignatureRequest"); //односторонние или двусторонние
			
			//пока закоментируем фильтры аннулирования и согласования - необходим расширенный фильтр
			//ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RevocationAccepted");  		//Документ аннулирован
			//ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RevocationRejected");  		//отказано в аннулировании
			//ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"Disapproved");  			//отказано в согласованияя
			//ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"SignatureRequestRejected");	//отказано в подписании
			
		ИначеЕсли  ФильтрПоСостояниюДокументооборота = 2 Тогда // не завершен
			
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 1), фильтрDirection+"NotFinished"); //сф
 	   		ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 2), фильтрDirection+"WaitingForRecipientSignature"); //строго двусторонние
 	   		ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 4), фильтрDirection+"WaitingForRecipientSignature"); //односторонние или двусторонние
			
			//пока закоментируем фильтры аннулирования и согласования - необходим расширенный фильтр
			//ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RevocationIsRequestedByMe");  	//ожидается Аннулирование
			//ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RequestsMyRevocation");  		//запрошено Аннулирование
			//ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"WaitingForResolution");  		//передано на согласование
			//ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"Approved");  					//согласовано
			
		ИначеЕсли  ФильтрПоСостояниюДокументооборота = 3 Тогда // прекращен                                                
						
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RevocationAccepted");
			Если фильтрDirection = "Outbound" Тогда
				ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"InvalidSenderSignature");
	  		КонецЕсли;
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 2), фильтрDirection+"RecipientSignatureRequestRejected"); //строго двусторонние
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 4), фильтрDirection+"RecipientSignatureRequestRejected"); //односторонние или двусторонние
			
		КонецЕсли;
				
		Возврат СписокФильтров;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьФилиаловDepartmentId()
		
		ФилиаловDepartmentId = Новый Массив;
		
		Возврат ФилиаловDepartmentId;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьDepartmentByKpp(Organization, DepartmentKpp)
		
		Для Индекс = 0 По Organization.Departments.Count - 1 Цикл
			dep=	Organization.Departments.GetItem(Индекс);
			Если dep.Kpp = DepartmentKpp Тогда
				Возврат dep.Id;
			КонецЕсли;
		КонецЦикла;
		
		Возврат "";
		
	Конецфункции
	
	&НаКлиенте
	Функция ВернутьМассивDepartmentId(стрЯщик, Box, ПодразделениеID, ВключатьПодчиненныеПодазделения)
		
		ПодразделениеID_текущее = ПодразделениеID;
		Если ПустаяСтрока(ПодразделениеID) Тогда
			Если НЕ ПустаяСтрока(стрЯщик.КПППодразделения) Тогда
				ПодразделениеID_текущее = ПолучитьDepartmentByKpp(Box, стрЯщик.КПППодразделения);   
			КонецЕсли;
		КонецЕсли;
		
		массивDepartmentId = Новый Массив;
		Если ПодразделениеID_текущее = "" Тогда
			ФилиаловDepartmentId = ПолучитьФилиаловDepartmentId();
			ЕстьПодразделенеКоторыеНужноОбходить = Ложь;
			массивDepartmentId.Добавить(Новый Структура("DepartmentID, вклДочерниеПодразделения", "", Ложь));
			Если ВключатьПодчиненныеПодазделения Тогда
				Для Индекс = 0 По Box.Departments.Count - 1 Цикл
					
					department=	Box.Departments.GetItem(Индекс);
					
					Если ФилиаловDepartmentId.Найти(department.Id) = Неопределено Тогда
						массивDepartmentId.Добавить(Новый Структура("DepartmentID, вклДочерниеПодразделения", department.Id, Истина));
					Иначе
						ЕстьПодразделенеКоторыеНужноОбходить = Истина;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Если ЕстьПодразделенеКоторыеНужноОбходить = Ложь Тогда
				массивDepartmentId.Очистить();
			КонецЕсли;
		Иначе
			массивDepartmentId.Добавить(Новый Структура("DepartmentID, вклДочерниеПодразделения", ПодразделениеID_текущее, ВключатьПодчиненныеПодазделения));
		КонецЕсли;
		
		Если массивDepartmentId.Количество()=0 Тогда 
			//добавим пустой фильтр 
			массивDepartmentId.Добавить(Новый Структура("DepartmentID, вклДочерниеПодразделения", "", ВключатьПодчиненныеПодазделения));
		КонецЕсли;	
		
		Возврат  массивDepartmentId;
		
	КонецФункции	
	
	&НаКлиенте
	Функция ПолучитьСтруктуруЗадачи(НаименованиеЯщика, Организация, AsyncTask)
		
		Задача=	Новый Структура();
		Задача.Вставить("НаименованиеЯщика", 	НаименованиеЯщика);
		Задача.Вставить("Организация", 			Организация);
		Задача.Вставить("Task", 				AsyncTask);
		
		Возврат Задача;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОтобразитьСтрокуСостояния(КратноеКолВоЗадач, НомерЗадачи, СчетчикСостояний, Организация)
		
		КратныйНомерЗадачи=	Цел(НомерЗадачи/20);
		Если КратныйНомерЗадачи > СчетчикСостояний Тогда
			СчетчикСостояний=	СчетчикСостояний + 1;
			Состояние("Загрузка документов организации " + Организация, СчетчикСостояний*100/?(КратноеКолВоЗадач = 0, 1, КратноеКолВоЗадач));
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьМассивАсинхронныхЗадач(СписокФильтров, ДатаНачала, ДатаОкончания, СписокЯщиков, counterAgentBoxId, ПараметрыВыборки)
		
		ПодразделениеID=					?(ПараметрыВыборки.Свойство("ПодразделениеID"), ПараметрыВыборки.ПодразделениеID, "");
		ВключатьПодчиненныеПодазделения=	?(ПараметрыВыборки.Свойство("ВключатьПодчиненныеПодазделения"), ПараметрыВыборки.ВключатьПодчиненныеПодазделения, Истина);
		ОтбиратьПоДатеДокумента=			?(ПараметрыВыборки.Свойство("ОтбиратьПоДатеДокумента"), ПараметрыВыборки.ОтбиратьПоДатеДокумента, Ложь);
		
		МассивЗадач=	Новый Массив;
		
		Для Каждого стрЯщик Из СписокЯщиков Цикл
			
			ОбработкаПрерыванияПользователя();
			
			Box=	 			ПолучитьЯщикДиадокДляОрганизации(стрЯщик.Объект, стрЯщик.ОтпечатокСертификата, стрЯщик.Значение);
			НаименованиеЯщика=	Box.name;
			массивDepartmentId=	ВернутьМассивDepartmentId(стрЯщик, Box, ПодразделениеID, ВключатьПодчиненныеПодазделения);
			
			Для каждого ФильтрАпи Из СписокФильтров Цикл
				Для каждого depId Из массивDepartmentId Цикл
					
					ОбработкаПрерыванияПользователя();
					
					Состояние("Получение документов по организации " + стрЯщик.Объект);
					
					DocumentsTask=	Box.getDocumentsTask();
					
					Если ОтбиратьПоДатеДокумента Тогда
						DocumentsTask.FromDocumentDate=			ДатаНачала;
						Если НЕ ПараметрыВыборки.Направление = "OutboundWaitingForSenderSignature" Тогда
							DocumentsTask.ToDocumentDate=		КонецДня(Датаокончания);
						КонецЕсли;
					Иначе
						DocumentsTask.FromSendDate=			ДатаНачала;
						Если НЕ ПараметрыВыборки.Направление = "OutboundWaitingForSenderSignature" Тогда
							DocumentsTask.ToSendDate=		КонецДня(Датаокончания);
						КонецЕсли;
					КонецЕсли;
					DocumentsTask.Category=					ФильтрАпи.ТипДокумента + "." + ФильтрАпи.Фильтр;
					DocumentsTask.counterAgentId=			counterAgentBoxId;
					DocumentsTask.DepartmentId=				depId.DepartmentId;
					DocumentsTask.ExcludeSubdepartments=	НЕ depId.вклДочерниеПодразделения;
					
					AsyncTask=	DocumentsTask.GetDocumentsAsync();
					Задача=		ПолучитьСтруктуруЗадачи(НаименованиеЯщика, СтрЯщик.Объект, AsyncTask);
					МассивЗадач.Добавить(Задача);
					
				КонецЦикла;
			КонецЦикла;
		Конеццикла;  
		
		Возврат МассивЗадач;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьРезультатВыполненияЗадач(МассивЗадач, ПараметрыВыборки)
		
		ПолучатьРезолюции=	?(ПараметрыВыборки.Свойство("ПолучатьРезолюции"), ПараметрыВыборки.ПолучатьРезолюции, Ложь);
		
		Результат=	Новый Массив;
		
		Пока МассивЗадач.Количество() > 0 Цикл
			
			ИндексЗадач=	0;
			Пока ИндексЗадач < МассивЗадач.Количество() Цикл
				Задача = МассивЗадач[ИндексЗадач];
				Если МассивЗадач[ИндексЗадач].Task.IsCompleted Тогда
					
					DocumentsTask=	Задача.Task.Result;
					
					КратноеКолВоЗадач=	Цел((DocumentsTask.Count-1)/10);
					СчетчикСостояний=	0;
					
					Для ИндексДокументов = 0 По DocumentsTask.Count-1 Цикл
						
						ОбработкаПрерыванияПользователя();
						
						ОтобразитьСтрокуСостояния(КратноеКолВоЗадач, ИндексДокументов, СчетчикСостояний, Задача.НаименованиеЯщика);
						
						Document = DocumentsTask.GetItem(ИндексДокументов);
						
						СтруктураДокумента = Новый Структура;
						ЗаполнитьСтрокуТаблицыДокумента(Document, СтруктураДокумента, Задача.НаименованиеЯщика, Ложь, ПолучатьРезолюции);
						Результат.Добавить(СтруктураДокумента);
						
					КонецЦикла;
					МассивЗадач.Удалить(ИндексЗадач);
					
				Иначе 
					ИндексЗадач=	ИндексЗадач + 1;
				КонецЕсли;
			КонецЦикла
		конецЦикла;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ВернутьВыборкуРНКИСчетовФактурДиадок(ДатаНачала, ДатаОкончания, Организация = Неопределено, counterAgentBoxId = "", ПараметрыВыборки) Экспорт
			
		Направление				= ?(ПараметрыВыборки.Свойство("Направление"), ПараметрыВыборки.Направление, "Inbound");
		СписокДокументооборота	= ?(ПараметрыВыборки.Свойство("СписокДокументооборота"), ПараметрыВыборки.СписокДокументооборота, 0);
		ТипыДокументов			= ?(ПараметрыВыборки.Свойство("ТипыДокументов"), ПараметрыВыборки.ТипыДокументов, "");
		
		Если НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
			ДатаОкончания = Дата("20500101");
		КонецЕсли;
		
		СписокФильтров = ПолучитьСписокФильтровДляДокументовДиадок(Направление, СписокДокументооборота, ТипыДокументов);
		
		СписокЯщиков= МетодСервера(,"ПолучитьТаблицуЯщиковДиадокОрганизации", Организация, Платформа.ПараметрыКлиент.ОрганизацииФилиалыСопоставленныеСДиадоком, Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.ПредставлениеПользователя);
		МассивЗадач	= ПолучитьМассивАсинхронныхЗадач(СписокФильтров, ДатаНачала, ДатаОкончания, СписокЯщиков, counterAgentBoxId, ПараметрыВыборки);
		Результат	= ПолучитьРезультатВыполненияЗадач(МассивЗадач, ПараметрыВыборки);
				
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьМассивСтруктурКонтент() Экспорт
		
		МассивЗадачПолучениеКонтента = Новый Массив;
		
		Если НЕ МассивСтруктурКонтент = Неопределено Тогда
		
			Для каждого СтрокаМассива Из МассивСтруктурКонтент Цикл
				
				ОбработкаПрерыванияПользователя();
				
				Пока МассивЗадачПолучениеКонтента.Количество() = МаксимальноеКоличествоЗаданий Цикл
					ОбработатьМассивЗадачКонтента(МассивЗадачПолучениеКонтента, МассивСтруктурКонтент);
				КонецЦикла;

				Попытка
					ДобавитьЗадачуПолучитьКонтент(МассивЗадачПолучениеКонтента, СтрокаМассива.DocumentId, СтрокаМассива.BoxId, СтрокаМассива.Document);
				Исключение
										
				КонецПопытки;
				
			КонецЦикла;
			
			Пока НЕ МассивЗадачПолучениеКонтента.Количество() = 0 Цикл
				ОбработатьМассивЗадачКонтента(МассивЗадачПолучениеКонтента, МассивСтруктурКонтент);
			КонецЦикла;
			
		Иначе	
			
		КонецЕсли;
		
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДобавитьЗадачуПолучитьКонтент(МассивЗадачПолучениеКонтента, DocumentId, BoxId, Document)
		
		Попытка
			
			ЭтоНеформализованныйДокумент = ?(НЕ Найти(Document.Type,"Nonformalized")=0 ИЛИ Document.Type="Contract", Истина, Ложь);
			Если НЕ ЭтоНеформализованныйДокумент Тогда 
			
				СтруктураЗадачи = Новый Структура();
				СтруктураЗадачи.Вставить("DocumentId", DocumentId);
				СтруктураЗадачи.Вставить("BoxId", BoxId);
				СтруктураЗадачи.Вставить("Task", Document.GetContentAsync());
			
				МассивЗадачПолучениеКонтента.Добавить(СтруктураЗадачи);
			
			КонецЕсли;
			
		Исключение
			
		КонецПопытки;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработатьМассивЗадачКонтента(МассивЗадачПолучениеКонтента, МассивСтруктурКонтент)
		
		МассивУдаляемых = Новый Массив();
		
		Для каждого ЗадачаПолучениеКонтента Из МассивЗадачПолучениеКонтента Цикл
			Если ЗадачаПолучениеКонтента.Task.IsCompleted Тогда
				
				Попытка
					
					DocumentContent = ЗадачаПолучениеКонтента.Task.Result;
					
					СтрокаМассива = ПолучитьЗначениеКонтентаКэш(ЗадачаПолучениеКонтента.BoxId, ЗадачаПолучениеКонтента.DocumentId);
					
					Если НЕ СтрокаМассива = Неопределено Тогда
						СтрокаМассива.Content = DocumentContent;
					Иначе
						
					КонецЕсли;
														
				Исключение
					
				КонецПопытки;		
				
				МассивУдаляемых.Добавить(ЗадачаПолучениеКонтента);
				
			КонецЕсли;
		КонецЦикла;
		
		Для каждого УдаляемыйЭлемент Из МассивУдаляемых Цикл
			Индекс=	МассивЗадачПолучениеКонтента.Найти(УдаляемыйЭлемент);
			Если НЕ Индекс = Неопределено Тогда
				МассивЗадачПолучениеКонтента.Удалить(Индекс);
			КонецЕсли;
		КонецЦикла;
		
	КонецПроцедуры
		
	&НаКлиенте
	Функция ПолучитьЗначениеКонтентаКэш(BoxID, DocumentId) Экспорт
		й=1;
		Если МассивСтруктурКонтент.Количество()=0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Для каждого СтрокаМассива из МассивСтруктурКонтент Цикл
			
			Если СтрокаМассива.BoxID = BoxID
				И СтрокаМассива.DocumentId = DocumentId Тогда
				
				Возврат СтрокаМассива;
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Неопределено;
		
	КонецФункции
			
	&НаКлиенте
	Процедура ЗаполнитьСтрокуТаблицыДокумента(док, строкаДок, НаименованиеЯщика=неопределено, заполнятьПервичныйДокумент = истина, ПолучатьРезолюции =  Ложь) Экспорт
			
		строкаДок.Вставить("ТипДокумента", ПредставлениеТипаЭД(Док));
		строкаДок.Вставить("BoxID", "" + Док.OrganizationID);
		строкаДок.Вставить("DocumentId", "" + Док.DocumentId);
		строкаДок.Вставить("DocumentType", Док.Type);
		строкаДок.Вставить("CounteragentId", Док.counteragent.id);
		строкаДок.Вставить("Дата", ?(Док.DocumentDate<'20000101',"" , Док.DocumentDate));
		строкаДок.Вставить("Номер", ПредсталениеНомераЭД(Док));
		строкаДок.Вставить("Продавец", ПредставлениеПродавца(Док, НаименованиеЯщика)); 
		строкаДок.Вставить("Покупатель", ПредставлениеПокупателя(Док, НаименованиеЯщика)); 
		строкаДок.Вставить("Подразделение", ПредставлениеПодразделения(Док));
		строкаДок.Вставить("Сумма", ПредставлениеСуммы(Док));
		строкаДок.Вставить("СостояниеДокументооборота", ПредставлениеСтатуса(Док));
		строкаДок.Вставить("СостояниеСогласования", ПредставлениеСтатусаСогласования(Док));
		строкаДок.Вставить("СостояниеПередачиЧерезРоуминг", ПредставлениеСтатусаРоуминг(Док));
		строкаДок.Вставить("СостояниеПередачиЧерезРоумингДетали", ?(Док.RoamingNotificationStatus = "RoamingNotificationStatusError", Док.RoamingNotificationStatusDescription, ""));
		строкаДок.Вставить("Status", "" + Док.Status);
		строкаДок.Вставить("ЭтоТестовыйДок", Док.IsTest);
		
		Если Док.RoamingNotificationStatus <> "RoamingNotificationStatusNone" И Док.RoamingNotificationStatus <> "RoamingNotificationStatusError" Тогда
			строкаДок.СостояниеДокументооборота= строкаДок.СостояниеПередачиЧерезРоуминг + ?(ЗначениеЗаполнено(строкаДок.СостояниеДокументооборота), " " + строкаДок.СостояниеДокументооборота, "");
			строкаДок.СостояниеПередачиЧерезРоуминг= "";
		КонецЕсли;
		
		Если МассивСтруктурКонтент=Неопределено Тогда
			МассивСтруктурКонтент = Новый Массив;
		КонецЕсли;
		
		МассивСтруктурКонтент.Добавить(Новый Структура("BoxID, DocumentId, Document, Content", Док.OrganizationID, Док.DocumentId, Док));
				
		Department = Док.Department;
		
		Resolutions=	Новый  Массив();
		Если ПолучатьРезолюции Тогда
			Если док.Type = "XmlTorg12" ИЛИ док.Type = "XmlAcceptanceCertificate" Тогда
				Для Индекс = 0 По Док.Resolutions.Count - 1 Цикл
					Резолюция=	Док.Resolutions.GetItem(Индекс);
					Resolutions.Добавить(Новый Структура("Author, CreationDate, ResolutionType", Резолюция.Author, Резолюция.CreationDate, Резолюция.ResolutionType));
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		строкаДок.Вставить("Resolutions", Resolutions);
		
		Если найти(док.Type,"Invoice")>0  Тогда
			строкаДок.Вставить("СуммаНДС",ПредставлениеСуммыНДС(док));
			строкаДок.Вставить("ДатаУчета",док.ConfirmationDate);
		Иначе
			строкаДок.Вставить("СуммаНДС",0);
			строкаДок.Вставить("Валюта","");
			строкаДок.Вставить("ДатаУчета",док.Timestamp+4*60*60);  // Timestamp - по UTC, переводим в МСК +4 часа
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьЯщикДиадокДляОрганизации(Организация, ОтпечатокСертификата=неопределено, BoxID=неопределено, Connection = неопределено) Экспорт
		
		Если BoxID = неопределено тогда
			BoxID= МетодСервера(,"Организация_2_BoxID", Организация);
			Если BoxID = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		ВыборкаПоОрганизации =  НайтиСтрокиКонтекстДиадокаПоОтбору(новый Структура("Организация", Организация));
		Если ВыборкаПоОрганизации.количество()>0 тогда 
			Connection =    ВыборкаПоОрганизации[0].Connection;
			возврат ВыборкаПоОрганизации[0].Box
		КонецЕсли;
		
		Connection = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection;
		Box = Connection.GetOrganizationById(BoxID);  
		Если Box.AuthenticateType = "Certificate" Тогда
			Magic = Box.GetReceiptGenerationProcess();
			Magic.start();
		КонецЕсли;
		
		СтрокаОрганизации = Новый Структура("Организация,ОтпечатокСертификата,Connection,Box,BoxId,Magic");
		СтрокаОрганизации.Организация 			= Организация;
		СтрокаОрганизации.ОтпечатокСертификата 	= ?(НЕ box.Certificate = Неопределено, box.Certificate.Thumbprint, "");
		СтрокаОрганизации.Connection 			= Connection;
		СтрокаОрганизации.Box					= Box;
		СтрокаОрганизации.BoxId					= Box.Id;
		СтрокаОрганизации.Magic					= Magic;
		
		Платформа.ПараметрыКлиент.КонтекстДиадока.Добавить(СтрокаОрганизации);
		
		возврат Box;
		
	КонецФункции
	
	&НаКлиенте
	Функция НайтиСтрокиКонтекстДиадокаПоОтбору(СтруктураОтбор)
		Возврат НайтиСтрокиВМассивеСтруктур(Платформа.ПараметрыКлиент.КонтекстДиадока, СтруктураОтбор);//КонтекстДиадока.НайтиСтроки(СтруктураОтбор);
	КонецФункции
	
	&НаКлиенте
	Функция РазвернутьGUID(СтрНач)
		
		СтрРез = "";
		Если СтрДлина(Стрнач) > 36 Тогда
			СтрРез = лев(Стрнач, 32);
			СтрРез = Сред(СтрРез, 1, 8) + "-" + Сред(СтрРез, 9, 4) + "-" + Сред(СтрРез, 13, 4) + "-" + Сред(СтрРез, 17, 4) + "-" + Сред(СтрРез, 21, 12);
		ИначеЕсли СтрДлина(Стрнач) < 36 Тогда
			СтрРез = Сред(Стрнач, 1, 8) + "-" + Сред(Стрнач, 9, 4) + "-" + Сред(Стрнач, 13, 4) + "-" + Сред(Стрнач, 17, 4) + "-" + Сред(Стрнач, 21, 12);
		Иначе
			СтрРез = СтрНач;
		КонецЕсли;
		
		Возврат СтрРез;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьURLНаДокументДиадока(BoxId, DocumentId)  
		
		MessageId = Сред(DocumentId, 1, 36);
		EntityId = Сред(DocumentId, 37, 36);
		
		Возврат  РазвернутьGUID(BoxId) + "/Document/Show?letterId=" + РазвернутьGUID(MessageId) + "&documentId=" + РазвернутьGUID(EntityId);
		
	КонецФункции
	
	&НаСервере
	Функция ПолучитьПутьКWEBСерверу()
		
		Возврат МетодСервера(,"ПолучитьПутьКWEBСерверу");
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОткрытьСтраницуВДиадоке(ПутьПослеСлэша)
		
		ТекстURL = ПолучитьПутьКWEBСерверу() + ПутьПослеСлэша;
		
		//// Открытие ссылки в IE
		//Попытка
		//	IE=	Новый COMОбъект("InternetExplorer.Application"); 
		//Исключение
		//	Сообщить("Ошибка при открытии приложения Internet Explorer");
		//	Сообщить(ОписаниеОшибки());
		//	Возврат;
		//КонецПопытки;
		//
		//IE.Visible=	True; 
		//IE.Navigate2(ТекстURL);
		
		ЗапуститьПриложение(текстURL);
		
	КонецПроцедуры 
	
	&НаКлиенте
	Процедура  ПоказатьДокументВДиадоке(OrganizationId, DocumentId) Экспорт
		
		ТекстURL = ПолучитьURLНаДокументДиадока(OrganizationId, DocumentId);
		ОткрытьСтраницуВДиадоке(ТекстURL);
		
	КонецПроцедуры 
	
	&НаСервере
	Функция ОбновитьДанныеДокумента(ДокументСсылка, DocumentDate, DocumentNumber)
		
		Возврат МетодСервера(,"ОбновитьДанныеДокумента", ДокументСсылка, DocumentDate, DocumentNumber);
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОбработчикСозданиеДокумента(ДокументССылка, ЭДОбъект) Экспорт
		
		РезультатОбновления=	ОбновитьДанныеДокумента(ДокументССылка, ЭДОбъект.DocumentDate, ЭДОбъект.DocumentNumber);
		Если РезультатОбновления.ДанныеОбновлены = Истина Тогда
			ОтобразитьИзменениеДанных(ДокументССылка, ВидИзмененияДанных.Изменение);
		КонецЕсли;
		
		Если НЕ РезультатОбновления.ОписаниеОшибки = "" Тогда
			ПоказатьПредупреждение(, РезультатОбновления.ОписаниеОшибки, 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
		КонецЕсли;
		
		МетодСервера(,"Установить_DocumentID_Для_Документ", ДокументССылка, ЭДОбъект.DocumentId, ЭДОбъект.OrganizationId);
		
		ПараметрыОповещения=	Новый Структура;
		ПараметрыОповещения.Вставить("ТипСущности", "Документ");
		ПараметрыОповещения.Вставить("BoxID", 		ЭДОбъект.OrganizationID);
		ПараметрыОповещения.Вставить("DocumentID", 	ЭДОбъект.DocumentID);
		ПараметрыОповещения.Вставить("Документ1С",	ДокументССылка);
		
		МетодКлиента(,"ОповеститьФормы", "ИзменениеСвязиДД1С", ПараметрыОповещения, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьContent(Источник, Приемник)
		
		Для каждого ЭлементИсточника Из Источник Цикл
			Попытка
				Если ТипЗнч(ЭлементИсточника.Значение) = Тип("Структура") Тогда
					ЗаполнитьContent(ЭлементИсточника.Значение, Приемник[ЭлементИсточника.Ключ]);
				Иначе
					Приемник[ЭлементИсточника.Ключ]=	ЭлементИсточника.Значение;
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЦикла;
		
	КонецПроцедуры

	&НаКлиенте
	Функция ПолучитьПараметрыПодписанияТ12вДиадок(ФИОПодписанта) экспорт 
		
		СпособПодписания = МетодСервера(,"ПолучитьНастройкуПользователя",МетодКлиента("Модуль_Клиент","ИдентификаторСвойства", "ИдентификаторСвойстваСпособПодписанияТ12"));
		СпособПодписания = ?(СпособПодписания="", "1", СпособПодписания);
		
		ДолжностьПодписанта = МетодСервера(,"ПолучитьНастройкуПользователя",МетодКлиента("Модуль_Клиент","ИдентификаторСвойства", "ИдентификаторСвойстваДолжностьПодписанта"));
		
		Результат = новый Структура;
		Результат.Вставить("СпособПодписания", СпособПодписания);
		Результат.Вставить("ДолжностьПодписанта",  ДолжностьПодписанта);
		Результат.Вставить("ФИОПодписанта", ФИОПодписанта);
		
		Возврат Результат;
		
	КонецФункции
	
//} ИНТЕГРАЦИЯ
////////////////////////////////////////////////////////////////////////////////

//{ ИНТЕРАКТИВНЫЕ ДЕЙСТВИЯ

	&НаКлиенте
	Функция ОтправитьНаОбработку(ЭДОбъект, ПараметрыОтправкиНаСогласование) Экспорт
		
		Если НЕ ПараметрыОтправкиНаСогласование = Неопределено Тогда
			
			Если ПустаяСтрока(ПараметрыОтправкиНаСогласование.TargetUserId) И ПустаяСтрока(ПараметрыОтправкиНаСогласование.TargetDepartmentId) Тогда
				
				ПараметрыФормы=	Новый Структура();
				ПараметрыФормы.Вставить("Заголовок", 		"Ошибка работы с контрагентом");
				ПараметрыФормы.Вставить("ОписаниеОшибки", 	"Не указан получатель документа.");
				ПараметрыФормы.Вставить("Подробности", 		"Не указан получатель документа.");
				
				МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы,);

				Возврат Ложь;
				
			Иначе
				
				//Пока неясно как получить ИД самого себя, так как себе же нельзя отправить на согласование
				Попытка
					
					ЗаданиеНаСогласование=							ЭДОбъект.CreateResolutionRequestTask();
					ЗаданиеНаСогласование.ResolutionRequestType=	ПараметрыОтправкиНаСогласование.ResolutionRequestType;
					Если ЗначениеЗаполнено(ПараметрыОтправкиНаСогласование.TargetUserId) Тогда
						ЗаданиеНаСогласование.TargetUserId=			ПараметрыОтправкиНаСогласование.TargetUserId;
					ИначеЕсли ЗначениеЗаполнено(ПараметрыОтправкиНаСогласование.TargetDepartmentId) Тогда
						ЗаданиеНаСогласование.TargetDepartmentId=	ПараметрыОтправкиНаСогласование.TargetDepartmentId;
					КонецЕсли;
					ЗаданиеНаСогласование.Comment=					ПараметрыОтправкиНаСогласование.Комментарий;
					ЗаданиеНаСогласование.Send();
					
					Возврат Истина;
					
				Исключение
					
					ОписаниеОшибки=	ОписаниеОшибки();
					Если Найти(ОписаниеОшибки, "Resolution request author user id and target user id must differ")>0 Тогда
						ОписаниеОшибки=	"Нельзя отправить документ на согласование или подписание самому себе";
					ИначеЕсли Найти(ОписаниеОшибки, "User can not add signature request to document")>0
						ИЛИ Найти(ОписаниеОшибки, "User can not add approvement request to document")>0
						ИЛИ Найти(ОписаниеОшибки, "document that cannot be rejected")>0 Тогда
						ОписаниеОшибки=	"Документ уже отправлен на подписание или согласование";
					Иначе
						ОписаниеОшибки=	"Неизвестная ошибка обработки";
					КонецЕсли;
					
					ПараметрыФормы=	Новый Структура();
					ПараметрыФормы.Вставить("Заголовок", 		"Ошибка обработки");
					ПараметрыФормы.Вставить("ОписаниеОшибки", 	ОписаниеОшибки);
					ПараметрыФормы.Вставить("Подробности", 		ОписаниеОшибки());
					
					МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы,);
					
					Возврат Ложь;
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПодписатьВДиадоке(Document, Подписать, BoxID, DocumentID, ТекстОтказа = "", СтруктураДляТитула = Неопределено) Экспорт
		
		Если Подписать = Истина Тогда 
			
			Если СтруктураДляТитула = Неопределено Тогда
				Document.Accept();
			Иначе
				
				ТекстОшибки = ПроверитьТитулПолучателяТорг12(СтруктураДляТитула);
				Если ТекстОшибки <>"" Тогда
					ВызватьИсключение("##9[]" + ТекстОшибки);
				КонецЕсли;	
				
				Если Document.Type = "Nonformalized" Тогда
					Document.Accept();
				Иначе
					ReplySendTask=	Document.CreateReplySendTask();
					ЗаполнитьContent(СтруктураДляТитула, ReplySendTask.Content);
					ReplySendTask.Send();
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе 	
			Document.Reject(ТекстОтказа);
		КонецЕсли;	
		
	КонецПроцедуры

	&НаКлиенте
	Функция ПолучитьКраткоеИмяФайла(ПолноеИмяФайла)
		
		Результат =  ПолноеИмяФайла;
		Пока Найти(Результат, "\")>0 цикл 
			Результат = Прав(Результат, СтрДлина(Результат) -  Найти(Результат, "\"));
		КонецЦикла;
		
		Возврат Результат;
		
		
	КонецФункции	
	
	&НаКлиенте
	Функция ОтправитьПринятьПриглашениеКонтрагенту(Organization, CounteragentId, ИНН, Комментарий, ИмяФайла = "", КонтрагентСсылка = Неопределено) Экспорт
		
		Результат=	Ложь;
		
		Если ЗначениеЗаполнено(ИмяФайла) Тогда
			
			TrustConnectionRequest=					Organization.CreateAcquireCounteragentTask(ИмяФайла);
			
			TrustConnectionRequest.CounteragentBoxId= CounteragentId;
			TrustConnectionRequest.Inn  			= ИНН;
			TrustConnectionRequest.Message 			= Комментарий;
			TrustConnectionRequest.FileName 		= ПолучитьКраткоеИмяФайла(ИмяФайла);
			
			Попытка
				TrustConnectionRequest.SendAsync();
				Результат=	Истина;
			Исключение 
				
				ПараметрыФормы=	Новый Структура();
				ПараметрыФормы.Вставить("Заголовок", 		"Ошибка работы с контрагентом");
				ПараметрыФормы.Вставить("ОписаниеОшибки", 	"Не удалось обработать контрагента");
				ПараметрыФормы.Вставить("Подробности", 		ОписаниеОшибки());
				
				МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы,);

				Результат=	Ложь;
			КонецПопытки;
			
		Иначе
			
			Counteragent=	Organization.GetCounteragentById(CounteragentId);
			Попытка
				Counteragent.AcquireCounteragent(Комментарий);
				Результат=	Истина;
			Исключение 
				
				ПараметрыФормы=	Новый Структура();
				ПараметрыФормы.Вставить("Заголовок", 		"Ошибка работы с контрагентом");
				ПараметрыФормы.Вставить("ОписаниеОшибки", 	"Не удалось обработать контрагента");
				ПараметрыФормы.Вставить("Подробности", 		ОписаниеОшибки());
				
				МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы,);

				Результат=	Ложь;
			КонецПопытки;
			
		КонецЕсли;
		
		Если НЕ КонтрагентСсылка = Неопределено Тогда
			УстановитьРеквизитКонтрагента(КонтрагентСсылка, CounteragentId);
		КонецЕсли;	
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ВыбратьПараметрыПолучателяПриОтправкеНаСогласование(ОбъектВладелецОбработчика, ТекущиеДанные, ResolutionRequestType, Режим) Экспорт
		
		Organization = ПолучитьЯщикДиадокДляОрганизации(ТекущиеДанные.Организация);
		
		ПараметрыФормы=	Новый Структура();
		ПараметрыФормы.Вставить("ResolutionRequestType", 	ResolutionRequestType);
		ПараметрыФормы.Вставить("Режим", 					Режим);
		ПараметрыФормы.Вставить("Организация", 				ТекущиеДанные.Организация);
		ПараметрыФормы.Вставить("OrganizationId", 			Organization.Id);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораПолучателя", ПараметрыФормы,, "ОбработчикОткрытиеФормыВыбораПолучателя", ТекущиеДанные, ОбъектВладелецОбработчика);

	КонецПроцедуры
	
	// { RGS TChubarova 16.05.2016 0:53:03 - Диадок Безводинских Согласование файлов
	&НаКлиенте
	Процедура ВыбратьПараметрыПолучателяПриОтправкеНаСогласованиеНеформализ(ОбъектВладелецОбработчика, ОрганизацияОтправитель, ResolutionRequestType, Режим) Экспорт
		
		Organization = ПолучитьЯщикДиадокДляОрганизации(ОрганизацияОтправитель);
		
		ПараметрыФормы=	Новый Структура();
		//ПараметрыФормы.Вставить("Конфигурация", 			Параметры.Конфигурация);
		ПараметрыФормы.Вставить("ResolutionRequestType", 	ResolutionRequestType);
		ПараметрыФормы.Вставить("Режим", 					Режим);
		ПараметрыФормы.Вставить("Организация", 				ОрганизацияОтправитель);
		ПараметрыФормы.Вставить("OrganizationId", 			Organization.Id);
		
		// { RGS TChubarova 16.05.2016 2:02:54 - 
		//ОткрытьФормуОбъектаМодально(ПутьКФормам + "ФормаВыбораПолучателя", ПараметрыФормы, "ОбработчикОткрытиеФормыВыбораПолучателяНеформализ", ОрганизацияОтправитель, ОбъектВладелецОбработчика);
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораПолучателя", ПараметрыФормы,, "ОбработчикОткрытиеФормыВыбораПолучателяНеформализ", ОрганизацияОтправитель, ОбъектВладелецОбработчика);
		// } RGS TChubarova 16.05.2016 2:02:57 - 
	КонецПроцедуры 	// } RGS TChubarova 16.05.2016 0:53:03 - Диадок Безводинских Согласование файлов
	
	&НаКлиенте
	Процедура ОтправитьЭДОбъектНаСогласование(ОбъектВладелецОбработчика, Организация, ResolutionRequestType, Режим) Экспорт
		
		ВыбратьПараметрыПолучателяПриОтправкеНаСогласование(ОбъектВладелецОбработчика, Организация, ResolutionRequestType, Режим);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПодтверждениеВыполненияОперации(ИмяОбработчика, ВладелецОбработчика, ПараметрыОбработчика= Неопределено) Экспорт
		
		ПоказатьВопрос(Новый ОписаниеОповещения(ИмяОбработчика, ВладелецОбработчика, ПараметрыОбработчика), "Подтвердите выполнение операции", РежимДиалогаВопрос.ОКОтмена, 120, КодВозвратаДиалога.Отмена);
		
	КонецПроцедуры
	
//} ИНТЕРАКТИВНЫЕ ДЕЙСТВИЯ

//{ ПОЛУЧИТЬ...

	&НаСервере
	Функция ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадокСервер(МассивСсылок, OrganizationId)
		
		Результат=	Новый Массив;
		
		Для Каждого InitialDocumentId Из МассивСсылок Цикл
			СсылкаРНК = ПолучитьDocumentID_2_Документ(InitialDocumentId, OrganizationId);
			Если НЕ СсылкаРНК = Неопределено Тогда
				Результат.Добавить(СсылкаРНК);
			КонецЕсли;
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	функция ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадок(ОбъектДиадок) Экспорт
		
		InitialDocumentIds=	Новый Массив;
		Для Индекс = 0 По ОбъектДиадок.InitialDocumentIds.Count - 1 Цикл
			InitialDocumentIds.Добавить(ОбъектДиадок.InitialDocumentIds.GetItem(Индекс));
		КонецЦикла;
		
		OrganizationId=		ОбъектДиадок.Organization.Id;
		МассивСсылок=		ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадокСервер(InitialDocumentIds, OrganizationId);
		
		Возврат МассивСсылок;
		
	КонецФункции	
		
	&НаСервереБезКонтекста
	Функция НайтиПодходящийСчетФактуруИзРНК(Знач МассивСсылокРНК, Знач ИдентификаторДокументаВДиадок)
		
		Запрос=			Новый Запрос;
		Запрос.Текст=	
		"ВЫБРАТЬ
		|	СчетФактура.Ссылка,
		|	ВЫБОР
		|		КОГДА УжеСопоставленные.Значение ЕСТЬ NULL 
		|				ИЛИ УжеСопоставленные.Значение = """"
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК УжеСопоставлен
		|ИЗ
		|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактура
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ДополнительныеСведения.Объект КАК Объект,
		|			ДополнительныеСведения.Свойство КАК Свойство,
		|			ДополнительныеСведения.Значение КАК Значение
		|		ИЗ
		|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|		ГДЕ
		|			ДополнительныеСведения.Свойство = &Свойство) КАК УжеСопоставленные
		|		ПО СчетФактура.Ссылка = УжеСопоставленные.Объект
		|ГДЕ
		|	СчетФактура.ДокументОснование В (&МассивСсылокРНК)";
		
		Запрос.УстановитьПараметр("МассивСсылокРНК", МассивСсылокРНК);
		Запрос.УстановитьПараметр("Свойство", ИдентификаторДокументаВДиадок);
		
		РезультатЗапроса=	Запрос.Выполнить();
		Выборка=			РезультатЗапроса.Выбрать();
		
		Возврат Выборка;
		
	КонецФункции

	&НаСервере
	Функция ПолучитьПодходящуюСФ(МассивСсылокРНК)
		
		ИдентификаторДокументаВДиадок=	ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(МетодСервера(,"ИдентификаторСвойстваИдентификаторДокументаВДиадок"));
		
		ВыборкаПодходящихСФ=	НайтиПодходящийСчетФактуруИзРНК(МассивСсылокРНК, ИдентификаторДокументаВДиадок);
		Если ВыборкаПодходящихСФ.Количество() > 0 Тогда
			ВыборкаПодходящихСФ.Следующий();
			Возврат ВыборкаПодходящихСФ.Ссылка;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьМассивТиповДокументов(ФильтрПоТипамДокументов, ВидТипаДокумента)
		
		//ВидТипаДокумента = 0  все виды 	
		//ВидТипаДокумента = 1  документооборот СФ
		//ВидТипаДокумента = 2  строго двусторонний документ
		//ВидТипаДокумента = 3  строго односторонний документ
		//ВидТипаДокумента = 4  односторонний или двусторонний документ
		
		МассивТиповДокументов = Новый Массив;
		
		Если ФильтрПоТипамДокументов= "" И ВидТипаДокумента=0 Тогда
			МассивТиповДокументов.Добавить("Any");
			Возврат МассивТиповДокументов;			
		КонецЕсли;
		
		Если ФильтрПотипамДокументов = "" ИЛИ ФильтрПотипамДокументов = "1" Тогда // Накладные
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
				МассивТиповДокументов.Добавить("NonformalizedTorg12");
				МассивТиповДокументов.Добавить("NonformalizedAcceptanceCertificate");
				МассивТиповДокументов.Добавить("XmlTorg12");
				МассивТиповДокументов.Добавить("XmlAcceptanceCertificate");
			КонецЕсли;	
		ИначеЕсли ФильтрПотипамДокументов = "11" Тогда // только Торг-12
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
				МассивТиповДокументов.Добавить("NonformalizedTorg12");
				МассивТиповДокументов.Добавить("XmlTorg12");
			КонецЕсли;	
		ИначеЕсли ФильтрПотипамДокументов = "12" Тогда // только Акт
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
				МассивТиповДокументов.Добавить("NonformalizedAcceptanceCertificate");
				МассивТиповДокументов.Добавить("XmlAcceptanceCertificate");
			КонецЕсли;	
		КонецЕсли;		
		
		Если ФильтрПотипамДокументов = "" ИЛИ ФильтрПотипамДокументов=2 Тогда // Счет-фактуры
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=1 Тогда
				МассивТиповДокументов.Добавить("AnyInvoiceDocumentType");
	   		КонецЕсли;	   
	   	ИначеЕсли ФильтрПотипамДокументов = "21" Тогда // только Счет-фактура
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=1 Тогда
	    		МассивТиповДокументов.Добавить("Invoice");
			КонецЕсли;	   
	   	ИначеЕсли ФильтрПотипамДокументов = "22" Тогда // только ИСФ
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=1 Тогда
				МассивТиповДокументов.Добавить("InvoiceRevision");
	       	КонецЕсли; 	   
	   	ИначеЕсли ФильтрПотипамДокументов = "23" Тогда // только КСФ и ИКСФ
	   		Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=1 Тогда
	    		МассивТиповДокументов.Добавить("InvoiceCorrection");
	    	   	МассивТиповДокументов.Добавить("InvoiceCorrectionRevision");
	       	КонецЕсли;   
	   	КонецЕсли;
	   
	   	Если ФильтрПотипамДокументов = "" ИЛИ ФильтрПотипамДокументов = "3" Тогда // Счет на оплату
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=3 Тогда
	    		МассивТиповДокументов.Добавить("NonformalizedProforma");
	    	КонецЕсли;	
	   	КонецЕсли;
	   
	   	Если ФильтрПотипамДокументов = "" ИЛИ ФильтрПотипамДокументов = "4" Тогда 	// Неформализованные документы
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда 			//строго двусторонний док
	    		МассивТиповДокументов.Добавить("ReconciliationAct"); 		//акт сверки
	    		МассивТиповДокументов.Добавить("Contract"); 				//договор
	    		МассивТиповДокументов.Добавить("TrustConnectionRequest");	//приглашение к ЭДО
	    		МассивТиповДокументов.Добавить("PriceList"); 				//ценовой лист
	    	КонецЕсли;	
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=3 Тогда 		//строго односторонний док
	    		МассивТиповДокументов.Добавить("ServiceDetails"); 		//детализация
	    	КонецЕсли;	
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=4 Тогда 		//как односторонний так и двусторонний док
	    		МассивТиповДокументов.Добавить("PriceListAgreement"); 	//протокол согласования цены
	    		МассивТиповДокументов.Добавить("CertificateRegistry"); 	//реестр сертификатов
	    		МассивТиповДокументов.Добавить("Nonformalized");  		//прочее
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "41" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
	    		МассивТиповДокументов.Добавить("ReconciliationAct");
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "42" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=3 Тогда
	    		МассивТиповДокументов.Добавить("ServiceDetails");
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "43" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
	    		МассивТиповДокументов.Добавить("Contract"); 
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "44" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=4 Тогда
	    		МассивТиповДокументов.Добавить("PriceListAgreement");
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "45" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
	    		МассивТиповДокументов.Добавить("TrustConnectionRequest");
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "46" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=4 Тогда
	    		МассивТиповДокументов.Добавить("CertificateRegistry");
	    	КонецЕсли;	
	   //ИначеЕсли ФильтрПотипамДокументов = "47" Тогда
	   // 	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
	   // 		МассивТиповДокументов.Добавить("PriceList"); 
	   // 	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "47" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=4 Тогда
	    		МассивТиповДокументов.Добавить("Nonformalized");
	    	КонецЕсли;	
	    КонецЕсли;
		
		Возврат МассивТиповДокументов;
		
	КонецФункции
	
	&НаСервереБезКонтекста
	Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита)
		
		Возврат Ссылка[ИмяРеквизита];
		
	КонецФункции

	&НаСервере
	Функция ПолучитьНастройкиОрганизации(Организация)
				
		СтруктураНастройки = Новый Структура();
		СтруктураНастройки.Вставить("Организация", Организация);
		
		ПечатнаяФормаДляТоваров = МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваПечатнаяФормаДляТоваров"));
		СтруктураНастройки.Вставить("ПечатнаяФормаДляТоваров", 		?(ЗначениеЗаполнено(ПечатнаяФормаДляТоваров), ПечатнаяФормаДляТоваров, "Торг-12"));
		
		ПечатнаяФормаДляУслуг = МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваПечатнаяФормаДляУслуг"));
		СтруктураНастройки.Вставить("ПечатнаяФормаДляУслуг", 		?(ЗначениеЗаполнено(ПечатнаяФормаДляУслуг), ПечатнаяФормаДляУслуг, "Акт"));
		СтруктураНастройки.Вставить("ОтправкаНепроведенных", 		МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваОтправлятьНеПроведенные")));
		
		СтруктураНастройки.Вставить("ФормироватьСчетНаОсновании", 	МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваФормироватьСчетНаОсновании")));
		ИДВнешнейПечатнойФормыСчета = МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваВнешняяПечатнаяФормаСчета"));
		Если ЗначениеЗаполнено(ИДВнешнейПечатнойФормыСчета) = Истина Тогда
			ВнешняяПечатнаяФормаСчета =	Справочники.ДополнительныеОтчетыИОбработки.ПолучитьСсылку(Новый УникальныйИдентификатор(ИДВнешнейПечатнойФормыСчета));
		Иначе
			ВнешняяПечатнаяФормаСчета =	Неопределено;
		КонецЕсли;
		СтруктураНастройки.Вставить("ВнешняяПечатнаяФормаСчета", ВнешняяПечатнаяФормаСчета);
		
		ИДВнешнейПечатнойФормыАкта = МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваВнешняяПечатнаяФормаАкта"));
		Если ЗначениеЗаполнено(ИДВнешнейПечатнойФормыАкта) = Истина Тогда
			ВнешняяПечатнаяФормаАкта = Справочники.ДополнительныеОтчетыИОбработки.ПолучитьСсылку(Новый УникальныйИдентификатор(ИДВнешнейПечатнойФормыАкта));
		Иначе
			ВнешняяПечатнаяФормаАкта = Неопределено;
		КонецЕсли;
		СтруктураНастройки.Вставить("ВнешняяПечатнаяФормаАкта", ВнешняяПечатнаяФормаАкта);
		СтруктураНастройки.Вставить("СпособФормированияАкта", МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваСпособФормированияАкта")));

		ИДВнешнейПечатнойФормыАктаСверки = МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваВнешняяПечатнаяФормаАктаСверки"));
		Если ЗначениеЗаполнено(ИДВнешнейПечатнойФормыАктаСверки) = Истина Тогда
			ВнешняяПечатнаяФормаАктаСверки = Справочники.ДополнительныеОтчетыИОбработки.ПолучитьСсылку(Новый УникальныйИдентификатор(ИДВнешнейПечатнойФормыАктаСверки));
		Иначе
			ВнешняяПечатнаяФормаАктаСверки = Неопределено;
		КонецЕсли;
		СтруктураНастройки.Вставить("ВнешняяПечатнаяФормаАктаСверки", ВнешняяПечатнаяФормаАктаСверки);
		СтруктураНастройки.Вставить("СпособФормированияАктаСверки", МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваСпособФормированияАктаСверки")));
		
		МассивДополнительныхВнешнихПечатныхФорм = ПолучитьМассивДополнительныхВнешнихПечатныхФорм(Организация);
		СписокДополнительныхВнешнихПечатныхФорм = Новый СписокЗначений;
		Для каждого ВнешняПФ из МассивДополнительныхВнешнихПечатныхФорм Цикл
			СписокДополнительныхВнешнихПечатныхФорм.Добавить(ВнешняПФ, ВнешняПФ.Наименование);
		КонецЦикла;
		
		СтруктураНастройки.Вставить("СписокДополнительныхВнешнихПечатныхФорм", СписокДополнительныхВнешнихПечатныхФорм);
		
		
		СтруктураНастройки.Вставить("ПодставлятьКППГрузоотправителя",	МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваПодставлятьКППГрузоотправителя")));
		СтруктураНастройки.Вставить("ТекстКомментарияДиадок",			МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификатрСвойстваТекстКомментарияДиадок")));
				
		Возврат СтруктураНастройки;
		
	КонецФункции
	
	&НаСервере
	Функция ПолучитьМассивДополнительныхВнешнихПечатныхФорм(Организация)
		
		МассивНомеров = ПолучитьМассивНумерованныхВнешнихпечатныхФорм();
		Результат = Новый Массив;
		
		ТекущийСписокФорм = МетодСервера(,"ПолучитьЗначениеСвойства", Организация, МетодСервера(,"ИдентификаторСвойстваСписокДопВПФ"));
		Для ц = 1 по СтрДлина(ТекущийСписокФорм) цикл 
			Если Сред(ТекущийСписокФорм, ц, 1) = "1" Тогда
				ВПФ = ПолучитьВнешнююПечатнуюФормуПоНомеру(МассивНомеров, ц);
				Если НЕ ВПФ = Неопределено Тогда 
					Результат.Добавить(ВПФ);
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаСервере
	Функция ПолучитьМассивНумерованныхВнешнихПечатныхФорм()
		
		Результат = Новый массив;
		
		Запрос = Новый запрос;
		Запрос.Текст = "ВЫБРАТЬ
			|	ДополнительныеСведения.Объект 	КАК Объект,
			|	ДополнительныеСведения.Значение КАК Значение
			|ИЗ
			|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
			|ГДЕ
			|	ДополнительныеСведения.Свойство.Наименование = &Наименование
			|	И НЕ ДополнительныеСведения.Значение = """"";
			
		Запрос.УстановитьПараметр("Наименование", МетодСервера(,"ИдентификаторСвойстваНомерВПФ"));
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл 
			Результат.Добавить(Новый Структура("ВнешняяПечатнаяФорма, Номер", Выборка.Объект, Число(Выборка.Значение)));
		КонецЦикла;	
		
		Возврат Результат;
		
	КонецФункции
	
	&НаСервере
	Функция ПолучитьВнешнююПечатнуюФормуПоНомеру(МассивНомеров, ПорядковыйНомер)
		
		Для каждого Элемент из МассивНомеров Цикл
			Если Элемент.Номер = ПорядковыйНомер Тогда 
				Возврат Элемент.ВнешняяПечатнаяФорма
			КонецЕсли;
		КонецЦикла;
		
		Возврат Неопределено;
		
	КонецФункции
			
	
	МаксимальноеКоличествоЗаданий = 100;