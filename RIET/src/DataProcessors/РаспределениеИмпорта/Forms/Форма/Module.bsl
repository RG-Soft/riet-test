
// Установить период

#Если НЕ ТонкийКлиент И НЕ ВебКлиент Тогда //17.09.2012 пахоменков. В тонком клиенте это выдаст ошибку.
&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.УстановитьПериод(ДатаНач, ?(ДатаКон ='0001-01-01', ДатаКон, КонецДня(ДатаКон)));
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	Если НастройкаПериода.Редактировать() Тогда
		ДатаНач = НастройкаПериода.ПолучитьДатуНачала();
		ДатаКон = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
	
	Элементы.СписокShipmentClosing.Период.ДатаНачала = ДатаНач;
	Элементы.СписокShipmentClosing.Период.ДатаОкончания = ДатаКон; 
	Элементы.СписокShipmentClosing.Обновить();
	
КонецПроцедуры
#КонецЕсли
	
&НаКлиенте
Процедура ДатаНачПриИзменении(Элемент)
	
    Элементы.СписокShipmentClosing.Период.ДатаНачала = ДатаНач;
	Элементы.СписокShipmentClosing.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаКонПриИзменении(Элемент)
	
	ДатаКон = КонецДня(ДатаКон);
	Элементы.СписокShipmentClosing.Период.ДатаОкончания = ДатаКон; 
	Элементы.СписокShipmentClosing.Обновить();
	
КонецПроцедуры

         
&НаКлиенте
Процедура СписокShipmentClosingВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено и Поле.Имя <> "СписокShipmentClosingShipmentСlosing" Тогда 
		СтандартнаяОбработка = Ложь;
		ОткрытьЗначение(Элемент.ТекущиеДанные.РИЗП);
	КонецЕсли;
	
КонецПроцедуры


//создание РИЗП за период

&НаКлиенте
Процедура СоздатьРИЗПЗаПериод(Команда)
	
	Отказ = Ложь;
	
	Если ДатаНач = Дата(1,1,1) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не установлена дата начала периода создания документов РИЗП!",,
		                                                  "СписокShipmentClosingУстановитьИнтервал",,Отказ);
	КонецЕсли;
	
	Если ДатаКон = Дата(1,1,1) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не установлена дата окончания периода создания документов РИЗП!",,
		                                                  "СписокShipmentClosingУстановитьИнтервал",,Отказ);
	КонецЕсли;

	//добавила Федотова Л., РГ-Софт, 14.10.11  ->
	Если ДатаРИЗП = Дата(1,1,1) Тогда 
		Если Вопрос("Не установлена дата, на которую требуется создание документов РИЗП. Дата в документах РИЗП будет равна текущей. Продолжить?", 
			РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		    Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	// <-
	
	Если Не Отказ Тогда 
		Состояние("Выполняется создание документов РИЗП");
		ОбработкаПрерыванияПользователя();
		СоздатьРИЗПЗаПериодНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьРИЗПЗаПериодНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	Запрос.Текст =  "ВЫБРАТЬ
	                |	ЗакрытиеПоставки.Ссылка КАК ShipmentСlosing
	                |ИЗ
	                |	Документ.ЗакрытиеПоставки КАК ЗакрытиеПоставки
	                |ГДЕ
	                |	ЗакрытиеПоставки.Дата >= &ДатаНач
	                |	И ЗакрытиеПоставки.Дата <= &ДатаКон
	                |	И (НЕ ЗакрытиеПоставки.Ссылка В
	                |				(ВЫБРАТЬ
	                |					РаспределениеИмпортаПоЗакрытиюПоставки.ShipmentСlosing
	                |				ИЗ
	                |					Документ.РаспределениеИмпортаПоЗакрытиюПоставки КАК РаспределениеИмпортаПоЗакрытиюПоставки
	                |				ГДЕ
	                |					(НЕ РаспределениеИмпортаПоЗакрытиюПоставки.ПометкаУдаления)))
	                |	И ЗакрытиеПоставки.Проведен
	                |	И ЗакрытиеПоставки.Поставка.ИмпортЭкспорт = ЗНАЧЕНИЕ(Перечисление.ИмпортЭкспорт.Import)
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	ЗакрытиеПоставки.Ссылка
	                |
	                |УПОРЯДОЧИТЬ ПО
	                |	ЗакрытиеПоставки.Дата";
					
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		           		
		ТЗСтрокиИнвойсаSLI = Документы.РаспределениеИмпортаПоЗакрытиюПоставки.ПолучитьСтрокиИнвойсаCurrentShipmentClosing(Выборка.ShipmentСlosing,ТекущаяДата());
		
		Если ТЗСтрокиИнвойсаSLI.Количество() > 0 Тогда
			
			//создаем документ
			НовыйДок = Документы.РаспределениеИмпортаПоЗакрытиюПоставки.СоздатьДокумент();
			НовыйДок.ShipmentСlosing = Выборка.ShipmentСlosing;
						
			ЗаполнениеПроведениеДокументаРИЗП(НовыйДок, ТЗСтрокиИнвойсаSLI);
						            			
		КонецЕсли;
				
	КонецЦикла;
	
	Элементы.СписокShipmentClosing.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеПроведениеДокументаРИЗП(ДокументРИЗП, ТЗСтрокиИнвойсаSLI)
			
	//РежимЗаписи = РежимЗаписиДокумента.Проведение;
	//МассивТипов = ТЗСтрокиИнвойсаSLI.ВыгрузитьКолонку("Type");
	//Если МассивТипов.Найти(Справочники.ТипыНоменклатуры.НайтиПоНаименованию("M&S")) <> Неопределено Тогда 
	//	РежимЗаписи = ?(ДокументРИЗП.Проведен, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписиДокумента.Запись);
	//КонецЕсли;
	
	//заполняем ТЧ
	СписокГТД = Документы.РаспределениеИмпортаПоЗакрытиюПоставки.ЗагрузитьСписокГТД(ДокументРИЗП.ShipmentСlosing);
	ТЗDSS = Документы.РаспределениеИмпортаПоЗакрытиюПоставки.ЗагрузитьПроводкиDSSПОApInvoice(ДокументРИЗП.ShipmentСlosing, СписокГТД, Новый Массив);
	
	//сопоставляем проводки и строки инвойса
	СтруктураТЧ = Документы.РаспределениеИмпортаПоЗакрытиюПоставки.MatchDSSИInvoiceLines(ТЗСтрокиИнвойсаSLI, ТЗDSS, ДокументРИЗП.ShipmentСlosing);
	
	ДокументРИЗП.DSS.Загрузить(СтруктураТЧ.ТЧDSS);
	ДокументРИЗП.СопоставлениеInvoiceLinesИDSS.Загрузить(СтруктураТЧ.ТЧСопоставление);
	
	ДокументРИЗП.Delta = РасчитатьДельту(СтруктураТЧ, ТЗСтрокиИнвойсаSLI.Итог("InvoiceTotalSum"));
	
	//добавила Федотова Л., РГ-Софт, 14.10.11  ->
	Если НЕ ДатаРИЗП = Дата(1,1,1) Тогда
		ДокументРИЗП.Дата = ДатаРИЗП;
	КонецЕсли; 
    //<-
	
	//пробуем записать
	Попытка
		ДокументРИЗП.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		ТекстОшибки = "Не удалось записать документ для """ + ДокументРИЗП.ShipmentСlosing + """:
		|" + ОписаниеОшибки();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецПопытки;
	
	// { RGS LFedotova 26.09.2018 23:54:04 - вопрос SLI-0007681
	ПроверитьИПровестиДокументПриВозможности(ДокументРИЗП);		
	// } RGS LFedotova 26.09.2018 23:54:10 - вопрос SLI-0007681 
	
КонецПроцедуры


Процедура ПроверитьИПровестиНаСервере()
// { RGS LFedotova 26.09.2018 23:54:04 - вопрос SLI-0007681

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	Запрос.Текст =  "ВЫБРАТЬ
	                |	РаспределениеИмпортаПоЗакрытиюПоставки.Ссылка КАК РИЗП,
	                |	РаспределениеИмпортаПоЗакрытиюПоставки.ShipmentСlosing,
	                |	РаспределениеИмпортаПоЗакрытиюПоставки.CreationDate
	                |ИЗ
	                |	Документ.РаспределениеИмпортаПоЗакрытиюПоставки КАК РаспределениеИмпортаПоЗакрытиюПоставки
	                |ГДЕ
	                |	(НЕ РаспределениеИмпортаПоЗакрытиюПоставки.ПометкаУдаления)
	                |	И РаспределениеИмпортаПоЗакрытиюПоставки.Дата <= &ДатаКон
	                |	И РаспределениеИмпортаПоЗакрытиюПоставки.Дата >= &ДатаНач";
					
					
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ТЗСтрокиИнвойсаSLI = Документы.РаспределениеИмпортаПоЗакрытиюПоставки.ПолучитьСтрокиИнвойсаCurrentShipmentClosing(
			                                                                      Выборка.ShipmentСlosing, Выборка.CreationDate);
		Если ТЗСтрокиИнвойсаSLI.Количество() > 0 Тогда
			ДокументРИЗП = Выборка.РИЗП.ПолучитьОбъект();
			ПроверитьИПровестиДокументПриВозможности(ДокументРИЗП);
		КонецЕсли;
				
	КонецЦикла;
	
	Элементы.СписокShipmentClosing.Обновить();

КонецПроцедуры

&НаСервере
Процедура ПроверитьИПровестиДокументПриВозможности(ДокументРИЗП)
	
	//Проверим соответствия "ERP treatment" - "Type"
	//Asset - FA, 
	//Expense - M&S,
	//Inventory - INVENTORY,	
	//и если они выполняются, попробуем провести
	МожноПроводить = Истина;
	ТЗСтрокиИнвойсаSLI = Документы.РаспределениеИмпортаПоЗакрытиюПоставки.ПолучитьСтрокиИнвойсаCurrentShipmentClosing(ДокументРИЗП.ShipmentСlosing, ДокументРИЗП.CreationDate);
	СтруктураОтбора = Новый Структура;
	Type = Справочники.ТипыНоменклатуры.ПустаяСсылка();
	Для каждого Строка Из ДокументРИЗП.СопоставлениеInvoiceLinesИDSS Цикл
		ERPtreatment = Строка.InvoiceLine.Классификатор;
		
		СтруктураОтбора.Вставить("Invoiceline", Строка.Invoiceline);
		МассивСтрок = ТЗСтрокиИнвойсаSLI.НайтиСтроки(СтруктураОтбора);
		Если МассивСтрок.Количество() > 0 Тогда  
			Type = МассивСтрок[0].Type;
		КонецЕсли;
		
		Если НЕ(ERPtreatment = Перечисления.ТипыЗаказа.A И Type = Справочники.ТипыНоменклатуры.НайтиПоНаименованию("FA",Истина) 
			ИЛИ ERPtreatment = Перечисления.ТипыЗаказа.E И Type = Справочники.ТипыНоменклатуры.НайтиПоНаименованию("M&S",Истина)
			ИЛИ ERPtreatment = Перечисления.ТипыЗаказа.I И Type = Справочники.ТипыНоменклатуры.НайтиПоНаименованию("INVENTORY",Истина)) Тогда
		
			МожноПроводить = Ложь;;
			Прервать;
		
		КонецЕсли; 
		
	КонецЦикла; 
	
	Если МожноПроводить Тогда
	
		Попытка
			ДокументРИЗП.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстОшибки = "Не удалось провести документ для """ + ДокументРИЗП.ShipmentСlosing + """:
			|" + ОписаниеОшибки();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
	
	КонецЕсли;
	
КонецПроцедуры
// } RGS LFedotova 26.09.2018 23:54:10 - вопрос SLI-0007681 

&НаСервере
Функция РасчитатьДельту(СтруктураТЧ, InvoiceTotalSum)

    DSSTotalTranAmount = Документы.РаспределениеИмпортаПоЗакрытиюПоставки.ПолучитьDSSTotalTranAmount(
	                                    CustomsКлиентСервер.ВыгрузитьКолонкуКоллекции(СтруктураТЧ.ТЧDSS,"ПроводкаDSSСКП"), 
                                        РГСофтКлиентСервер.ВыгрузитьКолонкуКоллекцииБезПустыхЗначений(СтруктураТЧ.ТЧСопоставление,"ПроводкаDSSСКП"));

	Delta = InvoiceTotalSum - DSSTotalTranAmount;
	Возврат Delta;		

КонецФункции

//перезаполнение созданных РИЗП

&НаКлиенте
Процедура ПерезаполнитьЗаПериод(Команда)
	
	Отказ = Ложь;
	
	Если ДатаНач = Дата(1,1,1) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не установлена дата начала периода перепроведения документов РИЗП!",,
		                                                  "СписокShipmentClosingУстановитьИнтервал",,Отказ);
	КонецЕсли;
	
	Если ДатаКон = Дата(1,1,1) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не установлена дата окончания периода перепроведения документов РИЗП!",,
		                                                  "СписокShipmentClosingУстановитьИнтервал",,Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда 
		Состояние("Выполняется перепроведение документов РИЗП");
		ОбработкаПрерыванияПользователя();
		ПерезаполнитьРИЗПЗаПериодНаСервере();
	КонецЕсли;
	
КонецПроцедуры
   
&НаСервере
Процедура ПерезаполнитьРИЗПЗаПериодНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	Запрос.Текст =  "ВЫБРАТЬ
	                |	РаспределениеИмпортаПоЗакрытиюПоставки.Ссылка КАК РИЗП,
	                |	РаспределениеИмпортаПоЗакрытиюПоставки.ShipmentСlosing,
	                |	РаспределениеИмпортаПоЗакрытиюПоставки.CreationDate
	                |ИЗ
	                |	Документ.РаспределениеИмпортаПоЗакрытиюПоставки КАК РаспределениеИмпортаПоЗакрытиюПоставки
	                |ГДЕ
	                |	(НЕ РаспределениеИмпортаПоЗакрытиюПоставки.ПометкаУдаления)
	                |	И РаспределениеИмпортаПоЗакрытиюПоставки.Дата <= &ДатаКон
	                |	И РаспределениеИмпортаПоЗакрытиюПоставки.Дата >= &ДатаНач";
					
					
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ТЗСтрокиИнвойсаSLI = Документы.РаспределениеИмпортаПоЗакрытиюПоставки.ПолучитьСтрокиИнвойсаCurrentShipmentClosing(
			                                                                      Выборка.ShipmentСlosing, Выборка.CreationDate);
		Если ТЗСтрокиИнвойсаSLI.Количество() > 0 Тогда
			ДокументРИЗП = Выборка.РИЗП.ПолучитьОбъект();
			ЗаполнениеПроведениеДокументаРИЗП(ДокументРИЗП, ТЗСтрокиИнвойсаSLI);
		КонецЕсли;
				
	КонецЦикла;
	
	Элементы.СписокShipmentClosing.Обновить();
                 	
КонецПроцедуры

// { RGS LFedotova 27.09.2018 15:37:39 - вопрос SLI-0007681
&НаКлиенте
Процедура ПроверитьИПровести(Команда)

	Отказ = Ложь;
	
	Если ДатаНач = Дата(1,1,1) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не установлена дата начала периода перепроведения документов РИЗП!",,
		                                                  "СписокShipmentClosingУстановитьИнтервал",,Отказ);
	КонецЕсли;
	
	Если ДатаКон = Дата(1,1,1) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не установлена дата окончания периода перепроведения документов РИЗП!",,
		                                                  "СписокShipmentClosingУстановитьИнтервал",,Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда 
		Состояние("Выполняется проведение документов РИЗП");
		ОбработкаПрерыванияПользователя();
		ПроверитьИПровестиНаСервере();
	КонецЕсли;
	
КонецПроцедуры
// } RGS LFedotova 27.09.2018 15:38:06 - вопрос SLI-0007681