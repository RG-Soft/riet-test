
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ОбновитьСписокВариантовDomestic()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ProjectMobilizationPlanningDomestic.Variant,
	|	ProjectMobilizationPlanningDomestic.User,
	|	МАКСИМУМ(ProjectMobilizationPlanningDomestic.ModificationDate) КАК ModificationDate
	|ИЗ
	|	РегистрСведений.ProjectMobilizationPlanningDomestic КАК ProjectMobilizationPlanningDomestic
	|ГДЕ
	|	ProjectMobilizationPlanningDomestic.ProjectMobilization = &ProjectMobilization
	|	И &УсловиеUser
	|
	|СГРУППИРОВАТЬ ПО
	|	ProjectMobilizationPlanningDomestic.Variant,
	|	ProjectMobilizationPlanningDomestic.User
	|
	|УПОРЯДОЧИТЬ ПО
	|	ModificationDate УБЫВ";
	
	Если Не ПоказатьВариантыВсехПользователей Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеUser", "ProjectMobilizationPlanningDomestic.User = &User");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ProjectMobilization",	ProjectMobilization);
	Запрос.УстановитьПараметр("User",					Пользователь);
	Запрос.УстановитьПараметр("УсловиеUser",			Истина);
	
	Таблица = Запрос.Выполнить().Выгрузить();

	СписокVariant.Загрузить(Таблица);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВариантовInternational()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ProjectMobilizationPlanningPerItem.Variant,
	|	ProjectMobilizationPlanningPerItem.User,
	|	МАКСИМУМ(ProjectMobilizationPlanningPerItem.ModificationDate) КАК ModificationDate
	|ИЗ
	|	РегистрСведений.ProjectMobilizationPlanningPerItem КАК ProjectMobilizationPlanningPerItem
	|ГДЕ
	|	ProjectMobilizationPlanningPerItem.ProjectMobilization = &ProjectMobilization
	|	И &УсловиеUser
	|
	|СГРУППИРОВАТЬ ПО
	|	ProjectMobilizationPlanningPerItem.Variant,
	|	ProjectMobilizationPlanningPerItem.User
	|
	|УПОРЯДОЧИТЬ ПО
	|	ModificationDate УБЫВ";
	
	Если Не ПоказатьВариантыВсехПользователей Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеUser", "ProjectMobilizationPlanningPerItem.User = &User");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ProjectMobilization",	ProjectMobilization);
	Запрос.УстановитьПараметр("User",					Пользователь);
	Запрос.УстановитьПараметр("УсловиеUser",			Истина);
	
	Таблица = Запрос.Выполнить().Выгрузить();

	СписокVariant.Загрузить(Таблица);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВариантов()
	
	Если DomesticInternational = Перечисления.DomesticInternational.Domestic Тогда
		ОбновитьСписокВариантовDomestic();
	ИначеЕсли DomesticInternational = Перечисления.DomesticInternational.International Тогда
		ОбновитьСписокВариантовInternational();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораВарианта()
	
	ТекСтрока = Элементы.СписокVariant.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		СтруктураВыбора = Новый Структура("Variant, VariantOwner", ТекСтрока.Variant, ТекСтрока.User);
		ОповеститьОВыборе(СтруктураВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьВариантDomestic(Variant, User)
	
	НаборЗаписей = РегистрыСведений.ProjectMobilizationPlanningDomestic.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.ProjectMobilization.Значение			= ProjectMobilization;
	НаборЗаписей.Отбор.ProjectMobilization.Использование	= Истина;
	НаборЗаписей.Отбор.Variant.Значение						= Variant;
	НаборЗаписей.Отбор.Variant.Использование				= Истина;
	НаборЗаписей.Отбор.User.Значение						= User;
	НаборЗаписей.Отбор.User.Использование					= Истина;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьВариантInternational(Variant, User)
	
	НаборЗаписей = РегистрыСведений.ProjectMobilizationPlanningPerItem.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.ProjectMobilization.Значение			= ProjectMobilization;
	НаборЗаписей.Отбор.ProjectMobilization.Использование	= Истина;
	НаборЗаписей.Отбор.Variant.Значение						= Variant;
	НаборЗаписей.Отбор.Variant.Использование				= Истина;
	НаборЗаписей.Отбор.User.Значение						= User;
	НаборЗаписей.Отбор.User.Использование					= Истина;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьВариантНаСервере(Variant, User)
	
	Если DomesticInternational = Перечисления.DomesticInternational.Domestic Тогда
		УдалитьВариантDomestic(Variant, User);
	ИначеЕсли DomesticInternational = Перечисления.DomesticInternational.International Тогда
		УдалитьВариантInternational(Variant, User);
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Пользователь = ПараметрыСеанса.ТекущийПользователь;
	
	Параметры.Свойство("ProjectMobilization",	ProjectMobilization);
	Параметры.Свойство("DomesticInternational",	DomesticInternational);
	
	ОбновитьСписокВариантов();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНЫХ ПАНЕЛЕЙ

&НаКлиенте
Процедура КомандаОбновитьНажатие(Команда)
	
	ОбновитьСписокВариантов();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьНажатие(Команда)
	
	ОбработчикВыбораВарианта();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокVariantВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработчикВыбораВарианта();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоказатьВариантыВсехПользователейНажатие(Команда)
	
	ПоказатьВариантыВсехПользователей = Не ПоказатьВариантыВсехПользователей;
	
	Элементы.ФормаКомандаПоказатьВариантыВсехПользователей.Пометка = ПоказатьВариантыВсехПользователей;
	
	ОбновитьСписокВариантов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеВопросаУдалитьВариант(Ответ, ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьВариантНаСервере(ДопПараметры.Variant, ДопПараметры.User);
		ОбновитьСписокВариантов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалитьВариантНажатие(Команда)
	
	ТекСтрока = Элементы.СписокVariant.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Если ТекСтрока.User = Пользователь Тогда
			
			СтруктураПараметров = Новый Структура("Variant, User");
			ЗаполнитьЗначенияСвойств(СтруктураПараметров, ТекСтрока);
			
			ДействиеВопроса = Новый ОписаниеОповещения("ДействиеВопросаУдалитьВариант", ЭтотОбъект, СтруктураПараметров);
			ТекстВопроса = НСтр("ru = 'Удалить вариант?'; en = 'Delete variant?'");
			ПоказатьВопрос(ДействиеВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		Иначе
			ТекстПредупрждения = НСтр("ru = 'Можно удалить только свой вариант.'; en = 'You can only delete your own variant.'");
			ПоказатьПредупреждение(, ТекстПредупрждения);
		КонецЕсли;
		
	Иначе
		ТекстПредупрждения = НСтр("ru = 'Выберите строку с варинатом.'; en = 'Select the line with the variant.'");
		ПоказатьПредупреждение(, ТекстПредупрждения);
	КонецЕсли;	
		
КонецПроцедуры
