
Процедура ПередЗаписью(Отказ, Замещение)
	
	Для Каждого Запись Из ЭтотОбъект Цикл
					
		КлючЗаписи = Неопределено;
		
		Если НЕ ЗначениеЗаполнено(Запись.TransportRequest) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""Transport request"" is empty!",
				ПолучитьКлючЗаписи(Запись, КлючЗаписи), "TransportRequest", "Запись", Отказ);			
		КонецЕсли;
			
		Если НЕ ЗначениеЗаполнено(Запись.Stage) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""Stage"" is empty!",
				ПолучитьКлючЗаписи(Запись, КлючЗаписи), "Stage", "Запись", Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Запись.ModificationDate) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""Modification date"" is empty!",
				ПолучитьКлючЗаписи(Запись, КлючЗаписи), "ModificationDate", "Запись", Отказ);
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьКлючЗаписи(Запись, КлючЗаписи)
	
	Если КлючЗаписи = Неопределено Тогда
		СтруктураЗаписи = Новый Структура("TransportRequest");
		ЗаполнитьЗначенияСвойств(СтруктураЗаписи, Запись);
		КлючЗаписи = РегистрыСведений.StagesOfTransportRequests.СоздатьКлючЗаписи(СтруктураЗаписи);
	КонецЕсли; 
		
	Возврат КлючЗаписи;
	
КонецФункции

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбщегоНазначенияПовтИсп.ИдентификаторРабочейКонфигурации() <> "SLI RU" Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
		
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Запись.Stage = Перечисления.TransportRequestStages.CompletelyDelivered Тогда
			ПроцедурыОбменаДанными.ЗаписатьДляОтложеннойРегистрации(Запись.TransportRequest,"СправочникДокумент");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
