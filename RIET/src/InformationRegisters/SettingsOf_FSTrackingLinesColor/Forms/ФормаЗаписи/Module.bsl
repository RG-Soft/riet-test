
&НаКлиенте
Процедура ColorНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДиалогВыбора = Новый ДиалогВыбораЦвета;
	ДиалогВыбора.Показать(Новый ОписаниеОповещения("ЗавершениеВыбораЦвета", ЭтотОбъект));
КонецПроцедуры


&НаКлиенте
Процедура ЗавершениеВыбораЦвета(Результат, Параметры) Экспорт

	Если Результат <> Неопределено Тогда
		СтруктураЦвета = ПолучитьАбсолютныйЦвет(Результат);
		Запись.Color = СтруктураЦвета.Color;
		Запись.Red = СтруктураЦвета.Red;
		Запись.Green = СтруктураЦвета.Green;
		Запись.Blue = СтруктураЦвета.Blue;
		
		Модифицированность = Истина;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьАбсолютныйЦвет(Цвет) Экспорт
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Область("R1C1").ЦветФона = Цвет;
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ТабДок.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.MXL7);
	ТабДок.Прочитать(ИмяВременногоФайла);
	УдалитьФайлы(ИмяВременногоФайла);
	ЦветКЗС = ТабДок.Область("R1C1").ЦветФона;
	
	СтруктураЦвета = Новый Структура;
	СтруктураЦвета.Вставить("Red", ЦветКЗС.Красный);
	СтруктураЦвета.Вставить("Green", ЦветКЗС.Зеленый);
	СтруктураЦвета.Вставить("Blue", ЦветКЗС.Синий);
	
	Результат = "#" + Из_10_В_Любую(ЦветКЗС.Красный, 16) + Из_10_В_Любую(ЦветКЗС.Зеленый, 16) + Из_10_В_Любую(ЦветКЗС.Синий, 16);
	
	СтруктураЦвета.Вставить("Color", Результат);
	
	Возврат СтруктураЦвета;
	
КонецФункции

&НаСервереБезКонтекста
Функция Из_10_В_Любую(Знач Значение=0,Нотация=36) Экспорт
	
	Если Нотация <= 0 Тогда 
		Возврат("00"); 
	КонецЕсли;
	
	Значение = Число(Значение);
	
	Если Значение<=0 Тогда 
		Возврат("00"); 
	КонецЕсли;
	
	Значение = Цел(Значение);
	
	Результат = "";
	
	Пока Значение > 0 Цикл
		Результат = Сред("0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ", Значение % Нотация + 1, 1) + Результат;
		Значение = Цел(Значение/Нотация);
	КонецЦикла;
	
	Если СтрДлина(Результат) = 1 Тогда
		Результат = "0" + Результат;	
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции 