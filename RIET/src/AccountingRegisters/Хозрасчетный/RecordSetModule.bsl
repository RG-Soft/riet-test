
Процедура ПередЗаписью(Отказ, РежимЗаписи)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество()>0 Тогда
	    Заголовок = СокрЛП(ЭтотОбъект.Отбор.Регистратор.Значение);
	Иначе
		Возврат;
	КонецЕсли; 

	Для Каждого Проводка Из ЭтотОбъект Цикл
		Если ОбщегоНазначения.ЗначениеНеЗаполнено(Проводка.СчетДт) И не Проводка.СчетКт.Забалансовый Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Проводка № "+(Проводка.НомерСтроки+1) +" <"+Проводка.Содержание+">: не заполнен счет дебета.",Отказ,Заголовок);
		КонецЕсли;
		Если ОбщегоНазначения.ЗначениеНеЗаполнено(Проводка.СчетКт) И не Проводка.СчетДт.Забалансовый Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Проводка № "+(Проводка.НомерСтроки+1) +" <"+Проводка.Содержание+">: не заполнен счет кредита.",Отказ,Заголовок);
		КонецЕсли;
		
		// Приведение пустых значений субконто составного типа.
		Для Каждого Субконто Из Проводка.СубконтоДт Цикл
			Если Субконто.Ключ.ТипЗначения.Типы().Количество() > 1
			   И ОбщегоНазначения.ЗначениеНеЗаполнено(Субконто.Значение) 
			   И НЕ (Субконто.Значение = Неопределено) Тогда
				Проводка.СубконтоДт.Вставить(Субконто.Ключ, Неопределено);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Субконто Из Проводка.СубконтоКт Цикл
			Если Субконто.Ключ.ТипЗначения.Типы().Количество() > 1
			   И ОбщегоНазначения.ЗначениеНеЗаполнено(Субконто.Значение) 
			   И НЕ (Субконто.Значение = Неопределено) Тогда
				Проводка.СубконтоКт.Вставить(Субконто.Ключ, Неопределено);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	//заполнение реквизита ПодразделениеОрганизации по умолчанию
	//добавил Трефиленков Дмитрий, РГ-Софт
	Для Каждого Проводка Из ЭтотОбъект Цикл
		Если ОбщегоНазначения.ЗначениеНеЗаполнено(Проводка.ПодразделениеОрганизацииДт) Тогда
			Если ОбщегоНазначения.ЕстьРеквизитДокумента("ПодразделениеОрганизации", Отбор.Регистратор.Значение.Метаданные()) Тогда
				Проводка.ПодразделениеОрганизацииДт = Отбор.Регистратор.Значение.ПодразделениеОрганизации;
			КонецЕсли;
		КонецЕсли;
		Если ОбщегоНазначения.ЗначениеНеЗаполнено(Проводка.ПодразделениеОрганизацииКт) Тогда
			Если ОбщегоНазначения.ЕстьРеквизитДокумента("ПодразделениеОрганизации", Отбор.Регистратор.Значение.Метаданные()) Тогда
				Проводка.ПодразделениеОрганизацииКт = Отбор.Регистратор.Значение.ПодразделениеОрганизации;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

