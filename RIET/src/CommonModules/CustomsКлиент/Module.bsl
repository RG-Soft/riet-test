
/////////////////////////////////////////////////////////////////////////////
// PO

Процедура ОткрытьPOПоНомеру(НомерPO, ВладелецФормы) Экспорт
	
	Если ЗначениеЗаполнено(НомерPO) Тогда
		
		PO = CustomsСервер.НайтиPOПоНомеру(НомерPO);
		
		Если Не ЗначениеЗаполнено(PO) Тогда
			PO = CustomsСервер.НайтиBORGПоНомеру(НомерPO);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(PO) Тогда
			ПоказатьЗначение(,PO);
		Иначе
			
			ТекстОшибки = "Не удалось найти PO по номеру """ + НомерPO + """!";
			ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки);
			
		КонецЕсли;
		
	Иначе
		
		ТекстОшибки = "PO no. не заполнен!";
		ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки);
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ЗАГРУЗКА

Функция ПолучитьПолноеИмяPDFToTXTProgram() Экспорт
	
	#Если ВебКлиент Тогда
	ВызватьИсключение "The system can not convert pdf files in a web browser, use 1c thin client instead.";
	#Иначе
	ДвоичныеДанные = CustomsСервер.ПолучитьДвоичныеДанныеPDFToTXT();
	ПолноеИмяВременногоФайла = ПолучитьИмяВременногоФайла("exe");
	Попытка
		ДвоичныеДанные.Записать(ПолноеИмяВременногоФайла);
		Возврат ПолноеИмяВременногоФайла;
	Исключение
		Сообщить("Не удалось записать файл преобразования PDF в текст: " + ОписаниеОшибки());
		Возврат "";
	КонецПопытки;
	#КонецЕсли
	
КонецФункции

Процедура ПопытатьсяУдалитьPDFToTXTProgram(ПолноеИмяPDFToTXTProgram) Экспорт

	Если ЗначениеЗаполнено(ПолноеИмяPDFToTXTProgram) Тогда
		Попытка
			УдалитьФайлы(ПолноеИмяPDFToTXTProgram);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Не удалось удалить временный файл преобразования PDF в текст: " + ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьТекстовыйДокументИзФайлаPDF(ПолныйПутьКФайлуPDF, ПолноеИмяPDFToTXTProgram="") Экспорт
	
	НадоУдалитьPDFToTXTProgram = Ложь;
	Если НЕ ЗначениеЗаполнено(ПолноеИмяPDFToTXTProgram) Тогда
		
		ПолноеИмяPDFToTXTProgram = ПолучитьПолноеИмяPDFToTXTProgram();
		Если НЕ ЗначениеЗаполнено(ПолноеИмяPDFToTXTProgram) Тогда
			Возврат Неопределено;
		КонецЕсли;
		НадоУдалитьPDFToTXTProgram = Истина;
		
	КонецЕсли;
	
	#Если ВебКлиент Тогда
	ВызватьИсключение "The system can not convert pdf files in a web browser, use 1c thin client instead";
	#Иначе
	ВременныйТекстовыйФайл = Новый Файл(ПолучитьИмяВременногоФайла("txt"));
	
	// Извлекаем текст схемой
	ЗапуститьПриложение(ПолноеИмяPDFToTXTProgram + " -layout """ + ПолныйПутьКФайлуPDF + """ """ 
						+ ВременныйТекстовыйФайл.ПолноеИмя, ВременныйТекстовыйФайл.Путь, Истина);
		
	// Проверяем, сконвертировался ли LAYOUT файл
	Если НЕ ВременныйТекстовыйФайл.Существует() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Файл """ + ПолныйПутьКФайлуPDF + """ не сконвертировался!");
		Возврат Неопределено;
	КонецЕсли;
		
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ВременныйТекстовыйФайл.ПолноеИмя);
	
	// Удаляем временный текстовый файл
	Попытка
		УдалитьФайлы(ВременныйТекстовыйФайл.ПолноеИмя);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Не удалось удалить временный текстовый файл: " + ОписаниеОшибки());
	КонецПопытки;
	
	Если НадоУдалитьPDFToTXTProgram Тогда
		ПопытатьсяУдалитьPDFToTXTProgram(ПолноеИмяPDFToTXTProgram);
	КонецЕсли;
	
	Возврат ТекстовыйДокумент;
	
	#КонецЕсли
	
КонецФункции
	

