
Процедура ПриЗаписиОбъектовПриЗаписи(Ссылка) Экспорт
	
	// ищем правила регистрации среди активных настроек
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбменСВнешнимиИБПравилаРегистрации.Ссылка КАК УзелОбмена
	|ИЗ
	|	ПланОбмена.ргОбменСВнешнимиИБ.ПравилаРегистрации КАК ОбменСВнешнимиИБПравилаРегистрации
	|ГДЕ
	|	ОбменСВнешнимиИБПравилаРегистрации.ТипОбъекта = &ТипОбъекта
	|	И ОбменСВнешнимиИБПравилаРегистрации.Регистрировать
	|	И НЕ ОбменСВнешнимиИБПравилаРегистрации.Ссылка.ПометкаУдаления
	|	И ОбменСВнешнимиИБПравилаРегистрации.Ссылка.РегистрироватьИзменения
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбменСВнешнимиИБПравилаРегистрации.Ссылка";
	
	Запрос.УстановитьПараметр("ТипОбъекта", Ссылка.Метаданные().ПолноеИмя());
	
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		ЗарегистрироватьВУзле(Выборка.УзелОбмена, Ссылка);
	КонецЦикла;
		
КонецПроцедуры

Процедура ПриЗаписиРегистровПриЗаписи(СтрокаXML) Экспорт
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	Источник = ПрочитатьXML(ЧтениеXML);
   
	// ищем правила регистрации среди активных настроек
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбменСВнешнимиИБ_Регистрация.Ссылка КАК УзелОбмена
	|ИЗ
	|	ПланОбмена.ргОбменСВнешнимиИБ.ПравилаРегистрации КАК ОбменСВнешнимиИБ_Регистрация
	|ГДЕ
	|	ОбменСВнешнимиИБ_Регистрация.ТипОбъекта = &ТипОбъекта
	|	И ОбменСВнешнимиИБ_Регистрация.Регистрировать
	|	И НЕ ОбменСВнешнимиИБ_Регистрация.Ссылка.ПометкаУдаления
	|	И ОбменСВнешнимиИБ_Регистрация.Ссылка.РегистрироватьИзменения
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбменСВнешнимиИБ_Регистрация.Ссылка";
	
	Запрос.УстановитьПараметр("ТипОбъекта",  Источник.Метаданные().ПолноеИмя());
	
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		// сначала надо определить измерения Основного отбора
		МассивОсновныхОтборов = Новый Массив;
		Для Каждого Измерение Из Источник.Метаданные().Измерения Цикл
			Если Измерение.ОсновнойОтбор Тогда
				МассивОсновныхОтборов.Добавить(Измерение);
			КонецЕсли; 
		КонецЦикла; 
		
		// если есть отбор по периоду
		Если Источник.Метаданные().ОсновнойОтборПоПериоду Тогда 
			ЭлементОтбора = Новый Структура;
			ЭлементОтбора.Вставить("Имя", "Период");
			МассивОсновныхОтборов.Добавить(ЭлементОтбора);
		КонецЕсли;
		
		// потом в обходе набора - отбираем по основному отбору и регистрируем
		НаборРегистра = Источник;
		Для Каждого СтрокаНабора Из Источник Цикл
			Для Каждого ЭлОтбора Из МассивОсновныхОтборов Цикл
				НаборРегистра.Отбор[ЭлОтбора.Имя].Установить(СтрокаНабора[ЭлОтбора.Имя]);
			КонецЦикла; 
			ЗарегистрироватьВУзле(Выборка.УзелОбмена, Источник);
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьВУзле(Узел, Источник)
	
	Если Не Узел.Пустая() Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(Узел, Источник);
	КонецЕсли; 
	
КонецПроцедуры

Функция ВернутьКоличествоОбъектовДляВыгрузки(УзелОбмена) Экспорт 
	
	Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
        Возврат 0;
    КонецЕсли; 
    
    ТекстЗапроса = "ВЫБРАТЬ 0 КАК Количество";
    
    УзелОбменаСостав = УзелОбмена.Метаданные().Состав;
	
	Для каждого ОбъектСостава Из УзелОбменаСостав Цикл
          ОбъектМетаданных = ОбъектСостава.Метаданные;
          ПолноеИмяОбъекта = ОбъектМетаданных.ПолноеИмя();
          ПолноеИмяОбъекта = СтрЗаменить(ПолноеИмяОбъекта,".Перерасчет.",".");
          ТекстЗапроса = ТекстЗапроса + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ 1 ИЗ " + ПолноеИмяОбъекта + ".Изменения ГДЕ Узел = &Узел";
    КонецЦикла; 
    
    Запрос = Новый Запрос;
    Запрос.Текст= "ВЫБРАТЬ СУММА(Результат.Количество) КАК Количество ИЗ (" + ТекстЗапроса + ") КАК Результат";
    Запрос.УстановитьПараметр("Узел", УзелОбмена);
    
    Выборка	= Запрос.Выполнить().Выбрать();
    Выборка.Следующий();
	
    Возврат Выборка.Количество;
	
КонецФункции

Процедура ЗапуститьВыгрузку_WEBDB() Экспорт 
	
	//ФоновыеЗадания.Выполнить("ргМодульОбменаДаннымиПрив.ВыгрузкаВоВнешниеИБ",,
	//		Новый УникальныйИдентификатор,"Выгрузка во внешние ИБ");
	ФоновыеЗадания.Выполнить("ргМодульОбменаДаннымиПрив.ВыгрузкаВоВнешниеИБ",,,"Фоновая выгрузка во внешние ИБ");
				
КонецПроцедуры

Процедура ВыгрузкаВоВнешниеИБ() Экспорт
	
	ОбработкаОбмена = Обработки.ргВыгрузкаВоВнешниеИБ.Создать();
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбменСВнешнимиИБ.Ссылка КАК База,
	|	ИСТИНА КАК Выгружать
	|ИЗ
	|	ПланОбмена.ргОбменСВнешнимиИБ КАК ОбменСВнешнимиИБ
	|ГДЕ
	|	НЕ ОбменСВнешнимиИБ.ПометкаУдаления
	|	И ОбменСВнешнимиИБ.Активен
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбменСВнешнимиИБ.Код";
	
	ОбработкаОбмена.СписокБаз.Загрузить(Запрос.Выполнить().Выгрузить());
 	ОбработкаОбмена.ВыполнитьВыгрузку();
	
КонецПроцедуры
