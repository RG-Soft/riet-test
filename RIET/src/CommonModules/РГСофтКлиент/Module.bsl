
Процедура ПроверитьНеобходимостьЗаписиДокумента(РежимЗаписи, Проведен, Модифицированность, Отказ) Экспорт
	
	Если (РежимЗаписи = РежимЗаписиДокумента.Запись
		ИЛИ РежимЗаписи = РежимЗаписиДокумента.Проведение И Проведен)
		И НЕ Модифицированность Тогда
		ПоказатьОповещениеПользователя("Внимание", , "Документ не был изменен.
			|" + РежимЗаписи + " не требуется.", 30);
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры 

Процедура ПроверитьНеобходимостьЗаписиСправочника(Модифицированность, Отказ) Экспорт
	
	Если НЕ Модифицированность Тогда
		ПоказатьОповещениеПользователя("Внимание", , "Справочник не был изменен.
			|Запись не требуется.");
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

Функция ЗадатьВопросОбОчисткеТЧПередЗаполнением(ТЧ) Экспорт
	
	Если ТЧ.Количество() Тогда
		
		ТекстВопроса = "Перед заполнением табличная часть будет очищена.
			|Продолжить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, "Внимание");
		
		Возврат Ответ = КодВозвратаДиалога.Да; 
		
	КонецЕсли;
	
	Возврат Истина;
		
КонецФункции

Процедура УстановитьОтборДинамическогоСпискаПоЗначениюВТекущейКолонке(ОтборДинамическогоСписка, ЭлементУправленияДинамическогоСписка, Пометка) Экспорт
	
	ТекущиеДанные = ЭлементУправленияДинамическогоСписка.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Пометка = НЕ Пометка;
	
	ПолноеИмяКолонки = ЭлементУправленияДинамическогоСписка.ТекущийЭлемент.Имя;
	КраткоеИмяКолонки = СтрЗаменить(ПолноеИмяКолонки, ЭлементУправленияДинамическогоСписка.Имя, "");
	Значение = ТекущиеДанные[КраткоеИмяКолонки]; 
	РГСофтКлиентСервер.УстановитьЭлементОтбора(
		ОтборДинамическогоСписка,
		КраткоеИмяКолонки,
		Значение,
		?(ТипЗнч(Значение) = Тип("Строка"), ВидСравненияКомпоновкиДанных.Содержит, ВидСравненияКомпоновкиДанных.Равно),
		,
		Пометка);	
	
КонецПроцедуры
	
Функция ПолучитьПометкуКнопкиОтбораПоЗначениюВТекущейКолонке(ОтборДинамическогоСписка, ЭлементУправленияДинамическогоСписка) Экспорт
	
	Пометка = Ложь;
	ПолноеИмяКолонки = ЭлементУправленияДинамическогоСписка.ТекущийЭлемент.Имя;
	КраткоеИмяКолонки = СтрЗаменить(ПолноеИмяКолонки, ЭлементУправленияДинамическогоСписка.Имя, "");
	НайденныеЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(ОтборДинамическогоСписка, КраткоеИмяКолонки);
	Если НайденныеЭлементыОтбора.Количество() Тогда
		Если НайденныеЭлементыОтбора[0].Использование Тогда
			Пометка = Истина;
		КонецЕсли; 
	КонецЕсли; 
	Возврат Пометка;
	
КонецФункции
       
