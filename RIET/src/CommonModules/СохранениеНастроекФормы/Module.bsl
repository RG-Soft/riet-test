Процедура СохранитьРеквизитыФормы(Объект, СтруктураРеквизитов, Форма) Экспорт  
	
	ФормаСохраненияНастроек = ПолучитьОбщуюФорму("ФормаСохраненияНастроек", Форма);
	ФормаСохраненияНастроек.НаименованиеНастройки = "Основная";
	
	НаборЗаписей = РегистрыСведений.ХранилищеДополнительнойИнформации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	
	НаборЗаписей.Прочитать();
	
	Для каждого Запись Из НаборЗаписей Цикл	
		Если Запись.ИспользоватьПриОткрытии Тогда
			ФормаСохраненияНастроек.НаименованиеНастройки = Запись.Наименование;
			ФормаСохраненияНастроек.Настройки.Вставить(0,Запись.Наименование, Запись.Наименование, Истина);
		Иначе
			ФормаСохраненияНастроек.Настройки.Добавить(Запись.Наименование, Запись.Наименование);
		КонецЕсли;
	КонецЦикла;
	Результат = ФормаСохраненияНастроек.ОткрытьМодально();
	
	Если НЕ ТипЗнч(Результат) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	НаименованиеНастройки = Результат.НаименованиеНастройки;
	ИспользоватьПриОткрытии = Результат.ИспользоватьПриОткрытии;
	МассивКУдалению = Результат.НастройкиКУдалению;
	Если ИспользоватьПриОткрытии Тогда
		Для Каждого Запись Из НаборЗаписей Цикл
			 Запись.ИспользоватьПриОткрытии = Ложь;
		КонецЦикла;
	КонецЕсли;
	НаборЗаписей.Записать();
	НаборЗаписей.Отбор.Наименование.Установить(НаименованиеНастройки);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() > 0 Тогда
		НоваяЗапись = НаборЗаписей[0];				
	Иначе
		НоваяЗапись = НаборЗаписей.Добавить();
	КонецЕсли;
	НоваяЗапись.Наименование = НаименованиеНастройки;
	НоваяЗапись.Объект = Объект;
	НоваяЗапись.Хранилище = Новый ХранилищеЗначения(СтруктураРеквизитов);
	НоваяЗапись.ИспользоватьПриОткрытии = ИспользоватьПриОткрытии;
	НаборЗаписей.Записать();
	Если МассивКУдалению.Количество() > 0 Тогда
		Объект = Форма.Заголовок;
		Для каждого ЭлементМассива Из МассивКУдалению Цикл
			НаборЗаписей = РегистрыСведений.ХранилищеДополнительнойИнформации.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(Объект);
			НаборЗаписей.Отбор.Наименование.Установить(ЭлементМассива);
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры //СохранитьРеквизитыФормы

Процедура ВосстановитьРеквизитыФормы(Объект, СтруктураРеквизитов, Форма, ПриОткрытии) Экспорт
	Результат = Ложь;
	МассивКУдалению = Новый Массив;
	ФормаВосстановленияНастроек = ПолучитьОбщуюФорму("ФормаВосстановленияНастроек", Форма);
	СписокЗначений = ФормаВосстановленияНастроек.Настройки;
	НаборЗаписей = РегистрыСведений.ХранилищеДополнительнойИнформации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Прочитать();
	Для каждого Запись Из НаборЗаписей Цикл
		Если Запись.ИспользоватьПриОткрытии Тогда
			СписокЗначений.Добавить(Запись.Наименование, Запись.Наименование, Истина);
			Результат = Запись.Наименование;
		Иначе
			СписокЗначений.Добавить(Запись.Наименование, Запись.Наименование);
		КонецЕсли;
	КонецЦикла;
	Если СписокЗначений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПриОткрытии Тогда
		Результат = ФормаВосстановленияНастроек.ОткрытьМодально();
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Булево") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		НаименованиеНастройки = Результат.НаименованиеНастройки;
		МассивКУдалению = Результат.НастройкиКУдалению;
	Иначе
		НаименованиеНастройки = Результат;
	КонецЕсли;
	
	НаборЗаписей.Отбор.Наименование.Установить(НаименованиеНастройки);
	НаборЗаписей.Прочитать();
	Для каждого Запись Из НаборЗаписей Цикл
		Если ПриОткрытии И НЕ Запись.ИспользоватьПриОткрытии Тогда
			Продолжить;
		КонецЕсли;	
		Структура = Запись.Хранилище.Получить();
	КонецЦикла;                                              
	
	Для каждого Элемент Из СтруктураРеквизитов Цикл
		ЭлементФормы = Форма.ЭлементыФормы.Найти(Элемент.Ключ);
		Если ЭлементФормы <> Неопределено Тогда 
			// есть такой элемент
			Для Каждого ЭлементСтруктуры Из Структура Цикл
				Если Элемент.Ключ = ЭлементСтруктуры.Ключ Тогда
					ЭлементФормы.Значение = ЭлементСтруктуры.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
	КонецЦикла; 
	Если МассивКУдалению.Количество() > 0 Тогда
		Объект = Форма.Заголовок;
		Для каждого ЭлементМассива Из МассивКУдалению Цикл
			НаборЗаписей = РегистрыСведений.ХранилищеДополнительнойИнформации.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(Объект);
			НаборЗаписей.Отбор.Наименование.Установить(ЭлементМассива);
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры //ВосстановитьРеквизитыФормы
	
