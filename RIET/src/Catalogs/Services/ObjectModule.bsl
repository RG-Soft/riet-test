
////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизитыБезДополнительныхДанных();
			
	ПроверитьРеквизитыБезДополнительныхДанных(Отказ);
	
КонецПроцедуры

Процедура ДозаполнитьРеквизитыБезДополнительныхДанных()
	
	Код = СокрЛП(Код);
	DescriptionEng = СокрЛП(DescriptionEng);
	Наименование = СокрЛП(Код) + "-" + DescriptionEng;
	
	Если НЕ ЭтоГруппа Тогда
		Ports.Свернуть("Port", "");
		ОбщегоНазначения.ОчиститьТаблицуОтСтрокСПустымиРеквизитами(Ports, "Port");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьРеквизитыБезДополнительныхДанных(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Код)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Code"" is empty!",
			ЭтотОбъект, "Код", , Отказ);
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(DescriptionEng) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Description eng."" is empty!",
			ЭтотОбъект, "DescriptionEng", , Отказ);	
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		
		Если НЕ ЗначениеЗаполнено(SumCalculationMethod) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""Sum calculation method"" is empty!",
				ЭтотОбъект, "SumCalculationMethod", , Отказ);
		КонецЕсли; 
		
		Если НЕ ЗначениеЗаполнено(AllocationArea) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""Allocation area"" is empty!",
				ЭтотОбъект, "AllocationArea", , Отказ);
		КонецЕсли; 

		Если НЕ ЗначениеЗаполнено(AllocationMethod) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""Allocation method"" is empty!",
				ЭтотОбъект, "AllocationMethod", , Отказ);
		КонецЕсли; 
		
		Если DutiesRecharge
			И ЗначениеЗаполнено(AllocationArea)
			И НЕ AllocationArea = Перечисления.AllocationAreas.CCD Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"For Duties recharge you can only choose ""CCDs"" as Allocation method!",
				ЭтотОбъект, "AllocationMethod", , Отказ);
			
		КонецЕсли; 
		
	КонецЕсли;	
		
КонецПроцедуры
