
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СтруктураНастройки") Тогда
		
		СтруктураНастройки = Параметры.СтруктураНастройки;
		Если СтруктураНастройки.Имя = "ВыборИзService" Тогда
			НастроитьДляВыбораИзService(СтруктураНастройки);
		ИначеЕсли СтруктураНастройки.Имя = "ВыборИзPriceListOfServices" Тогда
			НастроитьДляВыбораИзPriceListOfServices(СтруктураНастройки);
		КонецЕсли;
		
	КонецЕсли;
	
	Параметры.Отбор.Вставить("ПометкаУдаления", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДляВыбораИзService(СтруктураНастройки)
	
	СтруктураОтбора = Параметры.Отбор;
	Port = СтруктураНастройки.Port;
	Agent = СтруктураНастройки.Agent;
	ДатаПолученияЦены = СтруктураНастройки.ДатаПолученияЦены;
	Если ЗначениеЗаполнено(Agent)
		И ЗначениеЗаполнено(ДатаПолученияЦены) Тогда
		Services = CustomsСервер.ПолучитьМассивServicesДляВыбораИзДокументаService(Port, Agent, ДатаПолученияЦены);
		СтруктураОтбора.Вставить("Ссылка", Services);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДляВыбораИзPriceListOfServices(СтруктураНастройки)
	
	СтруктураОтбора = Параметры.Отбор;
	SumCalculationMethod = Перечисления.SumCalculationMethods[СтруктураНастройки.ИмяSumCalculationMethod];
	СтруктураОтбора.Вставить("SumCalculationMethod", SumCalculationMethod);	
	
	СписокИсключенных = Новый СписокЗначений;
	СписокИсключенных.ЗагрузитьЗначения(СтруктураНастройки.CurrentServices);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"Ссылка",
		СписокИсключенных,
		ВидСравненияКомпоновкиДанных.НеВСписке,
		,
		Истина);
		
КонецПроцедуры
