
//////////////////////////////////////////////////////////////////////
// ПРИ КОПИРОВАНИИ

Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтоГруппа Тогда
		РГСофт.ОчиститьCreationModification(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////
// ПЕРЕД ЗАПИСЬЮ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизиты();
	
	ПроверитьРеквизиты(Отказ);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////

Процедура ДозаполнитьРеквизиты()
	
	Наименование = СокрЛП(Наименование);
	
	Если Не ЭтоГруппа Тогда 
		
		SegmentCode = СокрЛП(SegmentCode);
		NameEng = СокрЛП(NameEng);
		   		
		Если ЭтоНовый() Тогда
			РГСофт.ЗаполнитьCreation(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьРеквизиты(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Владелец) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Поле 'Legal entity' не заполнено!",
			ЭтотОбъект, "Владелец", , Отказ);
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Поле 'Name' не заполнено!",
			ЭтотОбъект, "Наименование", , Отказ);
	КонецЕсли;
	
	//проверим уникальность для владельца
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	SegmentsOfNonLawsonCompanies.Ссылка
	               |ИЗ
	               |	Справочник.SegmentsOfNonLawsonCompanies КАК SegmentsOfNonLawsonCompanies
	               |ГДЕ
	               |	SegmentsOfNonLawsonCompanies.Владелец = &Владелец
	               |	И SegmentsOfNonLawsonCompanies.Наименование = &Наименование
	               |	И SegmentsOfNonLawsonCompanies.Ссылка <> &Ссылка";
				   
	Выборка = Запрос.Выполнить().Выбрать();		
	Если Выборка.Следующий() Тогда 
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Подразделение с таким наименованием уже существует для плательщика '" + СокрЛП(Владелец) + "'!",
			ЭтотОбъект, "Наименование", , Отказ);
	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(CostCenter) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Поле 'Cost center' не заполнено!",
			ЭтотОбъект, "CostCenter", , Отказ);
	КонецЕсли;
		
КонецПроцедуры
