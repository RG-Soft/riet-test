&НаКлиенте
Перем КонтекстЭДО;

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Получение контекста ЭДО" И КонтекстЭДО <> Неопределено И ТипЗнч(Параметр) = Тип("Структура") Тогда
		Параметр.КонтекстЭДО = КонтекстЭДО;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДополнительныеПараметры = Новый Структура("Элемент", Элемент);
	ОписаниеОповещения = Новый ОписаниеОповещения("СертификатНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	КонтекстЭДО.ВыбратьСертификат(ЭтоЭлектроннаяПодписьВМоделиСервиса, Объект.Сертификаты, "AddressBook", , Истина, ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбораЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если РезультатВыбора <> Неопределено Тогда
		
		Объект.Сертификаты.Очистить();
		Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
			НовСтр = Объект.Сертификаты.Добавить();
			НовСтр.Сертификат = РезультатВыбора.Отпечаток;
		Иначе
			Для Каждого Стр Из РезультатВыбора Цикл
				НовСтр = Объект.Сертификаты.Добавить();
				НовСтр.Сертификат = Стр.Отпечаток;
			КонецЦикла;
		КонецЕсли;
		
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭтоЭлектроннаяПодписьВМоделиСервиса, Элемент, Объект.Сертификаты, СертификатПредставление);
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// инициализируем контекст формы - контейнера клиентских методов
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДО = Результат.КонтекстЭДО;
	
	ДополнительныеПараметры = Новый Структура("КонтекстЭДО", КонтекстЭДО);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииПослеСозданияОбъектаДляРаботыСКриптографиейЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	Если ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Истина);
	Иначе
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.СоздатьОбъектДляРаботыСКриптографиейСУстановкой(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииПослеСозданияОбъектаДляРаботыСКриптографиейЗавершение(Крипт, ДополнительныеПараметры) Экспорт
	
	Если Крипт = Неопределено Тогда
		//не удалось создать объект для работы с криптографией
		//заблокируем поле сертификата
		Элементы.Сертификат.Доступность = Ложь;
		
	Иначе
		
		КонтекстЭДО = ДополнительныеПараметры.КонтекстЭДО;
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭтоЭлектроннаяПодписьВМоделиСервиса, Элементы.Сертификат, Объект.Сертификаты, СертификатПредставление);	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.Сертификаты.Очистить();
	Если КонтекстЭДО <> Неопределено Тогда
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭтоЭлектроннаяПодписьВМоделиСервиса, Элемент, Объект.Сертификаты, СертификатПредставление);
	Иначе
		СертификатПредставление = "";
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Объект.Сертификаты.Количество() > 0 Тогда
		Если КонтекстЭДО <> Неопределено Тогда
			КонтекстЭДО.ПоказатьСертификат(ЭтоЭлектроннаяПодписьВМоделиСервиса, Объект.Сертификаты, "AddressBook");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоЭлектроннаяПодписьВМоделиСервиса = ЭлектроннаяПодписьВМоделиСервисаВызовСервера.ЭтоЭлектроннаяПодписьВМоделиСервиса();
	
КонецПроцедуры
