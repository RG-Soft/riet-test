
//////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ ОБЪЕКТА

Процедура ПередЗаписью(Отказ)
	
	СформироватьНаименование();
	ЗаменитьРусскиеБуквыНаЛатинскиеВМаркировке();
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьВозможностьИзменения(Отказ);		
			
КонецПроцедуры

Процедура СформироватьНаименование()
	
	РГСофтКлиентСервер.УстановитьЗначение(Характеристика, СокрЛП(Характеристика));
	
	Если НЕ ЗначениеЗаполнено(Характеристика)
		ИЛИ Характеристика = ":" Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Владелец", Владелец);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СтрокиГТД.ОписаниеТовара
			|ИЗ
			|	Справочник.СтрокиГТД КАК СтрокиГТД
			|ГДЕ
			|	СтрокиГТД.Ссылка = &Владелец";
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		Добавка = Выборка.ОписаниеТовара;
		
	Иначе
		
		Добавка = Характеристика;
		
	КонецЕсли;
	
	РГСофтКлиентСервер.УстановитьЗначение(Наименование, СокрЛП(Код) + "-" + СокрЛП(Добавка));
	
КонецПроцедуры

Процедура ЗаменитьРусскиеБуквыНаЛатинскиеВМаркировке()
	
	НоваяМаркировка = СтрЗаменить(МаркировкаТовара, "у", "y");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "У", "Y");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "к", "k");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "К", "K");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "е", "e");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "Е", "E");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "Н", "H");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "х", "x");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "Х", "X");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "В", "B");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "р", "p");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "Р", "P");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "о", "o");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "О", "O");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "с", "c");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "С", "C");
	НоваяМаркировка = СтрЗаменить(НоваяМаркировка, "Т", "T");
	РГСофтКлиентСервер.УстановитьЗначение(МаркировкаТовара, СокрЛП(НоваяМаркировка));
	
КонецПроцедуры

Процедура ПроверитьВозможностьИзменения(Отказ)
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗакрытиеПоставкиСопоставление.Ссылка.Представление КАК InvoiceLinesMatchingПредставление
		|ИЗ
		|	Документ.ЗакрытиеПоставки.Сопоставление КАК ЗакрытиеПоставкиСопоставление
		|ГДЕ
		|	ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД = &Ссылка
		|	И ЗакрытиеПоставкиСопоставление.Ссылка.Проведен";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Нельзя изменять CCD line good """ + ЭтотОбъект + """, так он указан в проведенном документе """ + Выборка.InvoiceLinesMatchingПредставление + """!",
			ЭтотОбъект, , , Отказ);
	КонецЕсли;
				
КонецПроцедуры 
