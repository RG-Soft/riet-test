
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
                    		
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
 	  
	//отбор ЗаказНаряды
	ЗначениеОтбора = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, Неопределено);
	ЗаказНаряды.Параметры.УстановитьЗначениеПараметра("Договор", ЗначениеОтбора);
	            	 	
	// Проверка ведения однофирменности 
	Элементы.Организация.ТолькоПросмотр = НЕ УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(Пользователи.ТекущийПользователь(), "УчетПоВсемОрганизациям");
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		ТекстНаименования = ?(Параметры.ЗначенияЗаполнения.Свойство("Наименование"), Параметры.ЗначенияЗаполнения.Наименование, ""); 
		ЗаполнитьНовыйЭлемент(ТекстНаименования);
	КонецЕсли;
	
	//Добавила Федотова Л., РГ-Софт, 14.10.15, вопрос SLI-0005864
	Исполнено = РассчитатьИсполнениеБюджетаПоДоговоруНаСервере();
	Элементы.ДекорацияИсполнениеБюджетаПоДоговору.Заголовок = "Исполнено: " + Исполнено;
	//
	УправлениеФормой(ЭтаФорма);
	
	//-> RG-Soft VIvanov 2016/08/16
	Если РольДоступна("ERMПравоТолькоИзмененияУсловийДоговоров") Тогда
		Элементы.ГруппаШапка.Доступность = Ложь;
		Элементы.Комментарий.Доступность = Ложь;
		Элементы.Основные.Доступность = Ложь;
		Элементы.ГруппаSiebelWO.Доступность = Ложь;
		Элементы.ГруппаКодыCRMпоСегментам.Доступность = Ложь;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ERMАналитика;
	КонецЕсли;
	//<- RG-Soft VIvanov 2016/08/16
     
КонецПроцедуры

//Добавила Федотова Л., РГ-Софт, 14.10.15, вопрос SLI-0005864
&НаКлиенте
Процедура РассчитатьИсполнениеБюджетаПоДоговору(Команда)
	Исполнено = 0;
	Если Объект.СуммаКонтракта > 0 Тогда
		ЗаполнитьРегистрИсполнениеБюджетаПоДоговору();
		Исполнено = РассчитатьИсполнениеБюджетаПоДоговоруНаСервере();
	КонецЕсли; 
	Элементы.ДекорацияИсполнениеБюджетаПоДоговору.Заголовок = "Исполнено: " + Исполнено;
КонецПроцедуры

//Добавила Федотова Л., РГ-Софт, 14.10.15, вопрос SLI-0005864
&НаСервере
Функция ЗаполнитьРегистрИсполнениеБюджетаПоДоговору()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПродажиВыручка_SB.Регистратор,
		|	ПродажиВыручка_SB.Период,
		|	ПродажиВыручка_SB.Регистратор.ВалютаДокумента КАК ВалютаДокумента,
		|	ПродажиВыручка_SB.Регистратор.КурсВзаиморасчетов КАК КурсВзаиморасчетов
		|ИЗ
		|	РегистрНакопления.ПродажиВыручка_SB КАК ПродажиВыручка_SB
		|ГДЕ
		|	ПродажиВыручка_SB.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И НЕ ПродажиВыручка_SB.Регистратор ССЫЛКА Документ.КорректировкаЗаписейРегистров";
	
	Запрос.УстановитьПараметр("ДоговорКонтрагента", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НаборЗаписейИсполнениеБюджетаКонтракта = РегистрыНакопления.ИсполнениеБюджетаКонтракта.СоздатьНаборЗаписей();
		НаборЗаписейИсполнениеБюджетаКонтракта.Отбор.Регистратор.Установить(ВыборкаДетальныеЗаписи.Регистратор);
		НоваяЗапись = НаборЗаписейИсполнениеБюджетаКонтракта.Добавить();
		
		НоваяЗапись.ДоговорКонтрагента = Объект.Ссылка;
		НоваяЗапись.Период = ВыборкаДетальныеЗаписи.Период;
		НоваяЗапись.Регистратор = ВыборкаДетальныеЗаписи.Регистратор;
		Если Объект.ВалютаВзаиморасчетов = ВыборкаДетальныеЗаписи.ВалютаДокумента Тогда 
			НоваяЗапись.Сумма = ВыборкаДетальныеЗаписи.Регистратор.СуммаДокумента;
		Иначе 	                                                     
			НоваяЗапись.Сумма = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(ВыборкаДетальныеЗаписи.Регистратор.СуммаДокумента, ВыборкаДетальныеЗаписи.ВалютаДокумента, Объект.ВалютаВзаиморасчетов, ВыборкаДетальныеЗаписи.КурсВзаиморасчетов, ОбщегоНазначения.ПолучитьКурсВалюты(Объект.ВалютаВзаиморасчетов, ВыборкаДетальныеЗаписи.Период).Курс);
		КонецЕсли;
		НоваяЗапись.Активность = Истина;
		
		НаборЗаписейИсполнениеБюджетаКонтракта.Записать();
	КонецЦикла;
	
КонецФункции //ЗаполнитьРегистрИсполнениеБюджетаПоДоговору

//Добавила Федотова Л., РГ-Софт, 14.10.15, вопрос SLI-0005864
&НаСервере
Функция РассчитатьИсполнениеБюджетаПоДоговоруНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсполнениеБюджетаКонтрактаОбороты.СуммаОборот
	|ИЗ
	|	РегистрНакопления.ИсполнениеБюджетаКонтракта.Обороты КАК ИсполнениеБюджетаКонтрактаОбороты
	|ГДЕ
	|	ИсполнениеБюджетаКонтрактаОбороты.ДоговорКонтрагента = &ДоговорКонтрагента";
	
	Запрос.УстановитьПараметр("ДоговорКонтрагента", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.СуммаОборот;
	КонецЦикла;
	Возврат 0;
	
КонецФункции // РассчитатьИсполнениеБюджетаПоДоговоруНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаказНаряды.Параметры.УстановитьЗначениеПараметра("Договор", Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ЭтаФорма.ВладелецФормы <> Неопределено Тогда
		ОповеститьОВыборе(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВедениеВзаиморасчетовПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)

	Если НЕ ЗначениеЗаполнено(Объект.ВидДоговора)
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее")) Тогда
		Объект.ТипЦен = Неопределено;
		Объект.СпособРасчетаКомиссионногоВознаграждения = Неопределено;
		Объект.ПроцентКомиссионногоВознаграждения = 0;
		Объект.СрокОплаты = 0;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.ВидДоговора)
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Прочее"))
			ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")) Тогда
		Объект.РасчетыВУсловныхЕдиницах = Ложь;
		Если Объект.ВедениеВзаиморасчетов = ПредопределенноеЗначение("Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоРасчетнымДокументам") тогда
			Объект.ВедениеВзаиморасчетов = ПредопределенноеЗначение("Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом");
		КонецЕсли;
	КонецЕсли;

	//добавил Трефиленков Дмитрий, РГ-Софт
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда
		Объект.ВедениеВзаиморасчетов = ПредопределенноеЗначение("Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоРасчетнымДокументам");
	КонецЕсли; //конец добавления

	Если (Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"))
			ИЛИ Объект.РасчетыВУсловныхЕдиницах Тогда
		Объект.РеализацияНаЭкспорт = Ложь;
	КонецЕсли;

	ЭтоДоговорПриобретения = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"));

	Если НЕ ЭтоДоговорПриобретения Тогда
		Объект.УчетАгентскогоНДС = Ложь;
		Объект.ВидАгентскогоДоговора = Неопределено;
	КонецЕсли;

	Если Объект.УчетАгентскогоНДС
			И (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")) Тогда
		Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.Нерезидент");
	КонецЕсли;
	 
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ВалютаВзаиморасчетовПриИзменении(Элемент)

	Если Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета и Объект.РасчетыВУсловныхЕдиницах Тогда
		Объект.РасчетыВУсловныхЕдиницах = Ложь;
	ИначеЕсли Объект.ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета тогда
		Объект.РасчетыВУсловныхЕдиницах = Истина;
	КонецЕсли;
	Если Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета и Объект.РеализацияНаЭкспорт Тогда
		Объект.РеализацияНаЭкспорт = Ложь;
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура РасчетыВУсловныхЕдиницахПриИзменении(Элемент)

	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда
		Элементы.РеализацияНаЭкспорт.Доступность = Не Объект.РасчетыВУсловныхЕдиницах;
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура РеализацияНаЭкспортПриИзменении(Элемент)

	Элементы.РасчетыВУсловныхЕдиницах.Доступность = Не Объект.РеализацияНаЭкспорт;

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры
     
&НаКлиенте
Процедура УчетАгентскогоНДСПриИзменении(Элемент)

	Если Объект.УчетАгентскогоНДС Тогда
		Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом") Тогда
			Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.Нерезидент");
		Иначе
			Объект.ВидАгентскогоДоговора = ПредопределенноеЗначение("Перечисление.ВидыАгентскихДоговоров.Аренда");
		КонецЕсли;
		Объект.РасчетыВУсловныхЕдиницах = Ложь;
	Иначе
		Объект.ВидАгентскогоДоговора = Неопределено;
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЗАКАЗНАРЯДЫ

&НаКлиенте
Процедура ЗаказНарядыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		
		Если Вопрос("Записать документ?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда 
			Возврат;
		КонецЕсли;
		
		Если Не Записать(Новый Структура) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Не удалось записать текущий документ, "
				+ ОписаниеОшибки());
			Возврат;
		КонецЕсли;
	
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ЗаказНаряды.Форма.ФормаЭлемента", Новый Структура("Договор", Объект.Ссылка), ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьНовыйЭлемент(ТекстНаименования) 

	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Объект.Наименование = СокрЛП(ТекстНаименования);
	
	//добавил Трефиленков Дмитрий, РГ-Софт
	Если Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем Тогда
		Объект.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоРасчетнымДокументам;
	КонецЕсли; //конец добавления
	
	Если Не ЗначениеЗаполнено(Объект.ВедениеВзаиморасчетов) Тогда
		Объект.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.ВалютаВзаиморасчетов) Тогда
		Объект.ВалютаВзаиморасчетов = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнаяВалютаВзаиморасчетов");
		Если Не ЗначениеЗаполнено(Объект.ВалютаВзаиморасчетов) Тогда
			Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
		КонецЕсли;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		
		Объект.Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнаяОрганизация");
		
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|Организации.Ссылка КАК ОрганизацияСсылка,
			|Организации.НаименованиеПолное
			|ИЗ
			|Справочник.Организации КАК Организации
			|";
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Объект.Организация = Выборка.ОрганизацияСсылка;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.ВидДоговора) 
		И Не ЗначениеЗаполнено(Объект.Владелец) Тогда
			Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Прочее;
	КонецЕсли;

КонецПроцедуры // ЗаполнитьНовыйЭлемент()

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	ВалютаРегламентированногоУчета = Форма.ВалютаРегламентированногоУчета;

	ЭтоДоговорКомиссии     = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"));
		
	ЭтоДоговорПриобретения = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"));
		
	ЭтоДоговорРеализации   = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"));
	
	ДоступностьКомиссионногоВознаграждения = (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"))
		ИЛИ (Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"));
		
	//Группа "Ведение взаиморасчетов"

	Элементы.РеализацияНаЭкспорт.Доступность =
		(Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"))
		И (Объект.ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета)
		И НЕ Объект.РасчетыВУсловныхЕдиницах;
                  	
	Если (ЭтоДоговорРеализации или Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком")) Тогда
		
		Если Объект.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета тогда
			Элементы.РасчетыВУсловныхЕдиницах.Доступность = Ложь;
			Элементы.Курс.Доступность = Ложь;
		ИначеЕсли Объект.УчетАгентскогоНДС тогда
			Элементы.РасчетыВУсловныхЕдиницах.Доступность = Ложь;
			Элементы.Курс.Доступность = Ложь;
		ИначеЕсли Объект.РеализацияНаЭкспорт Тогда
			Элементы.РасчетыВУсловныхЕдиницах.Доступность = Ложь;
			Элементы.Курс.Доступность = Ложь;
		Иначе
			Элементы.РасчетыВУсловныхЕдиницах.Доступность = Истина;
			Элементы.Курс.Доступность = Истина;
		Конецесли;

	Иначе
		Элементы.РасчетыВУсловныхЕдиницах.Доступность	= Ложь;
		Элементы.ВедениеВзаиморасчетов.Доступность		= Ложь;
		Элементы.Курс.Доступность = Ложь;
	КонецЕсли;

	//добавила Федотова Людмила, РГ-Софт
	Элементы.ВедениеВзаиморасчетов.Доступность = (Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"));
		
	//Группа "Комиссионное вознаграждение"

	Элементы.ГруппаКомиссионноеВознаграждение.Доступность = ДоступностьКомиссионногоВознаграждения;

	//Группа "НДС"

	Если Не ЭтоДоговорРеализации Тогда
		Элементы.ГруппаНДСПоНалоговомуАгенту.Доступность = ЭтоДоговорПриобретения;
		Элементы.ВидАгентскогоДоговора.Доступность = (ЭтоДоговорПриобретения И Объект.УчетАгентскогоНДС И НЕ ЭтоДоговорКомиссии);
	КонецЕсли;
	
	// { RGS VChaplygin 11.08.2016 10:08:47 - Доработка для ERM
	ermAmendmentПриИзмененииНаСервере(Форма);
	// } RGS VChaplygin 11.08.2016 10:09:09 - Доработка для ERM
	 
КонецПроцедуры

//-> RG-Soft VIvanov 2016/08/10
&НаКлиенте
Процедура crmContractIDНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.crmContracts.ФормаВыбора",,Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура crmContractIDОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение <> Неопределено Тогда
		ЗаполнитьРеквизитыCRM(ВыбранноеЗначение);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыCRM(ВыбранноеЗначение)
	
	Объект.crmContractID = ВыбранноеЗначение.Код;
	Объект.crmContractName = ВыбранноеЗначение.Наименование;
	Объект.crmDFNName = ВыбранноеЗначение.crmDFNName;
	Объект.crmCreatedBy = ВыбранноеЗначение.crmCreatedBy;
	Объект.crmContractCurrency = ВыбранноеЗначение.crmContractCurrency;
	Объект.crmContractValueUSD = ВыбранноеЗначение.crmContractValueUSD;
	Объект.crmCreatedDate = ВыбранноеЗначение.crmCreatedDate;
	Объект.crmEffectiveDate = ВыбранноеЗначение.crmEffectiveDate;
	Объект.crmExpiryDate = ВыбранноеЗначение.crmExpiryDate;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ermAmendmentПриИзмененииНаСервере(Форма)
	
	Объект = Форма.Объект;
	ermAmendmentNameEnable = Объект.ermAmendment;
	Если НЕ ermAmendmentNameEnable Тогда
		Объект.ermAmendmentName = "";
	КонецЕсли;
	
	Форма.Элементы.ermAmendmentName.Доступность = ermAmendmentNameEnable;
	
КонецПроцедуры

&НаКлиенте
Процедура ermAmendmentПриИзменении(Элемент)
	
	ermAmendmentПриИзмененииНаСервере(ЭтаФорма);

КонецПроцедуры
//<- RG-Soft VIvanov 2016/08/10




               
