&НаКлиенте
Перем КонтекстЭДОКлиент;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторФормыВладельца = Параметры.ИдентификаторФормыВладельца;
	РежимВыбораИзКаталога = Параметры.РежимВыбораИзКаталога;
	
	СписокВыбораВидовДокументов = Элементы.ВидДокумента.СписокВыбора;
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.СчетФактура);
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.ТоварноТранспортнаяНакладная);
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот);
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.ГрузоваяТаможеннаяДекларация);
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.ДобавочныйЛистГрузовойТаможеннойДекларации);
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.ТоварнаяНакладнаяТОРГ12);
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.СпецификацияЦены);
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.ДополнениеКДоговору);
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.Договор);
	СписокВыбораВидовДокументов.Добавить(Перечисления.ВидыПредставляемыхДокументов.КорректировочныйСчетФактура);
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.СкрытьЭлементыФормыПриИспользованииОднойОрганизации(ЭтаФорма, "НадписьОрганизация");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииПослеЗагрузкиФайла", ЭтотОбъект);
	
	Если РежимВыбораИзКаталога Тогда
		РезультатЗагрузки = ЗагрузитьКаталог();
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, РезультатЗагрузки);
	Иначе
		ЗагрузитьФайл(ОписаниеОповещения);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииПослеЗагрузкиФайла(РезультатЗагрузки, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатЗагрузки Тогда
		Закрыть();
	Иначе
		//установим все флажки по умолчанию
		Для Каждого Строка Из ОтображаемыеДокументы Цикл
			Строка.Выбрать = Истина;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент)
	
	ЗаполнитьПоОтборамТаблицуОтображаемыеДокументы();
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникПриИзменении(Элемент)
	
	ЗаполнитьПоОтборамТаблицуОтображаемыеДокументы();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого Строка Из ОтображаемыеДокументы Цикл
		Строка.Выбрать = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого Строка Из ОтображаемыеДокументы Цикл
		Строка.Выбрать = Истина;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция СкопироватьФайлыНаСервер_НаСервере(МассивФайлов)
	
	Попытка
		
		Для Каждого ЭлФайл Из МассивФайлов Цикл
			ПолноеИмяФайлаНаСервере = ПолноеИмяФайлаОбменаНаСервере + ЭлФайл.Имя;
			ПолучитьИзВременногоХранилища(ЭлФайл.АдресДанных).Записать(ПолноеИмяФайлаНаСервере);
		КонецЦикла;	
		Возврат Истина;
		
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	
КонецФункции

&НаКлиенте
Функция СкопироватьФайлыНаСервер(МассивИменЗагружаемыхФайлов)
	// составляем массив с объектами Файл
	МассивФайлов = Новый Массив;
	Для Каждого ИмяФайла Из МассивИменЗагружаемыхФайлов Цикл
		ФайлОбъект = Новый Файл(ПолноеИмяФайлаОбмена + ИмяФайла);
		
		Если НЕ ФайлОбъект.Существует() Тогда
			РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("В выбранном каталоге отсутствует файл описания ""scandescription.xml"".", ИдентификаторФормыВладельца);
			Возврат Ложь;
		КонецЕсли;

		МассивФайлов.Добавить(Новый Структура("Имя, ПолноеИмя, Расширение, АдресДанных", ФайлОбъект.Имя, ФайлОбъект.ПолноеИмя, ФайлОбъект.Расширение));
	КонецЦикла;
	
	ПомещаемыеФайлы = Новый Массив;
	Для Каждого ЭлФайл Из МассивФайлов Цикл 
		ОписаниеФайла = Новый ОписаниеПередаваемогоФайла(ЭлФайл.ПолноеИмя); 
		ПомещаемыеФайлы.Добавить(ОписаниеФайла);
	КонецЦикла;
	ПомещенныеФайлы = Новый Массив;
	
	Если ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы, , Ложь, УникальныйИдентификатор) Тогда
		
		Для каждого ЭлФайл Из МассивФайлов Цикл
			Для каждого ОписаниеПереданногоФайла Из ПомещенныеФайлы Цикл
				Если ОписаниеПереданногоФайла.Имя = ЭлФайл.ПолноеИмя Тогда
					ЭлФайл.АдресДанных = ОписаниеПереданногоФайла.Хранение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла; 
		
		Возврат СкопироватьФайлыНаСервер_НаСервере(МассивФайлов);
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если НЕ ЗначениеЗаполнено(ОрганизацияСсылка) Тогда
		ТекстСообщения = ВернутьСтр("ru = 'Для загрузки необходимо указать организацию.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Выбрать", Истина);
	
	СтрокиВыбранныхДокументов = ОтображаемыеДокументы.НайтиСтроки(ПараметрыОтбора);
	
	Если СтрокиВыбранныхДокументов.Количество() = 0 Тогда
		
		ТекстСообщения = ВернутьСтр("ru = 'Для загрузки необходимо выбрать хотя бы один документ.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	КонецЕсли;	
	
	МассивИменЗагружаемыхФайлов = Новый Массив;
	СтруктураРезультата = СформироватьСтруктуруРезультатНаСервере(МассивИменЗагружаемыхФайлов);
	
	Если РежимВыбораИзКаталога Тогда
		РезультатКопирования = СкопироватьФайлыНаСервер(МассивИменЗагружаемыхФайлов);
		Если НЕ РезультатКопирования Тогда
			ТекстСообщения = ВернутьСтр("ru = 'Не удалось поместить файлы загружаемых документов на сервер.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОповеститьОВыборе(СтруктураРезультата);
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузитьКаталог()
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбора.Заголовок = "Выберите каталог для загрузки";
	Если ДиалогВыбора.Выбрать() Тогда
		
		ВыбранныйКаталог = ДиалогВыбора.Каталог;
		
		ВыбранныйКаталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ВыбранныйКаталог);
		
		ИмяФайлаОписания = "scandescription.xml";
		
		ФайлОписания = Новый Файл(ВыбранныйКаталог + ИмяФайлаОписания);
		
		Если ФайлОписания.Существует() Тогда
			ПолноеИмяФайлаОбмена = ВыбранныйКаталог; // для отображения на форме
		Иначе
			РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("В выбранном каталоге отсутствует файл описания ""scandescription.xml"".", ИдентификаторФормыВладельца);
			Возврат Ложь;
		КонецЕсли;
		
		// составляем массив с объектами Файл
		МассивФайлов = Новый Массив;
		МассивФайлов.Добавить(Новый Структура("Имя, ПолноеИмя, Расширение, АдресДанных", ФайлОписания.Имя, ФайлОписания.ПолноеИмя, ФайлОписания.Расширение));
		
		ПомещаемыеФайлы = Новый Массив;
		Для Каждого ЭлФайл Из МассивФайлов Цикл 
			ОписаниеФайла = Новый ОписаниеПередаваемогоФайла(ЭлФайл.ПолноеИмя); 
			ПомещаемыеФайлы.Добавить(ОписаниеФайла);
		КонецЦикла;
		ПомещенныеФайлы = Новый Массив;
		
		Если ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы, , Ложь, УникальныйИдентификатор) Тогда
			
			Для каждого ЭлФайл Из МассивФайлов Цикл
				Для каждого ОписаниеПереданногоФайла Из ПомещенныеФайлы Цикл
					Если ОписаниеПереданногоФайла.Имя = ЭлФайл.ПолноеИмя Тогда
						ЭлФайл.АдресДанных = ОписаниеПереданногоФайла.Хранение;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла; 
			
			АдресФайлаОписанияВоВременномХранилище = МассивФайлов[0].АдресДанных;
			
			Возврат ЗагрузитьИзКаталогаНаСервере(АдресФайлаОписанияВоВременномХранилище);
			
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ЗагрузитьИзКаталогаНаСервере(АдресФайлаОписанияВоВременномХранилище)
	
	// помещаем файл описания во временный каталог на сервере
	ИмяФайлаОписания = "scandescription.xml";
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	КаталогРаспаковки = КонтекстЭДОСервер.СоздатьВременныйКаталогСервер();
	
	ПолноеИмяФайлаОписанияНаСервере = КаталогРаспаковки + ИмяФайлаОписания;
	
	ПолучитьИзВременногоХранилища(АдресФайлаОписанияВоВременномХранилище).Записать(ПолноеИмяФайлаОписанияНаСервере);
	
	// читаем XML
	ТекстXML = КонтекстЭДОСервер.ПрочитатьТекстИзФайла(ПолноеИмяФайлаОписанияНаСервере, , Истина);
	Если НЕ ЗначениеЗаполнено(ТекстXML) Тогда
		КонтекстЭДОСервер.УдалитьВременныйФайл(КаталогРаспаковки);
		Возврат Ложь;
	КонецЕсли;
	
	// загружаем XML в дерево
	ДеревоXML = КонтекстЭДОСервер.ЗагрузитьСтрокуXMLВДеревоЗначений(ТекстXML);
	Если НЕ ЗначениеЗаполнено(ДеревоXML) Тогда
		КонтекстЭДОСервер.УдалитьВременныйФайл(КаталогРаспаковки);
		Возврат Ложь;
	КонецЕсли;
	
	// разбираем дерево XML, заполняем таблицу ЗагруженныеДокументы
	Если НЕ ЗаполнитьТаблицуЗагруженныеДокументы(ДеревоXML) Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	//заполняем таблицу ОтображаемыеДокументы
	ЗаполнитьПоОтборамТаблицуОтображаемыеДокументы();
	
	ПолноеИмяФайлаОбменаНаСервере = КаталогРаспаковки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьФайл(ВыполняемоеОповещение)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьФайлПродолжение", ЭтотОбъект, ВыполняемоеОповещение);	
	КонтекстЭДОКлиент.ПоддерживаетсяИспользованиеРасширенияРаботыСФайлами(ОписаниеОповещения, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлПродолжение(Результат, ВыполняемоеОповещение) Экспорт 

	ПоддерживаетсяИспользованиеРасширенияРаботыСФайлами = Результат;
	
	Если ПоддерживаетсяИспользованиеРасширенияРаботыСФайлами Тогда
		ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогВыбора.Заголовок = "Выберите файл для загрузки";
		ДиалогВыбора.МножественныйВыбор = Ложь;
		ДиалогВыбора.ПроверятьСуществованиеФайла = Истина;
		НачалоИмениФайла = "SCAN_";
		ДиалогВыбора.Фильтр = "ZIP архив(" + НачалоИмениФайла + "*.zip)|" + НачалоИмениФайла + "*.zip";
		Если ДиалогВыбора.Выбрать() Тогда
			
			ВыбранныеФайлы = ДиалогВыбора.ВыбранныеФайлы;
			
			ПолноеИмяФайлаОбмена = ВыбранныеФайлы[0]; // для отображения на форме
			
			// составляем массив с объектами Файл
			МассивФайлов = Новый Массив;
			Для Каждого ЭлФайл Из ВыбранныеФайлы Цикл
				Файл = Новый Файл(ЭлФайл);
				МассивФайлов.Добавить(Новый Структура("Имя, ПолноеИмя, Расширение, АдресДанных", Файл.Имя, Файл.ПолноеИмя, Файл.Расширение));
			КонецЦикла;
			
			ПомещаемыеФайлы = Новый Массив;
			Для Каждого ЭлФайл Из МассивФайлов Цикл 
				ОписаниеФайла = Новый ОписаниеПередаваемогоФайла(ЭлФайл.ПолноеИмя); 
				ПомещаемыеФайлы.Добавить(ОписаниеФайла);
			КонецЦикла;
			ПомещенныеФайлы = Новый Массив;
			
			Если ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы, , Ложь, УникальныйИдентификатор) Тогда
				
				Для каждого ЭлФайл Из МассивФайлов Цикл
					Для каждого ОписаниеПереданногоФайла Из ПомещенныеФайлы Цикл
						Если ОписаниеПереданногоФайла.Имя = ЭлФайл.ПолноеИмя Тогда
							ЭлФайл.АдресДанных = ОписаниеПереданногоФайла.Хранение;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла; 
				
				АдресФайлаОбменаВоВременномХранилище = МассивФайлов[0].АдресДанных;
			
				Результат = ЗагрузитьФайлНаСервере(АдресФайлаОбменаВоВременномХранилище);
				ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
	
	Иначе
		
		АдресФайлаОбменаВоВременномХранилище = "";
		ИмяФайлаОбмена = "";                                                           
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьФайлЗавершение", ЭтотОбъект, ВыполняемоеОповещение);
		НачатьПомещениеФайла(ОписаниеОповещения, АдресФайлаОбменаВоВременномХранилище, ИмяФайлаОбмена, Истина, УникальныйИдентификатор);
	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлЗавершение(Результат, АдресФайлаОбменаВоВременномХранилище, ИмяФайлаОбмена, ВыполняемоеОповещение) Экспорт
	
	Если Результат Тогда
		Если Прав(ИмяФайлаОбмена,4) = ".zip" Тогда
			Результат = ЗагрузитьФайлНаСервере(АдресФайлаОбменаВоВременномХранилище);
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
		Иначе
			РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Выбранный файл не является zip-архивом.", ИдентификаторФормыВладельца);
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
		КонецЕсли;
	Иначе
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьФайлНаСервере(АдресФайлаОбменаВоВременномХранилище)
	
	ПолноеИмяФайлаОбменаНаСервере = ПолучитьИмяВременногоФайла();
	ПолучитьИзВременногоХранилища(АдресФайлаОбменаВоВременномХранилище).Записать(ПолноеИмяФайлаОбменаНаСервере);
	
	// распаковываем файл описания из архива обмена
	ИмяФайлаОписания = "scandescription.xml";
	ЧтениеЗИП = Новый ЧтениеZipФайла(ПолноеИмяФайлаОбменаНаСервере);
	ЭлементОписание = ЧтениеЗИП.Элементы.Найти(ИмяФайлаОписания);
	Если ЭлементОписание = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	КаталогРаспаковки = КонтекстЭДОСервер.СоздатьВременныйКаталогСервер();
	ЧтениеЗИП.Извлечь(ЭлементОписание, КаталогРаспаковки, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
	
	// читаем XML
	ТекстXML = КонтекстЭДОСервер.ПрочитатьТекстИзФайла(КаталогРаспаковки + ИмяФайлаОписания, , Истина);
	Если НЕ ЗначениеЗаполнено(ТекстXML) Тогда
		ЧтениеЗИП.Закрыть();
		КонтекстЭДОСервер.УдалитьВременныйФайл(ПолноеИмяФайлаОбменаНаСервере);
		КонтекстЭДОСервер.УдалитьВременныйФайл(КаталогРаспаковки);
		Возврат Ложь;
	КонецЕсли;
	
	// загружаем XML в дерево
	ДеревоXML = КонтекстЭДОСервер.ЗагрузитьСтрокуXMLВДеревоЗначений(ТекстXML);
	Если НЕ ЗначениеЗаполнено(ДеревоXML) Тогда
		ЧтениеЗИП.Закрыть();
		КонтекстЭДОСервер.УдалитьВременныйФайл(ПолноеИмяФайлаОбменаНаСервере);
		КонтекстЭДОСервер.УдалитьВременныйФайл(КаталогРаспаковки);
		Возврат Ложь;
	КонецЕсли;
	
	// разбираем дерево XML, заполняем таблицу ЗагруженныеДокументы
	Если НЕ ЗаполнитьТаблицуЗагруженныеДокументы(ДеревоXML) Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	//заполняем таблицу ОтображаемыеДокументы
	ЗаполнитьПоОтборамТаблицуОтображаемыеДокументы();
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеСтрокиУзла(Узел, Имя) 
	
	СтрокаУзла = Узел.Строки.Найти(Имя, "Имя");
	Если СтрокаУзла = Неопределено Тогда
		Возврат "";
	Иначе
		Возврат СтрокаУзла.Значение;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьСтрокаУзла(Узел, Имя) 
	
	СтрокаУзла = Узел.Строки.Найти(Имя, "Имя");
	Возврат СтрокаУзла <> Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтрокаУзла(Узел, Имя) 
	
	Возврат Узел.Строки.Найти(Имя, "Имя");
	
КонецФункции

&НаСервереБезКонтекста
Функция МассивСтрокУзла(Узел, Имя) 
	
	Возврат Узел.Строки.НайтиСтроки(Новый Структура("Имя", Имя));
	
КонецФункции

&НаСервереБезКонтекста
Функция ПрибавляемаяСтрока(Знач ПараметрСтрока) 
	
	Если ЗначениеЗаполнено(ПараметрСтрока) Тогда
		ПараметрСтрока = " " + ПараметрСтрока;
	КонецЕсли;
	
	Возврат ПараметрСтрока;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтрокаВЧисло(ИсходнаяСтрока)
	// Превращает строку в число без вызова исключений. Стандартная функция преобразования
	//   Число() строго контролирует отсутствие каких-либо символов кроме числовых.
	
	Результат = 0;
	ЗнаковПослеЗапятой = -1;
	ЗнакОтрицательный = Ложь;
	Для НомерСимвола = 1 По СтрДлина(ИсходнаяСтрока) Цикл
		КодСимвола = КодСимвола(ИсходнаяСтрока, НомерСимвола);
		Если КодСимвола = 32 Или КодСимвола = 160 Тогда // Пробел или неразрывный пробел.
			// Пропуск (действие не требуется).
		ИначеЕсли КодСимвола = 45 Тогда // Минус
			Если Результат <> 0 Тогда
				Возврат 0;
			КонецЕсли;
			ЗнакОтрицательный = Истина;
		ИначеЕсли КодСимвола = 44 Или КодСимвола = 46 Тогда // Запятая или точка.
			ЗнаковПослеЗапятой = 0; // Запуск отсчета знаков после запятой.
		ИначеЕсли КодСимвола > 47 И КодСимвола < 58 Тогда // Число.
			Если ЗнаковПослеЗапятой <> -1 Тогда
				ЗнаковПослеЗапятой = ЗнаковПослеЗапятой + 1;
			КонецЕсли;
			Число = КодСимвола - 48;
			Результат = Результат * 10 + Число;
		Иначе
			Возврат 0;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗнаковПослеЗапятой > 0 Тогда
		Результат = Результат / Pow(10, ЗнаковПослеЗапятой);
	КонецЕсли;
	Если ЗнакОтрицательный Тогда
		Результат = -Результат;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ОпределитьОрганизациюПоИННиКПП(ИНН, КПП)
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	Организации.Ссылка
	                      |ИЗ
	                      |	Справочник.Организации КАК Организации
	                      |ГДЕ
	                      |	Организации.ИНН = &ИНН");
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Если КПП <> "" Тогда
		Запрос.Текст = Запрос.Текст + "
	                      |	И Организации.КПП = &КПП";
		Запрос.УстановитьПараметр("КПП", КПП);
	КонецЕсли;
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	Возврат Справочники.Организации.ПустаяСсылка();
	
КонецФункции

&НаСервере
Функция ЗаполнитьТаблицуЗагруженныеДокументы(ДеревоXML)
	
	Если НЕ ЕстьСтрокаУзла(ДеревоXML, "Файл") Тогда
		РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Некорректная структура XML файла описания.", ИдентификаторФормыВладельца);
		Возврат Ложь;
	КонецЕсли;
	
	УзелФайл = СтрокаУзла(ДеревоXML, "Файл");
	
	Если НЕ ЕстьСтрокаУзла(УзелФайл, "ВерсФорм")
		ИЛИ НЕ ЕстьСтрокаУзла(УзелФайл, "ДатаВыгрузки")
		ИЛИ НЕ ЕстьСтрокаУзла(УзелФайл, "ВремяВыгрузки")
		ИЛИ НЕ ЕстьСтрокаУзла(УзелФайл, "Организация")
		ИЛИ НЕ ЕстьСтрокаУзла(УзелФайл, "Документ") Тогда
	
		РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Некорректная структура XML файла описания.", ИдентификаторФормыВладельца);
		Возврат Ложь;
	
	КонецЕсли;
	
	ВерсияФормата	 	= СтрокаВЧисло(ЗначениеСтрокиУзла(УзелФайл, "ВерсФорм"));
	Если ВерсияФормата >= 2 Тогда
	
		РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Некорректная структура XML файла описания. Версия формата " + Формат(ВерсияФормата, "ЧДЦ=2; ЧРД=.") + " не поддерживается.", ИдентификаторФормыВладельца);
		Возврат Ложь;
	
	КонецЕсли;

	УзелОрганизация 	= СтрокаУзла(УзелФайл, "Организация");
	УзелУчастники 		= СтрокаУзла(УзелФайл, "Участники");
	
	//разбираем узел Организация
	Если НЕ ЕстьСтрокаУзла(УзелОрганизация, "Наименование")
		ИЛИ НЕ ЕстьСтрокаУзла(УзелОрганизация, "ИНН") Тогда
		
		РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Некорректная структура XML файла описания.", ИдентификаторФормыВладельца);
		Возврат Ложь;
		
	КонецЕсли;
	
	ИНН = ЗначениеСтрокиУзла(УзелОрганизация, "ИНН");
	КПП = ЗначениеСтрокиУзла(УзелОрганизация, "КПП");
	
	ИННКПП = ИНН + ?(ЗначениеЗаполнено(КПП),"/" + КПП,"");
	
	ОрганизацияСсылка = ОпределитьОрганизациюПоИННиКПП(ИНН, КПП);
	
	Если НЕ ЗначениеЗаполнено(ОрганизацияСсылка) Тогда
		
		Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
			
			РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Пакет обмена предназначен для организации с реквизитами ИНН/КПП: " + ИННКПП + "
			|Установлена для загрузки текущая организация", ИдентификаторФормыВладельца);
			
			Модуль = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации");
			ОрганизацияСсылка = Модуль.ОрганизацияПоУмолчанию();

		Иначе
			РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("В справочнике организаций не обнаружено элемента с реквизитами ИНН/КПП: " + ИННКПП, ИдентификаторФормыВладельца);	
		КонецЕсли;
		
	КонецЕсли;

	ПредставлениеОрганизация = ЗначениеСтрокиУзла(УзелОрганизация, "Наименование") + " " + ИННКПП;
	
	ПредставлениеДатаВремяВыгрузки = ЗначениеСтрокиУзла(УзелФайл, "ДатаВыгрузки") + " " 
	+ СтрЗаменить(ЗначениеСтрокиУзла(УзелФайл, "ВремяВыгрузки"), ".", ":");
	
	СоответствиеУчастников = Новый Соответствие;
	СписокВыбораУчастников = Элементы.Участник.СписокВыбора;
	СписокВыбораУчастников.Очистить();
	
	РеквизитыУчастников.Очистить();
	
	Если УзелУчастники <> Неопределено Тогда
		Для каждого УзелУчастник Из МассивСтрокУзла(УзелУчастники, "Участник") Цикл
			
			Если НЕ ЕстьСтрокаУзла(УзелУчастник, "Идентификатор") Тогда
				
				РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Некорректная структура XML файла описания.", ИдентификаторФормыВладельца);
				Возврат Ложь;
				
			КонецЕсли;
			
			Идентификатор = ЗначениеСтрокиУзла(УзелУчастник, "Идентификатор");
			
			НоваяСтрокаРеквизиты = РеквизитыУчастников.Добавить();
			НоваяСтрокаРеквизиты.ИдентификаторУчастника 	= Идентификатор;
			
			Если ЕстьСтрокаУзла(УзелУчастник, "ЮридическоеЛицо") Тогда
				//это организация
				
				НоваяСтрокаРеквизиты.ЯвляетсяЮрЛицом = Истина;
				
				УзелЮридическоеЛицо = СтрокаУзла(УзелУчастник, "ЮридическоеЛицо");
				
				НаименованиеОрганизации = ЗначениеСтрокиУзла(УзелЮридическоеЛицо, "Наименование");
				ИНН 					= ЗначениеСтрокиУзла(УзелЮридическоеЛицо, "ИНН");
				КПП 					= ЗначениеСтрокиУзла(УзелЮридическоеЛицо, "КПП");
				
				НоваяСтрокаРеквизиты.НаименованиеОрганизации 	= НаименованиеОрганизации;
				НоваяСтрокаРеквизиты.ИНН 						= ИНН;
				НоваяСтрокаРеквизиты.КПП 						= КПП;
				
				ИННКПП = ИНН + ?(ЗначениеЗаполнено(КПП),"/" + КПП,"");
				ПредставлениеУчастника = НаименованиеОрганизации + ПрибавляемаяСтрока(ИННКПП);
				
			ИначеЕсли ЕстьСтрокаУзла(УзелУчастник, "ФизическоеЛицо") Тогда
				//это ИП	
				
				НоваяСтрокаРеквизиты.ЯвляетсяЮрЛицом = Ложь;
				
				УзелФизическоеЛицо = СтрокаУзла(УзелУчастник, "ФизическоеЛицо");
				
				ИНН = ЗначениеСтрокиУзла(УзелФизическоеЛицо, "ИНН");
				
				НоваяСтрокаРеквизиты.ИНН = ИНН;
				
				Если ЕстьСтрокаУзла(УзелФизическоеЛицо, "ФИО") Тогда
					УзелФИО = СтрокаУзла(УзелФизическоеЛицо, "ФИО");	
					ФамилияИП	= ЗначениеСтрокиУзла(УзелФИО, "ФамилияИП");
					ИмяИП 		= ЗначениеСтрокиУзла(УзелФИО, "ИмяИП");
					ОтчествоИП 	= ЗначениеСтрокиУзла(УзелФИО, "ОтчествоИП");
					
					НоваяСтрокаРеквизиты.ФамилияИП 	= ФамилияИП;
					НоваяСтрокаРеквизиты.ИмяИП 		= ИмяИП;
					НоваяСтрокаРеквизиты.ОтчествоИП = ОтчествоИП;
					
					ПредставлениеУчастника = ФамилияИП + ПрибавляемаяСтрока(ИмяИП) + ПрибавляемаяСтрока(ОтчествоИП) + ПрибавляемаяСтрока(ИНН);
				Иначе
					ПредставлениеУчастника = ИНН;
				КонецЕсли;
				
			Иначе
				
				РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Некорректная структура XML файла описания.", ИдентификаторФормыВладельца);
				Возврат Ложь;
				
			КонецЕсли;
			
			СоответствиеУчастников.Вставить(Идентификатор, ПредставлениеУчастника);
			СписокВыбораУчастников.Добавить(Идентификатор, ПредставлениеУчастника);
			
		КонецЦикла;
	КонецЕсли;
		
	СоответствиеВидовДокументов = Новый Соответствие;
	
	СоответствиеВидовДокументов.Вставить("01", Перечисления.ВидыПредставляемыхДокументов.СчетФактура);
	СоответствиеВидовДокументов.Вставить("02", Перечисления.ВидыПредставляемыхДокументов.ТоварноТранспортнаяНакладная);
	СоответствиеВидовДокументов.Вставить("03", Перечисления.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот);
	СоответствиеВидовДокументов.Вставить("04", Перечисления.ВидыПредставляемыхДокументов.ГрузоваяТаможеннаяДекларация);
	СоответствиеВидовДокументов.Вставить("05", Перечисления.ВидыПредставляемыхДокументов.ДобавочныйЛистГрузовойТаможеннойДекларации);
	СоответствиеВидовДокументов.Вставить("06", Перечисления.ВидыПредставляемыхДокументов.ТоварнаяНакладнаяТОРГ12);
	СоответствиеВидовДокументов.Вставить("07", Перечисления.ВидыПредставляемыхДокументов.СпецификацияЦены);
	СоответствиеВидовДокументов.Вставить("08", Перечисления.ВидыПредставляемыхДокументов.ДополнениеКДоговору);
	СоответствиеВидовДокументов.Вставить("09", Перечисления.ВидыПредставляемыхДокументов.Договор);
	СоответствиеВидовДокументов.Вставить("10", Перечисления.ВидыПредставляемыхДокументов.КорректировочныйСчетФактура);
	
	СоответствиеПредставлениеРолиУчастникаПоКоду = ПолучитьСоответствиеПредставлениеРолиУчастникаПоКоду();
	
	Для каждого УзелДокумент Из МассивСтрокУзла(УзелФайл, "Документ") Цикл
		
		Если НЕ ЕстьСтрокаУзла(УзелДокумент, "Вид") 
			ИЛИ НЕ ЕстьСтрокаУзла(УзелДокумент, "Файл") Тогда
			
			РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Некорректная структура XML файла описания.", ИдентификаторФормыВладельца);
			Возврат Ложь;
			
		КонецЕсли;
		
		//добавляем новую строку таблицы ЗагруженныеДокументы и заполняем её
		НоваяСтрокаДок = ЗагруженныеДокументы.Добавить();
		
		НоваяСтрокаДок.ИдентификаторДокумента = Новый УникальныйИдентификатор;
		
		НоваяСтрокаДок.ВидДокумента 		= СоответствиеВидовДокументов[ЗначениеСтрокиУзла(УзелДокумент, "Вид")];
		НоваяСтрокаДок.Дата 				= ДатаИзСтроки(ЗначениеСтрокиУзла(УзелДокумент, "Дата"));
		НоваяСтрокаДок.Номер 				= ЗначениеСтрокиУзла(УзелДокумент, "Номер");
		НоваяСтрокаДок.СуммаВсего		 	= СтрокаВЧисло(ЗначениеСтрокиУзла(УзелДокумент, "СуммаВсего"));
		НоваяСтрокаДок.СуммаНалога		 	= СтрокаВЧисло(ЗначениеСтрокиУзла(УзелДокумент, "СуммаНалога"));
		НоваяСтрокаДок.НомерОснования 		= ЗначениеСтрокиУзла(УзелДокумент, "НомерОснования");
		НоваяСтрокаДок.ДатаОснования		= ДатаИзСтроки(ЗначениеСтрокиУзла(УзелДокумент, "ДатаОснования"));
		НоваяСтрокаДок.Предмет				= ЗначениеСтрокиУзла(УзелДокумент, "Предмет");
		НоваяСтрокаДок.НачалоПериода		= ДатаИзСтроки(ЗначениеСтрокиУзла(УзелДокумент, "НачалоПериода"));
		НоваяСтрокаДок.КонецПериода			= ДатаИзСтроки(ЗначениеСтрокиУзла(УзелДокумент, "КонецПериода"));
		
		ПредставлениеДокумента = Строка(НоваяСтрокаДок.ВидДокумента);
		
		Если ЗначениеЗаполнено(НоваяСтрокаДок.Номер) Тогда
			ПредставлениеДокумента = ПредставлениеДокумента + " N" + НоваяСтрокаДок.Номер;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НоваяСтрокаДок.Дата) Тогда
			ПредставлениеДокумента = ПредставлениеДокумента + " от " + Формат(НоваяСтрокаДок.Дата, "ДЛФ=D");
		КонецЕсли;
		
		НоваяСтрокаДок.ПредставлениеДокумента = ПредставлениеДокумента;
		
		Для каждого УзелФайл Из МассивСтрокУзла(УзелДокумент, "Файл") Цикл
			
			Если НЕ ЕстьСтрокаУзла(УзелФайл, "Имя") 
				ИЛИ НЕ ЕстьСтрокаУзла(УзелФайл, "Размер") Тогда
				
				РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Некорректная структура XML файла описания.", ИдентификаторФормыВладельца);
				Возврат Ложь;
				
			КонецЕсли;
			
			//добавляем новую строку таблицы ФайлыДокументов и заполняем её
			НоваяСтрокаФайлы = ФайлыДокументов.Добавить();
			НоваяСтрокаФайлы.ИдентификаторДокумента = НоваяСтрокаДок.ИдентификаторДокумента;
			НоваяСтрокаФайлы.Имя = ЗначениеСтрокиУзла(УзелФайл, "Имя");
			НоваяСтрокаФайлы.Размер = СтрокаВЧисло(ЗначениеСтрокиУзла(УзелФайл, "Размер"));
			НоваяСтрокаФайлы.НомерСтраницы = СтрокаВЧисло(ЗначениеСтрокиУзла(УзелФайл, "НомерСтраницы"));
			НоваяСтрокаФайлы.ИмяНаДиске = ЗначениеСтрокиУзла(УзелФайл, "ИмяНаДиске");
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрокаФайлы.ИмяНаДиске) Тогда
				НоваяСтрокаФайлы.ИмяНаДиске = НоваяСтрокаФайлы.Имя; 
			КонецЕсли;
		
		КонецЦикла;
		
		Для каждого УзелУчастник Из МассивСтрокУзла(УзелДокумент, "Участник") Цикл
			
			Если НЕ ЕстьСтрокаУзла(УзелУчастник, "Идентификатор") Тогда
				
				РегламентированнаяОтчетностьКлиентСервер.СообщитьПользователю("Некорректная структура XML файла описания.", ИдентификаторФормыВладельца);
				Возврат Ложь;
				
			КонецЕсли;

			//добавляем новую строку таблицы ФайлыДокументов и заполняем её
			НоваяСтрокаУч = УчастникиДокументов.Добавить();
			НоваяСтрокаУч.ИдентификаторДокумента = НоваяСтрокаДок.ИдентификаторДокумента;
			НоваяСтрокаУч.Роль = СоответствиеПредставлениеРолиУчастникаПоКоду[ЗначениеСтрокиУзла(УзелУчастник, "Роль")];
			НоваяСтрокаУч.ИдентификаторУчастника = ЗначениеСтрокиУзла(УзелУчастник, "Идентификатор");
		
		КонецЦикла;

	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДатаИзСтроки(СтрДата)
	Если СтрДата = "" Тогда
		ВозвращаемаяДата = Дата(1, 1, 1);
	Иначе
		МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрДата, ".");
		Если Число(МассивПодстрок[0]) = 0 Тогда
			МассивПодстрок[0] = "1";
		КонецЕсли;
		Если Число(МассивПодстрок[1]) = 0 Тогда
			МассивПодстрок[1] = "1";
		КонецЕсли;
		Если Число(МассивПодстрок[2]) = 0 Тогда
			МассивПодстрок[2] = "1";
		КонецЕсли;
		ВозвращаемаяДата = Дата(МассивПодстрок[2], МассивПодстрок[1], МассивПодстрок[0]);
	КонецЕсли;
	
	Возврат ВозвращаемаяДата;
	
КонецФункции

&НаСервере
Функция ЗаполнитьПоОтборамТаблицуОтображаемыеДокументы()
	
	ПараметрыОтбора = Новый Структура;
	Если ЗначениеЗаполнено(ВидДокумента) Тогда
		ПараметрыОтбора.Вставить("ВидДокумента", ВидДокумента);
	КонецЕсли;
	
	Если ПараметрыОтбора.Количество() = 0 Тогда
		ОтобранныеСтроки = ЗагруженныеДокументы;	// ОтобранныеСтроки - ТЗ
	Иначе
		ОтобранныеСтроки = ЗагруженныеДокументы.НайтиСтроки(ПараметрыОтбора); // ОтобранныеСтроки - массив строк ТЗ ЗагруженныеДокументы
	КонецЕсли;
	
	ОтображаемыеДокументы.Очистить();
	
	
	ПараметрыОтбораТЗУчастников = Новый Структура;
	ПараметрыОтбораТЗУчастников.Вставить("ИдентификаторУчастника", Участник);
	
	Для каждого ОтобраннаяСтрока Из ОтобранныеСтроки Цикл
		
		Если ЗначениеЗаполнено(Участник) Тогда
			
			ПараметрыОтбораТЗУчастников.Вставить("ИдентификаторДокумента", ОтобраннаяСтрока.ИдентификаторДокумента);
			
			МассивУчастниковДокумента = УчастникиДокументов.НайтиСтроки(ПараметрыОтбораТЗУчастников); //массив строк ТЗ УчастникиДокументов
			Если МассивУчастниковДокумента.Количество() > 0 Тогда
				//среди участников документа есть требуемый
				НоваяСтрока = ОтображаемыеДокументы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ОтобраннаяСтрока); 	
			КонецЕсли;
			
		Иначе
			
			НоваяСтрока = ОтображаемыеДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОтобраннаяСтрока); 
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецФункции

&НаСервере
Функция СформироватьСтруктуруРезультатНаСервере(МассивИменЗагружаемыхФайлов)
	
	СтруктураРезультата = Новый Структура;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Выбрать", Истина);
	
	ВыбранныеСтроки = ОтображаемыеДокументы.НайтиСтроки(ПараметрыОтбора);
	
	ТЗОтображаемыеДокументы = ДанныеФормыВЗначение(ОтображаемыеДокументы, Тип("ТаблицаЗначений"));
	ВыбранныеДокументы = ТЗОтображаемыеДокументы.Скопировать(Новый Массив);
	
	ТЗФайлыДокументов = ДанныеФормыВЗначение(ФайлыДокументов, Тип("ТаблицаЗначений"));
	ФайлыВыбранныхДокументов = ТЗФайлыДокументов.Скопировать(Новый Массив);
	ПараметрыОтбораФайлов = Новый Структура;
	
	Для каждого ВыбраннаяСтрока Из ВыбранныеСтроки Цикл
		
		НоваяСтрока = ВыбранныеДокументы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыбраннаяСтрока); 
		
		ПараметрыОтбораФайлов.Вставить("ИдентификаторДокумента", ВыбраннаяСтрока.ИдентификаторДокумента);
		ФайлыВыбранногоДокумента = ТЗФайлыДокументов.НайтиСтроки(ПараметрыОтбораФайлов);
		
		Для каждого ФайлВыбранногоДокумента Из ФайлыВыбранногоДокумента Цикл
			НоваяСтрока = ФайлыВыбранныхДокументов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ФайлВыбранногоДокумента);
			
			МассивИменЗагружаемыхФайлов.Добавить(ФайлВыбранногоДокумента.ИмяНаДиске);
		КонецЦикла;
		
	КонецЦикла;
	
 	АдресТЗВыбранныеДокументы = ПоместитьВоВременноеХранилище(ВыбранныеДокументы, ИдентификаторФормыВладельца);
	СтруктураРезультата.Вставить("АдресТЗЗагруженныеДокументы", АдресТЗВыбранныеДокументы);
	
	АдресТЗУчастникиДокументов = ПоместитьВоВременноеХранилище(ДанныеФормыВЗначение(УчастникиДокументов, Тип("ТаблицаЗначений")), ИдентификаторФормыВладельца);
	СтруктураРезультата.Вставить("АдресТЗУчастникиДокументов", АдресТЗУчастникиДокументов);
	
	АдресТЗРеквизитыУчастников = ПоместитьВоВременноеХранилище(ДанныеФормыВЗначение(РеквизитыУчастников, Тип("ТаблицаЗначений")), ИдентификаторФормыВладельца);
	СтруктураРезультата.Вставить("АдресТЗРеквизитыУчастников", АдресТЗРеквизитыУчастников);
	
	
	АдресТЗФайлыДокументов = ПоместитьВоВременноеХранилище(ФайлыВыбранныхДокументов, ИдентификаторФормыВладельца);
	СтруктураРезультата.Вставить("АдресТЗФайлыДокументов", АдресТЗФайлыДокументов);

	СтруктураРезультата.Вставить("ОрганизацияСсылка", ОрганизацияСсылка);
	
	СтруктураРезультата.Вставить("ПолноеИмяФайлаОбмена", ПолноеИмяФайлаОбменаНаСервере);
	СтруктураРезультата.Вставить("РежимВыбораИзКаталога", РежимВыбораИзКаталога);
	
	Возврат СтруктураРезультата;
			
КонецФункции

&НаКлиенте
Процедура ОтображаемыеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеСтроки = Элемент.ТекущиеДанные;
	ОткрытьФормуЗагружаемогоДокумента(ДанныеСтроки);	
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеУчастникаПоИдентификатору(ИдентификаторУчастника)
	
	ПараметрыОтбораТЗУчастников = Новый Структура;
	ПараметрыОтбораТЗУчастников.Вставить("ИдентификаторУчастника", ИдентификаторУчастника);
	
	СтрокаРеквизитов = РеквизитыУчастников.НайтиСтроки(ПараметрыОтбораТЗУчастников)[0];
	
	ИННКПП = СтрокаРеквизитов.ИНН + ?(ЗначениеЗаполнено(СтрокаРеквизитов.КПП),"/" + СтрокаРеквизитов.КПП,"");
	
	Если СтрокаРеквизитов.ЯвляетсяЮрЛицом Тогда
		//это организация
		ПредставлениеУчастника = СтрокаРеквизитов.НаименованиеОрганизации + ПрибавляемаяСтрока(ИННКПП);
	Иначе
		//это ИП
		ПредставлениеУчастника = СтрокаРеквизитов.ФамилияИП + ПрибавляемаяСтрока(СтрокаРеквизитов.ИмяИП) + ПрибавляемаяСтрока(СтрокаРеквизитов.ОтчествоИП) + ПрибавляемаяСтрока(ИННКПП);
	КонецЕсли;
	
	Возврат ПредставлениеУчастника;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСоответствиеПредставлениеРолиУчастникаПоКоду()
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить("01","продавец");
	Соответствие.Вставить("02","поставщик");
	Соответствие.Вставить("03","заказчик");
	Соответствие.Вставить("04","отправитель");
	Соответствие.Вставить("05","грузоотправитель");
	Соответствие.Вставить("06","учредитель");
	Соответствие.Вставить("07","арендодатель");
	Соответствие.Вставить("08","займодатель");
	Соответствие.Вставить("09","векселедатель");
	Соответствие.Вставить("10","страхователь");
	Соответствие.Вставить("11","экспортер");
	Соответствие.Вставить("12","работодатель");
	Соответствие.Вставить("13","покупатель");
	Соответствие.Вставить("14","исполнитель");
	Соответствие.Вставить("15","получатель");
	Соответствие.Вставить("16","грузополучатель");
	Соответствие.Вставить("17","поверенный");
	Соответствие.Вставить("18","агент");
	Соответствие.Вставить("19","арендатор");
	Соответствие.Вставить("20","займополучатель (заемщик)");
	Соответствие.Вставить("21","векселеполучатель");
	Соответствие.Вставить("22","пользователь");
	Соответствие.Вставить("23","хранитель");
	Соответствие.Вставить("24","страховщик");
	Соответствие.Вставить("25","импортер");
	Соответствие.Вставить("26","работник");
	Соответствие.Вставить("27","участник");
	Соответствие.Вставить("28","плательщик");
	Соответствие.Вставить("29","декларант");
	Соответствие.Вставить("30","перевозчик");
	Соответствие.Вставить("31","экспедитор");
	Соответствие.Вставить("32","акционер");
	Соответствие.Вставить("33","эмитент");
	Соответствие.Вставить("34","инвестор");
	Соответствие.Вставить("35","генеральный подрядчик");
	Соответствие.Вставить("36","субподрядчик");
	Соответствие.Вставить("37","подрядчик");
	Соответствие.Вставить("38","посредник");
	Соответствие.Вставить("39","лицосоставившее документ");
	
	Возврат Соответствие;
	
КонецФункции

&НаСервере
Процедура ДополнитьСтруктуруПараметровНаСервере(СтруктураПараметров, ИдентификаторДокумента)
	
	ТЗУчастникиДокумента = Новый ТаблицаЗначений;
	ТЗУчастникиДокумента.Колонки.Добавить("РольУчастника");
	ТЗУчастникиДокумента.Колонки.Добавить("ПредставлениеУчастника");
	
	ТЗФайлыДокумента = ФайлыДокументов.Выгрузить(Новый Массив, "ИмяНаДиске, Имя, Размер, НомерСтраницы");
	
	ПараметрыОтбораТЗ = Новый Структура;
	ПараметрыОтбораТЗ.Вставить("ИдентификаторДокумента", ИдентификаторДокумента);
	
	//Участники документа
	МассивУчастниковДокумента = УчастникиДокументов.НайтиСтроки(ПараметрыОтбораТЗ); //массив строк ТЗ УчастникиДокументов
	Для каждого СтрокаУчастникДокумента Из МассивУчастниковДокумента Цикл
		
		НоваяСтрокаТЗУчастникиДокумента = ТЗУчастникиДокумента.Добавить(); 
		НоваяСтрокаТЗУчастникиДокумента.РольУчастника = СтрокаУчастникДокумента.Роль;
		НоваяСтрокаТЗУчастникиДокумента.ПредставлениеУчастника = ПредставлениеУчастникаПоИдентификатору(СтрокаУчастникДокумента.ИдентификаторУчастника);
	
	КонецЦикла;

	АдресТЗУчастникиДокумента = ПоместитьВоВременноеХранилище(ТЗУчастникиДокумента, УникальныйИдентификатор);
	СтруктураПараметров.Вставить("АдресТЗУчастникиДокумента", АдресТЗУчастникиДокумента);
	
	//Файлы документа
	МассивФайловДокумента = ФайлыДокументов.НайтиСтроки(ПараметрыОтбораТЗ); //массив строк ТЗ ФайлыДокументов
	Для каждого СтрокаФайлДокумента Из МассивФайловДокумента Цикл
		
		НоваяСтрокаТЗФайлыДокумента = ТЗФайлыДокумента.Добавить(); 
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТЗФайлыДокумента, СтрокаФайлДокумента);
	
	КонецЦикла;

	
	АдресТЗФайлыДокумента = ПоместитьВоВременноеХранилище(ТЗФайлыДокумента, УникальныйИдентификатор);
	СтруктураПараметров.Вставить("АдресТЗФайлыДокумента", АдресТЗФайлыДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗагружаемогоДокумента(ДанныеСтроки)
	
	СтруктураПараметров = Новый Структура;
	
	СтруктураПараметров.Вставить("ВидДокумента", 			ДанныеСтроки.ВидДокумента);
	СтруктураПараметров.Вставить("НомерДокумента", 			ДанныеСтроки.Номер);
	СтруктураПараметров.Вставить("ДатаДокумента", 			ДанныеСтроки.Дата);
	СтруктураПараметров.Вставить("НомерОснования", 			ДанныеСтроки.НомерОснования);
	СтруктураПараметров.Вставить("ДатаОснования", 			ДанныеСтроки.ДатаОснования);
	СтруктураПараметров.Вставить("КонецПериода", 			ДанныеСтроки.КонецПериода);
	СтруктураПараметров.Вставить("НачалоПериода", 			ДанныеСтроки.НачалоПериода);
	СтруктураПараметров.Вставить("ПредметДокумента", 		ДанныеСтроки.Предмет);
	СтруктураПараметров.Вставить("СуммаВсего", 				ДанныеСтроки.СуммаВсего);
	СтруктураПараметров.Вставить("СуммаНалога", 			ДанныеСтроки.СуммаНалога);
	
	СтруктураПараметров.Вставить("Организация", 			ОрганизацияСсылка);
	
	ДополнитьСтруктуруПараметровНаСервере(СтруктураПараметров, ДанныеСтроки.ИдентификаторДокумента);
	
	ПараметрыФормы = Новый Структура("СтруктураПараметров", СтруктураПараметров);
	ОткрытьФорму("Справочник.СканированныеДокументыДляПередачиВЭлектронномВиде.Форма.ФормаЗагружаемогоДокумента", ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры



	
