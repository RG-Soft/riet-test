
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = "<Роль участника>";
	СтруктураРеквизитов = Параметры.СтруктураРеквизитов;
	Если СтруктураРеквизитов <> Неопределено Тогда
		ЮрЛицоНаименование 	= СтруктураРеквизитов.ЮрЛицоНаименование; 
		ЮрЛицоИНН 			= СтруктураРеквизитов.ЮрЛицоИНН; 
		ЮрЛицоКПП 			= СтруктураРеквизитов.ЮрЛицоКПП; 
		ФизЛицоФамилия 		= СтруктураРеквизитов.ФизЛицоФамилия; 
		ФизЛицоИмя 			= СтруктураРеквизитов.ФизЛицоИмя; 
		ФизЛицоОтчество 	= СтруктураРеквизитов.ФизЛицоОтчество; 
		ФизЛицоИНН 			= СтруктураРеквизитов.ФизЛицоИНН; 
		ЯвляетсяЮрЛицом 	= СтруктураРеквизитов.ЯвляетсяЮрЛицом; 
		ВидДокумента 		= СтруктураРеквизитов.ВидДокумента; 
		Если ЗначениеЗаполнено(СтруктураРеквизитов.РольУчастника) Тогда
			Заголовок = СтруктураРеквизитов.РольУчастника;
		КонецЕсли;
	КонецЕсли;
	
	Если СправочникКонтрагентовДоступен() Тогда
		Контрагент = Справочники["Контрагенты"].ПустаяСсылка(); 
		Элементы.ГруппаКонтрагент.Видимость = Истина;
	Иначе
		Элементы.ГруппаКонтрагент.Видимость = Ложь;
	КонецЕсли;
	
	Если ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.СпецификацияЦены 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.Договор 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ДополнениеКДоговору Тогда 
		
		Элементы.ЮрЛицоИНН.АвтоОтметкаНезаполненного = Истина;
		
	Иначе
		
		Элементы.ЮрЛицоИНН.АвтоОтметкаНезаполненного = Ложь;
		
	КонецЕсли;
	
	Если ЯвляетсяЮрЛицом Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.ЮрЛицоНаименование;
	Иначе
		ЭтаФорма.ТекущийЭлемент = Элементы.ФизЛицоФамилия;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СправочникКонтрагентовДоступен()
	
	СправочникКонтрагенты = Метаданные.Справочники.Найти("Контрагенты");
	Если СправочникКонтрагенты = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если СправочникКонтрагенты.Реквизиты.Найти("НаименованиеПолное") = Неопределено 
	ИЛИ СправочникКонтрагенты.Реквизиты.Найти("ЮридическоеФизическоеЛицо") = Неопределено 
	ИЛИ СправочникКонтрагенты.Реквизиты.Найти("ИНН") = Неопределено 
	ИЛИ СправочникКонтрагенты.Реквизиты.Найти("КПП") = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭУ();
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверкаЗаполнения() = Ложь Тогда
		Возврат;
	КонецЕсли;

	Если ЯвляетсяЮрЛицом Тогда
		ФизЛицоИНН			= "";
		ФизЛицоФамилия  	= "";	
		ФизЛицоИмя      	= "";	
		ФизЛицоОтчество 	= "";	
	Иначе
	    ЮрЛицоНаименование 	= ""; 
		ЮрЛицоИНН 			= ""; 
		ЮрЛицоКПП 			= ""; 
	КонецЕсли;

	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ЮрЛицоНаименование", 	ЮрЛицоНаименование); 
	СтруктураРеквизитов.Вставить("ЮрЛицоИНН", 			ЮрЛицоИНН); 
	СтруктураРеквизитов.Вставить("ЮрЛицоКПП", 			ЮрЛицоКПП); 
	СтруктураРеквизитов.Вставить("ФизЛицоФамилия", 		ФизЛицоФамилия); 
	СтруктураРеквизитов.Вставить("ФизЛицоИмя", 			ФизЛицоИмя); 
	СтруктураРеквизитов.Вставить("ФизЛицоОтчество", 	ФизЛицоОтчество); 
	СтруктураРеквизитов.Вставить("ФизЛицоИНН", 			ФизЛицоИНН); 
	СтруктураРеквизитов.Вставить("ЯвляетсяЮрЛицом", 	ЯвляетсяЮрЛицом); 
	
	Закрыть(СтруктураРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	ВозвращаемоеЗначение = Неопределено;
	Закрыть(ВозвращаемоеЗначение);
КонецПроцедуры

&НаКлиенте
Функция ПроверкаЗаполнения()
	
	ЕстьОшибки = Ложь;
	//Если НЕ ЗначениеЗаполнено(РольУчастника) Тогда
	//	 	Сообщить("Не указана роль участника сделки!");
	//		ЕстьОшибки = Истина;
	//КонецЕсли;

	//Если ЯвляетсяЮрЛицом Тогда
	//	
	//	Если НЕ ЗначениеЗаполнено(ЮрЛицоНаименование) Тогда
	//	 	Сообщить("Не указано наименование организации!");
	//		ЕстьОшибки = Истина;
	//	КонецЕсли;
	//	
	//	Если НЕ ЗначениеЗаполнено(ЮрЛицоИНН) Тогда
	//	 	Сообщить("Не указан ИНН организации!");
	//		ЕстьОшибки = Истина;
	//	КонецЕсли;

	//	Если НЕ ЗначениеЗаполнено(ЮрЛицоКПП) Тогда
	//	 	Сообщить("Не указан КПП организации!");
	//		ЕстьОшибки = Истина;
	//	КонецЕсли;
	//	
	//Иначе
	//	
	//	Если НЕ ЗначениеЗаполнено(ФизЛицоФамилия) Тогда
	//	 	Сообщить("Не указана фамилия физического лица!");
	//		ЕстьОшибки = Истина;
	//	КонецЕсли;
	//	
	//	Если НЕ ЗначениеЗаполнено(ФизЛицоИмя) Тогда
	//	 	Сообщить("Не указано имя физического лица!");
	//		ЕстьОшибки = Истина;
	//	КонецЕсли;

	//	Если НЕ ЗначениеЗаполнено(ФизЛицоИНН) Тогда
	//	 	Сообщить("Не указан ИНН физического лица!");
	//		ЕстьОшибки = Истина;
	//	КонецЕсли;
	//	
	//КонецЕсли;
	
	
	Если ЕстьОшибки Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли; 

КонецФункции

&НаКлиенте
Процедура УправлениеЭУ()
	
	Если ЯвляетсяЮрЛицом Тогда
		Элементы.ГруппаЮрФиз.ТекущаяСтраница = Элементы.ГруппаЮрЛицо;
		КонтрагентПодсказка = "При выборе контрагента Наименование, ИНН и КПП заполнятся автоматически";
	Иначе
		Элементы.ГруппаЮрФиз.ТекущаяСтраница = Элементы.ГруппаФизЛицо;
		КонтрагентПодсказка = "При выборе контрагента ФИО и ИНН заполнятся автоматически";
	КонецЕсли;
	
	Элементы.КонтрагентПодсказка.Заголовок = КонтрагентПодсказка;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДаннымиКонтрагента()
	
	ЗаполнитьДаннымиКонтрагентаНаСервере();
	УправлениеЭУ();	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиКонтрагентаНаСервере()
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		//наличие всех реквизитов предварительно проверяется в функции СправочникКонтрагентовДоступен()
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Контрагенты.НаименованиеПолное,
		|	Контрагенты.ЮридическоеФизическоеЛицо,
		|	Контрагенты.ИНН,
		|	Контрагенты.КПП
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка = &КонтрагентСсылка";
		
		Запрос.УстановитьПараметр("КонтрагентСсылка", Контрагент);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			ЯвляетсяЮрЛицом = (Выборка.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
			
			Если ЯвляетсяЮрЛицом Тогда
				
				ЮрЛицоНаименование = Выборка.НаименованиеПолное; 
				ЮрЛицоИНН = Выборка.ИНН; 
				ЮрЛицоКПП = Выборка.КПП; 
				
			Иначе
				
				ФизЛицоИНН  = Выборка.ИНН;
				
				ФИО = РегламентированнаяОтчетностьКлиентСервер.РазложитьФИО(Выборка.НаименованиеПолное);
				ФизЛицоФамилия  = ФИО.Фамилия;	
				ФизЛицоИмя      = ФИО.Имя;	
				ФизЛицоОтчество = ФИО.Отчество;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЯвляетсяЮЛПриИзменении(Элемент)
	
	УправлениеЭУ();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ЗаполнитьДаннымиКонтрагента();
	
КонецПроцедуры


