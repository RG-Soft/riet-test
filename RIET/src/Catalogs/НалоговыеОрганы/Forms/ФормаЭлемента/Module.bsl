&НаКлиенте
//Перем КонтекстЭДО Экспорт;

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДО = Результат.КонтекстЭДО;
	
	Если КонтекстЭДО <> Неопределено Тогда
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭтоЭлектроннаяПодписьВМоделиСервиса, Элементы.Сертификат, Объект.Сертификаты, СертификатПредставление)
	Иначе
		
		Элементы.Сертификат.РедактированиеТекста = Ложь;
		Элементы.Сертификат.КнопкаВыбора = Ложь;
		Элементы.Сертификат.КнопкаОткрытия = Ложь;
		
		Если Объект.Сертификаты.Количество() <> 0 Тогда
			СертификатПредставление = "Сертификатов: " + Объект.Сертификаты.Количество();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Если ПустаяСтрока(Объект.ПолноеНаименование) Тогда
		Объект.ПолноеНаименование = Объект.Наименование
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолноеНаименованиеПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = Объект.ПолноеНаименование
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если КонтекстЭДО <> Неопределено Тогда
		ДополнительныеПараметры = Новый Структура("Элемент", Элемент);
		ОписаниеОповещения = Новый ОписаниеОповещения("СертификатНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		КонтекстЭДО.ВыбратьСертификат(ЭтоЭлектроннаяПодписьВМоделиСервиса, Объект.Сертификаты, "AddressBook", , Истина, ОписаниеОповещения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбораЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если РезультатВыбора <> Неопределено Тогда
		
		//Объект.Сертификаты = РезультатВыбора.Отпечаток;
		Объект.Сертификаты.Очистить();
		Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
			НовСтр = Объект.Сертификаты.Добавить();
			НовСтр.Сертификат = РезультатВыбора.Отпечаток;
		Иначе
			Для Каждого Стр Из РезультатВыбора Цикл
				НовСтр = Объект.Сертификаты.Добавить();
				НовСтр.Сертификат = Стр.Отпечаток;
			КонецЦикла;
		КонецЕсли;
		
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭтоЭлектроннаяПодписьВМоделиСервиса, Элемент, Объект.Сертификаты, СертификатПредставление);
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Объект.Сертификаты.Количество() > 0 Тогда
		Если КонтекстЭДО <> Неопределено Тогда
			КонтекстЭДО.ПоказатьСертификат(ЭтоЭлектроннаяПодписьВМоделиСервиса, Объект.Сертификаты, "AddressBook");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтпечатокОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.Сертификаты.Очистить();
	Если КонтекстЭДО <> Неопределено Тогда
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭтоЭлектроннаяПодписьВМоделиСервиса, Элемент, Объект.Сертификаты, СертификатПредставление);
	Иначе
		СертификатПредставление = "";
	КонецЕсли;
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура АдресСайтаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("АдресСайтаОткрытиеЗавершение", ЭтотОбъект);
	
	КонтекстЭДО.ПоддерживаетсяИспользованиеРасширенияРаботыСФайлами(ОписаниеОповещения, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСайтаОткрытиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		Если НЕ ПустаяСтрока(Объект.АдресСайта) Тогда
			ЗапуститьПриложение(Объект.АдресСайта);
		КонецЕсли;
	Иначе
		ПерейтиПоНавигационнойСсылке(Объект.АдресСайта);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//ЭтоЭлектроннаяПодписьВМоделиСервиса = ЭлектроннаяПодписьВМоделиСервисаВызовСервера.ЭтоЭлектроннаяПодписьВМоделиСервиса();
	  ЭтоЭлектроннаяПодписьВМоделиСервиса = Ложь;
КонецПроцедуры
