
&НаКлиенте
Процедура ТаблицаTRsВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаTRsSelect" Тогда 
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ТекДанные = Элементы.ТаблицаTRs.ТекущиеДанные;	
	
	Если ТекДанные <> Неопределено Тогда 
		ПоказатьЗначение(,ТекДанные.TransportRequest);
	КонецЕсли;     
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СтруктураНастройки") Тогда
		
		СтруктураНастройки = Параметры.СтруктураНастройки;
		Если СтруктураНастройки.Имя = "ВыборИзTripNonLawson" Тогда
			СписокParcelsInTrip.ЗагрузитьЗначения(СтруктураНастройки.МассивParcels);
			НастроитьДляВыбораИзTripNonLawson();
		КонецЕсли;
		
	КонецЕсли;

	Элементы.Список.Обновить();
	
	ЗаполнитьТаблицуTRs();
	 
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуTRs() 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокParcelsInTrip", СписокParcelsInTrip);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ParcelsOfTransportRequestsWithoutShipmentОстатки.TransportRequest,
	               |	ЛОЖЬ КАК Select
	               |ИЗ
	               |	РегистрНакопления.ParcelsOfTransportRequestsWithoutShipment.Остатки КАК ParcelsOfTransportRequestsWithoutShipmentОстатки
	               |ГДЕ
	               |	НЕ ParcelsOfTransportRequestsWithoutShipmentОстатки.Parcel В (&СписокParcelsInTrip)";
				   
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	РезультатЗапроса.Свернуть("TransportRequest,Select");

	ТаблицаTRs.Загрузить(РезультатЗапроса);
		
КонецПроцедуры

&НаСервере
Процедура НастроитьДляВыбораИзTripNonLawson() 
	
	// исключим уже добавленные parcels
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"Ссылка",
		СписокParcelsInTrip,
		ВидСравненияКомпоновкиДанных.НеВСписке,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаTRsПриИзменении(Элемент)
	
	УстановитьОтборПоTR();
		
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоTR()
	
	СтруктураОтбора = Новый Структура("Select", Истина);
	
	ТЗ = ТаблицаTRs.Выгрузить(СтруктураОтбора, "TransportRequest");
	
	МассивTR = ТЗ.ВыгрузитьКолонку("TransportRequest"); 	
	
	ИспользоватьОтбор = ?(МассивTR.Количество() > 0, Истина, Ложь); 
	
	СписокTR = Новый СписокЗначений;
	СписокTR.ЗагрузитьЗначения(МассивTR);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"TransportRequest",
		СписокTR,
		ВидСравненияКомпоновкиДанных.ВСписке,
		,
		ИспользоватьОтбор,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		
КонецПроцедуры

