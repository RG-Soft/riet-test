
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СтруктураНастройки") Тогда
		
		СтруктураНастройки = Параметры.СтруктураНастройки;
		Если СтруктураНастройки.Имя = "ВыборИзDOC" Тогда
			НастроитьДляВыбораИзDOС(СтруктураНастройки);
		ИначеЕсли СтруктураНастройки.Имя = "ВыборИзTrip" Тогда
			НастроитьДляВыбораИзTrip(СтруктураНастройки);
		КонецЕсли;
		
	КонецЕсли;
	
	// { RGS AGorlenko 01.12.2014 19:46:07 - значение задано в настройках динамического списка
	//Отбор = Параметры.Отбор;
	//Отбор.Вставить("Отменен", Ложь);
	// } RGS AGorlenko 01.12.2014 19:46:26 - значение задано в настройках динамического списка
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДляВыбораИзDOС(СтруктураНастройки) 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("CurrentDOC", СтруктураНастройки.CurrentDOC);
	Запрос.УстановитьПараметр("CurrentParcels", СтруктураНастройки.CurrentParcels);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Parcels.Ссылка КАК Parcel
		|ИЗ
		|	Справочник.Parcels КАК Parcels
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КонсолидированныйПакетЗаявокНаПеревозку.Parcels КАК DOCsParcels
		|		ПО Parcels.Ссылка = DOCsParcels.Parcel
		|			И ((НЕ DOCsParcels.Ссылка.Отменен))
		|			И ((НЕ DOCsParcels.Ссылка = &CurrentDOC))
		|ГДЕ
		|	(НЕ Parcels.Отменен)
		|	И (НЕ Parcels.LocalOnly)
		|	И Parcels.ExportRequest = ЗНАЧЕНИЕ(Документ.ExportRequest.ПустаяСсылка)
		|	И (НЕ Parcels.Ссылка В (&CurrentParcels))
		|	И DOCsParcels.Ссылка ЕСТЬ NULL ";
		
	ДоступныеParcels = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Parcel");	
	
	СтруктураОтбора = Параметры.Отбор;
	СтруктураОтбора.Вставить("LocalOnly", Ложь);
	СтруктураОтбора.Вставить("ExportRequest", Документы.ExportRequest.ПустаяСсылка());
	СтруктураОтбора.Вставить("Ссылка", ДоступныеParcels);
		
КонецПроцедуры

&НаСервере
Процедура НастроитьДляВыбораИзTrip(СтруктураНастройки) 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Draft", СтруктураНастройки.Draft);
	Запрос.УстановитьПараметр("CurrentTrip", СтруктураНастройки.CurrentTrip);
	Запрос.УстановитьПараметр("CurrentParcels", СтруктураНастройки.CurrentParcels);
	Запрос.УстановитьПараметр("WarehouseFrom", СтруктураНастройки.WarehouseFrom);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Parcels.Ссылка КАК Parcel
		|ИЗ
		|	Справочник.Parcels КАК Parcels
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Trip.Parcels КАК TripsParcels
		|		ПО Parcels.Ссылка = TripsParcels.Parcel
		|			И (НЕ TripsParcels.Ссылка.ПометкаУдаления)
		|			И (НЕ TripsParcels.Ссылка = &CurrentTrip)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КонсолидированныйПакетЗаявокНаПеревозку.Parcels КАК DOCsParcels
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Поставка.УпаковочныеЛисты КАК ShipmentsDOCs
		|			ПО DOCsParcels.Ссылка = ShipmentsDOCs.УпаковочныйЛист
		|				И (НЕ ShipmentsDOCs.Ссылка.Отменен)
		|		ПО Parcels.Ссылка = DOCsParcels.Parcel
		|			И (НЕ DOCsParcels.Ссылка.Отменен)
		|ГДЕ
		|	Parcels.Проверен
		|	И НЕ Parcels.Repacked
		|	И Parcels.ExportRequest = ЗНАЧЕНИЕ(Документ.ExportRequest.ПустаяСсылка)
		|	И НЕ Parcels.Ссылка В (&CurrentParcels)
		|	И (Parcels.LocalOnly
		|				И Parcels.DeliveredToWH <= &Draft
		|				И Parcels.DeliveredToWH <> ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ShipmentsDOCs.Ссылка.ATA <> ДАТАВРЕМЯ(1, 1, 1)
		|				И ShipmentsDOCs.Ссылка.ATA <= &Draft)
		|	И TripsParcels.Ссылка ЕСТЬ NULL ";
				
	ДоступныеParcels = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Parcel");	
	
	СтруктураОтбора = Параметры.Отбор;
	СтруктураОтбора.Вставить("WarehouseFrom", СтруктураНастройки.WarehouseFrom);
	СтруктураОтбора.Вставить("Проверен", Истина);
	СтруктураОтбора.Вставить("Repacked", Ложь);
	СтруктураОтбора.Вставить("ExportRequest", Документы.ExportRequest.ПустаяСсылка());
	СтруктураОтбора.Вставить("Ссылка", ДоступныеParcels);
		
КонецПроцедуры

