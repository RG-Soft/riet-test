
Процедура ПередЗаписью(Отказ)
	
	ДозаполнитьРеквизиты();
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьРеквизиты(Отказ);
	
КонецПроцедуры

Процедура ДозаполнитьРеквизиты()
	
	Код = СокрЛП(Код);
	Наименование = СокрЛП(Наименование);
	
КонецПроцедуры

Процедура ПроверитьРеквизиты(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Код)) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Code"" is empty!",
			ЭтотОбъект, "Код", , Отказ);
		
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		
		// У элемента (саб саб сегмента) должен быть заполнен родитель (саб сегмент)
		Если НЕ ЗначениеЗаполнено(Родитель) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""Sub segment"" is empty!",
				ЭтотОбъект, "Родитель", , Отказ);
			
		Иначе
					
			// В родителе (саб сегменте) должен быть указан родитель (сегмент)
			РодительСабСегмента = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Родитель, "Родитель");
			Если НЕ ЗначениеЗаполнено(РодительСабСегмента) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					"""Segment"" of sub-segment is empty!",
					ЭтотОбъект, "Родитель", , Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
