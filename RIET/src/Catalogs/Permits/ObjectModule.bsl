
Процедура ПриКопировании(ОбъектКопирования)
	
	CustomsСервер.ОчиститьCreationModification(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизитыБезДополнительныхДанных();
	
	ПроверитьРеквизитыБезДополнительныхДанных(Отказ);
	
КонецПроцедуры

Процедура ДозаполнитьРеквизитыБезДополнительныхДанных()
	
	PermitType = СокрЛП(PermitType);
	PermitDescription = СокрЛП(PermitDescription);
	Наименование = СокрЛП(Код) + " - " + PermitType + " - " + PermitDescription;
	
	GoodsDescription = СокрЛП(GoodsDescription);
	
	// Если срок истечения не указан - значит это бессрочный пермит - поэтому для удобства установим максимальный срок истечения
	Если НЕ ЗначениеЗаполнено(ExpiryDate) Тогда
		ExpiryDate = '30000101';	
	КонецЕсли;
	
	CustomsСервер.ЗаполнитьCreationModification(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПроверитьРеквизитыБезДополнительныхДанных(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Country) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Country"" is empty!",
			ЭтотОбъект, "Country", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СокрЛП(Код)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Permit no."" is empty!",
			ЭтотОбъект, "Код", , Отказ);
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(PermitType) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Permit type"" is empty!",
			ЭтотОбъект, "PermitType", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(PermitDescription) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Permit description"" is empty!",
			ЭтотОбъект, "PermitDescription", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(GoodsPartNoSerialNo) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Goods part no. / serial no"" is empty!",
			ЭтотОбъект, "GoodsPartNoSerialNo", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(GoodsDescription) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Goods description"" is empty!",
			ЭтотОбъект, "GoodsDescription", , Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ReceiptDate) Тогда
		
		Если ReceiptDate > ТекущаяДата() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""Receipt date"" can not be later than the current date!",
				ЭтотОбъект, "ReceiptDate", , Отказ);
		КонецЕсли;
		
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Receipt date"" is empty!",
			ЭтотОбъект, "ReceiptDate", , Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ExpiryDate) Тогда
		
		Если ExpiryDate < ReceiptDate Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""Expiry date"" can not be earlier than ""Receipt date""!",
				ЭтотОбъект, "ExpiryDate", , Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
