&НаКлиенте
Перем КонтекстЭДО;

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	// инициализируем контекст формы - контейнера клиентских методов
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДО = Результат.КонтекстЭДО;
	КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭтоЭлектроннаяПодписьВМоделиСервиса, Элементы.Сертификат, Объект.Сертификат, СертификатПредставление);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ДополнительныеПараметры = Новый Структура("Элемент", Элемент);
	ОписаниеОповещения = Новый ОписаниеОповещения("СертификатНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	КонтекстЭДО.ВыбратьСертификат(ЭтоЭлектроннаяПодписьВМоделиСервиса, Объект.Сертификат, "AddressBook",,, ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбораЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	Если РезультатВыбора <> Неопределено Тогда
		Объект.Сертификат = РезультатВыбора.Отпечаток;
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭтоЭлектроннаяПодписьВМоделиСервиса, Элемент, Объект.Сертификат, СертификатПредставление);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОчистка(Элемент, СтандартнаяОбработка)
	
	Сертификат = "";
	КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭтоЭлектроннаяПодписьВМоделиСервиса, Элементы.Сертификат, Объект.Сертификат, СертификатПредставление);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Объект.Сертификат) Тогда
		КонтекстЭДО.ПоказатьСертификат(ЭтоЭлектроннаяПодписьВМоделиСервиса, Объект.Сертификат, "AddressBook");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоЭлектроннаяПодписьВМоделиСервиса = ЭлектроннаяПодписьВМоделиСервисаВызовСервера.ЭтоЭлектроннаяПодписьВМоделиСервиса();
	
КонецПроцедуры
