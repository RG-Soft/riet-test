
//////////////////////////////////////////////////////////////////////
// ПРИ КОПИРОВАНИИ

Процедура ПриКопировании(ОбъектКопирования)
	
	РГСофт.ОчиститьCreationModification(ЭтотОбъект);
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////
// ПЕРЕД ЗАПИСЬЮ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизиты();
	
	ПроверитьРеквизиты(Отказ);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////

Процедура ДозаполнитьРеквизиты()
	
	Наименование = СокрЛП(Наименование);
		
	Если ЭтоНовый() Тогда
		РГСофт.ЗаполнитьCreation(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьРеквизиты(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Company) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		"'Company' is empty!",
		ЭтотОбъект, "Company", , Отказ);
		
	иначе
		
		//проверим уникальность для компании
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Company", Company);
		Запрос.УстановитьПараметр("Код", СокрЛП(Код));
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	ProductLines.Ссылка
		               |ИЗ
		               |	Справочник.ProductLines КАК ProductLines
		               |ГДЕ
		               |	ProductLines.Company = &Company
		               |	И ProductLines.Код = &Код
		               |	И ProductLines.Ссылка <> &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();		
		Если Выборка.Следующий() Тогда 
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Product line with current code already exists for '" + СокрЛП(Company) + "'!",
			ЭтотОбъект, "Код", , Отказ);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры
