
///////////////////////////////////////////////////////////////////////////////////////////////
// ПРИ КОПИРОВАНИИ

Процедура ПриКопировании(ОбъектКопирования)
	
	РГСофт.ОчиститьCreationModification(ЭтотОбъект);
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////////////
// ПЕРЕД ЗАПИСЬЮ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизитыБезДополнительныхДанных();
	
	ПроверитьРеквизитыБезДополнительныхДанных(Отказ);
		
КонецПроцедуры

Процедура ДозаполнитьРеквизитыБезДополнительныхДанных()
	
	Код = СокрЛП(Код);
	
	Наименование = СокрЛП(Наименование);
	
	CountryCode = СокрЛП(CountryCode);
	
	Если ЗначениеЗаполнено(CountryCode) И НЕ ЗначениеЗаполнено(RCACountry) Тогда
		RCACountry = ImportExportСерверПовтИспСеанс.ПолучитьRCACountryПоTMSID(CountryCode); 	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(RCACountry) И НЕ ЗначениеЗаполнено(ProcessLevel) Тогда
		ProcessLevel = ImportExportСерверПовтИспСеанс.ПолучитьProcessLevelПоRCACountry(RCACountry);	
	КонецЕсли;
	
	Address1 = СокрЛП(Address1);
	Address2 = СокрЛП(Address2);
	Address3 = СокрЛП(Address3);
	AddressRus = СокрЛП(AddressRus);
	
	Если ЭтоНовый() Тогда		
		РГСофт.ЗаполнитьCreation(ЭтотОбъект);		
	КонецЕсли;
	
	Для Каждого СтрокаRoles Из Roles Цикл
		СтрокаRoles.Role = СокрЛП(СтрокаRoles.Role);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьРеквизитыБезДополнительныхДанных(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Код)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Code / TMS Id' is empty!",
			ЭтотОбъект, "Код", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Name' is empty!",
			ЭтотОбъект, "Наименование", , Отказ);		
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(CountryCode) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Country code' is empty!",
			ЭтотОбъект, "CountryCode", , Отказ);		
	КонецЕсли;
		
	Если НЕ InTMS И НЕ ЗначениеЗаполнено(City) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'City' is empty!",
			ЭтотОбъект, "City", , Отказ);		
	КонецЕсли;
	
	Если НЕ InTMS И НЕ ЗначениеЗаполнено(Address1) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Address 1' is empty!",
			ЭтотОбъект, "Address1", , Отказ);		
	КонецЕсли;
	
	Если НЕ InTMS И Warehouse Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Warehouse' is used only for In TMS locations!",
			ЭтотОбъект, "Warehouse", , Отказ);		
	КонецЕсли;

	Если НЕ InTMS И Port Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Port' is used only for In TMS locations!",
			ЭтотОбъект, "Port", , Отказ);		
	КонецЕсли;
		
КонецПроцедуры
