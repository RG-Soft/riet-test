
/////////////////////////////////////////////////////////////////////////////////////
// ФОРМА

// ПРИ СОЗДАНИИ НА СЕРВЕРЕ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	TypeOfComparison = ПолучитьTypeOfComparison(Объект.Field);
	
КонецПроцедуры

// ПЕРЕД ЗАПИСЬЮ

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	РГСофтКлиент.ПроверитьНеобходимостьЗаписиСправочника(Модифицированность, Отказ);
	
КонецПроцедуры

// ПЕРЕД ЗАПИСЬЮ НА СЕРВЕРЕ

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РГСофтКлиентСервер.УстановитьЗначение(ТекущийОбъект.ModifiedBy, ПараметрыСеанса.ТекущийПользователь);
	ТекущийОбъект.ModificationDate = ТекущаяДата();
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////////
// ЭЛЕМЕНТЫ УПРАВЛЕНИЯ

&НаКлиенте
Процедура FieldПриИзменении(Элемент)
	
	TypeOfComparison = ПолучитьTypeOfComparison(Объект.Field);
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////////
// ТАБЛИЧНАЯ ЧАСТЬ POAs

&НаКлиенте
Процедура POAsПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ОткрытьФорму("Справочник.SeaAndAirPorts.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура POAsОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив")
		И ВыбранноеЗначение.Количество() > 0
		И ТипЗнч(ВыбранноеЗначение[0]) = Тип("СправочникСсылка.SeaAndAirPorts") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Для Каждого Значение Из ВыбранноеЗначение Цикл
			НоваяСтрока = Объект.POAs.Добавить();
			НоваяСтрока.POA = Значение;
		КонецЦикла;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура POAsВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено
		И ЗначениеЗаполнено(ТекущиеДанные.POA) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(,ТекущиеДанные.POA);
	КонецЕсли;
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////////
// ОБЩЕЕ

&НаСервереБезКонтекста
Функция ПолучитьTypeOfComparison(Field)
	
	Если Field = Перечисления.FieldsForRulesOfCVCRisks.PartNumber Тогда
		Возврат "equal";
	ИначеЕсли Field = Перечисления.FieldsForRulesOfCVCRisks.Description Тогда
		Возврат "contains";
	Иначе
		Возврат "";
	КонецЕсли; 
	
КонецФункции 

