
Функция ПолучитьПоКоду(Код) Экспорт
	
	// Получает AU по коду
	// В отличие от стандартной функции отсекаются помеченные на удаление элементы
	// Рекомендуется вызывать из модуля с повторным использованием значений
	
	Возврат РГСофт.НайтиСсылку("Справочник", "КостЦентры", "Код", Код);
	
КонецФункции

//-> RG-Soft VIvanov 2015/02/18
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Дата") Тогда
		СтандартнаяОбработка = Ложь;
		Если Параметры.Дата = Неопределено Тогда
			Параметры.Дата = ТекущаяДата();
		Конецесли;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СегментыКостЦентровСрезПоследних.КостЦентр,
		|	СегментыКостЦентровСрезПоследних.Код
		|ИЗ
		|	РегистрСведений.СегментыКостЦентров.СрезПоследних(&Дата, ) КАК СегментыКостЦентровСрезПоследних
		|ГДЕ
		|	СегментыКостЦентровСрезПоследних.Код ПОДОБНО &Код
		|	И СегментыКостЦентровСрезПоследних.КостЦентр.ПометкаУдаления = &ПометкаУдаления
		|	И СегментыКостЦентровСрезПоследних.КостЦентр.Segment = &Segment";
		Запрос.УстановитьПараметр("Дата", Параметры.Дата);
		Запрос.УстановитьПараметр("Код", Параметры.СтрокаПоиска + "%");
		Если Параметры.Отбор.Свойство("ПометкаУдаления") Тогда
			Запрос.УстановитьПараметр("ПометкаУдаления", Параметры.Отбор.ПометкаУдаления);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И СегментыКостЦентровСрезПоследних.КостЦентр.ПометкаУдаления = &ПометкаУдаления", "");
		КонецЕсли;
		Если Параметры.Отбор.Свойство("Segment") Тогда
			Запрос.УстановитьПараметр("Segment", Параметры.Отбор.Segment);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И СегментыКостЦентровСрезПоследних.КостЦентр.Segment = &Segment", "");
		КонецЕсли;
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.КостЦентр, Выборка.Код);
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры
//<- RG-Soft VIvanov