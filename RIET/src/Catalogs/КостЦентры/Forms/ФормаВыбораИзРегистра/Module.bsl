
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.Дата) Тогда
		Параметры.Дата = ТекущаяДата();
	КонецЕсли;
	Список.Параметры.УстановитьЗначениеПараметра("Дата", Параметры.Дата);
	Если Параметры.Свойство("СтруктураНастройки") Тогда
		
		СтруктураНастройки = Параметры.СтруктураНастройки;
		Если СтруктураНастройки.Имя = "ВыборИзAUsAndBORGs" Тогда
			НастроитьДляВыбораИзAUsAndBORGs(СтруктураНастройки);
		КонецЕсли;
		
	КонецЕсли;
	
	Отбор = Параметры.Отбор;
	
	Отбор.Вставить("ПометкаУдаления", Ложь);
	
	Если Параметры.Свойство("ТекущаяСтрока") Тогда
		Если ТипЗнч(Параметры.ТекущаяСтрока) = Тип("СправочникСсылка.КостЦентры") Тогда
			ПараметрТекущаяСтрока = Параметры.ТекущаяСтрока;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДляВыбораИзAUsAndBORGs(СтруктураНастройки)
	
	Если ЗначениеЗаполнено(СтруктураНастройки.BORG) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("BORG", СтруктураНастройки.BORG);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	AUsAndBORGs.AU
			|ИЗ
			|	Справочник.BORGs КАК BORGs
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.AUsAndBORGs КАК AUsAndBORGs
			|		ПО BORGs.Ссылка <> AUsAndBORGs.BORG
			|			И BORGs.Компания = AUsAndBORGs.BORG.Компания
			|ГДЕ
			|	BORGs.Ссылка = &BORG";
			
		МассивЗапрещенныхAUs = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("AU");
		Если МассивЗапрещенныхAUs.Количество() Тогда
			
			СписокЗапрещенныхAUs = Новый СписокЗначений;
			СписокЗапрещенныхAUs.ЗагрузитьЗначения(МассивЗапрещенныхAUs);
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
				Список.Отбор,
				"Ссылка",
				СписокЗапрещенныхAUs,
				ВидСравненияКомпоновкиДанных.НеВСписке,
				,
				Истина);
			
		КонецЕсли; 
				
	КонецЕсли; 
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ПараметрТекущаяСтрока) Тогда 
		Элементы.Список.ТекущаяСтрока = ПараметрТекущаяСтрока;
	КонецЕсли;
	
КонецПроцедуры
