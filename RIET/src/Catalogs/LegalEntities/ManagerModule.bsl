
Функция ПолучитьПоРеквизитамTMS(CompanyCode, CountryCode, ERPID, FinanceLocCode, FinanceProcess) Экспорт
	
	Если НЕ ЗначениеЗаполнено(CompanyCode)
		ИЛИ НЕ ЗначениеЗаполнено(CountryCode)
		ИЛИ НЕ ЗначениеЗаполнено(ERPID)
		ИЛИ НЕ ЗначениеЗаполнено(FinanceLocCode)
		ИЛИ НЕ ЗначениеЗаполнено(FinanceProcess) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("CompanyCode", CompanyCode);
	Запрос.УстановитьПараметр("CountryCode", CountryCode);
	Запрос.УстановитьПараметр("ERPID", ERPID);
	Запрос.УстановитьПараметр("FinanceLocCode", FinanceLocCode);
	Запрос.УстановитьПараметр("FinanceProcess", FinanceProcess);

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	LegalEntities.Ссылка
		|ИЗ
		|	Справочник.LegalEntities КАК LegalEntities
		|ГДЕ
		|	LegalEntities.CompanyCode = &CompanyCode
		|	И LegalEntities.CountryCode = &CountryCode
		|	И LegalEntities.ERPID = &ERPID
		|	И LegalEntities.FinanceLocCode = &FinanceLocCode
		|	И LegalEntities.FinanceProcess = &FinanceProcess
		|	И LegalEntities.InTMS
		|	И НЕ LegalEntities.ПометкаУдаления";
		
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Если Выборка.Количество() > 1 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Если РГСофтСерверПовтИспСеанс.ПолучитьПараметрСеансаShowNamesAndDescriptionsRUS() Тогда 
		СтандартнаяОбработка = Ложь;
		Поля.Добавить("NameRus");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если РГСофтСерверПовтИспСеанс.ПолучитьПараметрСеансаShowNamesAndDescriptionsRUS() Тогда 
		СтандартнаяОбработка = Ложь;
	  	Представление = Данные.NameRus;
	КонецЕсли;
	 	       
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если РГСофтСерверПовтИспСеанс.ПолучитьПараметрСеансаShowNamesAndDescriptionsRUS() 
		И ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда 
		
		СтандартнаяОбработка = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("NameRus", Параметры.СтрокаПоиска + "%");
		
		Запрос.Текст = "ВЫБРАТЬ
		|	LegalEntities.Ссылка
		|ИЗ
		|	Справочник.LegalEntities КАК LegalEntities
		|ГДЕ
		|	LegalEntities.NameRus ПОДОБНО &NameRus
		|	И НЕ LegalEntities.ПометкаУдаления";
		           		
		Если Параметры.Отбор.Свойство("ParentCompany") Тогда
			Запрос.Текст = Запрос.Текст + "
				|И LegalEntities.ParentCompany = &ParentCompany";
			Запрос.УстановитьПараметр("ParentCompany", Параметры.Отбор.ParentCompany);
		КонецЕсли;
		
		ТаблицаРезультатов = Запрос.Выполнить().Выгрузить();
		МассивСсылок = ТаблицаРезультатов.ВыгрузитьКолонку("Ссылка");
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(МассивСсылок);
		
	КонецЕсли;
	
КонецПроцедуры

