
///////////////////////////////////////////////////////////////////////////////////////////
// ПЕРЕД ЗАПИСЬЮ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизиты();
			
	ПроверитьРеквизиты(Отказ);
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////////

Процедура ДозаполнитьРеквизиты()
	
	Код = ВРег(СокрЛП(Код));
	Наименование = ВРег(СокрЛП(Наименование));
	TMSID = СокрЛП(TMSID);
	NameForMoveIT = СокрЛП(NameForMoveIT);
	
	РГСофтКлиентСервер.СокрЛПКолонокВТаблице(EMailBoxes, "EMailBox");
	ОбщегоНазначения.ОчиститьТаблицуОтСтрокСПустымиРеквизитами(EMailBoxes, "EMailBox");
	
	МассивEMailBoxes = EMailBoxes.ВыгрузитьКолонку("EMailBox");
	EMailBoxesList = СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(МассивEMailBoxes, ", ");
	
	Если ЭтоНовый() Тогда
		РГСофт.ЗаполнитьCreation(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьРеквизиты(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Код)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Code' is empty!",
			ЭтотОбъект, "Код", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Name' is empty!",
			ЭтотОбъект, "Наименование", , Отказ);		
	КонецЕсли; 
	
	// Проверим, что TMS ID уникален
	Если ЗначениеЗаполнено(TMSID) Тогда
		
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("TMSID", TMSID);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	CountriesHUBs.Представление
			|ИЗ
			|	Справочник.CountriesHUBs КАК CountriesHUBs
			|ГДЕ
			|	CountriesHUBs.TMSID = &TMSID
			|	И НЕ CountriesHUBs.ПометкаУдаления
			|	И CountriesHUBs.Ссылка <> &Ссылка";
			
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"TMS ID '" + TMSID + "' is already used in Country / HUB '" + СокрЛП(Выборка.Представление) + "'!",
				ЭтотОбъект, "TMSID", , Отказ);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
