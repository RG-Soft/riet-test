Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо И ОбщегоНазначения.ЗначениеНеЗаполнено(ИндивидуальныйПредприниматель) Тогда
		Сообщить("Организации - индивидуальному предпринимателю не сопоставлено физ. лицо!", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ИННОрг = ?(ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо, ИндивидуальныйПредприниматель.ИНН, ИНН);
	Если Ссылка.ИНН <> ИННОрг Тогда
		Если НЕ ПустаяСтрока(ИННОрг) И НЕ РегламентированнаяОтчетность.ИННСоответствуетТребованиям(ИННОрг, ЮридическоеФизическоеЛицо) Тогда
			Сообщить("ИНН организации задан неверно!", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	Если ЮридическоеФизическоеЛицо <> Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо И Ссылка.КПП <> КПП Тогда
		Если НЕ ПустаяСтрока(КПП) И СтрДлина(КПП) <> 9 Тогда
			Сообщить("КПП организации задан неверно!", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	Если Ссылка.ОГРН <> ОГРН Тогда
		Если НЕ ПустаяСтрока(ОГРН) Тогда
			ОшибкаОГРН = Ложь;
			Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
				Если СтрДлина(СокрЛП(ОГРН)) <> 15 Тогда
					Сообщить("ОГРНИП указан неверно! ОГРНИП должен состоять из 15 цифр!", СтатусСообщения.Важное);
					ОшибкаОГРН = Истина;
					Отказ = Истина;
				КонецЕсли;
				Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(ОГРН) Тогда
					Сообщить("ОГРНИП указан неверно! ОГРНИП должен состоять только из цифр!", СтатусСообщения.Важное);
					ОшибкаОГРН = Истина;
					Отказ = Истина;
				КонецЕсли;
				ОГРН14 = Число(Лев(Строка(ОГРН), 14));
				Если НЕ ОшибкаОГРН И Прав(Формат(ОГРН14 % 13, "ЧН=0; ЧГ=0"), 1) <> Прав(ОГРН, 1) Тогда
					Сообщить("Возможно, ОГРНИП указан неверно (контрольный разряд не совпадает с вычисленным)!", СтатусСообщения.Важное);
				КонецЕсли;
			Иначе
				Если СтрДлина(ОГРН) <> 13 Тогда
					Сообщить("ОГРН организации указан неверно! ОГРН должен состоять из 13 цифр!", СтатусСообщения.Важное);
					ОшибкаОГРН = Истина;
					Отказ = Истина;
				КонецЕсли;
				Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(ОГРН) Тогда
					Сообщить("ОГРН организации указан неверно! ОГРН должен состоять только из цифр!", СтатусСообщения.Важное);
					ОшибкаОГРН = Истина;
					Отказ = Истина;
				КонецЕсли;
				ОГРН12 = Число(Лев(Строка(ОГРН), 12));
				Если НЕ ОшибкаОГРН И Прав(Формат(ОГРН12 % 11, "ЧН=0; ЧГ=0"), 1) <> Прав(ОГРН, 1) Тогда
					Сообщить("Возможно, ОГРН организации указан неверно (контрольный разряд не совпадает с вычисленным)!", СтатусСообщения.Важное);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Если ОбщегоНазначения.ЗначениеНеЗаполнено(ЮридическоеФизическоеЛицо) Тогда
			Если СтрДлина(СокрЛП(ИНН)) = 12 Тогда
				ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
			Иначе
				ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
			КонецЕсли;
		КонецЕсли;
		Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо И НЕ ОбщегоНазначения.ЗначениеНеЗаполнено(ИндивидуальныйПредприниматель) Тогда
			ИНН = ИндивидуальныйПредприниматель.ИНН;
		КонецЕсли;
		Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			КПП								= "";
			КодОКОНХ						= "";
			ГоловнаяОрганизация 			= Неопределено;
		ИначеЕсли ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
			ИндивидуальныйПредприниматель 	= Неопределено;
		КонецЕсли; 
	КонецЕсли;
	
	//Изменил Трефиленков Дмитрий, РГ-Софт
	НастройкаПравДоступа.ПередЗаписьюНовогоОбъектаСПравамиДоступаПользователей(ЭтотОбъект, Отказ, ГоловнаяОрганизация);
	//конец изменения
КонецПроцедуры

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	ОбщегоНазначения.ДобавитьПрефиксУзла(Префикс);
КонецПроцедуры