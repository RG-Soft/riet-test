////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОткрытьПомощникПодключенияКСервису1СТакском(Команда)
	
	ЭлектронныеДокументыКлиент.ПомощникПодключенияКСервису1СТакском();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникПодключенияКПрямомуОбмену(Команда)
	
	ЭлектронныеДокументыКлиент.ПомощникПодключенияКПрямомуОбмену();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнятьАутентификациюНаСервереПриИзменении(Элемент)
	
	УстановитьКонтекстАутентификацииНаСервере(ВыполнятьАутентификациюНаСервере);
	
	Оповестить("Запись_НаборКонстант", Новый Структура, "КонтекстАвторизации");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьКонтекстАутентификацииНаСервере(ВыполнятьАутентификациюНаСервере)
	
	ВыбранныйВариант = ?(ВыполнятьАутентификациюНаСервере,
		Перечисления.КонтекстыРаботыСЭД.НаСервере, Перечисления.КонтекстыРаботыСЭД.НаКлиенте);
	Если Не ПравоДоступа("Установка", Метаданные.Константы.КонтекстАвторизации)
	 Или Константы.КонтекстАвторизации.Получить() = ВыбранныйВариант Тогда
		
		Возврат;
	КонецЕсли;
	
	Константы.КонтекстАвторизации.Установить(ВыбранныйВариант);
	
	// Требуется для обновления общих настроек на сервере и на клиенте.
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая()
	 Или ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		
		УстановитьКонтекстАутентификацииНаСервере(Ложь);
		Элементы.ВыполнятьАутентификациюНаСервере.Видимость = Ложь;
	Иначе
		ВыполнятьАутентификациюНаСервере = (Константы.КонтекстАвторизации.Получить() = Перечисления.КонтекстыРаботыСЭД.НаСервере);
	КонецЕсли;
	
КонецПроцедуры
