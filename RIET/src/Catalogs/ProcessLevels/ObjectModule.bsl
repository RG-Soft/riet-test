
///////////////////////////////////////////////////////////////////////////////////////
// ПЕРЕД ЗАПИСЬЮ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизиты();
	
	ПроверитьРеквизиты(Отказ);
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////

Процедура ДозаполнитьРеквизиты()
	
	Код = СокрЛП(Код);
	Наименование = СокрЛП(Наименование);
	EMailsForTemporaryItemsNotification = СокрЛП(EMailsForTemporaryItemsNotification);
	
	РГСофтКлиентСервер.СокрЛПКолонокВТаблице(FromTMSIDs, "FromTMSID");
	FromTMSIDs.Свернуть("FromTMSID", "");
	ОбщегоНазначения.ОчиститьТаблицуОтСтрокСПустымиРеквизитами(FromTMSIDs, "FromTMSID");
	FromTMSIDs.Сортировать("FromTMSID");
	
	МассивFromTMSIDs = FromTMSIDs.ВыгрузитьКолонку("FromTMSID");
	FromTMSIDsList = СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(МассивFromTMSIDs, ", ");
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьРеквизиты(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Код)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Code' is empty!",
			ЭтотОбъект, "Код", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Name' is empty!",
			ЭтотОбъект, "Наименование", , Отказ);
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Country) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Country' is empty!",
			ЭтотОбъект, "Country", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Corporation) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Corporation' is empty!",
			ЭтотОбъект, "Corporation", , Отказ);
	КонецЕсли;
	
	// Проверим, что нет другого Process level с такими же Country и From TMS ID
	Если FromTMSIDs.Количество() Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Country", Country);
		Запрос.УстановитьПараметр("FromTMSIDs", FromTMSIDs.ВыгрузитьКолонку("FromTMSID"));
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ProcessLevelsFromTMSIDs.FromTMSID,
			|	ProcessLevelsFromTMSIDs.Ссылка.Представление КАК Представление
			|ИЗ
			|	Справочник.ProcessLevels КАК ProcessLevels
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ProcessLevels.FromTMSIDs КАК ProcessLevelsFromTMSIDs
			|		ПО ProcessLevels.Ссылка = ProcessLevelsFromTMSIDs.Ссылка
			|ГДЕ
			|	ProcessLevels.Country = &Country
			|	И ProcessLevelsFromTMSIDs.FromTMSID В(&FromTMSIDs)
			|	И ProcessLevelsFromTMSIDs.Ссылка <> &Ссылка
			|	И НЕ ProcessLevels.ПометкаУдаления";
			
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СтрокаТЧ = FromTMSIDs.Найти(Выборка.FromTMSID, "FromTMSID");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Country '" + СокрЛП(Country) + "' and From TMS ID '" + Выборка.FromTMSID + "' is already used in Process level '" + СокрЛП(Выборка.Представление) + "'!",
				ЭтотОбъект, "FromTMSIDs[" + (СтрокаТЧ.НомерСтроки-1) + "].FromTMSID", , Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
