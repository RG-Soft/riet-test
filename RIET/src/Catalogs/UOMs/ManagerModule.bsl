
Функция ПолучитьПоTMSID(TMSID) Экспорт
	
	// Возвращает ссылку, найденную по TMSID
	// Отсекает помеченные на удаление
	// Если найдено несколько - возвращает Неопределено
	// Рекомендуется вызывать из модуля с повторным использованием значений
	
	Возврат РГСофт.НайтиСсылку("Справочник", "UOMs", "TMSID", TMSID);
	
КонецФункции

Функция ПолучитьПоTMSIDForItemUOM(TMSID) Экспорт
	
	// Возвращает ссылку, найденную по TMSIDForItemUOM
	// Отсекает помеченные на удаление
	// Если найдено несколько - возвращает Неопределено
	// Рекомендуется вызывать из модуля с повторным использованием значений
	
	Возврат РГСофт.НайтиСсылку("Справочник", "UOMs", "TMSIDForItemUOM", TMSID);
	
КонецФункции

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Если РГСофтСерверПовтИспСеанс.ПолучитьПараметрСеансаShowNamesAndDescriptionsRUS() Тогда 
		СтандартнаяОбработка = Ложь;
		Поля.Добавить("NameRus");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если РГСофтСерверПовтИспСеанс.ПолучитьПараметрСеансаShowNamesAndDescriptionsRUS() Тогда 
		СтандартнаяОбработка = Ложь;
	  	Представление = Данные.NameRus;
	КонецЕсли;
	 	       
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если РГСофтСерверПовтИспСеанс.ПолучитьПараметрСеансаShowNamesAndDescriptionsRUS() 
		И ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда 
		
		СтандартнаяОбработка = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("NameRus", Параметры.СтрокаПоиска + "%");
		
		Запрос.Текст = "ВЫБРАТЬ
		|	UOMs.Ссылка
		|ИЗ
		|	Справочник.UOMs КАК UOMs
		|ГДЕ
		|	UOMs.NameRus ПОДОБНО &NameRus
		|	И НЕ UOMs.ПометкаУдаления
		|	И UOMs.NameRusFilled";
		
		ТаблицаРезультатов = Запрос.Выполнить().Выгрузить();
		МассивСсылок = ТаблицаРезультатов.ВыгрузитьКолонку("Ссылка");
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(МассивСсылок);
		
	КонецЕсли;
	
КонецПроцедуры
