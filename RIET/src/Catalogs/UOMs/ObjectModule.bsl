 
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	ДозаполнитьРеквизитыБезДополнительныхДанных();
	
	ПроверитьРеквизитыБезДополнительныхДанных(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПоместитьДополнительныеДанныеВДополнительныеСвойстваПередЗаписью();
	
	ПроверитьВозможностьИзменения(Отказ, ДополнительныеСвойства.ВыборкаЗависимыхUOMs);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьЗаполнениеРеквизитов(
		Отказ, 
		ДополнительныеСвойства.ВыборкаРеквизитовStandardUOM,
		ДополнительныеСвойства.ВыборкаДублейПоTMSId);
		
КонецПроцедуры

Процедура ДозаполнитьРеквизитыБезДополнительныхДанных()
	
	Код = СокрЛП(Код);
	Наименование = СокрЛП(Наименование);
	
	NameRus = СокрЛП(NameRus);
	РГСофтКлиентСервер.УстановитьЗначение(NameRusFilled, ЗначениеЗаполнено(NameRus));
		
	//Заполним реквизиты для Standard Uom
	Если Standard Тогда 
		
		// Установим Conversion factor = 1
		РГСофтКлиентСервер.УстановитьЗначение(ConversionFactor, 1);
		
		// Установим StandardUOM = текущей ссылке
		Если ЭтоНовый() Тогда
			УстановитьСсылкуНового(Справочники.UOMs.ПолучитьСсылку());
			StandardUOM = ПолучитьСсылкуНового();
		Иначе
			РГСофтКлиентСервер.УстановитьЗначение(StandardUOM, Ссылка);
		КонецЕсли;
		
	КонецЕсли;
	
	TMSId = СокрЛП(TMSId);
	TMSIdForItemUOM = СокрЛП(TMSIdForItemUOM);
    InTMS = ЗначениеЗаполнено(TMSId) ИЛИ ЗначениеЗаполнено(TMSIdForItemUOM);
		
	Если ЭтоНовый() Тогда
		CreatedBy = ПараметрыСеанса.ТекущийПользователь;
		CreationDate = ТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьРеквизитыБезДополнительныхДанных(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Код)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Code' is empty!",
			ЭтотОбъект, "Код", , Отказ);
	КонецЕсли;
	
	Если НЕ Standard Тогда
		
		Если НЕ ЗначениеЗаполнено(ConversionFactor) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"'Conversion factor' is empty!",
				ЭтотОбъект, "ConversionFactor", , Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(StandardUOM) Тогда					
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"'Standard UOM' is empty!",
				ЭтотОбъект, "StandardUOM", , Отказ);
		КонецЕсли;
			
	КонецЕсли;
		
КонецПроцедуры

Процедура ПоместитьДополнительныеДанныеВДополнительныеСвойстваПередЗаписью()
	
	СтруктураПараметров = Новый Структура;
	СтруктураТекстов = Новый Структура;
	
	Если НЕ ЭтоНовый()
		И НЕ Standard Тогда
			
		СтруктураПараметров.Вставить("Ссылка", Ссылка);
		СтруктураТекстов.Вставить("ЗависимыеUOMs",
			"ВЫБРАТЬ
			|	UOMs.Представление
			|ИЗ
			|	Справочник.UOMs КАК UOMs
			|ГДЕ
			|	UOMs.StandardUOM = &Ссылка
			|	И UOMs.Ссылка <> &Ссылка
			|	И НЕ UOMS.ПометкаУдаления");
			
	КонецЕсли;
	
	Если НЕ ПометкаУдаления Тогда
		
		Если НЕ Standard Тогда
		
			СтруктураПараметров.Вставить("StandardUOM", StandardUOM);
			СтруктураТекстов.Вставить("РеквизитыStandardUOM",
				"ВЫБРАТЬ
				|	UOMs.Standard,
				|	UOMs.ПометкаУдаления
				|ИЗ
				|	Справочник.UOMs КАК UOMs
				|ГДЕ
				|	UOMs.Ссылка = &StandardUOM");
				
		КонецЕсли;
		
		Если ЗначениеЗаполнено(TMSId) Тогда
			
			СтруктураПараметров.Вставить("TMSId", TMSId);
			СтруктураПараметров.Вставить("Ссылка", Ссылка);
			СтруктураТекстов.Вставить("ДублиПоTMSId",
				"ВЫБРАТЬ
				|	UOMs.Представление
				|ИЗ
				|	Справочник.UOMs КАК UOMs
				|ГДЕ
				|	UOMs.TMSId = &TMSId
				|	И НЕ UOMs.ПометкаУдаления
				|	И UOMs.Ссылка <> &Ссылка");
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	СтруктураРезультатов = РГСофт.ПолучитьСтруктуруРезультатовТекстовЗапросов(СтруктураТекстов, СтруктураПараметров);
	
	ДополнительныеСвойства.Вставить("ВыборкаРеквизитовStandardUOM", Неопределено);
	Если СтруктураРезультатов.Свойство("РеквизитыStandardUOM") Тогда
		ДополнительныеСвойства.ВыборкаРеквизитовStandardUOM = СтруктураРезультатов.РеквизитыStandardUOM.Выбрать();
		ДополнительныеСвойства.ВыборкаРеквизитовStandardUOM.Следующий();
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ВыборкаЗависимыхUOMs", Неопределено);
	Если СтруктураРезультатов.Свойство("ЗависимыеUOMs") Тогда
		ДополнительныеСвойства.ВыборкаЗависимыхUOMs = СтруктураРезультатов.ЗависимыеUOMs.Выбрать();
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ВыборкаДублейПоTMSId", Неопределено);
	Если СтруктураРезультатов.Свойство("ДублиПоTMSId") Тогда
		ДополнительныеСвойства.ВыборкаДублейПоTMSId = СтруктураРезультатов.ДублиПоTMSId.Выбрать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьВозможностьИзменения(Отказ, ВыборкаЗависимыхUOMs)
	
	Если НЕ ЭтоНовый()
		И НЕ Standard Тогда
			
		Пока ВыборкаЗависимыхUOMs.Следующий() Цикл
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Faild to make this UOM non-standard, because it is already in use by ""UOM " + ВыборкаЗависимыхUOMs.Представление + """!",
				ЭтотОбъект, "Standard", , Отказ);
						
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеРеквизитов(Отказ, ВыборкаРеквизитовStandardUOM, ВыборкаДублейПоTMSId)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Standard Тогда
								
		Если НЕ ВыборкаРеквизитовStandardUOM.Standard Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Non-standard """ + StandardUOM + """ in the field ""Standard UOM""!",
				ЭтотОбъект, "StandardUOM", , Отказ);	
		КонецЕсли;				
		
		Если ВыборкаРеквизитовStandardUOM.ПометкаУдаления Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"""" + StandardUOM + """ is marked for deletion!",
				ЭтотОбъект, "StandardUOM", , Отказ);
		КонецЕсли;
						
	КонецЕсли;
	
	Если ЗначениеЗаполнено(TMSId) Тогда
		
		Пока ВыборкаДублейПоTMSId.Следующий() Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"TMS id """ + TMSId + """ is already used in UOM """ + СокрЛП(ВыборкаДублейПоTMSId.Представление) + """!",
				ЭтотОбъект, "TMSId", , Отказ);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
