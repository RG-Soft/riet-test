
Функция ПолучитьДляInvoice(Дата, Seller, ConsignTo, Incoterms) Экспорт
	
	// Возвращает массив подходящих контрактов
	
	// Если какой-то параметр не передан - запрос ничего не вернет - выходим
	Если НЕ ЗначениеЗаполнено(Дата)
		ИЛИ НЕ ЗначениеЗаполнено(Seller)
		ИЛИ НЕ ЗначениеЗаполнено(ConsignTo)
		ИЛИ НЕ ЗначениеЗаполнено(Incoterms) Тогда
		
		Возврат Новый Массив;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Seller", Seller);	
	Запрос.УстановитьПараметр("ConsignTo", ConsignTo);
	Запрос.УстановитьПараметр("Incoterms", Incoterms);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Contracts.Ссылка
		|ИЗ
		|	Справочник.Contracts КАК Contracts
		|ГДЕ
		|	Contracts.Seller = &Seller
		|	И Contracts.ConsignTo = &ConsignTo
		|	И Contracts.Incoterms = &Incoterms
		|	И Contracts.Date <= &Дата
		|	И Contracts.ExpiryDate >= &Дата
		|	И (НЕ Contracts.ПометкаУдаления)";
		
	УстановитьПривилегированныйРежим(Истина);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции