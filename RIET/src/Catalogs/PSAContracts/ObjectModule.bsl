
//////////////////////////////////////////////////////////////////////
// ПРИ КОПИРОВАНИИ

Процедура ПриКопировании(ОбъектКопирования)
	
	РГСофт.ОчиститьCreationModification(ЭтотОбъект);
	
КонецПроцедуры


//////////////////////////////////////////////////////////////////////
// ПЕРЕД ЗАПИСЬЮ

Процедура ПередЗаписью(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизитыБезДополнительныхДанных();
		
	ПроверитьЗаполнениеРеквизитов(Отказ);
		
КонецПроцедуры

Процедура ДозаполнитьРеквизитыБезДополнительныхДанных()
	
	Код = СокрЛП(Код);
	Client = СокрЛП(Client);
	
	Если ЭтоНовый() Тогда
		РГСофт.ЗаполнитьCreation(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеРеквизитов(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Код)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Code' is empty!",
			ЭтотОбъект, "Код", , Отказ);
	КонецЕсли;
	
	Если Ссылка <> Справочники.PSAContracts.NonPSAAZ
		И Ссылка <> Справочники.PSAContracts.NonPSATM Тогда
	
		Если НЕ ЗначениеЗаполнено(Country) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"'Country' is empty!",
				ЭтотОбъект, "Country", , Отказ);
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(Client) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"'Client' is empty!",
				ЭтотОбъект, "Client", , Отказ);
		КонецЕсли;
			
		Если НЕ ЗначениеЗаполнено(ExpiryDate) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"'Expiry date' is empty!",
				ЭтотОбъект, "ExpiryDate", , Отказ);
		КонецЕсли;
			
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Segment) Тогда
		
		SegmentParent = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Segment, "Родитель");
		Если ЗначениеЗаполнено(SegmentParent) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Choose Segment, not Sub Segment!",
				ЭтотОбъект, "Segment", , Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
