///////////////////////////////////////////////////////////////////////////////////////////////
// ПРИ КОПИРОВАНИИ

Процедура ПриКопировании(ОбъектКопирования)
	
	РГСофт.ОчиститьCreationModification(ЭтотОбъект);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////
// ПЕРЕД ЗАПИСЬЮ

Процедура ПередЗаписью(Отказ)
	 
	РГСофтКлиентСервер.УстановитьЗначение(Код, СокрЛП(Код));
	РГСофтКлиентСервер.УстановитьЗначение(NameRus, СокрЛП(NameRus));
	MOTs.Свернуть("MOT");
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьЗаполнениеРеквизитов(Отказ);
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	МассивMOT = MOTs.ВыгрузитьКолонку("MOT");
	MOTList = РГСофтКлиентСервер.ПолучитьСтрокуИзМассиваПодстрокСокрЛП(МассивMOT, ", ");
	
	Если ЭтоНовый() Тогда
		РГСофт.ЗаполнитьCreation(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры
     
///////////////////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьЗаполнениеРеквизитов(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
			
	Если ПустаяСтрока(Код) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Код' is empty!",
			ЭтотОбъект, "Код", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(EffectiveWeight) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Effective weight' is empty!",
			ЭтотОбъект, "EffectiveWeight", , Отказ);
	КонецЕсли;
		
	Если MOTs.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Add at least one MOT!",
			ЭтотОбъект, "MOTs", , Отказ);
	КонецЕсли;
		
	Для Каждого СтрокаТЧ Из MOTs Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.MOT) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"MOT in line " + СтрокаТЧ.НомерСтроки + " is empty!",
				ЭтотОбъект, "MOTs[" + (СтрокаТЧ.НомерСтроки - 1) + "].MOT", , Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
