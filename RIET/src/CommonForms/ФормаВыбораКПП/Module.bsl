////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.Контрагент) Тогда
		Отказ	= Истина;
		Возврат;
	КонецЕсли;
	
	СвойстваКонтрагента	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Контрагент, "КПП, ЮридическоеФизическоеЛицо");
	
	Если СвойстваКонтрагента.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ВернутьСтр("ru ='КПП для физических лиц не задается'"), , , , Отказ);
		Отказ	= Истина;
		Возврат;
	КонецЕсли;
	
	КППКонтрагента	= Параметры.КППКонтрагента;
	ЗаполнитьСписокВыбораКППКонтрагента(СписокВыбораКПП, Параметры.Контрагент);
	Если ПустаяСтрока(КППКонтрагента) И СписокВыбораКПП.Количество() > 0 Тогда
		КППКонтрагента	= СписокВыбораКПП[0].Значение;
	КонецЕсли;
	
	ТекстПодсказки = "";
	Если Параметры.РольКонтрагента = "Поставщик" Тогда
		Заголовок	= ВернутьСтр("ru = 'КПП поставщика'");
		ТекстПодсказки = ВернутьСтр("ru = 'При приобретении товаров и услуг через обособленные подразделения контрагентов, 
			|в строке 2б ""ИНН/КПП продавца"" счета-фактуры указывается КПП соответствующего обособленного подразделения'");
	ИначеЕсли Параметры.РольКонтрагента = "Покупатель" Тогда
		Заголовок	= ВернутьСтр("ru = 'КПП покупателя'");
		ТекстПодсказки = ВернутьСтр("ru = 'При реализации товаров и услуг обособленным подразделениям контрагентов, 
			|в строке 6б ""ИНН/КПП покупателя"" счета-фактуры указывается КПП соответствующего обособленного подразделения'");
	Иначе
		Заголовок	= ВернутьСтр("ru = 'КПП контрагента'");
		Элементы.КППКонтрагента.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	Элементы.КППКонтрагента.РасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ВопросСохраненияДанныхЗавершение", ЭтотОбъект);
		ТекстВопроса = ВернутьСтр("ru = 'Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КППКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ДанныеВыбора = СписокВыбораКПП;
			
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьИзменения();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораКППКонтрагента(СписокВыбораКПП, ГоловнойКонтрагент)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГоловнойКонтрагент", ГоловнойКонтрагент);

	// Первый элемент - всегда КПП головного контрагента, даже, если не задан
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Контрагенты.КПП КАК КПП,
		|	Контрагенты.Представление КАК ПредставлениеКонтрагента
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка = &ГоловнойКонтрагент
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Контрагенты.КПП,
		|	Контрагенты.Представление
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ Контрагенты.Ссылка = &ГоловнойКонтрагент
		|	И Контрагенты.ГоловнойКонтрагент = &ГоловнойКонтрагент
		|	И Контрагенты.ОбособленноеПодразделение
		|	И НЕ Контрагенты.ПометкаУдаления
		|	И НЕ Контрагенты.КПП ПОДОБНО """"";
	
	СписокВыбораКПП.Очистить();
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПредставлениеКПП	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 (%2)", ?(ПустаяСтрока(Выборка.КПП), "<не задан>", Выборка.КПП), Выборка.ПредставлениеКонтрагента);
		СписокВыбораКПП.Добавить(Выборка.КПП, ПредставлениеКПП);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения()
	
	Если ПроверитьЗаполнение() Тогда
		Модифицированность	= Ложь;
		
		ЗначениеВыбора	= Новый Структура("КППКонтрагента", КППКонтрагента);
			
		ОповеститьОВыборе(ЗначениеВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохраненияДанныхЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьИзменения();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры
