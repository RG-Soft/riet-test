
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Документ") Тогда 
		Документ = Параметры.Документ; 
		ВалютаДокумента = Документ.ВалютаДокумента;
	КонецЕсли;
	
	ЗаполнитьТаблицуНомеров();
		
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	УстановитьВидимостьКоманд();
		
КонецПроцедуры

&НаКлиенте
Процедура ДокументПриИзменении(Элемент)
										
	ПриИзмененииДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииДокумента()
	
	ЗаполнитьТаблицуНомеров();	
	
	ВалютаДокумента = Документ.ВалютаДокумента;
	 		
	УстановитьВидимостьКоманд();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКоманд()
	
	Элементы.НомераКоманднаяПанель.ПодчиненныеЭлементы.НомераПересчитатьВРуб.Видимость = ?((Не ЗначениеЗаполнено(ВалютаДокумента) Или ВалютаДокумента = ВалютаРегламентированногоУчета), Ложь, Истина);
	Элементы.НомераКоманднаяПанель.ПодчиненныеЭлементы.НомераЗаполнить.Видимость = ?(ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг"), Истина, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуНомеров()
	
	Номера.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НомераИнвойсовLawson.Документ,
	|	НомераИнвойсовLawson.Номер,
	|	НомераИнвойсовLawson.Тикет,
	|	НомераИнвойсовLawson.Сумма,
	|	НомераИнвойсовLawson.СуммаДокумента,
	//|	НомераИнвойсовLawson.СтавкаНДС,
	|	НомераИнвойсовLawson.СуммаДокументаБезНДС,
	|	НомераИнвойсовLawson.ДатаLawson,
	|	НомераИнвойсовLawson.РучнаяКорректировка,
	|	НомераИнвойсовLawson.Валюта
	|ИЗ
	|	РегистрСведений.НомераИнвойсовLawson КАК НомераИнвойсовLawson
	|ГДЕ
	|	НомераИнвойсовLawson.Документ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Номера.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура НомераПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ЗаполнитьСтрокуПоДаннымДокумента(Элемент.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокуПоДаннымДокумента(Идентификатор)
	
	ТекСтрока = Номера.НайтиПоИдентификатору(Идентификатор);
	
	ТекСтрока.СуммаДокументаБезНДС = SalesBook.ОпределитьСуммуДокументаБезНДС(Документ);
	ТекСтрока.СуммаДокумента = ОбщегоНазначения.ПолучитьСуммуДокументаСНДС(Документ);
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Если Документ.Товары.Количество() > 0 Тогда 
			ТекСтрока.СтавкаНДС = Документ.Товары[0].СтавкаНДС;
		ИначеЕсли Документ.Услуги.Количество() > 0 Тогда
			ТекСтрока.СтавкаНДС = Документ.Услуги[0].СтавкаНДС;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ПередачаОС") Тогда
		ТекСтрока.СтавкаНДС = Документ.ОС[0].СтавкаНДС;
		ТекСтрока.Сумма = ТекСтрока.СуммаДокумента;
	КонецЕсли;
	
КонецПроцедуры

//КОМАНДЫ

&НаКлиенте
Процедура Заполнить(Команда)
	
	Для Каждого Запись Из Номера Цикл
		Если Запись.РучнаяКорректировка Тогда
			Предупреждение("Были произведены ручные корректировки. Автоматическое заполнение невозможно!");
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Номера.Очистить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВложенныйЗапрос.Номер,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	//|	ВложенныйЗапрос.СтавкаНДС,  //Закомментировала Федотова Л., РГ-Софт, 22.03.13, вопрос SLI-0003408
	|	ВложенныйЗапрос.Ticket КАК Тикет,
	|	ВложенныйЗапрос.GLDate КАК ДатаLawson
	|ИЗ
	|	(ВЫБРАТЬ
	|		РеализацияТоваровУслугТовары.Ticket.SiebelOrder.LawsonInvoice КАК Номер,
	|		ВЫБОР
	|			КОГДА (НЕ РеализацияТоваровУслугТовары.Ссылка.СуммаВключаетНДС)
	|				ТОГДА РеализацияТоваровУслугТовары.Сумма + РеализацияТоваровУслугТовары.СуммаНДС
	|			ИНАЧЕ РеализацияТоваровУслугТовары.Сумма
	|		КОНЕЦ КАК Сумма,
	//|		РеализацияТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС, //Закомментировала Федотова Л., РГ-Софт, 22.03.13, вопрос SLI-0003408
	|		РеализацияТоваровУслугТовары.Ticket КАК Ticket,
	|		РеализацияТоваровУслугТовары.Ticket.SiebelOrder.GLDate КАК GLDate
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|	ГДЕ
	|		РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РеализацияТоваровУслугУслуги.Ticket.SiebelOrder.LawsonInvoice,
	|		ВЫБОР
	|			КОГДА (НЕ РеализацияТоваровУслугУслуги.Ссылка.СуммаВключаетНДС)
	|				ТОГДА РеализацияТоваровУслугУслуги.Сумма + РеализацияТоваровУслугУслуги.СуммаНДС
	|			ИНАЧЕ РеализацияТоваровУслугУслуги.Сумма
	|		КОНЕЦ,
	//|		РеализацияТоваровУслугУслуги.СтавкаНДС,  //Закомментировала Федотова Л., РГ-Софт, 22.03.13, вопрос SLI-0003408
	|		РеализацияТоваровУслугУслуги.Ticket,
	|		РеализацияТоваровУслугУслуги.Ticket.SiebelOrder.GLDate
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|	ГДЕ
	|		РеализацияТоваровУслугУслуги.Ссылка = &Ссылка) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номер,
	//|	ВложенныйЗапрос.СтавкаНДС, //Закомментировала Федотова Л., РГ-Софт, 22.03.13, вопрос SLI-0003408
	|	ВложенныйЗапрос.Ticket,
	|	ВложенныйЗапрос.GLDate");
	Запрос.УстановитьПараметр("Ссылка", Документ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Запись = Номера.Добавить();
		Запись.Документ = Документ;
		Запись.СуммаДокументаБезНДС = SalesBook.ОпределитьСуммуДокументаБезНДС(Документ);
		Запись.СуммаДокумента = ОбщегоНазначения.ПолучитьСуммуДокументаСНДС(Документ);
		Запись.Номер = Выборка.Номер;
		Запись.Сумма = Выборка.Сумма;
		//Закомментировала Федотова Л., РГ-Софт, 22.03.13, вопрос SLI-0003408
		//Запись.СтавкаНДС = Выборка.СтавкаНДС;
		Запись.Тикет = Выборка.Тикет;
		Запись.ДатаLawson = Выборка.ДатаLawson;
	КонецЦикла;
	          		
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНомера();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНомера()
	
	Если Не ЗначениеЗаполнено(Документ) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Не заполнено поле ""Документ""!");
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НомераИнвойсовLawson.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	НаборЗаписей.Прочитать();
	
	НаборЗаписей.Загрузить(Номера.Выгрузить());
	
	//Добавила Федотова Л, РГ-Софт, 26.11.13, вопрос SLI-0004006
	Для каждого Запись Из НаборЗаписей Цикл
	     Запись.Документ = Документ;
	КонецЦикла; 
	//
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Не удалось записать данные, " + ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьВРуб(Команда)
	
	ПересчитатьВРубНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьВРубНаСервере()
	
	ВалютаUSD = Справочники.Валюты.НайтиПоКоду(840);
	
	Для Каждого Запись Из Номера Цикл
		КурсUSD = ОбщегоНазначения.ПолучитьКурсВалюты(ВалютаUSD, Запись.Документ.Дата);
	    СуммаТикетаВДолларах = Запись.Тикет.Сумма*(100+УчетНДС.ПолучитьСтавкуНДС(Запись.СтавкаНДС))/100;
		Запись.Сумма = СуммаТикетаВДолларах * КурсUSD.Курс / КурсUSD.Кратность;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НомераТикетПриИзменении(Элемент)
	
	ТекДанные = Элементы.Номера.ТекущиеДанные;
	Если ТекДанные <> Неопределено И ЗначениеЗаполнено(ТекДанные.Тикет) Тогда
		ЗаполнитьСтрокуПоДаннымТикета(Элементы.Номера.ТекущаяСтрока, ТекДанные.Тикет);
	КонецЕсли;
	      		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокуПоДаннымТикета(Идентификатор, Тикет)
	
	ТекСтрока = Номера.НайтиПоИдентификатору(Идентификатор);
		
	ТекСтрока.РучнаяКорректировка = Истина;
	ТекСтрока.СтавкаНДС = Тикет.СтавкаНДС;
	ТекСтрока.ДатаLawson = Тикет.SiebelOrder.GLDate;
	ТекСтрока.Номер = Тикет.SiebelOrder.LawsonInvoice;
	 	
	ТекСтрока.Сумма = Тикет.Сумма*(100+УчетНДС.ПолучитьСтавкуНДС(ТекСтрока.СтавкаНДС))/100;
	//ТекСтрока.СуммаДокументаБезНДС = SalesBook.ОпределитьСуммуДокументаБезНДС(ТекСтрока.Документ);  //SLI-0005521
	ТекСтрока.СуммаДокументаБезНДС = SalesBook.ОпределитьСуммуДокументаБезНДС(Документ);
	//ТекСтрока.СуммаДокумента = ОбщегоНазначения.ПолучитьСуммуДокументаСНДС(ТекСтрока.Документ);   //SLI-0005521
	ТекСтрока.СуммаДокумента = ОбщегоНазначения.ПолучитьСуммуДокументаСНДС(Документ);
	   	
КонецПроцедуры

&НаКлиенте
Процедура НомераНомерПриИзменении(Элемент)
	
	ТекДанные = Элементы.Номера.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда 
		ТекДанные.РучнаяКорректировка = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НомераСуммаДокументаПриИзменении(Элемент)
	
	ТекДанные = Элементы.Номера.ТекущиеДанные;
	Если ВладелецФормы.ИмяФормы = "Документ.ПередачаОС.Форма.ФормаСпискаУправляемая" Тогда
		ТекДанные.Сумма = ТекДанные.СуммаДокумента;
	КонецЕсли;
КонецПроцедуры









