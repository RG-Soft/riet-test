
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыПодключения = Константы.КонтрольПроведенияПараметрыПодключенияКМодулюКонтроля.Получить().Получить();
	Если ПараметрыПодключения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыПодключения, "АдресПодключения, ИмяWebСервиса, ИмяПользователя, Пароль");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	Ответ = ПроверитьПодключениеНаСервере();
	Если Ответ Тогда
		ПоказатьПредупреждение(, "Успех");
	Иначе
		ПоказатьПредупреждение(, "Не удалось подключиться к веб-сервису");
	КонецЕсли;
КонецПроцедуры


&НаСервере
Функция ПроверитьПодключениеНаСервере()
	
	Определение = Новый WSОпределения(АдресПодключения + "/ws/" + НРег(ИмяWebСервиса) + ".1cws?wsdl", ИмяПользователя, Пароль);
	
	Попытка
		Прокси = Новый WSПрокси(Определение, ИмяWebСервиса, ИмяWebСервиса, ИмяWebСервиса+"Soap12");
		Прокси.Пользователь = ИмяПользователя;
		Прокси.Пароль = Пароль;
	
		Тип = Прокси.ФабрикаXDTO.Тип("http://v8.1c.ru/8.1/data/core", "Structure");
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции


&НаСервере
Процедура ОКНаСервере()
	СтруктураПараметров = Новый Структура("АдресПодключения, ИмяWebСервиса, ИмяПользователя, Пароль");
	
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, ЭтаФорма, "АдресПодключения, ИмяWebСервиса, ИмяПользователя, Пароль");
	
	Константы.КонтрольПроведенияПараметрыПодключенияКМодулюКонтроля.Установить(Новый ХранилищеЗначения(СтруктураПараметров, Новый СжатиеДанных(9)));
КонецПроцедуры


&НаКлиенте
Процедура ОК(Команда)
	ОКНаСервере();
КонецПроцедуры

