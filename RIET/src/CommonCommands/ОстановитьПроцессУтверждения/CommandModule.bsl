                 
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекстВопроса = ПроверитьСтатусУтверждения(ПараметрКоманды);
	Если ТекстВопроса = Неопределено Тогда 
		Возврат;
	КонецЕсли; 
		
	// Зададим вопрос, уверен ли пользователь
	ТекстВопроса = ТекстВопроса + "Вы действительно хотите отменить процесс утверждения документа """ + ПараметрКоманды + """?";
	
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет, "Внимание!"); 
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	      
	// Остановим процесс утверждения
	НастройкаПравДоступа.ОстановитьПроцессУтверждения(ПараметрКоманды);

	Оповестить("ОстановленПроцессУтверждения", ПараметрКоманды);
	
КонецПроцедуры
	
&НаСервере
Функция ПроверитьСтатусУтверждения(СсылкаНаУтверждаемыйОбъект) 
	
	// Проверим, что процесс утверждения действительно запущен
	ТекстВопроса = "";
	
	СтатусУтверждения = НастройкаПравДоступа.ПолучитьСтатусУтверждения(СсылкаНаУтверждаемыйОбъект);
	ПеречислениеСтатусыУтверждения = Перечисления.СтатусыУтвержденияОбъектов;
	
	Если СтатусУтверждения <> ПеречислениеСтатусыУтверждения.ВПроцессеУтверждения
		И СтатусУтверждения <> ПеречислениеСтатусыУтверждения.Новая
		И СтатусУтверждения <> ПеречислениеСтатусыУтверждения.Утверждена Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Документ """ + СсылкаНаУтверждаемыйОбъект + """ не утвержден или не находится в процессе утверждения!");
		Возврат Неопределено;
		
	иначе
		
		Если СтатусУтверждения = ПеречислениеСтатусыУтверждения.Утверждена Тогда
			ТекстВопроса = "Документ уже утвержден!" + Символы.ПС;
		ИначеЕсли СтатусУтверждения = ПеречислениеСтатусыУтверждения.ВПроцессеУтверждения Тогда
			ТекстВопроса = "Документ находится в процессе утверждения!" + Символы.ПС;
		КонецЕсли;
		
		Возврат ТекстВопроса;
		
	КонецЕсли;			
		
КонецФункции

