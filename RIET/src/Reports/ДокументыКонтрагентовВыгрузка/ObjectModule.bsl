Процедура СформироватьОтчет(ТабДокумент, НачПериода, КонПериода) Экспорт
	
	Макет=ПолучитьМакет("Макет");
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	
	ТабДокумент.Вывести(Шапка);
	
	Запрос = Новый Запрос(	"ВЫБРАТЬ
	                      	|	СУММА(ВложенныйЗапрос.СуммаСчета) КАК СуммаСчета,
	                      	|	СУММА(ВложенныйЗапрос.СуммаСчетаРуб) КАК СуммаСчетаРуб,
	                      	|	СУММА(ВложенныйЗапрос.СуммаСФ) КАК СуммаСФ,
	                      	|	СУММА(ВложенныйЗапрос.СуммаСФРуб) КАК СуммаСФРуб,
	                      	|	СУММА(ВложенныйЗапрос.СуммаПрихода) КАК СуммаПрихода,
	                      	|	СУММА(ВложенныйЗапрос.СуммаПриходаРуб) КАК СуммаПриходаРуб,
	                      	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	                      	|	ВложенныйЗапрос.НомерСчета КАК НомерСчета,
	                      	|	ВложенныйЗапрос.ТипДокумента,
	                      	|	ВложенныйЗапрос.ДатаДокумента,
	                      	|	ВложенныйЗапрос.ВалютаДокумента,
	                      	|	ВложенныйЗапрос.НомерДокумента,
	                      	|	ВложенныйЗапрос.ДатаРегистрацииДокумента,
	                      	|	ВложенныйЗапрос.НомерВаучера,
	                      	|	ВложенныйЗапрос.НомерПП,
	                      	|	ВложенныйЗапрос.ДатаПП
	                      	|ИЗ
	                      	|	(ВЫБРАТЬ
	                      	|		СчетаКонтрагентовОбороты.Контрагент КАК Контрагент,
	                      	|		СчетаКонтрагентовОбороты.НомерСчета КАК НомерСчета,
	                      	|		СчетаКонтрагентовОбороты.НомерСчета КАК НомерДокумента,
	                      	|		СчетаКонтрагентовОбороты.ДатаСчета КАК ДатаДокумента,
	                      	|		СчетаКонтрагентовОбороты.ДатаРегистрации КАК ДатаРегистрацииДокумента,
	                      	|		СчетаКонтрагентовОбороты.Валюта КАК ВалютаДокумента,
	                      	|		СчетаКонтрагентовОбороты.Сумма КАК СуммаСчета,
	                      	|		СчетаКонтрагентовОбороты.СуммаРуб КАК СуммаСчетаРуб,
	                      	|		NULL КАК СуммаСФ,
	                      	|		NULL КАК СуммаСФРуб,
	                      	|		NULL КАК СуммаПрихода,
	                      	|		NULL КАК СуммаПриходаРуб,
	                      	|		СчетаКонтрагентовОбороты.НомерВаучера КАК НомерВаучера,
	                      	|		СчетаКонтрагентовОбороты.НомерПП КАК НомерПП,
	                      	|		СчетаКонтрагентовОбороты.ДатаПП КАК ДатаПП,
	                      	|		""Счет"" КАК ТипДокумента
	                      	|	ИЗ
	                      	|		РегистрНакопления.СчетаКонтрагентов КАК СчетаКонтрагентовОбороты
	                      	|	ГДЕ
	                      	|		СчетаКонтрагентовОбороты.ДатаРегистрации МЕЖДУ &ДатаНач И &ДатаКон
	                      	|	
	                      	|	ОБЪЕДИНИТЬ ВСЕ
	                      	|	
	                      	|	ВЫБРАТЬ
	                      	|		СчетаФактурыКонтрагентовОбороты.Контрагент,
	                      	|		СчетаФактурыКонтрагентовОбороты.НомерСчета,
	                      	|		СчетаФактурыКонтрагентовОбороты.НомерСФ,
	                      	|		СчетаФактурыКонтрагентовОбороты.ДатаСФ,
	                      	|		СчетаФактурыКонтрагентовОбороты.ДатаРегистрации,
	                      	|		СчетаФактурыКонтрагентовОбороты.Валюта,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		СчетаФактурыКонтрагентовОбороты.Сумма,
	                      	|		СчетаФактурыКонтрагентовОбороты.СуммаРуб,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		""Счет-фактура""
	                      	|	ИЗ
	                      	|		РегистрНакопления.СчетаФактурыКонтрагентов КАК СчетаФактурыКонтрагентовОбороты
	                      	|	ГДЕ
	                      	|		СчетаФактурыКонтрагентовОбороты.ДатаРегистрации МЕЖДУ &ДатаНач И &ДатаКон
	                      	|	
	                      	|	ОБЪЕДИНИТЬ ВСЕ
	                      	|	
	                      	|	ВЫБРАТЬ
	                      	|		АктыКонтрагентовОбороты.Контрагент,
	                      	|		АктыКонтрагентовОбороты.НомерСчета,
	                      	|		АктыКонтрагентовОбороты.НомерПрихода,
	                      	|		АктыКонтрагентовОбороты.ДатаПрихода,
	                      	|		АктыКонтрагентовОбороты.ДатаРегистрации,
	                      	|		АктыКонтрагентовОбороты.Валюта,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		АктыКонтрагентовОбороты.Сумма,
	                      	|		АктыКонтрагентовОбороты.СуммаРуб,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		NULL,
	                      	|		""Акт/накладная""
	                      	|	ИЗ
	                      	|		РегистрНакопления.АктыКонтрагентов КАК АктыКонтрагентовОбороты
	                      	|	ГДЕ
	                      	|		АктыКонтрагентовОбороты.ДатаРегистрации МЕЖДУ &ДатаНач И &ДатаКон) КАК ВложенныйЗапрос
	                      	|
	                      	|СГРУППИРОВАТЬ ПО
	                      	|	ВложенныйЗапрос.Контрагент,
	                      	|	ВложенныйЗапрос.НомерСчета,
	                      	|	ВложенныйЗапрос.ТипДокумента,
	                      	|	ВложенныйЗапрос.ДатаДокумента,
	                      	|	ВложенныйЗапрос.НомерДокумента,
	                      	|	ВложенныйЗапрос.ВалютаДокумента,
	                      	|	ВложенныйЗапрос.ДатаРегистрацииДокумента,
	                      	|	ВложенныйЗапрос.НомерВаучера,
	                      	|	ВложенныйЗапрос.НомерПП,
	                      	|	ВложенныйЗапрос.ДатаПП
	                      	|ИТОГИ
	                      	|	СУММА(СуммаСчета),
	                      	|	СУММА(СуммаСчетаРуб),
	                      	|	СУММА(СуммаСФ),
	                      	|	СУММА(СуммаСФРуб),
	                      	|	СУММА(СуммаПрихода),
	                      	|	СУММА(СуммаПриходаРуб)
	                      	|ПО
	                      	|	Контрагент,
	                      	|	НомерСчета");
	
	Запрос.УстановитьПараметр("ДатаНач", НачПериода);
	Запрос.УстановитьПараметр("ДатаКон", КонПериода);
	Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Пока Выборка.Следующий() Цикл
		Счет = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Счет.Следующий() Цикл
			Строка=Макет.ПолучитьОбласть("Строка");
			Строка.Параметры.Заполнить(Счет);
			
			Если Не ОбщегоНазначения.ЗначениеНеЗаполнено(Счет.Контрагент) ТОгда
				Строка.Параметры.ИмяКонтрагента = Счет.Контрагент.Наименование;
				Строка.Параметры.КодКонтрагента = Счет.Контрагент.КонтрагентLawson.Код;
			КонецЕсли;
			
			НомераСФ="";
			НомераАктов="";
			Детали = Счет.Выбрать();
			Пока Детали.Следующий() Цикл
				
				Если Детали.ТипДокумента = "Счет" Тогда
					Строка.Параметры.ВалютаСчета = Детали.ВалютаДокумента;
					Строка.Параметры.НомерСчета = Детали.НомерДокумента;
					Строка.Параметры.ДатаСчета = Детали.ДатаДокумента;
					Строка.Параметры.ДатаРегистрацииСчета= Детали.ДатаРегистрацииДокумента;
					Строка.Параметры.НомерВаучера = Детали.НомерВаучера;
					Строка.Параметры.НомерПП = Детали.НомерПП;
					Строка.Параметры.ДатаПП = Детали.ДатаПП;
				ИначеЕсли Детали.ТипДокумента = "Счет-фактура" Тогда
					Строка.Параметры.ВалютаСФ = Детали.ВалютаДокумента;
					Строка.Параметры.ДатаСФ= Детали.ДатаДокумента;
					Строка.Параметры.ДатаРегистрацииСФ= Детали.ДатаРегистрацииДокумента;
					НомераСФ=НомераСФ + Детали.НомерДокумента+", ";
				ИначеЕсли Детали.ТипДокумента = "Акт/накладная" Тогда
					Строка.Параметры.ВалютаПрихода = Детали.ВалютаДокумента;
					Строка.Параметры.ДатаПрихода= Детали.ДатаДокумента;
					Строка.Параметры.ДатаРегистрацииПрихода= Детали.ДатаРегистрацииДокумента;
					НомераАктов=НомераАктов + Детали.НомерДокумента+", ";
				КонецЕсли;
				
			КонецЦикла;
			
			Если СтрДлина(НомераСФ)>1 Тогда
				
				НомераСФ = Лев(НомераСФ, СтрДлина(НомераСФ) - 2);
				
			КонецЕсли;
			
			Если СтрДлина(НомераАктов)>1 Тогда
				
				НомераАктов = Лев(НомераАктов, СтрДлина(НомераАктов) - 2);
				
			КонецЕсли;
			
			Строка.Параметры.НомераСФ=НомераСФ;
			Строка.Параметры.НомераАктов=НомераАктов;
			
			ТабДокумент.Вывести(Строка);
			
			
		КонецЦикла;
	КонецЦикла;
	
	
КонецПроцедуры