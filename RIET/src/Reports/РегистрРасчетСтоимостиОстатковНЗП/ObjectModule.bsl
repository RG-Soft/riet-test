#Если Клиент Тогда
	
Перем НП Экспорт;

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	НачалоПериода = НачалоДня(ДатаНач);
	КонецПериода  = КонецДня(ДатаКон);

	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");

	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(НачалоПериода, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(КонецПериода, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = Организация.НаименованиеПолное;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	Если НоменклатурнаяГруппа <> Справочники.НоменклатурныеГруппы.ПустаяСсылка() Тогда
		ОбластьЗаголовок.Параметры.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
	Иначе
		ОбластьЗаголовок.Параметры.НоменклатурнаяГруппа = "По всем";
	КонецЕсли;
	
	Если Подразделение <> Справочники.ПодразделенияОрганизаций.ПустаяСсылка() Тогда
		ОбластьЗаголовок.Параметры.Подразделение = Подразделение;
	Иначе
		ОбластьЗаголовок.Параметры.Подразделение = "По всем";
		
	КонецЕсли;

	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ОбщегоНазначения.ЗначениеНеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;
	
	СписокСчетов = Новый Массив;
		СписокСчетов.Добавить(ПланыСчетов.Налоговый.ПрямыеРасходыОсновногоПроизводства);
		СписокСчетов.Добавить(ПланыСчетов.Налоговый.ПрямыеРасходыВспомогательныхПроизводств);
	

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач",     Новый Граница(НачалоДня(ДатаНач), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаКон",     Новый Граница(КонецДня(ДатаКон),  ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Амортизация", Перечисления.ВидыРасходовНУ.Амортизация);
	Запрос.УстановитьПараметр("ОплатаТруда", Перечисления.ВидыРасходовНУ.ОплатаТруда);
	Запрос.УстановитьПараметр("МатериальныеРасходы", Перечисления.ВидыРасходовНУ.МатериальныеРасходы);
	Запрос.УстановитьПараметр("НалогиИСборы", Перечисления.ВидыРасходовНУ.НалогиИСборы);
	Запрос.УстановитьПараметр("ВидУчета",Перечисления.ВидыУчетаПоПБУ18.НУ);
    Запрос.УстановитьПараметр("Счет",  СписокСчетов);
	Запрос.УстановитьПараметр("ВидСубконто",ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВыбПодразделение",Подразделение);
	Запрос.УстановитьПараметр("ВыбНоменклатурнаяГруппа",НоменклатурнаяГруппа);

	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2 КАК НоменклатурнаяГруппа,
	|	СУММА(ВЫБОР КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто3.ВидРасходовНУ <> &Амортизация И НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто3.ВидРасходовНУ <> &НалогиИСборы И НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто3.ВидРасходовНУ <> &МатериальныеРасходы И НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто3.ВидРасходовНУ <> &ОплатаТруда ТОГДА ВЫБОР КОГДА (НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт КОНЕЦ ИНАЧЕ 0 КОНЕЦ) КАК ДругиеРасходы,
	|	СУММА(ВЫБОР КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто3.ВидРасходовНУ = &МатериальныеРасходы ТОГДА ВЫБОР КОГДА (НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт КОНЕЦ ИНАЧЕ 0 КОНЕЦ) КАК МатериальныеРасходы,
	|	СУММА(ВЫБОР КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто3.ВидРасходовНУ = &Амортизация ТОГДА ВЫБОР КОГДА (НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт КОНЕЦ ИНАЧЕ 0 КОНЕЦ) КАК Амортизация,
	|	СУММА(ВЫБОР КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто3.ВидРасходовНУ = &ОплатаТруда ТОГДА ВЫБОР КОГДА (НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт КОНЕЦ ИНАЧЕ 0 КОНЕЦ) КАК ОплатаТруда,
	|	СУММА(ВЫБОР КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто3.ВидРасходовНУ = &НалогиИСборы ТОГДА ВЫБОР КОГДА (НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт КОНЕЦ ИНАЧЕ 0 КОНЕЦ) КАК ЕСН,
	|	СУММА(ВЫБОР КОГДА (НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаОборотДт КОНЕЦ) КАК СуммаПрямыхРасхдовЗаМесяц,
	|	СУММА(ВЫБОР КОГДА (НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНачальныйОстаток) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНачальныйОстаток КОНЕЦ) КАК СтоимостьНачальныйОстаток,
	|	СУММА(ВЫБОР КОГДА (НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаКонечныйОстаток) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаКонечныйОстаток КОНЕЦ) КАК СтоимостьКонечныйОстаток,
	|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Период КАК Период
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ОстаткиИОбороты(&ДатаНач,
	|&ДатаКон,
	|Месяц,
	|ДвиженияИГраницыПериода,
	|Счет В (&Счет),
	|,
	|Организация = &Организация И ВидУчета = &ВидУчета";
	
	Если Подразделение <> Справочники.ПодразделенияОрганизаций.ПустаяСсылка() И НоменклатурнаяГруппа <> Справочники.НоменклатурныеГруппы.ПустаяСсылка() Тогда
		Запрос.Текст = Запрос.Текст + " И
		|	Субконто1 В(&ВыбПодразделение) И
		|	Субконто2 В(&ВыбНоменклатурнаяГруппа)";
	ИначеЕсли НоменклатурнаяГруппа <> Справочники.НоменклатурныеГруппы.ПустаяСсылка() Тогда
		Запрос.Текст = Запрос.Текст + " И
		|	Субконто2 В(&ВыбНоменклатурнаяГруппа)";
	ИначеЕсли Подразделение <> Справочники.ПодразделенияОрганизаций.ПустаяСсылка() Тогда
		Запрос.Текст = Запрос.Текст + " И
		|	Субконто1 В(&ВыбПодразделение)";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|) КАК НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты
    |
	|СГРУППИРОВАТЬ ПО
	|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто3.ВидРасходовНУ,
	|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2,
	|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	НоменклатурнаяГруппа
	|
	|ИТОГИ СУММА(ДругиеРасходы), СУММА(МатериальныеРасходы), СУММА(Амортизация), СУММА(ОплатаТруда), СУММА(ЕСН), СУММА(СуммаПрямыхРасхдовЗаМесяц), СУММА(СтоимостьНачальныйОстаток), СУММА(СтоимостьКонечныйОстаток) ПО
	|	Период,
	|	НоменклатурнаяГруппа";
	Результат = Запрос.Выполнить();

	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого         = Макет.ПолучитьОбласть("Итого");
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");

	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);

	ВыборкаПериодов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаПериодов.Следующий() Цикл

		МесяцГод = Формат(ВыборкаПериодов.Период,"ДФ=MMMM.yyyy");
		МесяцГод = СтрЗаменить(МесяцГод,"."," ");

		СуммаПрямыхРасхдовЗаМесяц = ?(ВыборкаПериодов.СуммаПрямыхРасхдовЗаМесяц = NULL, 0, ВыборкаПериодов.СуммаПрямыхРасхдовЗаМесяц);

		ОбластьИтого.Параметры.МесяцГод                          = МесяцГод;
		ОбластьИтого.Параметры.СуммаОстатокНЗПНаНачалоМесяца     = ВыборкаПериодов.СтоимостьНачальныйОстаток;
		ОбластьИтого.Параметры.СуммаМатериальныхРасходовЗаМесяц  = ВыборкаПериодов.МатериальныеРасходы;
		ОбластьИтого.Параметры.СуммаДругихРасходовЗаМесяц        = ВыборкаПериодов.ДругиеРасходы;
		ОбластьИтого.Параметры.СуммаРасходовНаОплатуТрудаЗаМесяц = ВыборкаПериодов.ОплатаТруда;
		ОбластьИтого.Параметры.СуммаЕСНЗаМесяц                   = ВыборкаПериодов.ЕСН;
		ОбластьИтого.Параметры.СуммаАмортизацииЗаМесяц           = ВыборкаПериодов.Амортизация;

		ОбластьИтого.Параметры.СуммаПрямыхРасхдовЗаМесяц         =  СуммаПрямыхРасхдовЗаМесяц;
		ОбластьИтого.Параметры.СуммаПрямыхРасходовНаКонецМесяца  = ВыборкаПериодов.СтоимостьНачальныйОстаток + СуммаПрямыхРасхдовЗаМесяц;
		ОбластьИтого.Параметры.СуммаОстатокНЗПНаКонецМесяца      = ВыборкаПериодов.СтоимостьКонечныйОстаток;
		ОбластьИтого.Параметры.СуммаОтносящаясяКВыпускуПродукции = ВыборкаПериодов.СтоимостьНачальныйОстаток + СуммаПрямыхРасхдовЗаМесяц - ВыборкаПериодов.СтоимостьКонечныйОстаток;

		ДокументРезультат.Вывести(ОбластьИтого);

		ВыборкаСтрок = ВыборкаПериодов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаСтрок.Следующий() Цикл

			СуммаПрямыхРасхдовЗаМесяц = ВыборкаСтрок.СуммаПрямыхРасхдовЗаМесяц;

			ОбластьСтрока.Параметры.МесяцГод                          = МесяцГод;
			ОбластьСтрока.Параметры.ВидНоменклатуры                   = ВыборкаСтрок.НоменклатурнаяГруппа;
			ОбластьСтрока.Параметры.СуммаОстатокНЗПНаНачалоМесяца     = ВыборкаСтрок.СтоимостьНачальныйОстаток;
			ОбластьСтрока.Параметры.СуммаМатериальныхРасходовЗаМесяц  = ВыборкаСтрок.МатериальныеРасходы;
			ОбластьСтрока.Параметры.СуммаРасходовНаОплатуТрудаЗаМесяц = ВыборкаСтрок.ОплатаТруда;
			ОбластьСтрока.Параметры.СуммаЕСНЗаМесяц                   = ВыборкаСтрок.ЕСН;
			ОбластьСтрока.Параметры.СуммаАмортизацииЗаМесяц           = ВыборкаСтрок.Амортизация;
 			ОбластьСтрока.Параметры.СуммаДругихРасходовЗаМесяц        = ВыборкаСтрок.ДругиеРасходы;
			
			ОбластьСтрока.Параметры.СуммаПрямыхРасхдовЗаМесяц         = СуммаПрямыхРасхдовЗаМесяц;
			ОбластьСтрока.Параметры.СуммаПрямыхРасходовНаКонецМесяца  = ВыборкаСтрок.СтоимостьНачальныйОстаток + СуммаПрямыхРасхдовЗаМесяц;
			ОбластьСтрока.Параметры.СуммаОстатокНЗПНаКонецМесяца      = ВыборкаСтрок.СтоимостьКонечныйОстаток ;
			ОбластьСтрока.Параметры.СуммаОтносящаясяКВыпускуПродукции = ВыборкаСтрок.СтоимостьНачальныйОстаток + СуммаПрямыхРасхдовЗаМесяц - ВыборкаСтрок.СтоимостьКонечныйОстаток;

			ДокументРезультат.Вывести(ОбластьСтрока);

		КонецЦикла;

	КонецЦикла;

	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);

	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;

	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры // СформироватьОтчет

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;

#КонецЕсли