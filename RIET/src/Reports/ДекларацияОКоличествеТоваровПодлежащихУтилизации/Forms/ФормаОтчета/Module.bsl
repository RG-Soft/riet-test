
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Отчет.Период = НачалоГода(ТекущаяДата());
	ДатаФормирования = ТекущаяДата();
	ТаблицаНомеров.Область("R" + Формат(1, "ЧГ=0") + "C1").Текст  = "Строка инвойса";
	ТаблицаНомеров.Область("R" + Формат(1, "ЧГ=0") + "C2").Текст  = "Номер ГТД";
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере(ТабДок)
	Объект = РеквизитФормыВЗначение("Отчет");
	ДанныеШапки = ПолучитьДанныеШапки ();
	ДанныеТЧ = ПолучитьДанныеТЧ();
	
	Макет = Объект.ПолучитьМакет("ФормаОтчета");
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакетаШапка.Параметры.ОтчетныйГод = Год(Отчет.Период);
	Если ДанныеШапки.Количество() Тогда 
		ОбластьМакетаШапка.Параметры.Заполнить(ДанныеШапки[0]);
	КонецЕсли;
	ТабДок.Вывести(ОбластьМакетаШапка);
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц(); 
	
	ОбластьМакетаЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДок.Вывести(ОбластьМакетаЗаголовокТаблицы);
	
	ОбластьМакетаШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДок.Вывести(ОбластьМакетаШапкаТаблицы);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	НомерСтроки = 1;
	Для Каждого Строка Из ДанныеТЧ Цикл 
		ОбластьМакета.Параметры.Заполнить(Строка);
		ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
		Если Цел(Строка.ВесНетто) = Строка.ВесНетто Тогда 
			ОбластьМакета.Параметры.ВесНетто = Строка.ВесНетто;
		Иначе
			ОбластьМакета.Параметры.ВесНетто = Цел(Строка.ВесНетто) + 1;
		КонецЕсли;
		МассивОбластей = Новый Массив;
		Если НомерСтроки = 1 Тогда 
			МассивОбластей.Добавить(ОбластьМакетаШапка);
			МассивОбластей.Добавить(ОбластьМакетаШапкаТаблицы);
		КонецЕсли;
		МассивОбластей.Добавить(ОбластьМакета);
		Если ТабДок.ПроверитьВывод(МассивОбластей)Тогда 
			ТабДок.Вывести(ОбластьМакета);
		Иначе 
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДок.Вывести(ОбластьМакетаШапкаТаблицы);
			ТабДок.Вывести(ОбластьМакета);
		КонецЕсли;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакетаПодвал.Параметры.Подписант = Подписант;
	ОбластьМакетаПодвал.Параметры.ДатаФормирования = Строка(Формат(ДатаФормирования,"ДФ=dd.MM.yyyy")) + " г."; 
	ТабДок.Вывести(ОбластьМакетаПодвал);
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ЭкземпляровНаСтранице = 1;
	ТабДок.РазмерСтраницы = "A4";
	ПолучитьНомераГТД();
	Если НомераГТД.Количество() Тогда 
		СформироватьНаСервере(ТабДок);
	КонецЕсли;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ВерхнийКолонтитул.НачальнаяСтраница = 1;
	ТабДок.ВерхнийКолонтитул.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
	ТабДок.ВерхнийКолонтитул.ТекстВЦентре = "стр. [&НомерСтраницы] из [&СтраницВсего]";
	ТабДок.ВерхнийКолонтитул.Выводить = Истина;
	ТабДок.Показать();
	
КонецПроцедуры

&НаСервере
Функция  ПолучитьДанныеТЧ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НомераГТД.НомерГТД КАК НомерГТД,
	|	НомераГТД.СтрокаИнвойса
	|ПОМЕСТИТЬ НомераГТД
	|ИЗ
	|	&НомераГТД КАК НомераГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗакрытиеПоставкиСопоставление.СтрокаИнвойса.SoldTo КАК SoldTo,
	|	СУММА(ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД.Количество) КАК Количество,
	|	ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД.Владелец.КодТНВЭД КАК КодТНВЭД,
	|	ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД.Владелец.ТНВЭД КАК ТНВЭД,
	|	ВЫРАЗИТЬ(ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД.Владелец.ОписаниеТовара КАК СТРОКА(150)) КАК НаименованиеТовара,
	|	НомераГТД.НомерГТД
	|ПОМЕСТИТЬ ИтоговаяТаблица
	|ИЗ
	|	Документ.ЗакрытиеПоставки.Сопоставление КАК ЗакрытиеПоставкиСопоставление
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ПодразделенияОрганизаций.LegalEntity.ParentCompany КАК LegalEntityParentCompany
	|		ИЗ
	|			Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|		ГДЕ
	|			ПодразделенияОрганизаций.ИНН = &ИНН
	|			И НЕ ПодразделенияОрганизаций.LegalEntity.ParentCompany ЕСТЬ NULL) КАК ВложенныйЗапрос
	|		ПО ЗакрытиеПоставкиСопоставление.СтрокаИнвойса.SoldTo = ВложенныйЗапрос.LegalEntityParentCompany
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НомераГТД КАК НомераГТД
	|		ПО ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД.Владелец.ГТД.Номер = НомераГТД.НомерГТД
	|			И ЗакрытиеПоставкиСопоставление.СтрокаИнвойса.Наименование = НомераГТД.СтрокаИнвойса
	|ГДЕ
	|	ЗакрытиеПоставкиСопоставление.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД.Владелец.ТНВЭД.DisposableGoods = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД.Владелец.КодТНВЭД,
	|	ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД.Владелец.ТНВЭД,
	|	ЗакрытиеПоставкиСопоставление.СтрокаИнвойса.SoldTo,
	|	ВЫРАЗИТЬ(ЗакрытиеПоставкиСопоставление.ТоварСтрокиГТД.Владелец.ОписаниеТовара КАК СТРОКА(150)),
	|	НомераГТД.НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтоговаяТаблица.SoldTo,
	|	ИтоговаяТаблица.КодТНВЭД,
	|	ИтоговаяТаблица.ТНВЭД,
	|	ИтоговаяТаблица.НаименованиеТовара,
	|	СУММА(СтрокиГТД.ВесНетто) КАК ВесНетто
	|ИЗ
	|	ИтоговаяТаблица КАК ИтоговаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтрокиГТД КАК СтрокиГТД
	|		ПО ИтоговаяТаблица.ТНВЭД = СтрокиГТД.ТНВЭД
	|			И ИтоговаяТаблица.НомерГТД = СтрокиГТД.ГТД.Номер
	|
	|СГРУППИРОВАТЬ ПО
	|	ИтоговаяТаблица.SoldTo,
	|	ИтоговаяТаблица.КодТНВЭД,
	|	ИтоговаяТаблица.НаименованиеТовара,
	|	ИтоговаяТаблица.ТНВЭД";
	
	Запрос.УстановитьПараметр("НомераГТД", НомераГТД.Выгрузить()); 
	Запрос.УстановитьПараметр("ИНН", Отчет.ПодразделениеОрганизации.ИНН);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(Отчет.Период));
	Запрос.УстановитьПараметр("КонецПериода", КонецГода(Отчет.Период));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Функция  ПолучитьДанныеШапки()
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Наименование,
	|	ПодразделенияОрганизаций.НаименованиеПолное,
	|	ПодразделенияОрганизаций.ИНН,
	|	ПодразделенияОрганизаций.КПП,
	|	ПодразделенияОрганизаций.КодОКВЭД,
	|	ПодразделенияОрганизаций.КодПоОКАТО,
	|	ПодразделенияОрганизаций.КодПоОКТМО,
	|	ПодразделенияОрганизаций.ОГРН,
	|	ПодразделенияОрганизаций.СвидетельствоСерияНомер,
	|	ПодразделенияОрганизаций.СвидетельствоНаименованиеОргана,
	|	Адреса.Представление КАК ЮрАдрес,
	|	Телефоны.Представление КАК Телефон
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК Телефоны
	|		ПО ПодразделенияОрганизаций.Ссылка = Телефоны.Объект
	|			И (Телефоны.Вид = ЗНАЧЕНИЕ(справочник.ВидыКонтактнойИнформации.ТелефонОрганизации))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК Адреса
	|		ПО ПодразделенияОрганизаций.Ссылка = Адреса.Объект
	|			И (Адреса.Вид = ЗНАЧЕНИЕ(справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации))
	|ГДЕ
	|	ПодразделенияОрганизаций.Ссылка = &Подразделение";
	
	Запрос.УстановитьПараметр("Подразделение", Отчет.ПодразделениеОрганизации);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции

&НаКлиенте
Функция ПолучитьНомераГТД() 
	
	НомерСтроки = 2;
	НомерГТД = СокрЛП(ТаблицаНомеров.Область("R" + НомерСтроки + "C2").Текст); 
	Пока ЗначениеЗаполнено(НомерГТД) Цикл
	    НоваяСтр = НомераГТД.Добавить();
		НоваяСтр.НомерГТД = НомерГТД;
		// { RGS LFedotova 04.05.2017 21:22:14 - вопрос S-I-0003036
		//СтрокаИнвойса = СокрЛП(ТаблицаНомеров.Область("R" + НомерСтроки + "C1").Текст); 
		СтрокаИнвойса = СокрЛП(ТаблицаНомеров.Область("R" + СтрЗаменить(НомерСтроки,Символы.НПП,"") + "C1").Текст); 
		// } RGS LFedotova 04.05.2017 21:23:47 - вопрос S-I-0003036 
		СтрокаИнвойса = СокрЛП(ТаблицаНомеров.Область("R" + СтрЗаменить(НомерСтроки,Символы.НПП,"") + "C1").Текст); 
		НоваяСтр.СтрокаИнвойса = СтрокаИнвойса;
		НомерСтроки = НомерСтроки + 1;
		// { RGS LFedotova 04.05.2017 21:24:34 - вопрос S-I-0003036
		//НомерГТД = СокрЛП(ТаблицаНомеров.Область("R" + НомерСтроки + "C2").Текст);
		НомерГТД = СокрЛП(ТаблицаНомеров.Область("R" + СтрЗаменить(НомерСтроки,Символы.НПП,"") + "C2").Текст);
		// } RGS LFedotova 04.05.2017 21:24:41 - вопрос S-I-0003036 
	КонецЦикла;
	
КонецФункции



