
Перем СведенияОбОрганизации;

#Если Клиент Тогда
Процедура ОбновитьОтчет(ТабличныйДокумент) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Предупреждение("Не указана организация!");
		Возврат;
	КонецЕсли;
	
	Макет = ПолучитьМакет("Макет");
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЖурналУчетаСчетовФактур";

	Данные = ПолучитьДанныеДляОтчета();
	
	СведенияОрганизации = КонтактнаяИнформация.СведенияОЮрФизЛице(Организация);
	НаименованиеОрганизацииДляПечатныхФорм = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОрганизации, "НаименованиеДляПечатныхФорм,");
	
	Отступ = Макет.ПолучитьОбласть("Отступ");
	
	// ШАПКА
	ШапкаИнформация = Макет.ПолучитьОбласть("ШапкаИнформация");
	//ТабличныйДокумент.Вывести(ШапкаИнформация);
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ЗаполнитьШапкуОтчета(Шапка);
	ТабличныйДокумент.Вывести(Шапка);
	
	ТабличныйДокумент.Вывести(Отступ);
	
	// ЧАСТЬ 1
	
	Часть1Заголовок = Макет.ПолучитьОбласть("Часть1Заголовок");	
	ТабличныйДокумент.Вывести(Часть1Заголовок);
	
	Часть1Строка = Макет.ПолучитьОбласть("Часть1Строка");
	
	СтрокиЧасть1 = Данные.ВыставленныеСчетаФактуры;
	
	НомерПП = 1;
	
	Если НЕ СформироватьОтчетПоСтандартнойФорме И ГруппироватьПоКонтрагентам Тогда
		//Режим построения с группировкой по контрагенту
		СекцияКонтрагента = Макет.ПолучитьОбласть("Контрагент");
		ГруппировкаПоКонтрагенту = СтрокиЧасть1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ГруппировкаПоКонтрагенту.Количество() = 0 Тогда
			ДобавитьПустуюСтроку(Часть1Строка);
			ТабличныйДокумент.Вывести(Часть1Строка);
		Иначе
			ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
			Пока ГруппировкаПоКонтрагенту.Следующий() Цикл
				СекцияКонтрагента.Параметры.Заполнить(ГруппировкаПоКонтрагенту);
				ТабличныйДокумент.Вывести(СекцияКонтрагента, 1);	
				ГруппировкаПоСчетуФактуре = ГруппировкаПоКонтрагенту.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ГруппировкаПоСчетуФактуре.Следующий() Цикл
					ЗаполнитьСтрокуОтчета(Часть1Строка, ГруппировкаПоСчетуФактуре, НаименованиеОрганизацииДляПечатныхФорм);
					ТабличныйДокумент.Вывести(Часть1Строка, 2);
					НомерПП = НомерПП + 1;
				КонецЦикла; 
			КонецЦикла;	
			ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
		КонецЕсли;	
	Иначе
		Выборка = СтрокиЧасть1.Выбрать();
		Если Выборка.Количество() = 0 Тогда
			ДобавитьПустуюСтроку(Часть1Строка);
			ТабличныйДокумент.Вывести(Часть1Строка);
		Иначе
			Пока Выборка.Следующий() Цикл
				ЗаполнитьСтрокуОтчета(Часть1Строка, Выборка, НаименованиеОрганизацииДляПечатныхФорм);
				Часть1Строка.Параметры.Номер = НомерПП;
				ТабличныйДокумент.Вывести(Часть1Строка);
				НомерПП = НомерПП + 1;
			КонецЦикла;
		КонецЕсли;	
	КонецЕсли;	
		
	ТабличныйДокумент.Вывести(Отступ);
	
	// ЧАСТЬ 2
	
	Часть2Заголовок = Макет.ПолучитьОбласть("Часть2Заголовок");	
	ТабличныйДокумент.Вывести(Часть2Заголовок);
	
	Часть2Строка = Макет.ПолучитьОбласть("Часть2Строка");
	
	СтрокиЧасть2 = Данные.ПолученныеСчетаФактуры;
	
    НомерПП = 1;
	
	Если НЕ СформироватьОтчетПоСтандартнойФорме И ГруппироватьПоКонтрагентам Тогда
		//Режим построения с группировкой по контрагенту
		СекцияКонтрагента = Макет.ПолучитьОбласть("Контрагент");
		ГруппировкаПоКонтрагенту = СтрокиЧасть2.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ГруппировкаПоКонтрагенту.Количество() = 0 Тогда
			ДобавитьПустуюСтроку(Часть2Строка);
			ТабличныйДокумент.Вывести(Часть2Строка);
		Иначе
			ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
			Пока ГруппировкаПоКонтрагенту.Следующий() Цикл
				СекцияКонтрагента.Параметры.Заполнить(ГруппировкаПоКонтрагенту);
				ТабличныйДокумент.Вывести(СекцияКонтрагента, 1);	
				ГруппировкаПоСчетуФактуре = ГруппировкаПоКонтрагенту.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ГруппировкаПоСчетуФактуре.Следующий() Цикл
					ЗаполнитьСтрокуОтчета(Часть2Строка, ГруппировкаПоСчетуФактуре, НаименованиеОрганизацииДляПечатныхФорм);
					ТабличныйДокумент.Вывести(Часть2Строка, 2);
					НомерПП = НомерПП + 1;
				КонецЦикла; 
			КонецЦикла;	
			ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
		КонецЕсли;	
	Иначе
		Выборка = СтрокиЧасть2.Выбрать();
		Если Выборка.Количество() = 0 Тогда
			ДобавитьПустуюСтроку(Часть2Строка);
			ТабличныйДокумент.Вывести(Часть2Строка);
		Иначе
			Пока Выборка.Следующий() Цикл
				ЗаполнитьСтрокуОтчета(Часть2Строка, Выборка, НаименованиеОрганизацииДляПечатныхФорм);
				Часть2Строка.Параметры.Номер = НомерПП;
				ТабличныйДокумент.Вывести(Часть2Строка);
				НомерПП = НомерПП + 1;
			КонецЦикла;
		КонецЕсли;	
	КонецЕсли;	

		
	ТабличныйДокумент.Вывести(Отступ);
	
	// ПОДВАЛ
	
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ЗаполнитьПодвалОтчета(Подвал);
	ТабличныйДокумент.Вывести(Подвал);
	
КонецПроцедуры

Функция ПолучитьДанныеДляОтчета()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Квартал", НачалоКвартала(Период));
	Запрос.УстановитьПараметр("Контрагент", КонтрагентДляОтбора);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЖурналУчетаСчетовФактурСрезПоследних.Организация КАК Организация,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактура КАК СчетФактураДокумент,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаВыставленияПолучения КАК ДатаПередачиПолучения,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент КАК Контрагент,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.ИНН КАК КонтрагентИНН,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.КПП КАК КонтрагентКПП,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.НаименованиеПолное, 1, 250) = """"
	|			ТОГДА ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.Наименование
	|		ИНАЧЕ ПОДСТРОКА(ЖурналУчетаСчетовФактурСрезПоследних.Контрагент.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК КонтрагентНаименование,
	|	ЖурналУчетаСчетовФактурСрезПоследних.Валюта.Код КАК Валюта,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуре КАК СуммаДокумента,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДС КАК СуммаНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ЧастьЖурнала КАК ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодВидаОперации КАК КодВидаОперации,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУменьшение КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаПоСчетуФактуреРазницаУвеличение КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшение КАК СуммаНДСРазницаУменьшение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличение КАК СуммаНДСРазницаУвеличение,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС КАК СчетФактураБезНДС,
	|	ЖурналУчетаСчетовФактурСрезПоследних.КодСпособаВыставленияПолучения КАК КодСпособаВыставления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерКорректировочногоСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактурСрезПоследних.НомерИсправления КАК НомерИсправления,
	|	ЖурналУчетаСчетовФактурСрезПоследних.ДатаИсправления КАК ДатаИсправления,
	|	ВЫБОР
	|		КОГДА НЕ ЖурналУчетаСчетовФактурСрезПоследних.ПоСтавкеБезНДС
	|			ТОГДА ВЫБОР
	|					КОГДА ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУвеличение = 0
	|							И ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДСРазницаУменьшение = 0
	|							И ЖурналУчетаСчетовФактурСрезПоследних.СуммаНДС = 0
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетФактураПоСтавке0,
	|	ЖурналУчетаСчетовФактурСрезПоследних.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется
	|ПОМЕСТИТЬ ЗаписиРегистраЖурналУчетаСчетовФактур
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур.СрезПоследних(
	|			&Квартал,
	|			Организация = &Организация
	|				И Период = &Квартал
	|				И ДатаВыставленияПолучения <> ДАТАВРЕМЯ(1, 1, 1)) КАК ЖурналУчетаСчетовФактурСрезПоследних";
	
	Если НЕ СформироватьОтчетПоСтандартнойФорме Тогда
		Если ОтбиратьПоКонтрагенту Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Период = &Квартал", "И Период = &Квартал И Контрагент В ИЕРАРХИИ (&Контрагент)");
		КонецЕсли;
	КонецЕсли;	
	
	Запрос.Выполнить();
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Организация,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураДокумент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодВидаОперации,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаПередачиПолучения КАК ДатаПередачиПолучения,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Контрагент КАК Контрагент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентНаименование КАК КонтрагентНаименование,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Валюта,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокумента КАК СуммаДокумента,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДС КАК СуммаНДС,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураБезНДС,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ЧастьЖурнала,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентИНН,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентКПП,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодСпособаВыставления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерИсправления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаИсправления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураПоСтавке0,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураНеВыставляется
	|ИЗ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур КАК ЗаписиРегистраЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПередачиПолучения";
	
	Если НЕ СформироватьОтчетПоСтандартнойФорме И ГруппироватьПоКонтрагентам Тогда
		Запрос.Текст = Запрос.Текст + "
			|ИТОГИ
			|	СУММА(СуммаДокумента),
			|	СУММА(СуммаНДС),
			| 	МАКСИМУМ(КонтрагентНаименование)
			|ПО
			|	Контрагент";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО", "УПОРЯДОЧИТЬ ПО
			|КонтрагентНаименование, ");	
			
	КонецЕсли;		
			
	ВыставленныеСчетаФактуры = Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Организация,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураДокумент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодВидаОперации,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаПередачиПолучения КАК ДатаПередачиПолучения,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Контрагент КАК Контрагент,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентНаименование КАК КонтрагентНаименование,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.Валюта,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаДокумента КАК СуммаДокумента,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДС КАК СуммаНДС,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураБезНДС,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ЧастьЖурнала,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентИНН,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КонтрагентКПП,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.КодСпособаВыставления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.НомерИсправления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ДатаИсправления,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураПоСтавке0,
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.СчетФактураНеВыставляется
	|ИЗ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур КАК ЗаписиРегистраЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЗаписиРегистраЖурналУчетаСчетовФактур.ЧастьЖурнала = ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПередачиПолучения";
	
	Если НЕ СформироватьОтчетПоСтандартнойФорме И ГруппироватьПоКонтрагентам Тогда
		Запрос.Текст = Запрос.Текст + "
			|ИТОГИ
			|	СУММА(СуммаДокумента),
			|	СУММА(СуммаНДС),
			| 	МАКСИМУМ(КонтрагентНаименование)
			|ПО
			|	Контрагент";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО", "УПОРЯДОЧИТЬ ПО
			|КонтрагентНаименование, ");	
	КонецЕсли;	
	
	ПолученныеСчетаФактуры = Запрос.Выполнить();

	Возврат Новый Структура("ВыставленныеСчетаФактуры, ПолученныеСчетаФактуры", ВыставленныеСчетаФактуры, ПолученныеСчетаФактуры);
	
КонецФункции

Процедура ЗаполнитьШапкуОтчета(Шапка)
	
	СведенияОрганизации = КонтактнаяИнформация.СведенияОЮрФизЛице(Организация);
	Шапка.Параметры.Организация = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОрганизации, "НаименованиеДляПечатныхФорм");
	Шапка.Параметры.ИННКПП = "" + Организация.ИНН + ?(НЕ ЗначениеЗаполнено(Организация.КПП), "", ("/" + Организация.КПП));
	Шапка.Параметры.Квартал = Формат(Период, "ДФ = к");
	Шапка.Параметры.Год = Формат(Период, "ДФ = гггг");
	
КонецПроцедуры

Процедура ЗаполнитьПодвалОтчета(Подвал);
	
	Если СведенияОбОрганизации = Неопределено Тогда
		
		СписокПоказателей = Новый Массив;
		СписокПоказателей.Добавить("ФИОРук");
		СписокПоказателей.Добавить("ФИО");
		СведенияОбОрганизации = РегламентированнаяОтчетностьДляСовместимости.ПолучитьСведенияОбОрганизации(Организация, ТекущаяДата(), СписокПоказателей);
		СведенияОЮрФизЛице = КонтактнаяИнформация.СведенияОЮрФизЛице(Организация, ТекущаяДата());
		СведенияОбОрганизации.Вставить("Свидетельство", ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОЮрФизЛице, "Свидетельство,"));
		
	КонецЕсли;
	
	Подвал.Параметры.ИмяРук = СведенияОбОрганизации.ФИОРук;
	Подвал.Параметры.ИмяОрг = ?(РегламентированнаяОтчетность.ЭтоПБОЮЛ(Организация), СведенияОбОрганизации.ФИО, "");
	Подвал.Параметры.Свидетельство = СведенияОбОрганизации.Свидетельство;

КонецПроцедуры

Процедура ЗаполнитьСтрокуОтчета(СтрокаОтчета, СтрокаДанных, НаименованиеОрганизацииДляПечатныхФорм)
	
	ЗаполнитьЗначенияСвойств(СтрокаОтчета.Параметры, СтрокаДанных);
	
	Если НЕ ЗначениеЗаполнено(СтрокаДанных.КонтрагентИНН) 
		И НЕ ЗначениеЗаполнено(СтрокаДанных.КонтрагентКПП) Тогда
		СтрокаОтчета.Параметры.КонтрагентИННКПП = "";
	Иначе
		Если НЕ ЗначениеЗаполнено(СтрокаДанных.КонтрагентКПП) Тогда
			СтрокаОтчета.Параметры.КонтрагентИННКПП = СтрокаДанных.КонтрагентИНН;
		Иначе
			СтрокаОтчета.Параметры.КонтрагентИННКПП = СтрокаДанных.КонтрагентИНН + "/" + СтрокаДанных.КонтрагентКПП;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаДанных.Контрагент = СтрокаДанных.Организация Тогда
		СтрокаОтчета.Параметры.КонтрагентНаименование = НаименованиеОрганизацииДляПечатныхФорм;
	КонецЕсли;
	
	Если СтрокаДанных.СчетФактураБезНДС Тогда
		
		Если ЗначениеЗаполнено(СтрокаДанных.СуммаПоСчетуФактуреРазницаУменьшение) Тогда
			СтрокаОтчета.Параметры.СуммаНДСРазницаУменьшение = "Без налога (НДС)";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаДанных.СуммаПоСчетуФактуреРазницаУвеличение) Тогда
			СтрокаОтчета.Параметры.СуммаНДСРазницаУвеличение = "Без налога (НДС)";
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(СтрокаДанных.СуммаПоСчетуФактуреРазницаУвеличение) 
			И НЕ ЗначениеЗаполнено(СтрокаДанных.СуммаПоСчетуФактуреРазницаУменьшение) Тогда			
			СтрокаОтчета.Параметры.СуммаНДС = "Без налога (НДС)";
		КонецЕсли;
		
	ИначеЕсли СтрокаДанных.СчетФактураПоСтавке0 Тогда
		
		Если ЗначениеЗаполнено(СтрокаДанных.СуммаПоСчетуФактуреРазницаУменьшение) Тогда
			СтрокаОтчета.Параметры.СуммаНДСРазницаУменьшение = "0";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаДанных.СуммаПоСчетуФактуреРазницаУвеличение) Тогда
			СтрокаОтчета.Параметры.СуммаНДСРазницаУвеличение = "0";
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(СтрокаДанных.СуммаПоСчетуФактуреРазницаУвеличение) 
			И НЕ ЗначениеЗаполнено(СтрокаДанных.СуммаПоСчетуФактуреРазницаУменьшение) Тогда			
			СтрокаОтчета.Параметры.СуммаНДС = "0";
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтрокаДанных.СчетФактураНеВыставляется Тогда
		СтрокаОтчета.Параметры.ДатаПередачиПолучения = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПустуюСтроку(СтрокаОтчета)
	
	Для К = 0 По СтрокаОтчета.Параметры.Количество() - 1 Цикл
		
		СтрокаОтчета.Параметры[К] = "--";
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли