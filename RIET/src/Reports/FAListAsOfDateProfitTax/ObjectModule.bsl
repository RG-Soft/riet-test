
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	//ТаблицаНабораДанных = Новый ТаблицаЗначений;
	ТаблицаНабораДанных = ВернутьТаблицу();
	
	//Связь между таблицей значений и именами в СКД 
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаНабораДанных", ТаблицаНабораДанных);

	//Макет компоновки 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);

	//Компоновка данных 
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);

	//Вывод результата 
	ДокументРезультат.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

Функция ВернутьТаблицу()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОсновныеСредстваLawsonОСФискал.ОсновноеСредство КАК ОсновноеСредство,
		|	ОсновныеСредстваLawsonОСФискал.Ссылка.Код КАК ОСLawsonКод
		|ИЗ
		|	Справочник.ОсновныеСредстваLawson.ОСФискал КАК ОсновныеСредстваLawsonОСФискал
		|ИТОГИ ПО
		|	ОсновноеСредство";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОсновноеСредство = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ОсновноеСредство", Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	ТЗ.Колонки.Добавить("ОСLawsonКод", Новый ОписаниеТипов("Строка"));
	Пока ВыборкаОсновноеСредство.Следующий() Цикл
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.ОсновноеСредство = ВыборкаОсновноеСредство.ОсновноеСредство;
		ВыборкаДетальныеЗаписи = ВыборкаОсновноеСредство.Выбрать();
	    ОСLawsonКод = "";
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ОСLawsonКод = ОСLawsonКод + ВыборкаДетальныеЗаписи.ОСLawsonКод + ", ";
		КонецЦикла;
		Если Прав(ОСLawsonКод,2) = ", " Тогда
		    ОСLawsonКод = Лев(ОСLawsonКод, СтрДлина(ОСLawsonКод) - 2);
		КонецЕсли; 
		НоваяСтрока.ОСLawsonКод = ОСLawsonКод; 
		
	КонецЦикла;
	Возврат ТЗ;
	
КонецФункции