
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Функция ПолучитьТипДиаграммы()
	
	ТекТипДиаграммы = ТипДиаграммы.Гистограмма; 
	
	Если Отчет.Group = "Month" Тогда
		ТекТипДиаграммы = ТипДиаграммы.Гистограмма; 
	ИначеЕсли Отчет.Group = "Total" Тогда
		ТекТипДиаграммы = ТипДиаграммы.ГистограммаОбъемная; 
	КонецЕсли;
	
	Возврат ТекТипДиаграммы;
	
КонецФункции

&НаСервере
Функция ПолучитьТипДиаграммыSum()
	
	ТекТипДиаграммы = ТипДиаграммы.Гистограмма; 
	
	Если Отчет.Group = "Month" Тогда
		ТекТипДиаграммы = ТипДиаграммы.График; 
	ИначеЕсли Отчет.Group = "Total" Тогда
		ТекТипДиаграммы = ТипДиаграммы.ГистограммаОбъемная; 
	КонецЕсли;
	
	Возврат ТекТипДиаграммы;
	
КонецФункции

&НаСервере
Функция ПолучитьВидПодписей()
	
	ТекВидПодписей = ВидПодписейКДиаграмме.Значение; 
	
	Если Отчет.Group = "Month" Тогда
		ТекВидПодписей = ВидПодписейКДиаграмме.Нет; 
	ИначеЕсли Отчет.Group = "Total" Тогда
		ТекВидПодписей = ВидПодписейКДиаграмме.Значение; 
	КонецЕсли;
	
	Возврат ТекВидПодписей;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимость()
	
	Элементы.ParentCompany.Видимость		= Не Элементы.ParentCompany.Видимость;
	Элементы.SourceLocation.Видимость		= Не Элементы.SourceLocation.Видимость;
	Элементы.DestinationLocation.Видимость	= Не Элементы.DestinationLocation.Видимость;
	Элементы.Geomarket.Видимость			= Не Элементы.Geomarket.Видимость;
	Элементы.SubGeomarket.Видимость			= Не Элементы.SubGeomarket.Видимость;
	Элементы.Segment.Видимость				= Не Элементы.Segment.Видимость;
	Элементы.SubSegment.Видимость			= Не Элементы.SubSegment.Видимость;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ВЫВОДА ДИАГРАММ

&НаСервере
Процедура ЗаполнтьУсловияЗапроса(ТекстЗапроса)
	
	Если ЗначениеЗаполнено(Отчет.ParentCompany) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеParentCompany", "ParentCompany = &ParentCompany");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отчет.Geomarket) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеGeomarket", "Geomarket = &Geomarket");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отчет.SubGeomarket) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеSubGeomarket", "SubGeomarket = &SubGeomarket");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отчет.Segment) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеSegment", "Segment = &Segment");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отчет.SubSegment) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеSubSegment", "SubSegment = &SubSegment");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отчет.SourceLocation) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеSourceLocation", "SourceLocation = &SourceLocation");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отчет.DestinationLocation) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеDestinationLocation", "DestinationLocation = &DestinationLocation");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыЗапроса(Запрос)
	
	Запрос.УстановитьПараметр("НачалоПериода",				Новый Граница(НачалоДня(Отчет.НачалоПериода), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонецПериода",				Новый Граница(КонецДня(Отчет.КонецПериода), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ParentCompany",				Отчет.ParentCompany);
	Запрос.УстановитьПараметр("Geomarket",					Отчет.Geomarket);
	Запрос.УстановитьПараметр("SubGeomarket",				Отчет.SubGeomarket);
	Запрос.УстановитьПараметр("Segment",					Отчет.Segment);
	Запрос.УстановитьПараметр("SubSegment",					Отчет.SubSegment);
	Запрос.УстановитьПараметр("SourceLocation",				Отчет.SourceLocation);
	Запрос.УстановитьПараметр("DestinationLocation",		Отчет.DestinationLocation);
	Запрос.УстановитьПараметр("УсловиеParentCompany",		Истина);
	Запрос.УстановитьПараметр("УсловиеGeomarket",			Истина);
	Запрос.УстановитьПараметр("УсловиеSubGeomarket",		Истина);
	Запрос.УстановитьПараметр("УсловиеSegment",				Истина);
	Запрос.УстановитьПараметр("УсловиеSubSegment",			Истина);
	Запрос.УстановитьПараметр("УсловиеSourceLocation",		Истина);
	Запрос.УстановитьПараметр("УсловиеDestinationLocation",	Истина);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекстЗапросаDestinationTotal() 

	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ " + Отчет.TopCount + "
	|	LocalDistributionCostsMilageWeightVolumeОбороты.DestinationLocation КАК DestinationLocation,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.MilageOfParcelОборот КАК Milage,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.WeightОборот КАК Weight,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.SumOfMilageОборот КАК Sum
	|ИЗ
	|	РегистрНакопления.LocalDistributionCostsMilageWeightVolume.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			&УсловиеParentCompany
	|				И &УсловиеGeomarket
	|				И &УсловиеSubGeomarket
	|				И &УсловиеSegment
	|				И &УсловиеSubSegment
	|				И &УсловиеSourceLocation
	|				И &УсловиеDestinationLocation) КАК LocalDistributionCostsMilageWeightVolumeОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	&ТекстПорядка УБЫВ";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПорядка", Отчет.Resources);
	
	ЗаполнтьУсловияЗапроса(ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ПолучитьТекстЗапросаDestinationMonth() 

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	LocalDistributionCostsMilageWeightVolumeОбороты.Период КАК ПериодМесяц,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.DestinationLocation КАК DestinationLocation,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.MilageOfParcelОборот КАК Milage,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.WeightОборот КАК Weight,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.SumOfMilageОборот КАК Sum
	|ИЗ
	|	РегистрНакопления.LocalDistributionCostsMilageWeightVolume.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Месяц,
	|			&УсловиеParentCompany
	|				И &УсловиеGeomarket
	|				И &УсловиеSubGeomarket
	|				И &УсловиеSegment
	|				И &УсловиеSubSegment
	|				И &УсловиеSourceLocation
	|				И DestinationLocation В(&DestinationLocation)) КАК LocalDistributionCostsMilageWeightVolumeОбороты";
	
	ЗаполнтьУсловияЗапроса(ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуМесяцев()
	
	ТаблицаМесяцев = Новый ТаблицаЗначений;
	
	ТаблицаМесяцев.Колонки.Добавить("ПериодМесяц", ОбщегоНазначения.ПолучитьОписаниеТиповДаты(ЧастиДаты.Дата));
	
	ТекДата = Отчет.НачалоПериода;
	Пока ТекДата <= КонецМесяца(Отчет.КонецПериода) Цикл
		
		НоваяСтрока = ТаблицаМесяцев.Добавить();
		
		НоваяСтрока.ПериодМесяц = НачалоМесяца(ТекДата);
		
		ТекДата = ДобавитьМесяц(ТекДата, 1);
		
	КонецЦикла;
	
	Возврат ТаблицаМесяцев;
	
КонецФункции

&НаСервере
Функция ИнициализацияИтогов()
	
	СоответствиеИтогов = Новый Соответствие;
	
	ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
	Для Каждого Строка Из ТаблицаМесяцев Цикл
		СоответствиеИтогов.Вставить(Строка.ПериодМесяц, 0);
	КонецЦикла;
	
	Возврат СоответствиеИтогов;
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуDestinationMonthСАналитикой(ТаблицаDestinationTotal, ТаблицаDestinationMonth)
	
	ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаМесяцев.ПериодМесяц
	|ПОМЕСТИТЬ ТаблицаМесяцев
	|ИЗ
	|	&ТаблицаМесяцев КАК ТаблицаМесяцев
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаTotal.Счетчик,
	|	ТаблицаTotal.DestinationLocation
	|ПОМЕСТИТЬ ТаблицаTotal
	|ИЗ
	|	&ТаблицаTotal КАК ТаблицаTotal
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаMonth.DestinationLocation,
	|	ТаблицаMonth.ПериодМесяц,
	|	ТаблицаMonth.Milage,
	|	ТаблицаMonth.Weight,
	|	ТаблицаMonth.Sum
	|ПОМЕСТИТЬ ТаблицаMonth
	|ИЗ
	|	&ТаблицаMonth КАК ТаблицаMonth
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаTotal.Счетчик,
	|	ТаблицаTotal.DestinationLocation,
	|	ТаблицаМесяцев.ПериодМесяц
	|ПОМЕСТИТЬ ТаблицаАналитики
	|ИЗ
	|	ТаблицаTotal КАК ТаблицаTotal,
	|	ТаблицаМесяцев КАК ТаблицаМесяцев
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАналитики.Счетчик КАК Счетчик,
	|	ТаблицаАналитики.DestinationLocation,
	|	ТаблицаАналитики.ПериодМесяц КАК ПериодМесяц,
	|	ЕСТЬNULL(ТаблицаMonth.Milage, 0) КАК Milage,
	|	ЕСТЬNULL(ТаблицаMonth.Weight, 0) КАК Weight,
	|	ЕСТЬNULL(ТаблицаMonth.Sum, 0) КАК Sum
	|ИЗ
	|	ТаблицаАналитики КАК ТаблицаАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаMonth КАК ТаблицаMonth
	|		ПО ТаблицаАналитики.DestinationLocation = ТаблицаMonth.DestinationLocation
	|			И ТаблицаАналитики.ПериодМесяц = ТаблицаMonth.ПериодМесяц
	|
	|УПОРЯДОЧИТЬ ПО
	|	Счетчик,
	|	ПериодМесяц";
	
	Запрос.УстановитьПараметр("ТаблицаМесяцев",	ТаблицаМесяцев);
	Запрос.УстановитьПараметр("ТаблицаTotal",	ТаблицаDestinationTotal);
	Запрос.УстановитьПараметр("ТаблицаMonth",	ТаблицаDestinationMonth);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуDestination()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = ПолучитьТекстЗапросаDestinationTotal();
	
	УстановитьПараметрыЗапроса(Запрос);
	
	ТаблицаDestinationTotal = Запрос.Выполнить().Выгрузить();
	ТаблицаDestinationTotal.Колонки.Добавить("Счетчик", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(10));
	
	ТекСчетчик = 1;
	Для Каждого СтрокаTotal Из ТаблицаDestinationTotal Цикл
		СтрокаTotal.Счетчик = ТекСчетчик;
		ТекСчетчик = ТекСчетчик + 1;
	КонецЦикла;
	
	Если Отчет.Group = "Month" Тогда
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = ПолучитьТекстЗапросаDestinationMonth();
		
		УстановитьПараметрыЗапроса(Запрос);
		
		Запрос.УстановитьПараметр("DestinationLocation", ТаблицаDestinationTotal.ВыгрузитьКолонку("DestinationLocation"));
		
		ТаблицаDestinationMonth = Запрос.Выполнить().Выгрузить();
		
		ТаблицаDestinationMonth = ПолучитьТаблицуDestinationMonthСАналитикой(ТаблицаDestinationTotal, ТаблицаDestinationMonth);
		
	КонецЕсли;	
		
	Возврат ?(Отчет.Group = "Month", ТаблицаDestinationMonth, ТаблицаDestinationTotal);
	
КонецФункции

&НаСервере
Функция ПолучитьТекстЗапросаSourceTotal(DestinationLocation, Счетчик)
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ " + Отчет.TopCount + "
	|	&ПараметрСчетчик КАК Счетчик,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.SourceLocation КАК SourceLocation,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.DestinationLocation КАК DestinationLocation,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.MilageOfParcelОборот КАК Milage,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.WeightОборот КАК Weight,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.SumOfMilageОборот КАК Sum
	|ИЗ
	|	РегистрНакопления.LocalDistributionCostsMilageWeightVolume.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			&УсловиеParentCompany
	|				И &УсловиеGeomarket
	|				И &УсловиеSubGeomarket
	|				И &УсловиеSegment
	|				И &УсловиеSubSegment
	|				И &УсловиеSourceLocation
	|				И &УсловиеDestinationLocation) КАК LocalDistributionCostsMilageWeightVolumeОбороты";
	
	// Счетчик
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПараметрСчетчик", "&Счетчик" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
	
	// DestinationLocation
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеDestinationLocation", "DestinationLocation = &DestinationLocation" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
	
	ЗаполнтьУсловияЗапроса(ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ПолучитьТекстЗапросаSourceMonth(DestinationLocation, МассивSourceLocation, Счетчик)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	LocalDistributionCostsMilageWeightVolumeОбороты.Период КАК ПериодМесяц,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.SourceLocation КАК SourceLocation,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.DestinationLocation КАК DestinationLocation,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.MilageOfParcelОборот КАК Milage,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.WeightОборот КАК Weight,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.SumOfMilageОборот КАК Sum
	|ИЗ
	|	РегистрНакопления.LocalDistributionCostsMilageWeightVolume.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			МЕсяц,
	|			&УсловиеParentCompany
	|				И &УсловиеGeomarket
	|				И &УсловиеSubGeomarket
	|				И &УсловиеSegment
	|				И &УсловиеSubSegment
	|				И &УсловиеSourceLocation
	|				И &УсловиеDestinationLocation) КАК LocalDistributionCostsMilageWeightVolumeОбороты";
		
	// DestinationLocation
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеDestinationLocation", "DestinationLocation = &DestinationLocation" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
	
	// SourceLocation
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеSourceLocation", "SourceLocation В (&SourceLocation" + Формат(Счетчик, "ЧЦ=2; ЧВН=") + ")");
	
	ЗаполнтьУсловияЗапроса(ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуSourceMonthСАналитикой(ТаблицаSourceTotal, ТаблицаSourceMonth)
	
	ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаМесяцев.ПериодМесяц
	|ПОМЕСТИТЬ ТаблицаМесяцев
	|ИЗ
	|	&ТаблицаМесяцев КАК ТаблицаМесяцев
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаTotal.Счетчик,
	|	ТаблицаTotal.DestinationLocation,
	|	ТаблицаTotal.SourceLocation
	|ПОМЕСТИТЬ ТаблицаTotal
	|ИЗ
	|	&ТаблицаTotal КАК ТаблицаTotal
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаMonth.DestinationLocation,
	|	ТаблицаMonth.SourceLocation,
	|	ТаблицаMonth.ПериодМесяц,
	|	ТаблицаMonth.Milage,
	|	ТаблицаMonth.Weight,
	|	ТаблицаMonth.Sum
	|ПОМЕСТИТЬ ТаблицаMonth
	|ИЗ
	|	&ТаблицаMonth КАК ТаблицаMonth
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаTotal.Счетчик,
	|	ТаблицаTotal.DestinationLocation,
	|	ТаблицаTotal.SourceLocation,
	|	ТаблицаМесяцев.ПериодМесяц
	|ПОМЕСТИТЬ ТаблицаАналитики
	|ИЗ
	|	ТаблицаTotal КАК ТаблицаTotal,
	|	ТаблицаМесяцев КАК ТаблицаМесяцев
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАналитики.Счетчик КАК Счетчик,
	|	ТаблицаАналитики.DestinationLocation,
	|	ТаблицаАналитики.SourceLocation,
	|	ТаблицаАналитики.ПериодМесяц КАК ПериодМесяц,
	|	ЕСТЬNULL(ТаблицаMonth.Milage, 0) КАК Milage,
	|	ЕСТЬNULL(ТаблицаMonth.Weight, 0) КАК Weight,
	|	ЕСТЬNULL(ТаблицаMonth.Sum, 0) КАК Sum
	|ИЗ
	|	ТаблицаАналитики КАК ТаблицаАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаMonth КАК ТаблицаMonth
	|		ПО ТаблицаАналитики.DestinationLocation = ТаблицаMonth.DestinationLocation
	|			И ТаблицаАналитики.SourceLocation = ТаблицаMonth.SourceLocation
	|			И ТаблицаАналитики.ПериодМесяц = ТаблицаMonth.ПериодМесяц
	|
	|УПОРЯДОЧИТЬ ПО
	|	Счетчик,
	|	ПериодМесяц";
	
	Запрос.УстановитьПараметр("ТаблицаМесяцев",	ТаблицаМесяцев);
	Запрос.УстановитьПараметр("ТаблицаTotal",	ТаблицаSourceTotal);
	Запрос.УстановитьПараметр("ТаблицаMonth",	ТаблицаSourceMonth);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуSource(ТаблицаDestination)
	
	Запрос = Новый Запрос;
	
	ПервыйРаз		= Истина;
	ТекстЗапроса	= "";
	
	Для Каждого СтрокаDestination Из ТаблицаDestination Цикл
		
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйРаз, "", Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС) + ПолучитьТекстЗапросаSourceTotal(СтрокаDestination.DestinationLocation, СтрокаDestination.Счетчик);
		
		Запрос.УстановитьПараметр("Счетчик" + Формат(СтрокаDestination.Счетчик, "ЧЦ=2; ЧВН="),				СтрокаDestination.Счетчик);
		Запрос.УстановитьПараметр("DestinationLocation" + Формат(СтрокаDestination.Счетчик, "ЧЦ=2; ЧВН="),	СтрокаDestination.DestinationLocation);
		
		ПервыйРаз	= Ложь;
		
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + "УПОРЯДОЧИТЬ ПО Счетчик, " + Отчет.Resources + " УБЫВ";
	
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьПараметрыЗапроса(Запрос);
		
	ТаблицаSourceTotal = Запрос.Выполнить().Выгрузить();
	
	ТекСчетчик = 1;
	Для Каждого СтрокаTotal Из ТаблицаSourceTotal Цикл
		СтрокаTotal.Счетчик = ТекСчетчик;
		ТекСчетчик = ТекСчетчик + 1;
	КонецЦикла;
	
	Если Отчет.Group = "Month" Тогда
		
		Запрос = Новый Запрос;
		
		ПервыйРаз		= Истина;
		ТекстЗапроса	= "";
		
		Для Каждого СтрокаDestination Из ТаблицаDestination Цикл
			
			МассивSourceLocation = Новый Массив;
			
			МассивСтрокТаблицаSourceTotal = ТаблицаSourceTotal.НайтиСтроки(Новый Структура("DestinationLocation", СтрокаDestination.DestinationLocation));
			Для Каждого СтрокаSource Из МассивСтрокТаблицаSourceTotal Цикл
				МассивSourceLocation.Добавить(СтрокаSource.SourceLocation);
			КонецЦикла;
			
			ТекстЗапроса = ТекстЗапроса + ?(ПервыйРаз, "", Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС) + ПолучитьТекстЗапросаSourceMonth(СтрокаDestination.DestinationLocation, МассивSourceLocation, СтрокаDestination.Счетчик);
			
			Запрос.УстановитьПараметр("DestinationLocation" + Формат(СтрокаDestination.Счетчик, "ЧЦ=2; ЧВН="),	СтрокаDestination.DestinationLocation);
			Запрос.УстановитьПараметр("SourceLocation" + Формат(СтрокаDestination.Счетчик, "ЧЦ=2; ЧВН="),		МассивSourceLocation);
			
			ПервыйРаз	= Ложь;
			
		КонецЦикла;
				
		Запрос.Текст = ТекстЗапроса;
		
		УстановитьПараметрыЗапроса(Запрос);
			
		ТаблицаSourceMonth = Запрос.Выполнить().Выгрузить();
		
		ТаблицаSourceMonth = ПолучитьТаблицуSourceMonthСАналитикой(ТаблицаSourceTotal, ТаблицаSourceMonth);
		
	КонецЕсли;	
	
	Возврат ?(Отчет.Group = "Month", ТаблицаSourceMonth, ТаблицаSourceTotal);
	
КонецФункции

&НаСервере
Функция ПолучитьТекстЗапросаSum(SourceLocation, DestinationLocation, Счетчик)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(LocalDistributionCostsMilageWeightVolumeОбороты.Период, МЕСЯЦ) КАК ПериодМесяц,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.SourceLocation КАК SourceLocation,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.DestinationLocation КАК DestinationLocation,
	|	СУММА(1) КАК TripCount,
	|	СУММА(LocalDistributionCostsMilageWeightVolumeОбороты.MilageOfParcelОборот) КАК Milage,
	|	СУММА(LocalDistributionCostsMilageWeightVolumeОбороты.WeightОборот) КАК Weight,
	|	СУММА(LocalDistributionCostsMilageWeightVolumeОбороты.SumOfMilageОборот) КАК Sum
	|ИЗ
	|	РегистрНакопления.LocalDistributionCostsMilageWeightVolume.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Запись,
	|			&УсловиеParentCompany
	|				И &УсловиеGeomarket
	|				И &УсловиеSubGeomarket
	|				И &УсловиеSegment
	|				И &УсловиеSubSegment
	|				И &УсловиеSourceLocation
	|				И &УсловиеDestinationLocation) КАК LocalDistributionCostsMilageWeightVolumeОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(LocalDistributionCostsMilageWeightVolumeОбороты.Период, МЕСЯЦ),
	|	LocalDistributionCostsMilageWeightVolumeОбороты.SourceLocation,
	|	LocalDistributionCostsMilageWeightVolumeОбороты.DestinationLocation";
	
	Если ЗначениеЗаполнено(SourceLocation) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеSourceLocation", "SourceLocation = &SourceLocation" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(DestinationLocation) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеDestinationLocation", "DestinationLocation = &DestinationLocation" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
	КонецЕсли;	
	
	ЗаполнтьУсловияЗапроса(ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуSumMonthСАналитикой(ТаблицаSource, ТаблицаSum)
	
	ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаМесяцев.ПериодМесяц
	|ПОМЕСТИТЬ ТаблицаМесяцев
	|ИЗ
	|	&ТаблицаМесяцев КАК ТаблицаМесяцев
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаTotal.Счетчик,
	|	ТаблицаTotal.DestinationLocation,
	|	ТаблицаTotal.SourceLocation
	|ПОМЕСТИТЬ ТаблицаTotal
	|ИЗ
	|	&ТаблицаTotal КАК ТаблицаTotal
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаMonth.DestinationLocation,
	|	ТаблицаMonth.SourceLocation,
	|	ТаблицаMonth.ПериодМесяц,
	|	ТаблицаMonth.TripCount,
	|	ТаблицаMonth.Milage,
	|	ТаблицаMonth.Weight,
	|	ТаблицаMonth.Sum
	|ПОМЕСТИТЬ ТаблицаMonth
	|ИЗ
	|	&ТаблицаMonth КАК ТаблицаMonth
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаTotal.Счетчик,
	|	ТаблицаTotal.DestinationLocation,
	|	ТаблицаTotal.SourceLocation,
	|	ТаблицаМесяцев.ПериодМесяц
	|ПОМЕСТИТЬ ТаблицаАналитики
	|ИЗ
	|	ТаблицаTotal КАК ТаблицаTotal,
	|	ТаблицаМесяцев КАК ТаблицаМесяцев
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАналитики.Счетчик КАК Счетчик,
	|	ТаблицаАналитики.DestinationLocation,
	|	ТаблицаАналитики.SourceLocation,
	|	ТаблицаАналитики.ПериодМесяц КАК ПериодМесяц,
	|	ЕСТЬNULL(ТаблицаMonth.TripCount, 0) КАК TripCount,
	|	ЕСТЬNULL(ТаблицаMonth.Milage, 0) КАК Milage,
	|	ЕСТЬNULL(ТаблицаMonth.Weight, 0) КАК Weight,
	|	ЕСТЬNULL(ТаблицаMonth.Sum, 0) КАК Sum
	|ИЗ
	|	ТаблицаАналитики КАК ТаблицаАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаMonth КАК ТаблицаMonth
	|		ПО ТаблицаАналитики.DestinationLocation = ТаблицаMonth.DestinationLocation
	|			И ТаблицаАналитики.SourceLocation = ТаблицаMonth.SourceLocation
	|			И ТаблицаАналитики.ПериодМесяц = ТаблицаMonth.ПериодМесяц
	|
	|УПОРЯДОЧИТЬ ПО
	|	Счетчик,
	|	ПериодМесяц";
	
	Запрос.УстановитьПараметр("ТаблицаМесяцев",	ТаблицаМесяцев);
	Запрос.УстановитьПараметр("ТаблицаTotal",	ТаблицаSource);
	Запрос.УстановитьПараметр("ТаблицаMonth",	ТаблицаSum);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуSum(ТаблицаSource)
	
	Запрос = Новый Запрос;
	
	ПервыйРаз		= Истина;
	ТекстЗапроса	= "";
	
	Для Каждого СтрокаSource Из ТаблицаSource Цикл
		
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйРаз, "", Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС) + ПолучитьТекстЗапросаSum(СтрокаSource.SourceLocation, СтрокаSource.DestinationLocation, СтрокаSource.Счетчик);
		
		Запрос.УстановитьПараметр("SourceLocation" + Формат(СтрокаSource.Счетчик, "ЧЦ=2; ЧВН="),		СтрокаSource.SourceLocation);
		Запрос.УстановитьПараметр("DestinationLocation" + Формат(СтрокаSource.Счетчик, "ЧЦ=2; ЧВН="),	СтрокаSource.DestinationLocation);
		
		ПервыйРаз	= Ложь;
		
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьПараметрыЗапроса(Запрос);
	
	ТаблицаSum = Запрос.Выполнить().Выгрузить();
	
	ТаблицаSum = ПолучитьТаблицуSumMonthСАналитикой(ТаблицаSource, ТаблицаSum);
	
	Если Отчет.Group = "Total" Тогда
		ТаблицаSum.Свернуть("Счетчик, SourceLocation, DestinationLocation", "TripCount, Milage, Weight, Sum");
	КонецЕсли;
	
	Возврат ТаблицаSum;
	
КонецФункции

&НаСервере
Функция ПолучитьПредставлениеРесурса(ТолькоРазмерность = Ложь)
	
	ПредставлениеResources = Отчет.Resources;
	
	Если Отчет.Resources = "Milage" Тогда
		ПредставлениеResources = ?(ТолькоРазмерность, "km", "Milage, km");
	ИначеЕсли Отчет.Resources = "Weight" Тогда
		ПредставлениеResources = ?(ТолькоРазмерность, "kg", "Weight, kg");
	ИначеЕсли Отчет.Resources = "Sum" Тогда
		ПредставлениеResources = ?(ТолькоРазмерность, "1k USD", "Sum, 1k USD");
	КонецЕсли;	
	
	Возврат ПредставлениеResources;
	
КонецФункции

&НаСервере
Процедура ОбновитьДиаграммыОтчета(МакетДиаграмма, ТаблицаDestination, ТаблицаSource, ТаблицаSum)
	
	ЗаголовокДиаграммы = "Top destination location (" + ПолучитьПредставлениеРесурса() + ")";
	
	ОбластьМакета = МакетДиаграмма.ПолучитьОбласть("ДиаграммаСтрока|ДиаграммаСтолбец");
	ОбластьМакета.Параметры.Заголовок = ЗаголовокДиаграммы;
	ОбластьМакета.Параметры.ОсьЗначений = ПолучитьПредставлениеРесурса();
	
	ДиаграммаDestination = ОбластьМакета.Рисунки.Диаграмма.Объект;
	
	ДиаграммаDestination.Очистить();
	
	ДиаграммаDestination.ОбластьЗаголовка.Текст			= ЗаголовокДиаграммы;
	ДиаграммаDestination.ОбластьЗаголовка.ЦветТекста	= WebЦвета.Синий;
	ДиаграммаDestination.ТипДиаграммы					= ПолучитьТипДиаграммы();
	ДиаграммаDestination.ВидПодписей					= ПолучитьВидПодписей();
	ДиаграммаDestination.АвтоУстановкаТекстаСерий		= Ложь;
	ДиаграммаDestination.АвтоУстановкаТекстаТочек		= Ложь;
	ДиаграммаDestination.ОтображатьЛегенду				= Истина;
	ДиаграммаDestination.Обновление						= Ложь;
	
	Для Каждого	Строка Из ТаблицаDestination Цикл
		
		Если Отчет.Group = "Total" Тогда
			Точка = ДиаграммаDestination.УстановитьТочку(ПолучитьПредставлениеРесурса());
		ИначеЕсли Отчет.Group = "Month" Тогда
			Точка = ДиаграммаDestination.УстановитьТочку(Строка.ПериодМесяц);
			Точка.Текст = Формат(Строка.ПериодМесяц, "Л=en; ДФ='MMM yy'");
		КонецЕсли;
		
		Значение = ?(Отчет.Resources = "Sum", Строка.Sum / 1000, Строка[Отчет.Resources]);
		Серия = ДиаграммаDestination.УстановитьСерию(Строка.DestinationLocation);
		ДиаграммаDestination.УстановитьЗначение(Точка, Серия, Формат(Значение, "ЧДЦ=; ЧН=0"));
		 
	КонецЦикла;
	
	ДиаграммаDestination.Обновление	= Истина;
	
	ТабДокументДиаграммы.Вывести(ОбластьМакета);
	
	ТаблицаDestinationКопия = ТаблицаDestination.Скопировать();
	ТаблицаDestinationКопия.Свернуть("DestinationLocation");
	
	Для Каждого СтрокаDestination Из ТаблицаDestinationКопия Цикл
		
		// Диаграмма Source
		ЗаголовокДиаграммы = "Top source for " + СтрокаDestination.DestinationLocation + " (" + ПолучитьПредставлениеРесурса() + ")";
		
		ОбластьМакета = МакетДиаграмма.ПолучитьОбласть("ДиаграммаСтрока|ДиаграммаСтолбец");
		ОбластьМакета.Параметры.Заголовок = ЗаголовокДиаграммы;
		ОбластьМакета.Параметры.ОсьЗначений = ПолучитьПредставлениеРесурса();
		
		ДиаграммаSource = ОбластьМакета.Рисунки.Диаграмма.Объект;
		
		// Заполнение свойств диаграммы
		ДиаграммаSource.ОбластьЗаголовка.Текст		= ЗаголовокДиаграммы;
		ДиаграммаSource.ОбластьЗаголовка.ЦветТекста	= WebЦвета.Синий;
		ДиаграммаSource.ТипДиаграммы				= ПолучитьТипДиаграммы();
		ДиаграммаSource.ВидПодписей					= ПолучитьВидПодписей();
		ДиаграммаSource.АвтоУстановкаТекстаСерий	= Ложь;
		ДиаграммаSource.АвтоУстановкаТекстаТочек	= Ложь;
		ДиаграммаSource.ОтображатьЛегенду			= Истина;
		ДиаграммаSource.Обновление					= Ложь;
				
		СтруктураОтбора = Новый Структура("DestinationLocation", СтрокаDestination.DestinationLocation);
		МассивСтрок = ТаблицаSource.НайтиСтроки(СтруктураОтбора);
		Для Каждого СтрокаSource Из МассивСтрок Цикл
			
			Если Отчет.Group = "Total" Тогда
				Точка = ДиаграммаSource.УстановитьТочку(ПолучитьПредставлениеРесурса());
			ИначеЕсли Отчет.Group = "Month" Тогда
				Точка = ДиаграммаSource.УстановитьТочку(СтрокаSource.ПериодМесяц);
				Точка.Текст = Формат(СтрокаSource.ПериодМесяц, "Л=en; ДФ='MMM yy'");
			КонецЕсли;
			
			Значение = ?(Отчет.Resources = "Sum", СтрокаSource.Sum / 1000, СтрокаSource[Отчет.Resources]);
			Серия = ДиаграммаSource.УстановитьСерию(СтрокаSource.SourceLocation);
			ДиаграммаSource.УстановитьЗначение(Точка, Серия, Формат(Значение, "ЧДЦ=; ЧН=0"));
			
		КонецЦикла;
		
		ДиаграммаSource.Обновление = Истина;
		
		ТабДокументДиаграммы.Вывести(ОбластьМакета);
		
		// Диаграмма Sum
		Если Отчет.Resources <> "Sum" Тогда
		
			ЗаголовокДиаграммы = "Cost per " + ПолучитьПредставлениеРесурса(Истина) + " for " + СтрокаDestination.DestinationLocation;
			
			ОбластьМакета = МакетДиаграмма.ПолучитьОбласть("ДиаграммаСтрока|ДиаграммаСтолбец");
			ОбластьМакета.Параметры.Заголовок = ЗаголовокДиаграммы;
			ОбластьМакета.Параметры.ОсьЗначений = "Cost per " + ПолучитьПредставлениеРесурса(Истина) + ", USD";
			
			ДиаграммаSum = ОбластьМакета.Рисунки.Диаграмма.Объект;
			
			// Заполнение свойств диаграммы
			ДиаграммаSum.ОбластьЗаголовка.Текст			= ЗаголовокДиаграммы;
			ДиаграммаSum.ОбластьЗаголовка.ЦветТекста	= WebЦвета.Синий;
			ДиаграммаSum.ТипДиаграммы					= ПолучитьТипДиаграммыSum();
			ДиаграммаSum.ВидПодписей					= ПолучитьВидПодписей();
			ДиаграммаSum.АвтоУстановкаТекстаСерий		= Ложь;
			ДиаграммаSum.АвтоУстановкаТекстаТочек		= Ложь;
			ДиаграммаSum.ОтображатьЛегенду				= Истина;
			ДиаграммаSum.Обновление						= Ложь;
			
			СтруктураОтбора = Новый Структура("DestinationLocation", СтрокаDestination.DestinationLocation);
			МассивСтрок = ТаблицаSum.НайтиСтроки(СтруктураОтбора);
			Для Каждого СтрокаSum Из МассивСтрок Цикл
						
				Если Отчет.Group = "Total" Тогда
					Точка = ДиаграммаSum.УстановитьТочку("Cost per " + ПолучитьПредставлениеРесурса(Истина));
				ИначеЕсли Отчет.Group = "Month" Тогда
					Точка = ДиаграммаSum.УстановитьТочку(СтрокаSum.ПериодМесяц);
					Точка.Текст = Формат(СтрокаSum.ПериодМесяц, "Л=en; ДФ='MMM yy'");
				КонецЕсли;
				
				Значение = ?(Отчет.Resources = "Sum", СтрокаSum.Sum / 1000, ?(СтрокаSum[Отчет.Resources] = 0, 0, СтрокаSum.Sum / СтрокаSum[Отчет.Resources]));
				Серия = ДиаграммаSum.УстановитьСерию(СтрокаSum.SourceLocation);
				ДиаграммаSum.УстановитьЗначение(Точка, Серия, Формат(Значение, "ЧДЦ=2"));
				
			КонецЦикла;
			
			ДиаграммаSum.Обновление = Истина;
			
			ТабДокументДиаграммы.Присоединить(ОбластьМакета);
			
		КонецЕсли;	
			
		// Диаграмма Trip count
		ЗаголовокДиаграммы = "Trip count for " + СтрокаDestination.DestinationLocation;
		
		ОбластьМакета = МакетДиаграмма.ПолучитьОбласть("ДиаграммаСтрока|ДиаграммаСтолбец");
		ОбластьМакета.Параметры.Заголовок = ЗаголовокДиаграммы;
		ОбластьМакета.Параметры.ОсьЗначений = "Trip count";
		
		ДиаграммаTripCount = ОбластьМакета.Рисунки.Диаграмма.Объект;
		
		// Заполнение свойств диаграммы
		ДиаграммаTripCount.ОбластьЗаголовка.Текст		= ЗаголовокДиаграммы;
		ДиаграммаTripCount.ОбластьЗаголовка.ЦветТекста	= WebЦвета.Синий;
		ДиаграммаTripCount.ТипДиаграммы					= ПолучитьТипДиаграммыSum();
		ДиаграммаTripCount.ВидПодписей					= ПолучитьВидПодписей();
		ДиаграммаTripCount.АвтоУстановкаТекстаСерий		= Ложь;
		ДиаграммаTripCount.АвтоУстановкаТекстаТочек		= Ложь;
		ДиаграммаTripCount.ОтображатьЛегенду			= Истина;
		ДиаграммаTripCount.Обновление					= Ложь;
		
		СтруктураОтбора = Новый Структура("DestinationLocation", СтрокаDestination.DestinationLocation);
		МассивСтрок = ТаблицаSum.НайтиСтроки(СтруктураОтбора);
		Для Каждого СтрокаSum Из МассивСтрок Цикл
			
			Если Отчет.Group = "Total" Тогда
				Точка = ДиаграммаTripCount.УстановитьТочку("Trip count");
			ИначеЕсли Отчет.Group = "Month" Тогда
				Точка = ДиаграммаTripCount.УстановитьТочку(СтрокаSum.ПериодМесяц);
				Точка.Текст = Формат(СтрокаSum.ПериодМесяц, "Л=en; ДФ='MMM yy'");
			КонецЕсли;
			
			Серия = ДиаграммаTripCount.УстановитьСерию(СтрокаSum.SourceLocation);
			ДиаграммаTripCount.УстановитьЗначение(Точка, Серия, СтрокаSum.TripCount);
			
		КонецЦикла;
		
		ДиаграммаTripCount.Обновление = Истина;
		
		ТабДокументДиаграммы.Присоединить(ОбластьМакета);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицыОтчета(МакетТаблица, ТаблицаDestination, ТаблицаSource, ТаблицаSum)
	
	// Заголовок
	ЗаголовокТаблицы = "Top destination location (" + ПолучитьПредставлениеРесурса() + ")";
	
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Заголовок|Data");
	ОбластьМакета.Параметры.ЗаголовокТаблицы = ЗаголовокТаблицы;
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	// Шапка
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Data");
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	Если Отчет.Group = "Month" Тогда
		
		ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
		Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Month");
			ОбластьМакета.Параметры.Заполнить(СтрокаМесяца);
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Total");
	ОбластьМакета.Параметры.Resources = ПолучитьПредставлениеРесурса();
	ТабДокументТаблицы.Присоединить(ОбластьМакета);
	
	ТекDestination	= Неопределено;
	ИтогоПоСтроке	= 0;
	ОбщийИтог		= 0;
	
	СоответствиеИтогов = ИнициализацияИтогов();
	
	Для Каждого Строка Из ТаблицаDestination Цикл
		
		Если ТекDestination <> Строка.DestinationLocation Тогда
			
			Если ТекDestination <> Неопределено Тогда
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Total");
				ОбластьМакета.Параметры.Значение = ИтогоПоСтроке;
				ТабДокументТаблицы.Присоединить(ОбластьМакета);
				
			КонецЕсли;
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Data");
			
			ОбластьМакета.Параметры.Заполнить(Строка);
			
			ОбластьМакета.Параметры.SourceLocation = "All sources";
			
			ТабДокументТаблицы.Вывести(ОбластьМакета);
			
			ТекDestination	= Строка.DestinationLocation;
			ИтогоПоСтроке	= 0;
			
		КонецЕсли;
		
		Если Отчет.Group = "Month" Тогда
			
			Значение = ?(Отчет.Resources = "Sum", Строка.Sum / 1000, Строка[Отчет.Resources]);
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Month");
			ОбластьМакета.Параметры.Значение = Значение;
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
			СоответствиеИтогов[Строка.ПериодМесяц] = СоответствиеИтогов[Строка.ПериодМесяц] + Значение;
			
		КонецЕсли;
		
		Значение = ?(Отчет.Resources = "Sum", Строка.Sum / 1000, Строка[Отчет.Resources]);
		ИтогоПоСтроке = ИтогоПоСтроке + Значение;
		ОбщийИтог = ОбщийИтог + Значение;
		
	КонецЦикла;
	
	Если ТекDestination <> Неопределено Тогда
		
		ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Total");
		ОбластьМакета.Параметры.Значение = ИтогоПоСтроке;
		ТабДокументТаблицы.Присоединить(ОбластьМакета);
		
	КонецЕсли;
	
	// Итоги
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги|Data");
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	Если Отчет.Group = "Month" Тогда
		
		ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
		Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги|Month");
			ОбластьМакета.Параметры.Значение = СоответствиеИтогов[СтрокаМесяца.ПериодМесяц];
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги|Total");
	ОбластьМакета.Параметры.Значение = ОбщийИтог;
	ТабДокументТаблицы.Присоединить(ОбластьМакета);
	
	// Отступ
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Заголовок|Data");
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	// Заголовок
	ЗаголовокТаблицы = "Top source location (" + ПолучитьПредставлениеРесурса() + ")";
	
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Заголовок|Data");
	ОбластьМакета.Параметры.ЗаголовокТаблицы = ЗаголовокТаблицы;
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	// Шапка
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Data");
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	Если Отчет.Group = "Month" Тогда
		
		ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
		Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Month");
			ОбластьМакета.Параметры.Заполнить(СтрокаМесяца);
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Total");
	ОбластьМакета.Параметры.Resources = ПолучитьПредставлениеРесурса();
	ТабДокументТаблицы.Присоединить(ОбластьМакета);
	
	ТекDestination	= Неопределено;
	ТекSource		= Неопределено;
	ИтогоПоСтроке	= 0;
	ОбщийИтог		= 0;
	
	СоответствиеИтогов = ИнициализацияИтогов();
	
	Для Каждого Строка Из ТаблицаSource Цикл
		
		Если ТекDestination <> Строка.DestinationLocation Или ТекSource <> Строка.SourceLocation Тогда
			
			Если ТекDestination <> Неопределено Тогда
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Total");
				ОбластьМакета.Параметры.Значение = ИтогоПоСтроке;
				ТабДокументТаблицы.Присоединить(ОбластьМакета);
				
			КонецЕсли;
			
			// Отступ
			Если ТекDestination <> Строка.DestinationLocation И ТекDestination <> Неопределено Тогда
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Отступ|Data");
				ТабДокументТаблицы.Вывести(ОбластьМакета);
				
				Если Отчет.Group = "Month" Тогда
					
					ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
					Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
						
						ОбластьМакета = МакетТаблица.ПолучитьОбласть("Отступ|Month");
						ТабДокументТаблицы.Присоединить(ОбластьМакета);
						
					КонецЦикла;
					
				КонецЕсли;
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Отступ|Total");
				ТабДокументТаблицы.Присоединить(ОбластьМакета);
				
			КонецЕсли;	
				
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Data");
			ОбластьМакета.Параметры.Заполнить(Строка);
			ТабДокументТаблицы.Вывести(ОбластьМакета);
			
			ТекDestination	= Строка.DestinationLocation;
			ТекSource		= Строка.SourceLocation;
			ИтогоПоСтроке	= 0;
			
		КонецЕсли;
		
		Если Отчет.Group = "Month" Тогда
			
			Значение = ?(Отчет.Resources = "Sum", Строка.Sum / 1000, Строка[Отчет.Resources]);
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Month");
			ОбластьМакета.Параметры.Значение = Значение;
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
			СоответствиеИтогов[Строка.ПериодМесяц] = СоответствиеИтогов[Строка.ПериодМесяц] + Значение;
			
		КонецЕсли;
		
		Значение = ?(Отчет.Resources = "Sum", Строка.Sum / 1000, Строка[Отчет.Resources]);
		ИтогоПоСтроке = ИтогоПоСтроке + Значение;
		ОбщийИтог = ОбщийИтог + Значение;
		
	КонецЦикла;
	
	Если ТекDestination <> Неопределено Тогда
		
		ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Total");
		ОбластьМакета.Параметры.Значение = ИтогоПоСтроке;
		ТабДокументТаблицы.Присоединить(ОбластьМакета);
		
	КонецЕсли;
	
	// Итоги
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги|Data");
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	Если Отчет.Group = "Month" Тогда
		
		ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
		Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги|Month");
			ОбластьМакета.Параметры.Значение = СоответствиеИтогов[СтрокаМесяца.ПериодМесяц];
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги|Total");
	ОбластьМакета.Параметры.Значение = ОбщийИтог;
	ТабДокументТаблицы.Присоединить(ОбластьМакета);
	
	Если Отчет.Resources <> "Sum" Тогда

		// Отступ
		ОбластьМакета = МакетТаблица.ПолучитьОбласть("Заголовок|Data");
		ТабДокументТаблицы.Вывести(ОбластьМакета);
		
		// Заголовок
		ЗаголовокТаблицы = ?(Отчет.Resources = "Sum", "Sum, 1k USD", "Cost per " + ПолучитьПредставлениеРесурса(Истина) + ", USD");
		
		ОбластьМакета = МакетТаблица.ПолучитьОбласть("Заголовок|Data");
		ОбластьМакета.Параметры.ЗаголовокТаблицы = ЗаголовокТаблицы;
		ТабДокументТаблицы.Вывести(ОбластьМакета);
		
		// Шапка
		ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Data");
		ТабДокументТаблицы.Вывести(ОбластьМакета);
		
		Если Отчет.Group = "Month" Тогда
			
			ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
			Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Month");
				ОбластьМакета.Параметры.Заполнить(СтрокаМесяца);
				ТабДокументТаблицы.Присоединить(ОбластьМакета);
				
			КонецЦикла;
			
		КонецЕсли;
		
		ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Total");
		ОбластьМакета.Параметры.Resources = ?(Отчет.Resources = "Sum", "Sum, 1k USD", "Cost per " + ПолучитьПредставлениеРесурса(Истина));
		ТабДокументТаблицы.Присоединить(ОбластьМакета);
		
		ТекDestination		= Неопределено;
		ТекSource			= Неопределено;
		ИтогоSumПоСтроке	= 0;
		ИтогоПоСтроке		= 0;
		ОбщийИтог			= 0;
		ОбщийИтогSum		= 0;
		
		СоответствиеИтогов = ИнициализацияИтогов();
		СоответствиеИтоговSum = ИнициализацияИтогов();
		
		Для Каждого Строка Из ТаблицаSum Цикл
			
			Если ТекDestination <> Строка.DestinationLocation Или ТекSource <> Строка.SourceLocation Тогда
				
				Если ТекDestination <> Неопределено Тогда
					
					Значение = ?(Отчет.Resources = "Sum", ИтогоSumПоСтроке / 1000, ?(ИтогоПоСтроке = 0, 0, ИтогоSumПоСтроке / ИтогоПоСтроке));
					
					ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка2|Total");
					ОбластьМакета.Параметры.Значение = Значение;
					ТабДокументТаблицы.Присоединить(ОбластьМакета);
					
				КонецЕсли;
				
				// Отступ
				Если ТекDestination <> Строка.DestinationLocation И ТекDestination <> Неопределено Тогда
					
					ОбластьМакета = МакетТаблица.ПолучитьОбласть("Отступ|Data");
					ТабДокументТаблицы.Вывести(ОбластьМакета);
					
					Если Отчет.Group = "Month" Тогда
						
						ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
						Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
							
							ОбластьМакета = МакетТаблица.ПолучитьОбласть("Отступ|Month");
							ТабДокументТаблицы.Присоединить(ОбластьМакета);
							
						КонецЦикла;
						
					КонецЕсли;
					
					ОбластьМакета = МакетТаблица.ПолучитьОбласть("Отступ|Total");
					ТабДокументТаблицы.Присоединить(ОбластьМакета);
					
				КонецЕсли;	
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка2|Data");
				ОбластьМакета.Параметры.Заполнить(Строка);
				ТабДокументТаблицы.Вывести(ОбластьМакета);
				
				ТекDestination		= Строка.DestinationLocation;
				ТекSource			= Строка.SourceLocation;
				ИтогоSumПоСтроке	= 0;
				ИтогоПоСтроке		= 0;
				
			КонецЕсли;
			
			Если Отчет.Group = "Month" Тогда
				
				Значение = ?(Отчет.Resources = "Sum", Строка.Sum / 1000, ?(Строка[Отчет.Resources] = 0, 0, Строка.Sum / Строка[Отчет.Resources]));
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка2|Month");
				ОбластьМакета.Параметры.Значение = Значение;
				ТабДокументТаблицы.Присоединить(ОбластьМакета);
				
				СоответствиеИтогов[Строка.ПериодМесяц] = СоответствиеИтогов[Строка.ПериодМесяц] + Строка[Отчет.Resources];
				СоответствиеИтоговSum[Строка.ПериодМесяц] = СоответствиеИтоговSum[Строка.ПериодМесяц] + Строка.Sum;
				
			КонецЕсли;
			
			ИтогоSumПоСтроке	= ИтогоSumПоСтроке + Строка.Sum;
			ИтогоПоСтроке		= ИтогоПоСтроке + Строка[Отчет.Resources];
			ОбщийИтог			= ОбщийИтог + Строка[Отчет.Resources];
			ОбщийИтогSum		= ОбщийИтогSum + Строка.Sum;
			
		КонецЦикла;
		
		Если ТекDestination <> Неопределено Тогда
			
			Значение = ?(Отчет.Resources = "Sum", ИтогоSumПоСтроке / 1000, ?(ИтогоПоСтроке = 0, 0, ИтогоSumПоСтроке / ИтогоПоСтроке));
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка2|Total");
			ОбластьМакета.Параметры.Значение = Значение;
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
		КонецЕсли;
		
		// Итоги
		ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги2|Data");
		ТабДокументТаблицы.Вывести(ОбластьМакета);
		
		Если Отчет.Group = "Month" Тогда
			
			ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
			Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
				
				Значение = ?(Отчет.Resources = "Sum", СоответствиеИтоговSum[СтрокаМесяца.ПериодМесяц] / 1000, ?(СоответствиеИтогов[СтрокаМесяца.ПериодМесяц] = 0, 0, СоответствиеИтоговSum[СтрокаМесяца.ПериодМесяц] / СоответствиеИтогов[СтрокаМесяца.ПериодМесяц]));
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги2|Month");
				ОбластьМакета.Параметры.Значение = Значение;
				ТабДокументТаблицы.Присоединить(ОбластьМакета);
				
			КонецЦикла;
			
		КонецЕсли;
		
		Значение = ?(Отчет.Resources = "Sum", ОбщийИтогSum / 1000, ?(ОбщийИтог = 0, 0, ОбщийИтогSum / ОбщийИтог));
		
		ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги2|Total");
		ОбластьМакета.Параметры.Значение = Значение;
		ТабДокументТаблицы.Присоединить(ОбластьМакета);
		
	КонецЕсли;	
		
	// Отступ
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Заголовок|Data");
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	// Заголовок
	ЗаголовокТаблицы = "Trip count";
	
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Заголовок|Data");
	ОбластьМакета.Параметры.ЗаголовокТаблицы = ЗаголовокТаблицы;
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	// Шапка
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Data");
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	Если Отчет.Group = "Month" Тогда
		
		ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
		Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Month");
			ОбластьМакета.Параметры.Заполнить(СтрокаМесяца);
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Шапка|Total");
	ОбластьМакета.Параметры.Resources = "Trip count";
	ТабДокументТаблицы.Присоединить(ОбластьМакета);
	
	ТекDestination	= Неопределено;
	ТекSource		= Неопределено;
	ИтогоПоСтроке	= 0;
	ОбщийИтог		= 0;
	
	СоответствиеИтогов = ИнициализацияИтогов();
	
	Для Каждого Строка Из ТаблицаSum Цикл
		
		Если ТекDestination <> Строка.DestinationLocation Или ТекSource <> Строка.SourceLocation Тогда
			
			Если ТекDestination <> Неопределено Тогда
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Total");
				ОбластьМакета.Параметры.Значение = ИтогоПоСтроке;
				ТабДокументТаблицы.Присоединить(ОбластьМакета);
				
			КонецЕсли;
			
			// Отступ
			Если ТекDestination <> Строка.DestinationLocation И ТекDestination <> Неопределено Тогда
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Отступ|Data");
				ТабДокументТаблицы.Вывести(ОбластьМакета);
				
				Если Отчет.Group = "Month" Тогда
					
					ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
					Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
						
						ОбластьМакета = МакетТаблица.ПолучитьОбласть("Отступ|Month");
						ТабДокументТаблицы.Присоединить(ОбластьМакета);
						
					КонецЦикла;
					
				КонецЕсли;
				
				ОбластьМакета = МакетТаблица.ПолучитьОбласть("Отступ|Total");
				ТабДокументТаблицы.Присоединить(ОбластьМакета);
				
			КонецЕсли;	
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Data");
			ОбластьМакета.Параметры.Заполнить(Строка);
			ТабДокументТаблицы.Вывести(ОбластьМакета);
			
			ТекDestination	= Строка.DestinationLocation;
			ТекSource		= Строка.SourceLocation;
			ИтогоПоСтроке	= 0;
			
		КонецЕсли;
		
		Если Отчет.Group = "Month" Тогда
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Month");
			ОбластьМакета.Параметры.Значение = Строка.TripCount;
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
			СоответствиеИтогов[Строка.ПериодМесяц] = СоответствиеИтогов[Строка.ПериодМесяц] + Строка.TripCount;
			
		КонецЕсли;
		
		ИтогоПоСтроке = ИтогоПоСтроке + Строка.TripCount;
		ОбщийИтог = ОбщийИтог + Строка.TripCount;
		
	КонецЦикла;
	
	Если ТекDestination <> Неопределено Тогда
		
		ОбластьМакета = МакетТаблица.ПолучитьОбласть("Строка|Total");
		ОбластьМакета.Параметры.Значение = ИтогоПоСтроке;
		ТабДокументТаблицы.Присоединить(ОбластьМакета);
		
	КонецЕсли;
	
	// Итоги
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги|Data");
	ТабДокументТаблицы.Вывести(ОбластьМакета);
	
	Если Отчет.Group = "Month" Тогда
		
		ТаблицаМесяцев = ПолучитьТаблицуМесяцев();
		Для Каждого СтрокаМесяца Из ТаблицаМесяцев Цикл
			
			ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги|Month");
			ОбластьМакета.Параметры.Значение = СоответствиеИтогов[СтрокаМесяца.ПериодМесяц];
			ТабДокументТаблицы.Присоединить(ОбластьМакета);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбластьМакета = МакетТаблица.ПолучитьОбласть("Итоги|Total");
	ОбластьМакета.Параметры.Значение = ОбщийИтог;
	ТабДокументТаблицы.Присоединить(ОбластьМакета);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтчет()
	
	ТабДокументДиаграммы.Очистить();
	ТабДокументТаблицы.Очистить();
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	МакетДиаграмма = ОтчетОбъект.ПолучитьМакет("МакетДиаграмма");
	МакетТаблица = ОтчетОбъект.ПолучитьМакет("МакетТаблица");
	
	// Диаграмма Destination
	ТаблицаDestination = ПолучитьТаблицуDestination();
	
	// Диаграммы Source и Sum
	ТаблицаКопия	= ТаблицаDestination.Скопировать();
	ТаблицаКопия.Свернуть("Счетчик, DestinationLocation");
	ТаблицаSource	= ПолучитьТаблицуSource(ТаблицаКопия);
	
	ТаблицаКопия	= ТаблицаSource.Скопировать();
	ТаблицаКопия.Свернуть("Счетчик, DestinationLocation, SourceLocation");
	ТаблицаSum		= ПолучитьТаблицуSum(ТаблицаКопия);
	
	ОбновитьДиаграммыОтчета(МакетДиаграмма, ТаблицаDestination, ТаблицаSource, ТаблицаSum);
	
	ОбновитьТаблицыОтчета(МакетТаблица, ТаблицаDestination, ТаблицаSource, ТаблицаSum);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отчет.TopCount		= 10;
	Отчет.НачалоПериода	= НачалоГода(ТекущаяДата());
	Отчет.КонецПериода	= КонецМесяца(ТекущаяДата());
	Отчет.Resources		= "Weight";
	Отчет.Group			= "Month";
	
	СтандартныйПериод.Вариант		= ВариантСтандартногоПериода.ПроизвольныйПериод;
	СтандартныйПериод.ДатаНачала	= НачалоГода(ТекущаяДата());
	СтандартныйПериод.ДатаОкончания	= КонецМесяца(ТекущаяДата());
	
	ОбновитьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ РЕКВИЗИТОВ ФОРМЫ

&НаКлиенте
Процедура СтандартныйПериодПриИзменении(Элемент)
	
	Отчет.НачалоПериода	= СтандартныйПериод.ДатаНачала;
	Отчет.КонецПериода	= СтандартныйПериод.ДатаОкончания;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНЫХ ПАНЕЛЕЙ

&НаКлиенте
Процедура КомандаСформироватьНажатие(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		ОбновитьОтчет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоказатьФильтрыНажатие(Команда)
	
	УстановитьВидимость();
	
КонецПроцедуры
