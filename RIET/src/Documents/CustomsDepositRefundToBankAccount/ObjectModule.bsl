
//////////////////////////////////////////////////////////////////////
// ОБРАБОТКА ЗАПОЛНЕНИЯ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.CustomsFilesLight") Тогда
		
		CustomsFileLightTypeOfTransaction = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "TypeOfTransaction");
		Если CustomsFileLightTypeOfTransaction = Перечисления.TypesOfCustomsFileLightTransaction.CustomsBond Тогда		
			CustomsBond = ДанныеЗаполнения;
			
		ИначеЕсли CustomsFileLightTypeOfTransaction = Перечисления.TypesOfCustomsFileLightTransaction.CustomsBondClosing Тогда	
			CustomsBond = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "CustomsBond");
			
		Иначе
			Сообщить("Type of transaction '" + CustomsFileLightTypeOfTransaction + "' is not supported!");
			
		КонецЕсли;
				
	КонецЕсли;
		
КонецПроцедуры


//////////////////////////////////////////////////////////////////////
// ПРИ КОПИРОВАНИИ

Процедура ПриКопировании(ОбъектКопирования)
	
	РГСофт.ОчиститьCreationModification(ЭтотОбъект);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////
// ПЕРЕД ЗАПИСЬЮ      

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизиты();
	
	ПроверитьРеквизиты(Отказ, РежимЗаписи);
		
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////

Процедура ДозаполнитьРеквизиты()
	
	Comment = СокрЛП(Comment);
	
	Если ЭтоНовый() Тогда
		РГСофт.ЗаполнитьCreation(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры 


///////////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьРеквизиты(Отказ, РежимЗаписи)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Date' is empty!",
			ЭтотОбъект, "Дата", , Отказ);
	КонецЕсли;
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(CustomsBond) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Customs bond' is empty!",
			ЭтотОбъект, "CustomsBond", , Отказ);
			
	Иначе
		РеквизитыCustomsBond = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(CustomsBond, "Дата, Проведен, TypeOfTransaction");
		
		Если ЗначениеЗаполнено(Дата) И РеквизитыCustomsBond.Дата > Дата Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Date of Customs bond " + Формат(РеквизитыCustomsBond.Дата, "ДЛФ=D") + " is after date of the current document!",
				ЭтотОбъект, "CustomsBond", , Отказ);		
		КонецЕсли;
		
		Если НЕ РеквизитыCustomsBond.Проведен Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Customs bond is not posted!",
				CustomsBond, , , Отказ);	
		КонецЕсли;
		
		Если РеквизитыCustomsBond.TypeOfTransaction <> Перечисления.TypesOfCustomsFileLightTransaction.CustomsBond Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"" + CustomsBond + " is not a customs bond (it is " + РеквизитыCustomsBond.TypeOfTransaction + ")!",
				ЭтотОбъект, "CustomsBond", , Отказ);	
		КонецЕсли;
		
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(Sum) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Sum' is empty!",
			ЭтотОбъект, "Sum", , Отказ);
	КонецЕсли;
		        		
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА ПРОВЕДЕНИЯ 

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.CustomsDepositsToRefund.Очистить();
	Движения.CustomsDepositsToRefund.Записывать = Истина;
		
	Движения.CustomsDepositsToRefund.ДобавитьЗапись(
		ВидДвиженияНакопления.Расход,
		Дата,
		CustomsBond,
		Sum);
		
	// запишем движение, чтобы проверить остатки
	Движения.CustomsDepositsToRefund.Записать();

	//проверим, что нет отрицательных остатков по депозиту	
	Граница = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	SumОстаток = РегистрыНакопления.CustomsDepositsToRefund.ПолучитьОстатокПоCustomsBond(Граница, CustomsBond);
	Если SumОстаток < 0 Тогда 
		Сообщить("The returned amount exceedes customs deposit to return amount by " + (-SumОстаток) + "!");
		Отказ = Истина;	
	КонецЕсли;

КонецПроцедуры
