
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВложенныйЗапрос.Реализация,
	               |	ВложенныйЗапрос.Номер КАК НомерСФ
	               |ПОМЕСТИТЬ РеализацииПоСегменту
	               |ИЗ
	               |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |		ВложенныйЗапрос.Реализация КАК Реализация,
	               |		СчетФактураВыданный.Номер КАК Номер
	               |	ИЗ
	               |		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |			РеализацияТоваровУслугТовары.КостЦентр КАК AU,
	               |			РеализацияТоваровУслугТовары.Ссылка КАК Реализация
	               |		ИЗ
	               |			Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	               |		ГДЕ
	               |			(НЕ РеализацияТоваровУслугТовары.Ссылка.ПометкаУдаления)
	               |		
	               |		ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |		ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |			РеализацияТоваровУслугУслуги.КостЦентр,
	               |			РеализацияТоваровУслугУслуги.Ссылка
	               |		ИЗ
	               |			Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	               |		ГДЕ
	               |			(НЕ РеализацияТоваровУслугУслуги.Ссылка.ПометкаУдаления)) КАК ВложенныйЗапрос
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
	               |			ПО ВложенныйЗапрос.Реализация = СчетФактураВыданный.ДокументОснование
	               |	ГДЕ
	               |		ВложенныйЗапрос.Реализация.Дата > &Дата
	               |		И ВложенныйЗапрос.AU = &AU) КАК ВложенныйЗапрос
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	РеализацииПоСегменту.Реализация,
	               |	НомераИнвойсовLawson.Тикет.SiebelOrder,
	               |	РеализацииПоСегменту.НомерСФ
	               |ПОМЕСТИТЬ SOРеализаций
	               |ИЗ
	               |	РеализацииПоСегменту КАК РеализацииПоСегменту
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НомераИнвойсовLawson КАК НомераИнвойсовLawson
	               |		ПО РеализацииПоСегменту.Реализация = НомераИнвойсовLawson.Документ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	SiebelOrdersDetails.Ссылка,
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ SiebelOrdersDetails.НомерСтроки) КАК НомерСтроки,
	               |	SOРеализаций.Реализация,
	               |	SiebelOrdersDetails.PartNumber,
	               |	SiebelOrdersDetails.Количество,
	               |	SiebelOrdersDetails.ЕдиницаИзмерения КАК UOM,
	               |	SiebelOrdersDetails.Сумма
	               |ПОМЕСТИТЬ Ордеры
	               |ИЗ
	               |	Документ.SiebelOrders.Details КАК SiebelOrdersDetails
	               |		ЛЕВОЕ СОЕДИНЕНИЕ SOРеализаций КАК SOРеализаций
	               |		ПО SiebelOrdersDetails.Ссылка = SOРеализаций.ТикетSiebelOrder
	               |ГДЕ
	               |	SiebelOrdersDetails.Ссылка В
	               |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |				SOРеализаций.ТикетSiebelOrder КАК ТикетSiebelOrder
	               |			ИЗ
	               |				SOРеализаций КАК SOРеализаций)
	               |	И SiebelOrdersDetails.PartNumber В(&МассивКодов)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	SiebelOrdersDetails.Ссылка,
	               |	SOРеализаций.Реализация,
	               |	SiebelOrdersDetails.PartNumber,
	               |	SiebelOrdersDetails.Количество,
	               |	SiebelOrdersDetails.ЕдиницаИзмерения,
	               |	SiebelOrdersDetails.Сумма
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацииПоСегменту.Реализация,
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ InventoryСписание.Ссылка) КАК Использований
	               |ПОМЕСТИТЬ Привязано
	               |ИЗ
	               |	РеализацииПоСегменту КАК РеализацииПоСегменту
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.InventoryСписание КАК InventoryСписание
	               |		ПО (InventoryСписание.Реализация = РеализацииПоСегменту.Реализация)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РеализацииПоСегменту.Реализация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	SOРеализаций.Реализация,
	               |	SOРеализаций.Реализация.Контрагент КАК Контрагент,
	               |	КОЛИЧЕСТВО(Ордеры.НомерСтроки) КАК КоличествоСовпадений,
	               |	SOРеализаций.НомерСФ,
	               |	SOРеализаций.ТикетSiebelOrder.LawsonInvoice КАК LawsonInvoice,
	               |	Привязано.Использований
	               |ИЗ
	               |	SOРеализаций КАК SOРеализаций
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Ордеры КАК Ордеры
	               |		ПО SOРеализаций.ТикетSiebelOrder = Ордеры.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Привязано КАК Привязано
	               |		ПО SOРеализаций.Реализация = Привязано.Реализация
	               |ГДЕ
	               |	SOРеализаций.ТикетSiebelOrder В
	               |			(ВЫБРАТЬ
	               |				Ордеры.Ссылка КАК Ссылка
	               |			ИЗ
	               |				Ордеры КАК Ордеры)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	SOРеализаций.Реализация,
	               |	SOРеализаций.Реализация.Контрагент,
	               |	SOРеализаций.НомерСФ,
	               |	SOРеализаций.ТикетSiebelOrder.LawsonInvoice,
	               |	Привязано.Использований
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	SOРеализаций.Реализация.Дата УБЫВ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Ордеры.Ссылка,
	               |	Ордеры.Реализация,
	               |	Ордеры.PartNumber,
	               |	Ордеры.Количество,
	               |	Ордеры.UOM,
	               |	Ордеры.Сумма
	               |ИЗ
	               |	Ордеры КАК Ордеры";
	Запрос.УстановитьПараметр("МассивКодов", Параметры.МассивКодов);
	Запрос.УстановитьПараметр("Сегмент", Параметры.Сегмент);
	Запрос.УстановитьПараметр("Дата", Параметры.Дата);
	Запрос.УстановитьПараметр("AU", Параметры.AU);
	
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[4].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = СписокРеализаций.Добавить();
		СтрокаТЧ.Реализация = Выборка.Реализация;
		СтрокаТЧ.НомерСФ = Выборка.НомерСФ;
		СтрокаТЧ.LawsonInvoice = Выборка.LawsonInvoice;
		СтрокаТЧ.КоличествоСовпадений = Выборка.КоличествоСовпадений;
		СтрокаТЧ.Контрагент = Выборка.Контрагент;
		СтрокаТЧ.Использований = Выборка.Использований;
	КонецЦикла;
	
	Детали.Загрузить(Результат[5].Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРеализацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Элемент.ДанныеСтроки(ВыбраннаяСтрока).Реализация);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРеализацию(Команда)
	Данные = ЭтаФорма.Элементы.СписокРеализаций.ТекущиеДанные;
	Если Не Данные = Неопределено Тогда
		ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", Новый Структура("Ключ", Данные.Реализация));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокРеализацийПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда 
		Элементы.Таблица1.ОтборСтрок = Новый ФиксированнаяСтруктура("Реализация",Элемент.ТекущиеДанные.Реализация);
	КонецЕсли;
	
КонецПроцедуры
