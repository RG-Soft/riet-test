
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДанныеПрисоединенногоФайла = ПолучитьДанныеПрисоединенногоФайлаПакетаНаСервере(ПараметрКоманды);
	
	Если НЕ ДанныеПрисоединенногоФайла.СтатусПакета = ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Неизвестный")
		И ДанныеПрисоединенногоФайла.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском")
		И ЗначениеЗаполнено(ДанныеПрисоединенногоФайла.ОтправительЭД)
		И НЕ СтрНайти(ДанныеПрисоединенногоФайла.ОтправительЭД, "2AL") > 0 Тогда
		
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьДанныеФайла(ДанныеПрисоединенногоФайла.ПрисоединенныйФайл,
		ПараметрыВыполненияКоманды.Источник.УникальныйИдентификатор);
	
	ДанныеФайлаДляСохранения = Новый Структура;
	ДанныеФайлаДляСохранения.Вставить("Расширение", ДанныеФайла.Расширение);
	ДанныеФайлаДляСохранения.Вставить("ПолноеНаименование", ДанныеФайла.Наименование);
	ДанныеФайлаДляСохранения.Вставить("АдресХранилища", ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	
	ЭлектронныеДокументыКлиент.СохранитьКак(ДанныеФайлаДляСохранения);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеПрисоединенногоФайлаПакетаНаСервере(ПакетЭД)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЭДПрисоединенныеФайлы.Ссылка,
	|	ЭДПрисоединенныеФайлы.ВладелецФайла.СпособОбменаЭД КАК СпособОбменаЭД,
	|	ЭДПрисоединенныеФайлы.ВладелецФайла.Отправитель КАК Отправитель,
	|	ЭДПрисоединенныеФайлы.ВладелецФайла.СтатусПакета КАК СтатусПакета
	|ИЗ
	|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|ГДЕ
	|	ЭДПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла";
	
	Запрос.УстановитьПараметр("ВладелецФайла", ПакетЭД);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	РезультатЗапроса.Следующий();
	
	СтруктураДанныхПакета = Новый Структура;
	СтруктураДанныхПакета.Вставить("ПрисоединенныйФайл", РезультатЗапроса.Ссылка);
	СтруктураДанныхПакета.Вставить("ОтправительЭД",      РезультатЗапроса.Отправитель);
	СтруктураДанныхПакета.Вставить("СтатусПакета",       РезультатЗапроса.СтатусПакета);
	СтруктураДанныхПакета.Вставить("СпособОбменаЭД",     РезультатЗапроса.СпособОбменаЭД);
	
	Возврат СтруктураДанныхПакета;
	
КонецФункции
