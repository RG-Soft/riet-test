&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	// { РГ-Софт Пахоменков А. 19.01.2015 ПроверкаКонтрагентов
	ЗапуститьПроверкуКонтрагентовПриСозданииНаСервере();
	// } РГ-Софт Пахоменков А. 19.01.2015
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	//ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
	// { РГ-Софт Пахоменков А. 19.01.2015 ПроверкаКонтрагентов
	ПроверкаКонтрагентов.ПриЧтенииНаСервере(ЭтаФорма);
	// } РГ-Софт Пахоменков А. 19.01.2015
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// { РГ-Софт Пахоменков А. 19.01.2015 ПроверкаКонтрагентов
	Если ИспользованиеПроверкиВозможно Тогда
		ПроверкаКонтрагентов.СохранитьРезультатПроверкиКонтрагентовПослеЗаписи(ЭтаФорма);
	КонецЕсли;
	// } РГ-Софт Пахоменков А. 19.01.2015
	
	УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	// { РГ-Софт Пахоменков А. 19.01.2015 ПроверкаКонтрагентов
	ПроверкаКонтрагентовКлиент.СохранитьРезультатПроверкиКонтрагентовПриЗакрытии(ЭтаФорма);
	// } РГ-Софт Пахоменков А. 19.01.2015
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// { РГ-Софт Пахоменков А. 19.01.2015 ПроверкаКонтрагентов
	ПроверитьКонтрагентовПриНаступленииСобытия(ИмяСобытия, Параметр, Источник);
	// } РГ-Софт Пахоменков А. 19.01.2015
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	ПолучитьЗаголовок();
	
	// Если открыли данную форму из формы документа, то там надо поменять текст
	Если НЕ ВладелецФормы = Неопределено Тогда
		
		//изменила Наталья Петроченко 08.10.2012
		Если ТипЗнч(ВладелецФормы) = Тип("УправляемаяФорма") тогда

			// Обновляем информацию о счете-фактуре в открытых формах документов-оснований
			ДокументыОснования = Новый Массив;
			Для Каждого СтрокаТЧ Из Объект.ДокументыОснования Цикл
				ДокументыОснования.Добавить(СтрокаТЧ.ДокументОснование);
			КонецЦикла;
			ПараметрыЗаписи.Вставить("ДокументыОснования", ДокументыОснования);
			Оповестить("Запись_СчетФактураВыданный", ПараметрыЗаписи, Объект.Ссылка);
						
		иначе
			
			// Надо поменять текст про счет-фактуру в форме-владельце
			//-> RG-Soft VIVanov 01/08/12
			Если НЕ ВладелецФормы.Имя = "Список"  Тогда  //добавила условие Федотова Л, РГ-Софт, 14.03.13, вопрос SLI-0003402 
				Если ТипЗнч(ВладелецФормы.Ссылка) = тип("ДокументСсылка.СчетКнигиПокупок") тогда
					ВладелецФормы.ЗаполнитьТекстПроСчетФактуру(Объект.Ссылка);
				//<-
				Иначе
					ВладелецФормы.ЗаполнитьТекстПроСчетФактуру();
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если НЕ (УчетНДС.ПолучитьВерсиюПостановления(Объект.Дата) = 2) Тогда
		Объект.Исправление				= Ложь;
		Объект.НомерИсправления			= 0;
		Объект.НомерИсходногоДокумента	= "";
		Объект.ДатаИсходногоДокумента	= '00010101';
	КонецЕсли;
	
	Объект.КодВидаОперации	= ПолучитьКодВидаОперации();
	
	УправлениеФормой(ЭтаФорма);
	
	// { РГ-Софт Пахоменков А. 19.01.2015 ПроверкаКонтрагентов
	ЗапуститьПроверкуКонтрагентов();
	// } РГ-Софт Пахоменков А. 19.01.2015	
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("Организация",        Объект.Организация);
	ПараметрыДокумента.Вставить("Контрагент",         Объект.Контрагент);
	ПараметрыДокумента.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	
	НовыеПараметры = ПолучитьДанныеОрганизацияПриИзменении(ПараметрыДокумента);
	ЗаполнитьЗначенияСвойств(Объект, НовыеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("Организация",        Объект.Организация);
	ПараметрыДокумента.Вставить("Контрагент" ,        Объект.Контрагент);
	ПараметрыДокумента.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
		
	НовыеПараметры = ПолучитьДанныеКонтрагентПриИзменении(ПараметрыДокумента);
	ЗаполнитьЗначенияСвойств(Объект, НовыеПараметры);
	
	// { РГ-Софт Пахоменков А. 19.01.2015 ПроверкаКонтрагентов
	ЗапуститьПроверкуКонтрагентов(Элемент);
	// } РГ-Софт Пахоменков А. 19.01.2015
			
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	НовыеПараметры = ПолучитьДанныеДоговорКонтрагентаПриИзменении(Объект.ДоговорКонтрагента);
	ЗаполнитьЗначенияСвойств(Объект, НовыеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ЗаполнитьСчетФактуруНалоговыйАгентСервер(Объект.ДокументОснование);
		Объект.КодВидаОперации	= ПолучитьКодВидаОперации();
	КонецЕсли;	
    	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ОткрытьФормуРедактированияКомментария(Элемент.ТекстРедактирования, Объект.Комментарий, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсправлениеПриИзменении(Элемент)
	
	УстановитьОграничениеТипов(ЭтаФорма);
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		ТипОснования	= ТипЗнч(Объект.ДокументОснование);
		Если НЕ Элементы.ДокументОснование.ОграничениеТипа.СодержитТип(ТипОснования) Тогда
		
			ТекстВопроса = НСтр("ru='Выбранный документ-основание не соответствует виду счета-фактуры. 
				|Для продолжения требуется очистить документ-основание. Продолжить?'");
				
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
			
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Объект.Исправление = НЕ Объект.Исправление;
				УстановитьОграничениеТипов(ЭтаФорма);
				Возврат;
			Иначе
				Объект.ДокументОснование	= Неопределено;
				Объект.ДокументыОснования.Очистить();
				Объект.НомерПлатежноРасчетногоДокумента	= "";
				Объект.ДатаПлатежноРасчетногоДокумента	= '00010101';
				Объект.ДатаНомерДокументовОплаты.Очистить();
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;

	Если НЕ Объект.Исправление Тогда
		
		Объект.НомерИсправления	= 0;
		
		Объект.НомерИсходногоДокумента	= "";
		Объект.ДатаИсходногоДокумента	= '00010101';
		
	КонецЕсли;
	
	Объект.КодВидаОперации	= ПолучитьКодВидаОперации();	
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетФактураНеВыставляетсяПриИзменении(Элемент)
	
	Если Объект.СчетФактураНеВыставляется Тогда		
		Объект.Выставлен				= Ложь;
		Объект.ДатаВыставления			= '00010101';
		Объект.КодСпособаВыставления	= 1;
	КонецЕсли;	
	
	Объект.КодВидаОперации	= ПолучитьКодВидаОперации();
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура КодВидаОперацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущийКод = Элемент.СписокВыбора.НайтиПоЗначению(Объект.КодВидаОперации);
	ВыбранныйКод = ВыбратьИзСписка(Элемент.СписокВыбора, Элемент, ТекущийКод);
	Если ВыбранныйКод <> Неопределено Тогда
		Объект.КодВидаОперации = ВыбранныйКод.Значение;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЧНОЙ ЧАСТИ Авансы

&НаКлиенте
Процедура АвансыСчетНаОплатуПриИзменении(Элемент)
	ТекущиеДанные  = Элементы.Авансы.ТекущиеДанные;
	НовыеПараметры = ПолучитьДанныеСчетПриИзменении(ТекущиеДанные.СчетНаОплату, Объект.ДокументОснование);
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, НовыеПараметры);
	НомерСтроки = ТекущиеДанные.НомерСтроки;
	РассчитатьСуммуНДСТабЧасти(НомерСтроки);
КонецПроцедуры

&НаКлиенте
Процедура АвансыСуммаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Авансы.ТекущиеДанные;
	НомерСтроки = ТекущиеДанные.НомерСтроки;
	РассчитатьСуммуНДСТабЧасти(НомерСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыСтавкаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Авансы.ТекущиеДанные;
	НомерСтроки = ТекущиеДанные.НомерСтроки;
	РассчитатьСуммуНДСТабЧасти(НомерСтроки);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммуНДСТабЧасти(НомерСтроки)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(ДокументОбъект.Авансы[НомерСтроки - 1], ДокументОбъект);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаполнитьПоДокументуОснованию(Команда)
	
	ЗаполнитьПоДокументуОснованиюСервер();	
	
КонецПроцедуры
    
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
&НаСервере
Процедура ПодготовитьФормуНаСервере()

	СписокТиповИсправление.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	СписокТиповНеИсправление.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	СписокТиповНеИсправление.Добавить(Тип("ДокументСсылка.СчетКнигиПокупок"));
	
	УчетНДС.ЗаполнитьСписокКодовВидовОпераций(Перечисления.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры, ЭтаФорма.Элементы.КодВидаОперации.СписокВыбора);
	
	УстановитьОграничениеТипов(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы	= Форма.Элементы;
	Объект		= Форма.Объект;
	
	Если Объект.СформированПриВводеНачальныхОстатковНДС Тогда
		Форма.ТолькоПросмотр	= Истина;
	КонецЕсли;

	УказанДокументОснование	= ЗначениеЗаполнено(Объект.ДокументОснование);
	
	Элементы.Организация.Доступность	= НЕ УказанДокументОснование;
	Элементы.Контрагент.Доступность		= НЕ УказанДокументОснование;
	
	Элементы.СуммаНДСДокумента.Доступность	= (УчетНДС.ПолучитьВерсиюПостановления(Объект.Дата) = 2);
	Элементы.ВалютаНДСДокумента.Доступность	= (УчетНДС.ПолучитьВерсиюПостановления(Объект.Дата) = 2);
	
	Элементы.ГруппаИсправление.Доступность				= (УчетНДС.ПолучитьВерсиюПостановления(Объект.Дата) = 2);
	Элементы.ГруппаИсходныйДокумент.Доступность			= (УчетНДС.ПолучитьВерсиюПостановления(Объект.Дата) = 2) И Объект.Исправление;
	
	Элементы.ГруппаВыставлениеСчетаФактуры.Доступность	= (УчетНДС.ПолучитьВерсиюПостановления(Объект.Дата) = 2);

	Элементы.НомерИсправления.Доступность	= (УчетНДС.ПолучитьВерсиюПостановления(Объект.Дата) = 2) И Объект.Исправление;
	
	УстановитьЗаголовокФормы(Форма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДоговорКонтрагента(ПараметрыДокумента)
	
	Если ОбщегоНазначения.ЗначениеНеЗаполнено(ПараметрыДокумента.Контрагент) Тогда 
		ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	Иначе
		ДоговорКонтрагента = УправлениеПользователями.ДоступныйДоговорКонтрагента(ПараметрыДокумента.Контрагент.ОсновнойДоговорКонтрагента);
	КонецЕсли;

	Если не ОбщегоНазначения.ЗначениеНеЗаполнено(ДоговорКонтрагента) тогда
		Если Не ЗначениеЗаполнено(ПараметрыДокумента.Организация) Тогда
			//Фильтр по организации не вклчается, договор подходит.	
		ИначеЕсли ПараметрыДокумента.Организация = ДоговорКонтрагента.Организация Тогда
			//принадлежит организации из документа, договор подходит.	
		Иначе
			//Договор не подходит. Очистим поле договора в документе.
			ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка(); // Очистить старый договор
		КонецЕсли;
	КонецЕсли;

	Если НЕ ОбщегоНазначения.ЗначениеНеЗаполнено(ДоговорКонтрагента) Тогда
		ВидыДоговоров = Новый СписокЗначений;
		ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
		ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
		//ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
		ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
		ВидДоговора = ДоговорКонтрагента.ВидДоговора;
		Если ВидыДоговоров.НайтиПоЗначению(ВидДоговора) = Неопределено тогда
			//Договор не подходит. Очистим поле договора в документе.
			ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка(); // Очистить старый договор
		КонецЕсли;
	КонецЕсли;
	Возврат ДоговорКонтрагента;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПараметрыДоговораКонтрагента(ДоговорКонтрагента)
	
	ПараметрыДоговора = Новый Структура;
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ПараметрыДоговора.Вставить("ВалютаВзаиморасчетов", 
			ОбщегоНазначения.ПолучитьЗначениеРеквизита(ДоговорКонтрагента, "ВалютаВзаиморасчетов"));
	Иначе
		ПараметрыДоговора.Вставить("ВалютаВзаиморасчетов", Справочники.Валюты.ПустаяСсылка());
	КонецЕсли;
	
	Возврат ПараметрыДоговора;
		
КонецФункции


&НаСервереБезКонтекста
Функция ПолучитьДанныеОрганизацияПриИзменении(Знач ПараметрыДокумента)
	
	НовыеПараметры = Новый Структура;
	
	НовыйДоговор = ПолучитьДоговорКонтрагента(ПараметрыДокумента);
	НовыеПараметры.Вставить("ДоговорКонтрагента", НовыйДоговор);
	
	ПараметрыДоговора = ПолучитьПараметрыДоговораКонтрагента(НовыйДоговор);
	НовыеПараметры.Вставить("ВалютаДокумента", ПараметрыДоговора.ВалютаВзаиморасчетов);
	
	НовыеПараметры.Вставить("СуммаДокумента",    0);
	НовыеПараметры.Вставить("ДокументОснование", Неопределено);
	НовыеПараметры.Вставить("ДатаПлатежноРасчетногоДокумента",  '00010101');
	НовыеПараметры.Вставить("НомерПлатежноРасчетногоДокумента", "");
	
	Возврат НовыеПараметры;
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеКонтрагентПриИзменении(Знач ПараметрыДокумента)
	
	НовыеПараметры = Новый Структура;
	
	НовыйДоговор = ПолучитьДоговорКонтрагента(ПараметрыДокумента);
	НовыеПараметры.Вставить("ДоговорКонтрагента", НовыйДоговор);
	
	ПараметрыДоговора = ПолучитьПараметрыДоговораКонтрагента(НовыйДоговор);
	НовыеПараметры.Вставить("ВалютаДокумента", ПараметрыДоговора.ВалютаВзаиморасчетов);
	
	НовыеПараметры.Вставить("СуммаДокумента",    0);
	НовыеПараметры.Вставить("ДокументОснование", Неопределено);
	НовыеПараметры.Вставить("ДатаПлатежноРасчетногоДокумента",  '00010101');
	НовыеПараметры.Вставить("НомерПлатежноРасчетногоДокумента", "");
	
	Возврат НовыеПараметры;
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорКонтрагентаПриИзменении(Знач ДоговорКонтрагента)
	
	НовыеПараметры = Новый Структура;
	
	ПараметрыДоговора = ПолучитьПараметрыДоговораКонтрагента(ДоговорКонтрагента);
	НовыеПараметры.Вставить("ВалютаДокумента", ПараметрыДоговора.ВалютаВзаиморасчетов);
	
	НовыеПараметры.Вставить("СуммаДокумента",    0);
	НовыеПараметры.Вставить("СуммаНДСДокумента", 0);
	НовыеПараметры.Вставить("СуммаУвеличение", 0);
	НовыеПараметры.Вставить("СуммаУменьшение", 0);
	НовыеПараметры.Вставить("СуммаНДСУвеличение", 0);
	НовыеПараметры.Вставить("СуммаНДСУменьшение", 0);
	
	НовыеПараметры.Вставить("ДокументОснование", Неопределено);
	НовыеПараметры.Вставить("ДатаПлатежноРасчетногоДокумента",  '00010101');
	НовыеПараметры.Вставить("НомерПлатежноРасчетногоДокумента", "");
	
	Возврат НовыеПараметры;
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеСчетПриИзменении(Знач СчетНаПредоплату, ДокАванса)
	
	НовыеПараметры = Новый Структура;
	
	СоответствиеСтавок = Новый Соответствие();
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС10, Перечисления.СтавкиНДС.НДС10_110);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС18, Перечисления.СтавкиНДС.НДС18_118);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС20, Перечисления.СтавкиНДС.НДС20_120);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС10_110, Перечисления.СтавкиНДС.НДС10_110);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС18_118, Перечисления.СтавкиНДС.НДС18_118);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС20_120, Перечисления.СтавкиНДС.НДС20_120);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС0, Перечисления.СтавкиНДС.НДС18_118);
	
	Если НЕ ЗначениеЗаполнено(СчетНаПредоплату) Тогда
		Возврат НовыеПараметры;
	КонецЕсли;
	
	Отказ = Ложь;
	СтавкаСчета = Перечисления.СтавкиНДС.ПустаяСсылка(); 
	ПровркаСчетаНаПредоплату(СчетНаПредоплату, Отказ, СтавкаСчета);
	Если Отказ Тогда Возврат НовыеПараметры; КонецЕсли;
	
	СтавкаСчета = ?(ЗначениеЗаполнено(СоответствиеСтавок[СтавкаСчета]), СоответствиеСтавок[СтавкаСчета], СтавкаСчета);
	НовыеПараметры.Вставить("СтавкаНДС", СтавкаСчета);
					
	Возврат НовыеПараметры;
		
КонецФункции

&НаСервере
Процедура ЗаполнитьПоДокументуОснованиюСервер()
	
	Объект.Авансы.Очистить();
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ОбработкаЗаполнения(ДокументОбъект.ДокументОснование);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокФормы(Форма)
	
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Форма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Счет-фактура выданный %1 от %2 - налоговый агент'"),
			Объект.Номер,
			Объект.Дата);
	Иначе
		Форма.Заголовок = НСтр("ru='Счет-фактура выданный (создание) - налоговый агент'");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ЗаполнитьСчетФактуруНалоговыйАгентСервер(Знач ДокументОснование)
	
	Объект.Авансы.Очистить();
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ОбработкаЗаполнения(ДокументОснование);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
    		
КонецФункции

&НаСервере
Функция ПолучитьКодВидаОперации(КодВидаОперацииОснования = Неопределено)
	
	СтруктураПараметров	= Новый Структура;
	СтруктураПараметров.Вставить("Дата",						Объект.Дата);
	СтруктураПараметров.Вставить("ВидСчетаФактуры",				Объект.ВидСчетаФактуры);
	СтруктураПараметров.Вставить("Исправление",					Объект.Исправление);
	СтруктураПараметров.Вставить("ДоговорКонтрагента",			Объект.ДоговорКонтрагента);
	СтруктураПараметров.Вставить("СчетФактураНеВыставляется",	Объект.СчетФактураНеВыставляется);
	СтруктураПараметров.Вставить("СчетФактураБезНДС",			Объект.СчетФактураБезНДС);
	СтруктураПараметров.Вставить("КодВидаОперации",				Объект.КодВидаОперации);
	СтруктураПараметров.Вставить("ДокументыОснования",			Объект.ДокументыОснования.Выгрузить(,"ДокументОснование"));
	
	Возврат Документы.СчетФактураВыданный.ПолучитьКодВидаОперации(СтруктураПараметров, КодВидаОперацииОснования);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОграничениеТипов(Форма)

	Элементы	= Форма.Элементы;
	Объект		= Форма.Объект;
	
	Если Объект.Исправление Тогда
	
		ОписаниеТиповОснования = Новый ОписаниеТипов(Форма.СписокТиповИсправление.ВыгрузитьЗначения());
	Иначе
		
		ОписаниеТиповОснования = Новый ОписаниеТипов(Форма.СписокТиповНеИсправление.ВыгрузитьЗначения());
	
	КонецЕсли;
		
	Элементы.ДокументОснование.ОграничениеТипа = ОписаниеТиповОснования;

КонецПроцедуры

Процедура ПолучитьЗаголовок()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	УстановитьЗаголовокФормыДокумента(, ДокументОбъект, ЭтаФорма);
    ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПровркаСчетаНаПредоплату(Счет, Отказ, СтавкаСчета)
	
	Копия = Счет.Товары.Выгрузить(,"СтавкаНДС");
	Копия.Свернуть("СтавкаНДС");
	Если ЗначениеЗаполнено(Копия) Тогда
	СтавкаСчета = Копия[0].СтавкаНДС;
	Если Копия.Количество() > 1 Тогда
		
		Сообщить("В счете: " + Строка(Счет) + " - указано более одного вида ставки НДС. Дальнейшее заполнение счета-фактуры невозможно.", СтатусСообщения.Важное);
		Отказ = Истина;
		
	КонецЕсли;
	КонецЕсли;

	Копия = Счет.Услуги.Выгрузить(,"СтавкаНДС");
	Копия.Свернуть("СтавкаНДС");
	Если ЗначениеЗаполнено(Копия) Тогда
	СтавкаСчета = Копия[0].СтавкаНДС;
	Если Копия.Количество() > 1 Тогда
		
		Сообщить("В счете: " + Строка(Счет) + " - указано более одного вида ставки НДС. Дальнейшее заполнение счета-фактуры невозможно.", СтатусСообщения.Важное);
		Отказ = Истина;
		
	КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Формирует и устанавливает текст заголовка формы документа
//
// Параметры:
//  СтрокаВидаОперации - строка вида операции документа, 
//  ДокументОбъект     - объект документа, 
//  ФормаДокумента     - форма документа.
//
Процедура УстановитьЗаголовокФормыДокумента(СтрокаВидаОперации = "", ДокументОбъект, ФормаДокумента) Экспорт

	ФормаДокумента.АвтоЗаголовок = Ложь; // заголовок будем писать сами
	
	Заголовок = "" + ДокументОбъект + ": ";
	
	Если НЕ ПустаяСтрока(СтрокаВидаОперации) Тогда
		Заголовок = Заголовок + СтрокаВидаОперации + ". ";
	КонецЕсли;
		
	Если ДокументОбъект.ЭтоНовый() Тогда  
		Заголовок = Заголовок + "Новый";
	Иначе
		Если ДокументОбъект.Проведен Тогда
			Заголовок = Заголовок + "Проведен";
		ИначеЕсли ДокументОбъект.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить Тогда
			Заголовок = Заголовок + "Не проведен";
		Иначе
			Заголовок = Заголовок + "Записан";
		КонецЕсли;
	КонецЕсли;
	
	ФормаДокумента.Заголовок = Заголовок;

КонецПроцедуры // УстановитьЗаголовокФормыДокумента()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	УправлениеФормой(ЭтаФорма);
	
	// { РГ-Софт Пахоменков А. 19.01.2015 ПроверкаКонтрагентов
	ПроверитьКонтрагентовПриОткрытии();
	// } РГ-Софт Пахоменков А. 19.01.2015
	
КонецПроцедуры

#Область ПроверкаКонтрагентов

&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьПроверкуКонтрагентов(ДополнительныеПараметры = Неопределено)
	
	Если ИспользованиеПроверкиВозможно Тогда
		
		ПараметрыФоновогоЗадания = ПроверкаКонтрагентовКлиент.ПараметрыФоновогоЗадания(ДополнительныеПараметры);
		ПроверитьКонтрагентовФоновоеЗадание(ПараметрыФоновогоЗадания);
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		
		// Прерываем предыдущую проверку
		ПодключитьОбработчикОжидания("Подключаемый_ОбработатьРезультатПроверкиКонтрагентов", 1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	
	ФоновоеЗаданиеЗавершилось = ПроверкаКонтрагентовВызовСервера.ПроверкаКонтрагентовЗавершилась(ИдентификаторЗаданияПроверкиКонтрагента);
	
	// Если есть незавершившиеся фоновые задания, то продолжаем ждать результат
	Если ФоновоеЗаданиеЗавершилось Тогда
		ОтобразитьРезультатПроверкиКонтрагента();
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ОбработатьРезультатПроверкиКонтрагентов", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента()
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагента(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ПроверитьКонтрагентовФоновоеЗадание(ДополнительныеПараметры = Неопределено)
	
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВДокументеФоновоеЗадание(ЭтаФорма, ДополнительныеПараметры);
		
КонецПроцедуры

&НаСервере
Процедура ЗапуститьПроверкуКонтрагентовПриСозданииНаСервере()
	
	ИспользованиеПроверкиВозможно = ПроверкаКонтрагентовВызовСервера.ИспользованиеПроверкиВозможно();
	
	Если ИспользованиеПроверкиВозможно Тогда
		ПроверкаКонтрагентов.УправлениеФормойПриСозданииНаСервере(ЭтаФорма);
		ПроверитьКонтрагентовФоновоеЗадание();
	Иначе
		ПроверкаКонтрагентов.ПрорисоватьСостоянияКонтрагентовВДокументе(ЭтаФорма, Перечисления.СостоянияПроверкиКонтрагентов.ПроверкаНеИспользуется);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентовПриОткрытии()
	
	Если ИспользованиеПроверкиВозможно Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ОбработатьРезультатПроверкиКонтрагентов", 0.1, Истина);
	Иначе
		ПодключитьОбработчикОжидания("Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТребуетсяПроверкаКонтрагентов(Источник) 
	Возврат ПроверкаКонтрагентов.ТребуетсяПроверкаКонтрагентов(ЭтаФорма, Источник);
КонецФункции

&НаКлиенте
Процедура ПроверитьКонтрагентовПриНаступленииСобытия(ИмяСобытия, Параметр, Источник)
	
	Если ПроверкаКонтрагентовКлиент.СобытиеТребуетПроверкиКонтрагента(ЭтаФорма, ИмяСобытия, Параметр, Источник)
		И ТребуетсяПроверкаКонтрагентов(Источник) Тогда
		
		ЗапуститьПроверкуКонтрагентов(Источник);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
