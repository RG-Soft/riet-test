
//////////////////////////////////////////////////////////////////////
// ПРИ КОПИРОВАНИИ

Процедура ПриКопировании(ОбъектКопирования)
	
	РГСофт.ОчиститьCreationModification(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////
// ПЕРЕД ЗАПИСЬЮ      

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДозаполнитьРеквизиты();
	
	ПроверитьРеквизиты(Отказ, РежимЗаписи);
		
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////////////

Процедура ДозаполнитьРеквизиты()
	
	Comment = СокрЛП(Comment);
	РГСофтКлиентСервер.СокрЛПКолонокВТаблице(HTCCodeList, "HTCCode");
	ОбщегоНазначения.ОчиститьТаблицуОтСтрокСПустымиРеквизитами(HTCCodeList, "HTCCode");

	Если ЭтоНовый() Тогда
		CreatedBy = ПараметрыСеанса.ТекущийПользователь;
		Дата = ТекущаяДата();
	Иначе 
		РГСофт.ЗаполнитьModification(ЭтотОбъект);
	КонецЕсли;
		
КонецПроцедуры 


///////////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьРеквизиты(Отказ, РежимЗаписи)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	     		
	Если Не ЗначениеЗаполнено(SanctionsGroup) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Sanctions group' is empty!",
			ЭтотОбъект, "SanctionsGroup", , Отказ);
			
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Country) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"'Country' is empty!",
			ЭтотОбъект, "Country", , Отказ);
			
	КонецЕсли;
		
	Если HTCCodeList.Количество() = 0 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""HTC code list"" is empty!",
			ЭтотОбъект, "HTCCodeList", , Отказ);
			
	иначе
		
		МассивHTCCode = РГСофтКлиентСервер.ВыгрузитьКолонкуКоллекцииБезПустыхЗначенийИДублей(HTCCodeList, "HTCCode");
		СтруктураПоиска = Новый Структура("HTCCode");
		
		Для Каждого HTCCode из МассивHTCCode Цикл
			
			СтруктураПоиска.HTCCode = HTCCode;
			
			МассивСтрок = HTCCodeList.НайтиСтроки(СтруктураПоиска);
			Если МассивСтрок.Количество() > 1 Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					"HTC code " + HTCCode + " specified more than once!",
					ЭтотОбъект, "HTCCodeList", , Отказ);	
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого СтрокаHTCCodeList из HTCCodeList Цикл 
			    						
			Если Не ЗначениеЗаполнено(СтрокаHTCCodeList.StartDate) Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					"For HTC code " + СтрокаHTCCodeList.HTCCode + " start date is empty!",
					ЭтотОбъект, "HTCCodeList[" + (СтрокаHTCCodeList.НомерСтроки-1) + "].StartDate", , Отказ);
				
			КонецЕсли; 			   	 			
			
		КонецЦикла;
		
	КонецЕсли;
		        		
КонецПроцедуры
