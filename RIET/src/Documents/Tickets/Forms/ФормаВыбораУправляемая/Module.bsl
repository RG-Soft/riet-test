
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказатьВсе = Истина;
	//показываем все тикеты
	Список.Параметры.УстановитьЗначениеПараметра("ПоказатьВсе", ПоказатьВсе);
	
	//настроим отбор
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если Не РГСофтСерверПовтИспСеанс.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "УчетПоВсемОтветственным") Тогда
		
		ОсновнойОтветственный = РГСофтСерверПовтИспСеанс.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнойОтветственный");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
													"Ответственный",
													ОсновнойОтветственный,
													ВидСравненияКомпоновкиДанных.Равно,
													,Истина,
													РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто);
	КонецЕсли;
	
	Если Не РГСофтСерверПовтИспСеанс.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "УчетПоВсемИнвойсинговымЦентрам") Тогда
		
		ОсновнойИнвойсинговыйЦентр = РГСофтСерверПовтИспСеанс.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнойИнвойсинговыйЦентр");
		СписокИнвойсинговыхЦентров = Новый СписокЗначений;
		СписокИнвойсинговыхЦентров.Добавить(ОсновнойИнвойсинговыйЦентр);
		СписокИнвойсинговыхЦентров.Добавить(Справочники.ИнвойсинговыеЦентры.ПустаяСсылка());
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
													"ИнвойсинговыйЦентр",
													СписокИнвойсинговыхЦентров,
													ВидСравненияКомпоновкиДанных.ВСписке,
													,Истина,
													РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто);
	КонецЕсли;
	
	//параметры, передаваемые при открытии формы
	Если Параметры.Свойство("Подразделение") Тогда 
		 ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
													"ПодразделениеОрганизации",
													Параметры.Подразделение,
													ВидСравненияКомпоновкиДанных.Равно,
													,Истина,
													РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто);
	КонецЕсли;
	
	Если Параметры.Свойство("Договор") Тогда 
		 ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
													"ДоговорКонтрагента",
													Параметры.Договор,
													ВидСравненияКомпоновкиДанных.Равно,
													,Истина,
													РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто);
	КонецЕсли;
												
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсе(Команда)
	
	ПоказатьВсе = Не ПоказатьВсе;

	//если не "Показать все" - тогда отображаются только тикеты, по которым есть остатки
	Список.Параметры.УстановитьЗначениеПараметра("ПоказатьВсе", ПоказатьВсе);
	
КонецПроцедуры


&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповеститьОВыборе(Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

