
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СтруктураНастройки") Тогда
		
		СтруктураНастройки = Параметры.СтруктураНастройки;
		Если СтруктураНастройки.Имя = "ОтборДляПереносСуммТаможенныхПлатежей" Тогда
			ПолучитьСтруктуруОтбораДляПереносСуммТаможенныхПлатежей(СтруктураНастройки);
		ИначеЕсли СтруктураНастройки.Имя = "ВыборИзCustomsPaymentAllocation" Тогда
			НастроитьДляВыбораИзCustomsPaymentAllocation(СтруктураНастройки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтруктуруОтбораДляПереносСуммТаможенныхПлатежей(СтруктураНастройки)
	
	МассивUnallocatedCustomsPayments = CustomsСервер.ПолучитьМассивUnallocatedCustomsPayments();
	МассивUnallocatedCustomsPayments.Добавить(СтруктураНастройки.ПлатежОтправитель);
	Параметры.Отбор.Вставить("Ссылка", МассивUnallocatedCustomsPayments);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДляВыбораИзCustomsPaymentAllocation(СтруктураНастройки)
	
	СтруктураПодбора = CustomsСервер.ПолучитьСтруктуруПодбораCustomsPaymentsForAllocation(СтруктураНастройки.CustomsDocument, СтруктураНастройки.PaymentKind);
			
	Отбор = Параметры.Отбор;
	
	Если ЗначениеЗаполнено(СтруктураПодбора.Customs) Тогда
		Отбор.Вставить("Customs", СтруктураПодбора.Customs);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураПодбора.ParentCompanyForPayments) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор,
			"BankAccount.SoldTo",
			СтруктураПодбора.ParentCompanyForPayments,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураПодбора.CCDReference) Тогда
		
		СписокCCDReference = Новый СписокЗначений;
		СписокCCDReference.Добавить(СтруктураПодбора.CCDReference);
		СписокCCDReference.Добавить("");
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор,
			"CCDReference",
			СписокCCDReference,
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);	
			
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураНастройки.PaymentKind) Тогда
		Отбор.Вставить("PaymentKind", СтруктураНастройки.PaymentKind);
	КонецЕсли;
	
	Отбор.Вставить("Ссылка", СтруктураПодбора.CustomsPayments);
	Если ЗначениеЗаполнено(СтруктураНастройки.CurrentCustomsPayment) Тогда
		Отбор.Ссылка.Добавить(СтруктураНастройки.CurrentCustomsPayment);
	КонецЕсли; 
	
КонецПроцедуры
