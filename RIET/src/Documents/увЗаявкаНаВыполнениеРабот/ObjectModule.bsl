////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Структура, содержащая права пользователя
Перем Права;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ДОКУМЕНТА

// Процедура-обработчик события "ОбработкаЗаполнения" документа
//
Процедура ОбработкаЗаполнения(Основание)
	
	Если Основание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.увВопрос") Тогда
		увМониторСопровождения.увОпределитьПраваПользователя(увМониторСопровождения.увОпределитьТекущегоПользователя(), Права);
		Если Права.СозданиеЗаявок Тогда
			ТемаЗаявки = Основание.ТемаВопроса;
			ТребованияПоДоработке = Основание.Вопрос;
			ФизЛицо = Основание.Спросил;
		Иначе 
			Сообщить("Вы не имеете права создавать заявки");
		КонецЕсли;
	КонецЕсли;
    
КонецПроцедуры

// Процедура-обработчик события "ПередЗаписью" документа
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ЗначениеЗаполнено(Дата) Тогда	
		Сообщить("Не заполнено поле: Дата");
		Отказ = Истина;
	ИначеЕсли Не ЗначениеЗаполнено(Номер) Тогда	
		Сообщить("Не заполнено поле: Номер");
		Отказ = Истина;
	ИначеЕсли Не ЗначениеЗаполнено(ТемаЗаявки) Тогда	
		Сообщить("Не заполнено поле: Тема заявки");
		Отказ = Истина;	
	ИначеЕсли Не ЗначениеЗаполнено(ТребованияПоДоработке) Тогда	
		Сообщить("Не заполнено поле: Требования по доработке");
		Отказ = Истина;	
	КонецЕсли;
	
	увМониторСопровождения.увОпределитьПраваПользователя(увМониторСопровождения.увОпределитьТекущегоПользователя(), Права);
    Если Не Права.СозданиеЗаявок Тогда
		Отказ = Истина;
		#Если Клиент Тогда
		Предупреждение("Вы не имеете права создавать заявки");
		#КонецЕсли
	КонецЕсли;
			
КонецПроцедуры

// Процедура-обработчик события "ПриУстановкеНовогоНомера" документа
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Префикс = Константы.увПрефиксБазы.Получить();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Для Каждого СтрокаТЧ Из Согласование Цикл
		СтрокаТЧ.Утверждено = Ложь;
		СтрокаТЧ.ПринятоКСогласованию = Ложь;
	КонецЦикла;
	
	Утверждено = Ложь;
	УтвердилОтЗаказчика = Справочники.Пользователи.ПустаяСсылка();
	НачатоТестирование = Ложь;
	Отклонена = Ложь;
	ЗаявкаЗакрыта = Ложь;
	ПереданоНаРазработку = Ложь;
	
	ДатаЗавершенияРабот = '00010101';
	ДатаПередачиНаРазработку = '00010101';
	ДатаСдачиНаТестирование = '00010101';
	ДатаУтверждения = '00010101';
	
	СтоимостьРабот = 0;
	
КонецПроцедуры


