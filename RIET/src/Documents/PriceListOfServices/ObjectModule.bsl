
////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ДОКУМЕНТА     

// ПЕРЕД ЗАПИСЬЮ

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	ДозаполнитьРеквизитыБезДополнительныхДанных();
	
	ПоместитьДополнительныеДанныеВДополнительныеСвойстваПередЗаписью(РежимЗаписи);
	
	ПроверитьЗаполнениеРеквизитов(Отказ, РежимЗаписи, ДополнительныеСвойства.ТаблицаServices);
		
КонецПроцедуры

Процедура ДозаполнитьРеквизитыБезДополнительныхДанных()
	
	ргСофтКлиентСервер.УстановитьЗначение(Номер, СокрЛП(Номер));
	РГСофтКлиентСервер.УстановитьЗначение(Дата, НачалоДня(Дата));
	РГСофтКлиентСервер.УстановитьЗначение(Description, СокрЛП(Description));
	РГСофтКлиентСервер.УстановитьЗначение(Comment, СокрЛП(Comment));
	
	Для Каждого СтрокаТЧ Из StandardTariffs Цикл
		РГСофтКлиентСервер.УстановитьЗначение(СтрокаТЧ.Description, СокрЛП(СтрокаТЧ.Description));
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из CostPlus Цикл
		РГСофтКлиентСервер.УстановитьЗначение(СтрокаТЧ.Description, СокрЛП(СтрокаТЧ.Description));
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из Quoted Цикл
		РГСофтКлиентСервер.УстановитьЗначение(СтрокаТЧ.Description, СокрЛП(СтрокаТЧ.Description));
	КонецЦикла;
	
	Если ЭтоНовый() Тогда
		CreationDate = ТекущаяДата();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(LastModified) Тогда
		LastModified = ТекущаяДата();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Responsible) Тогда
		Responsible = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
КонецПроцедуры 

Процедура ПоместитьДополнительныеДанныеВДополнительныеСвойстваПередЗаписью(РежимЗаписи)
	
	СтруктрураПараметров = Новый Структура;
	СтруктураТекстов = Новый Структура;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Services = РГСофтКлиентСервер.СложитьМассивы(StandardTariffs.ВыгрузитьКолонку("Service"),  CostPlus.ВыгрузитьКолонку("Service"));
		Services = РГСофтКлиентСервер.СложитьМассивы(Services, Quoted.ВыгрузитьКолонку("Service"));
		Если Services.Количество() Тогда
			
			СтруктрураПараметров.Вставить("Services", Services);
			СтруктураТекстов.Вставить("РеквизитыServices",
				"ВЫБРАТЬ
				|	Services.Ссылка КАК Service,
				|	Services.SumCalculationMethod
				|ИЗ
				|	Справочник.Services КАК Services
				|ГДЕ
				|	Services.Ссылка В(&Services)");
			
		КонецЕсли; 		
						
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	СтруктураРезультатов = РГСофт.ПолучитьСтруктуруРезультатовТекстовЗапросов(СтруктураТекстов, СтруктрураПараметров);
	УстановитьПривилегированныйРежим(Ложь);
	
	ДополнительныеСвойства.Вставить("ТаблицаServices", Неопределено);
	Если СтруктураРезультатов.Свойство("РеквизитыServices") Тогда
		ДополнительныеСвойства.ТаблицаServices = СтруктураРезультатов.РеквизитыServices.Выгрузить();
		ДополнительныеСвойства.ТаблицаServices.Индексы.Добавить("Service");
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеРеквизитов(Отказ, РежимЗаписи, ТаблицаServices)
	
	ПроверитьЗаполнениеШапки(Отказ, РежимЗаписи);
	
	ПроверитьЗаполнениеТЧStandardTariffs(Отказ, РежимЗаписи, ТаблицаServices);
	
	ПроверитьЗаполнениеТЧCostPlus(Отказ, РежимЗаписи, ТаблицаServices);
	
	ПроверитьЗаполнениеТЧQuoted(Отказ, РежимЗаписи, ТаблицаServices);	
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеШапки(Отказ, РежимЗаписи)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Номер)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Поле ""No."" не заполнено!",
			ЭтотОбъект, "Номер", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Поле ""Start date"" не заполнено!",
			ЭтотОбъект, "Дата", , Отказ);
	КонецЕсли;
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Agent) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Поле ""Agent"" не заполнено!",
			ЭтотОбъект, "Agent", , Отказ);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ExpireDate)
		И ExpireDate < Дата Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"""Expire date"" не может быть раньше ""Start date""!",
			ЭтотОбъект, "ExpireDate", , Отказ);
	КонецЕсли;
	
	Если StandardTariffs.Количество() = 0
		И CostPlus.Количество() = 0
		И Quoted.Количество() = 0 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"At least one of the tables should be filled: Standard tariffs, Cost-plus or Quoted!",
			ЭтотОбъект, , , Отказ);
			
	КонецЕсли; 
	
КонецПроцедуры 

Процедура ПроверитьЗаполнениеТЧStandardTariffs(Отказ, РежимЗаписи, ТаблицаServices)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли; 
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли; 
	
	Для Каждого СтрокаТЧ Из StandardTariffs Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Service) Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Standard tariffs"": поле ""Service"" не заполнено!",
				ЭтотОбъект, "StandardTariffs[" + (СтрокаТЧ.НомерСтроки-1) + "].Service", , Отказ);	
		Иначе
			
			СтрокаТаблицыServices = ТаблицаServices.Найти(СтрокаТЧ.Service, "Service");
			Если СтрокаТаблицыServices.SumCalculationMethod <> Перечисления.SumCalculationMethods.StandardTariff Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					"For service " + СокрЛП(СтрокаТЧ.Service) + " in ""Standard tariffs"": sum calculation method is """ + СтрокаТаблицыServices.SumCalculationMethod + """!",
					ЭтотОбъект, "StandardTariffs[" + (СтрокаТЧ.НомерСтроки-1) + "].Service", , Отказ);
			КонецЕсли;
						
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Description) Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Standard tariffs"": поле ""Description"" не заполнено!",
				ЭтотОбъект, "StandardTariffs[" + (СтрокаТЧ.НомерСтроки-1) + "].Description", , Отказ);	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Price) Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Standard tariffs"": поле ""Price"" не заполнено!",
				ЭтотОбъект, "StandardTariffs[" + (СтрокаТЧ.НомерСтроки-1) + "].Price", , Отказ);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.StartDate)
			И СтрокаТЧ.StartDate < Дата Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Standard tariffs"": ""Start date"" строки не может быть раньше ""Start date"" прайс-листа!",
				ЭтотОбъект, "StandardTariffs[" + (СтрокаТЧ.НомерСтроки-1) + "].StartDate", , Отказ);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ExpireDate)
			И СтрокаТЧ.ExpireDate > ExpireDate Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Standard tariffs"": ""Expire date"" строки не может быть позже ""Expire date"" прайс-листа!",
				ЭтотОбъект, "StandardTariffs[" + (СтрокаТЧ.НомерСтроки-1) + "].ExpireDate", , Отказ);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.ExpireDate)
			И СтрокаТЧ.ExpireDate < СтрокаТЧ.StartDate Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Standard tariffs"": ""Expire date"" строки не может быть раньше ""Start date"" строки!",
				ЭтотОбъект, "StandardTariffs[" + (СтрокаТЧ.НомерСтроки-1) + "].ExpireDate", , Отказ);	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеТЧCostPlus(Отказ, РежимЗаписи, ТаблицаServices)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли; 
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из CostPlus Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Service) Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Cost plus"": поле ""Service"" не заполнено!",
				ЭтотОбъект, "CostPlus[" + (СтрокаТЧ.НомерСтроки-1) + "].Service", , Отказ);	
		Иначе
			
			СтрокаТаблицыServices = ТаблицаServices.Найти(СтрокаТЧ.Service, "Service");
			Если СтрокаТаблицыServices.SumCalculationMethod <> Перечисления.SumCalculationMethods.CostPlus Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					"For service " + СокрЛП(СтрокаТЧ.Service) + " in ""Cost plus"": sum calculation method is """ + СтрокаТаблицыServices.SumCalculationMethod + """!",
					ЭтотОбъект, "CostPlus[" + (СтрокаТЧ.НомерСтроки-1) + "].Service", , Отказ);
			КонецЕсли;
						
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Description) Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Cost plus"": поле ""Description"" не заполнено!",
				ЭтотОбъект, "CostPlus[" + (СтрокаТЧ.НомерСтроки-1) + "].Description", , Отказ);	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Percent) Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Cost plus"": поле ""Percent"" не заполнено!",
				ЭтотОбъект, "CostPlus[" + (СтрокаТЧ.НомерСтроки-1) + "].Percent", , Отказ);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.StartDate)
			И СтрокаТЧ.StartDate < Дата Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Cost plus"": ""Start date"" строки не может быть раньше ""Start date"" прайс-листа!",
				ЭтотОбъект, "CostPlus[" + (СтрокаТЧ.НомерСтроки-1) + "].StartDate", , Отказ);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ExpireDate)
			И СтрокаТЧ.ExpireDate > ExpireDate Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Cost plus"": ""Expire date"" строки не может быть позже ""Expire date"" прайс-листа!",
				ЭтотОбъект, "CostPlus[" + (СтрокаТЧ.НомерСтроки-1) + "].ExpireDate", , Отказ);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.ExpireDate)
			И СтрокаТЧ.ExpireDate < СтрокаТЧ.StartDate Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Cost plus"": ""Expire date"" строки не может быть раньше ""Start date"" строки!",
				ЭтотОбъект, "CostPlus[" + (СтрокаТЧ.НомерСтроки-1) + "].ExpireDate", , Отказ);	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры 

Процедура ПроверитьЗаполнениеТЧQuoted(Отказ, РежимЗаписи, ТаблицаServices)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли; 
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Quoted Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Service) Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Quoted"": поле ""Service"" не заполнено!",
				ЭтотОбъект, "Quoted[" + (СтрокаТЧ.НомерСтроки-1) + "].Service", , Отказ);	
		Иначе
			
			СтрокаТаблицыServices = ТаблицаServices.Найти(СтрокаТЧ.Service, "Service");
			Если СтрокаТаблицыServices.SumCalculationMethod <> Перечисления.SumCalculationMethods.Quoted Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					"For service " + СокрЛП(СтрокаТЧ.Service) + " in ""Quoted"": sum calculation method is """ + СтрокаТаблицыServices.SumCalculationMethod + """!",
					ЭтотОбъект, "Quoted[" + (СтрокаТЧ.НомерСтроки-1) + "].Service", , Отказ);
			КонецЕсли;
						
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Description) Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Quoted"": поле ""Description"" не заполнено!",
				ЭтотОбъект, "Quoted[" + (СтрокаТЧ.НомерСтроки-1) + "].Description", , Отказ);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.StartDate)
			И СтрокаТЧ.StartDate < Дата Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Quoted"": ""Start date"" строки не может быть раньше ""Start date"" прайс-листа!",
				ЭтотОбъект, "Quoted[" + (СтрокаТЧ.НомерСтроки-1) + "].StartDate", , Отказ);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ExpireDate)
			И СтрокаТЧ.ExpireDate > ExpireDate Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Quoted"": ""Expire date"" строки не может быть позже ""Expire date"" прайс-листа!",
				ЭтотОбъект, "Quoted[" + (СтрокаТЧ.НомерСтроки-1) + "].ExpireDate", , Отказ);	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.ExpireDate)
			И СтрокаТЧ.ExpireDate < СтрокаТЧ.StartDate Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Строка " + СтрокаТЧ.НомерСтроки + " таблицы ""Quoted"": ""Expire date"" строки не может быть раньше ""Start date"" строки!",
				ЭтотОбъект, "Quoted[" + (СтрокаТЧ.НомерСтроки-1) + "].ExpireDate", , Отказ);	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры 
