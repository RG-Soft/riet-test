
///////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
///////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда 
		ЗаполнитьРеквизитыНаОснованииДокумента();
	КонецЕсли;
	
	Элементы.ОснованиеКомпания.Доступность    = ДоверенностьКомпания;
	Элементы.ОснованиеКомпанияEng.Доступность = ДоверенностьКомпания;
	
	Элементы.ОснованиеКонтрагент.Доступность    = ДоверенностьКонтрагент;
	Элементы.ОснованиеКонтрагентEng.Доступность = ДоверенностьКонтрагент;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыНаОснованииДокумента()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.ДоговорКонтрагента.Наименование КАК ДоговорКонтрагентаНаименование,
	               |	РеализацияТоваровУслуг.ДоговорКонтрагента.НаименованиеEng КАК ДоговорКонтрагентаНаименованиеEng,
	               |	РеализацияТоваровУслуг.Руководитель.Наименование КАК РуководительНаименование,
	               |	РеализацияТоваровУслуг.Руководитель.Наименование КАК РуководительНаименованиеEng
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		
		Договор = Выборка.ДоговорКонтрагентаНаименование;
		ДоговорEng = Выборка.ДоговорКонтрагентаНаименованиеEng;
		МенеджерКомпания = Выборка.РуководительНаименование;
		МенеджерКомпанияEng = Выборка.РуководительНаименованиеEng;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверенностьКомпанияПриИзменении(Элемент)
	
	Элементы.ОснованиеКомпания.Доступность    = ДоверенностьКомпания;
	Элементы.ОснованиеКомпанияEng.Доступность = ДоверенностьКомпания;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверенностьКонтрагентПриИзменении(Элемент)
	
	Элементы.ОснованиеКонтрагент.Доступность    = ДоверенностьКонтрагент;
	Элементы.ОснованиеКонтрагентEng.Доступность = ДоверенностьКонтрагент;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
// КОМАНДЫ
///////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура Печать(Команда)
	
	Акт.Напечатать();
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	Акт = ПечатьAct();	
	
КонецПроцедуры

&НаСервере
Функция ПечатьAct()

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.УстановитьПараметр("ТекущийДокумент", ДокументОбъект.Ссылка);
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Номер,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента,
	|	РеализацияТоваровУслуг.Контрагент КАК Получатель,
	|	РеализацияТоваровУслуг.Организация КАК Поставщик,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	РеализацияТоваровУслуг.ВалютаДокумента,
	|	РеализацияТоваровУслуг.УчитыватьНДС,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.ПодразделениеОрганизации,
	|	РеализацияТоваровУслуг.Контрагент.DescriptionFull КАК ПолучательDescriptionFull
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ТекущийДокумент";
	Шапка = ЗапросШапка.Выполнить().Выбрать();
	Шапка.Следующий();

	ТабДокумент = Новый ТабличныйДокумент;   
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_Act";
	Макет       = Документы.РеализацияТоваровУслуг.ПолучитьМакет("ActSLB");

	ОбластьМакета = Макет.ПолучитьОбласть("Основная1");
	ОбластьМакета.Параметры.Договор = Договор;
	ОбластьМакета.Параметры.ДоговорEng = ДоговорEng;
	ОбластьМакета.Параметры.Номер = Шапка.Номер;
  	ОбластьМакета.Параметры.Дата = Формат(Шапка.Дата,"ДФ=dd.MM.yyyy");
  	ОбластьМакета.Параметры.Организация = ФормированиеПечатныхФорм.ОписаниеОрганизации(КонтактнаяИнформация.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата), "ПолноеНаименование,");
	ОбластьМакета.Параметры.Company = "Schlumberger Logelco Inc.";
  	ОбластьМакета.Параметры.Контрагент = ФормированиеПечатныхФорм.ОписаниеОрганизации(КонтактнаяИнформация.СведенияОЮрФизЛице(Шапка.Получатель, Шапка.Дата), "ПолноеНаименование,");
  	ОбластьМакета.Параметры.Client = Шапка.ПолучательDescriptionFull;		
	ОбластьМакета.Параметры.Место = Место;
	ОбластьМакета.Параметры.Place = МестоEng;
	ОбластьМакета.Параметры.Name   = МенеджерКонтрагентEng;
	ОбластьМакета.Параметры.Name1 = МенеджерКомпанияEng;
	ОбластьМакета.Параметры.Менеджер = МенеджерКонтрагентРод;
	ОбластьМакета.Параметры.Менеджер1 = МенеджерКомпанияРод;
	ОбластьМакета.Параметры.ДолжностьРод = ДолжностьКонтрагентРод;
	ОбластьМакета.Параметры.ДолжностьРод1 = ДолжностьКомпанияРод;
	ОбластьМакета.Параметры.Title = ДолжностьКонтрагентEng;
	ОбластьМакета.Параметры.Title1 = ДолжностьКомпанияEng;
	Если ДоверенностьКомпания Тогда
 		ОбластьМакета.Параметры.Основание1 = ", действующего на основании "+ОснованиеКомпания+",";
 		ОбластьМакета.Параметры.Power1 = ", acting under a power of "+ОснованиеКомпанияEng+",";
	КонецЕсли;
	Если ДоверенностьКонтрагент Тогда
		ОбластьМакета.Параметры.Основание = ", действующего на основании "+ОснованиеКонтрагент+",";
 		ОбластьМакета.Параметры.Power = ", acting under a power of "+ОснованиеКонтрагентEng+",";
	КонецЕсли;
	ОбластьМакета.Параметры.Описание = ДокументОбъект.Услуги[0].Содержание;
	ОбластьМакета.Параметры.Description = ДокументОбъект.Услуги[0].СодержаниеEng;
	ТабДокумент.Вывести(ОбластьМакета);

	Если ВыводитьSIR Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("SIR");
	  	ОбластьМакета.Параметры.SIR = ДокументОбъект.Услуги[0].TicketNumber;

		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	Если НЕ Шапка.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Основная2");
	  	ОбластьМакета.Параметры.Сумма = Шапка.СуммаДокумента;
	  	ОбластьМакета.Параметры.Валюта = Шапка.ВалютаДокумента;
	  	ОбластьМакета.Параметры.Currency = Шапка.ВалютаДокумента;
		ОбластьМакета.Параметры.СуммаПрописью = ФормированиеПечатныхФорм.СформироватьСуммуПрописью(Шапка.СуммаДокумента, Шапка.ВалютаДокумента);
		ОбластьМакета.Параметры.СуммаПрописьюENG = ФормированиеПечатныхФорм.СформироватьСуммуПрописьюENG(Шапка.СуммаДокумента, Шапка.ВалютаДокумента);
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		Если ВыделятьНДС Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("НДС");
		  	ОбластьМакета.Параметры.Сумма = ДокументОбъект.Услуги.Итог("СуммаНДС");
		  	ОбластьМакета.Параметры.Валюта = ВалютаCчета;
		  	ОбластьМакета.Параметры.Currency = Шапка.ВалютаДокумента;
			ОбластьМакета.Параметры.СуммаПрописью = ФормированиеПечатныхФорм.СформироватьСуммуПрописью(ДокументОбъект.Услуги.Итог("СуммаНДС"), Шапка.ВалютаДокумента);
			ОбластьМакета.Параметры.СуммаПрописьюENG = ФормированиеПечатныхФорм.СформироватьСуммуПрописьюENG(ДокументОбъект.Услуги.Итог("СуммаНДС"), Шапка.ВалютаДокумента);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("СуммаРуб");
	СуммаРуб = Окр(Шапка.СуммаДокумента * ОбщегоНазначения.КурсДокумента(ДокументОбъект.Ссылка, ВалютаРегламентированногоУчета),2);
  	ОбластьМакета.Параметры.Сумма = СуммаРуб;
  	ОбластьМакета.Параметры.Валюта = ВалютаРегламентированногоУчета;
  	ОбластьМакета.Параметры.Currency = ВалютаРегламентированногоУчета.НаименованиеEng;
	ОбластьМакета.Параметры.СуммаПрописью = ФормированиеПечатныхФорм.СформироватьСуммуПрописью(СуммаРуб, ВалютаРегламентированногоУчета);
	ОбластьМакета.Параметры.СуммаПрописьюENG = ФормированиеПечатныхФорм.СформироватьСуммуПрописьюENG(СуммаРуб, ВалютаРегламентированногоУчета);
	
	ТабДокумент.Вывести(ОбластьМакета);
	Если ВыделятьНДС Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("НДСРуб");       
		СуммаНДСРуб = Окр(ДокументОбъект.Услуги.Итог("СуммаНДС") * ОбщегоНазначения.КурсДокумента(ДокументОбъект.Ссылка, ВалютаРегламентированногоУчета),2);
	  	ОбластьМакета.Параметры.Сумма = СуммаНДСРуб;
	  	ОбластьМакета.Параметры.Валюта = ВалютаРегламентированногоУчета;
	  	ОбластьМакета.Параметры.Currency = ВалютаРегламентированногоУчета.НаименованиеEng;
		ОбластьМакета.Параметры.СуммаПрописью = ФормированиеПечатныхФорм.СформироватьСуммуПрописью(СуммаНДСРуб, ВалютаРегламентированногоУчета);
		ОбластьМакета.Параметры.СуммаПрописьюENG = ФормированиеПечатныхФорм.СформироватьСуммуПрописьюENG(СуммаНДСРуб, ВалютаРегламентированногоУчета);
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
 	ОбластьМакета = Макет.ПолучитьОбласть("Подпись");
	ОбластьМакета.Параметры.Name   = МенеджерКонтрагентEng;
	ОбластьМакета.Параметры.Name1 = МенеджерКомпанияEng;
	ОбластьМакета.Параметры.Title = ДолжностьКонтрагентEng;
	ОбластьМакета.Параметры.Title1 = ДолжностьКомпанияEng;
	ОбластьМакета.Параметры.ФИО = МенеджерКонтрагент;
	ОбластьМакета.Параметры.ФИО1 = МенеджерКомпания;
	ОбластьМакета.Параметры.Должность = ДолжностьКонтрагент;
	ОбластьМакета.Параметры.Должность1 = ДолжностьКомпания;
  	ОбластьМакета.Параметры.Дата = Формат(Шапка.Дата,"ДФ=dd.MM.yyyy");
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
	
КонецФункции // ПечатьАктаОбОказанииУслуг()


