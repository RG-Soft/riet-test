
///////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
///////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда 
		ЗаполнитьРеквизитыНаОснованииДокумента();
	КонецЕсли;
	
	Элементы.ОснованиеКомпания.Доступность    = ДоверенностьКомпания;
	Элементы.ОснованиеКомпанияEng.Доступность = ДоверенностьКомпания;
	
	Элементы.ОснованиеКонтрагент.Доступность    = ДоверенностьКонтрагент;
	Элементы.ОснованиеКонтрагентEng.Доступность = ДоверенностьКонтрагент;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыНаОснованииДокумента()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.ДоговорКонтрагента.Наименование КАК ДоговорКонтрагентаНаименование,
	               |	РеализацияТоваровУслуг.ДоговорКонтрагента.НаименованиеEng КАК ДоговорКонтрагентаНаименованиеEng,
	               |	РеализацияТоваровУслуг.Руководитель.Наименование КАК РуководительНаименование,
	               |	РеализацияТоваровУслуг.Руководитель.Наименование КАК РуководительНаименованиеEng
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		
		Договор = Выборка.ДоговорКонтрагентаНаименование;
		ДоговорEng = Выборка.ДоговорКонтрагентаНаименованиеEng;
		МенеджерКомпания = Выборка.РуководительНаименование;
		МенеджерКомпанияEng = Выборка.РуководительНаименованиеEng;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверенностьКомпанияПриИзменении(Элемент)
	
	Элементы.ОснованиеКомпания.Доступность    = ДоверенностьКомпания;
	Элементы.ОснованиеКомпанияEng.Доступность = ДоверенностьКомпания;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверенностьКонтрагентПриИзменении(Элемент)
	
	Элементы.ОснованиеКонтрагент.Доступность    = ДоверенностьКонтрагент;
	Элементы.ОснованиеКонтрагентEng.Доступность = ДоверенностьКонтрагент;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
// КОМАНДЫ
///////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура Печать(Команда)
	
	Акт.Напечатать();
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	Акт = ПечатьAct();	
	
КонецПроцедуры

&НаСервере
Функция ПечатьAct()

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.УстановитьПараметр("ТекущийДокумент", ДокументОбъект.Ссылка);
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Номер,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента,
	|	РеализацияТоваровУслуг.Контрагент КАК Получатель,
	|	РеализацияТоваровУслуг.Организация КАК Поставщик,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	РеализацияТоваровУслуг.ВалютаДокумента,
	|	РеализацияТоваровУслуг.УчитыватьНДС,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.ПодразделениеОрганизации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ТекущийДокумент";
	Шапка = ЗапросШапка.Выполнить().Выбрать();
	Шапка.Следующий();

	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_Act";
	Макет       = Документы.РеализацияТоваровУслуг.ПолучитьМакет("ActExxon");

	ОбластьМакета = Макет.ПолучитьОбласть("Основная1");
	ОбластьМакета.Параметры.Договор = СокрЛП(Договор);
	ОбластьМакета.Параметры.ДоговорEng = СокрЛП(ДоговорEng);
	ОбластьМакета.Параметры.Номер = СокрЛП(Шапка.Номер);
  	ОбластьМакета.Параметры.Дата = Формат(Шапка.Дата, "ДФ=dd.MM.yyyy");
  	ОбластьМакета.Параметры.ДатаEng = Формат(Шапка.Дата, "Л = en_US; ДЛФ=D");
  	ОбластьМакета.Параметры.Организация = ФормированиеПечатныхФорм.ОписаниеОрганизации(КонтактнаяИнформация.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата), "ПолноеНаименование,");
  	ОбластьМакета.Параметры.Company = "Schlumberger Logelco Inc.";
  	ОбластьМакета.Параметры.Контрагент = ФормированиеПечатныхФорм.ОписаниеОрганизации(КонтактнаяИнформация.СведенияОЮрФизЛице(Шапка.Получатель, Шапка.Дата), "ПолноеНаименование,");
  	ОбластьМакета.Параметры.Client = Шапка.Получатель.DescriptionFull;
	
	ОбластьМакета.Параметры.Место = Место;
	ОбластьМакета.Параметры.Place = МестоEng;
	ОбластьМакета.Параметры.Name   = МенеджерКонтрагентEng;
	ОбластьМакета.Параметры.Name1 = МенеджерКомпанияEng;
	ОбластьМакета.Параметры.Менеджер = МенеджерКонтрагентРод;
	ОбластьМакета.Параметры.Менеджер1 = МенеджерКомпанияРод;
	ОбластьМакета.Параметры.ДолжностьРод = ДолжностьКонтрагентРод;
	ОбластьМакета.Параметры.ДолжностьРод1 = ДолжностьКомпанияРод;
	ОбластьМакета.Параметры.ТитулПодрядчикEng = ТитулПодрядчикEng;
	ОбластьМакета.Параметры.ТитулКомпанияEng = ТитулКомпанияEng;
	
	Если ДоверенностьКомпания Тогда
 		ОбластьМакета.Параметры.Основание1 = ", действующего на основании "+ОснованиеКомпания+",";
 		ОбластьМакета.Параметры.Power1 = ", acting under a power of "+ОснованиеКомпанияEng+",";
	КонецЕсли;
	Если ДоверенностьКонтрагент Тогда
		ОбластьМакета.Параметры.Основание = ", действующего на основании "+ОснованиеКонтрагент+",";
 		ОбластьМакета.Параметры.Power = ", acting under a power of "+ОснованиеКонтрагентEng+",";
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьМакета);
	
	Если ДокументОбъект.Услуги.Количество()>0 Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаУслуги");
		Для каждого СтрокаУслуги Из ДокументОбъект.Услуги Цикл
			ОбластьМакета.Параметры.Описание = СтрокаУслуги.Содержание;
			ОбластьМакета.Параметры.Description = СтрокаУслуги.СодержаниеEng;
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ (Период.ДатаНачала = Дата(1,1,1) И Период.ДатаОкончания = Дата(1,1,1)) Тогда 
		ОбластьМакета = Макет.ПолучитьОбласть("Период");
		ОбластьМакета.Параметры.ДатаНач = Формат(Период.ДатаНачала, "ДЛФ=Д");
		ОбластьМакета.Параметры.ДатаКон = Формат(Период.ДатаОкончания, "ДЛФ=Д");
		ОбластьМакета.Параметры.ДатаНачEng = Формат(Период.ДатаНачала, "Л = en_US; ДЛФ=Д");
		ОбластьМакета.Параметры.ДатаКонEng = Формат(Период.ДатаОкончания, "Л = en_US; ДЛФ=Д");
	
		ТабДокумент.Вывести(ОбластьМакета);
 	КонецЕсли;

	Если ВыводитьSIR Тогда
		Если ДокументОбъект.Услуги.Количество()>0 Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("SIR");
			Для каждого СтрокаУслуги Из ДокументОбъект.Услуги Цикл
			  	ОбластьМакета.Параметры.SIR = СтрокаУслуги.TicketNumber;              
				ТабДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Если НЕ Шапка.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Основная2");
	  	ОбластьМакета.Параметры.Сумма = Шапка.СуммаДокумента;
	  	ОбластьМакета.Параметры.Валюта = ВалютаCчета;
	  	ОбластьМакета.Параметры.Currency = Шапка.ВалютаДокумента;
		ОбластьМакета.Параметры.СуммаПрописью = ФормированиеПечатныхФорм.СформироватьСуммуПрописью(Шапка.СуммаДокумента, Шапка.ВалютаДокумента);
		ОбластьМакета.Параметры.СуммаПрописьюENG = ФормированиеПечатныхФорм.СформироватьСуммуПрописьюENG(Шапка.СуммаДокумента, Шапка.ВалютаДокумента);
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		Если ВыделятьНДС Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("НДС");
		  	ОбластьМакета.Параметры.Сумма = ДокументОбъект.Услуги.Итог("СуммаНДС");
		  	ОбластьМакета.Параметры.Валюта = ВалютаCчета;
		  	ОбластьМакета.Параметры.Currency = Шапка.ВалютаДокумента;
			ОбластьМакета.Параметры.СуммаПрописью = ФормированиеПечатныхФорм.СформироватьСуммуПрописью(ДокументОбъект.Услуги.Итог("СуммаНДС"), Шапка.ВалютаДокумента);
			ОбластьМакета.Параметры.СуммаПрописьюENG = ФормированиеПечатныхФорм.СформироватьСуммуПрописьюENG(ДокументОбъект.Услуги.Итог("СуммаНДС"), Шапка.ВалютаДокумента);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
	КонецЕсли;
	
 	ОбластьМакета = Макет.ПолучитьОбласть("Подпись");
	ОбластьМакета.Параметры.Name   = МенеджерКонтрагентEng;
	ОбластьМакета.Параметры.Name1 = МенеджерКомпанияEng;
	ОбластьМакета.Параметры.Title = ДолжностьКонтрагентEng;
	ОбластьМакета.Параметры.Title1 = ДолжностьКомпанияEng;
	ОбластьМакета.Параметры.ФИО = МенеджерКонтрагент;
	ОбластьМакета.Параметры.ФИО1 = МенеджерКомпания;
	ОбластьМакета.Параметры.Должность = ДолжностьКонтрагент;
	ОбластьМакета.Параметры.Должность1 = ДолжностьКомпания;
  	ОбластьМакета.Параметры.Дата = Формат(Шапка.Дата,"ДФ=dd.MM.yyyy");
  	ОбластьМакета.Параметры.ДатаEng = Формат(Шапка.Дата, "Л = en_US; ДЛФ=D");
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьАктаОбОказанииУслуг()


