
///////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
///////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	   	
	ФормаКС = "КС-2";
	ПриИзмененииФормыКС(ЭтаФорма, ФормаКС);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаКСНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбранныйЭлемент = ВыбратьИзСписка(Элементы.ФормаКС.СписокВыбора, Элементы.ФормаКС);
	ФормаКС = ВыбранныйЭлемент.Значение;
	
	ПриИзмененииФормыКС(ЭтаФорма, ФормаКС);	
	
КонецПроцедуры
   
&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииФормыКС(Форма, ФормаКС)
	
	Элементы = Форма.Элементы;
	
	ВидимостьЭлементов = ?(ФормаКС = "КС-2", Ложь, Истина);
				
	Элементы.СогласованоФИО.Видимость = ВидимостьЭлементов;
	Элементы.СогласованоФИОEng.Видимость = ВидимостьЭлементов;
	Элементы.СогласованоДолжность.Видимость = ВидимостьЭлементов;
	Элементы.СогласованоДолжностьEng.Видимость = ВидимостьЭлементов;
	Элементы.СогласованоФИО1.Видимость = ВидимостьЭлементов;
	Элементы.СогласованоФИОEng1.Видимость = ВидимостьЭлементов;
	Элементы.СогласованоДолжность1.Видимость = ВидимостьЭлементов;
	Элементы.СогласованоДолжностьEng1.Видимость = ВидимостьЭлементов;
	
	Элементы.СдалФИОEng.Видимость = ВидимостьЭлементов;
	Элементы.СдалДолжностьEng.Видимость = ВидимостьЭлементов;
	Элементы.ПринялФИОEng.Видимость = ВидимостьЭлементов;
	Элементы.ПринялДолжностьEng.Видимость = ВидимостьЭлементов;
				
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
// КОМАНДЫ
///////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура Печать(Команда)
	
	ТабДок.Напечатать();
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	ТабДок = ПечатьКСФормы();	
	
КонецПроцедуры

&НаСервере
Функция ПечатьКСФормы()

	ЗапросШапка = Новый Запрос;
	ЗапросШапка.УстановитьПараметр("ТекущийДокумент", ДокументОбъект.Ссылка);
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Номер,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента,
	|	РеализацияТоваровУслуг.Контрагент КАК Получатель,
	|	РеализацияТоваровУслуг.Организация КАК Поставщик,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	РеализацияТоваровУслуг.ВалютаДокумента,
	|	РеализацияТоваровУслуг.УчитыватьНДС,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.ПодразделениеОрганизации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ТекущийДокумент";
	Шапка = ЗапросШапка.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_КС2";
	
	Если ФормаКС = "КС-2" Тогда
		Макет       = Документы.РеализацияТоваровУслуг.ПолучитьМакет("КС_2_2");
	Иначе
		Макет       = Документы.РеализацияТоваровУслуг.ПолучитьМакет("КС_2");
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаАкта");
	ОбластьМакета.Параметры.ОКПОЗаказчик = Шапка.Получатель.КодПоОКПО;
	ОбластьМакета.Параметры.ОКПОПодрядчик = Шапка.ПодразделениеОрганизации.КодПоОКПО;
	ОбластьМакета.Параметры.Договор = "" + Шапка.ДоговорКонтрагента;
	
	Подрядчик= ФормированиеПечатныхФорм.ОписаниеОрганизации(КонтактнаяИнформация.СведенияОЮрФизЛице(Шапка.ПодразделениеОрганизации, Шапка.Дата), "НаименованиеЗаказчика,ЮридическийАдрес,Телефоны,");
		
	Если ФормаКС = "КС-2" Тогда
		
		ОбластьМакета.Параметры.Подрядчик = Подрядчик;
	Иначе	
		ОбластьМакета.Параметры.Подрядчик = Подрядчик+
		"/ Schlumberger Logelco Inc., No 8 Aquilino de la Guardia Panama 1 Republic of Panama, tel.(+507)2056000 Fax. (507)249 4891 ( via Schlumberger Logelco Inc. division at " + 
		СтрЗаменить(ФормированиеПечатныхФорм.ОписаниеОрганизации(КонтактнаяИнформация.СведенияОЮрФизЛице(Шапка.ПодразделениеОрганизации, Шапка.Дата), "АнглийскийАдрес,Телефоны,"),"тел","tel");
	КонецЕсли;
	
	Заказчик="" + ФормированиеПечатныхФорм.ОписаниеОрганизации(КонтактнаяИнформация.СведенияОЮрФизЛице(Шапка.Получатель, Шапка.Дата), "ПолноеНаименование,ЮридическийАдрес,Телефоны,"); 
	Если ФормаКС = "КС-2" Тогда
		ОбластьМакета.Параметры.Инвестор = Заказчик;
		ОбластьМакета.Параметры.Заказчик = Заказчик;
		ОбластьМакета.Параметры.Месторождение = Стройка;
		ОбластьМакета.Параметры.Скважина = Объект;
	Иначе	
		ОбластьМакета.Параметры.Заказчик = Заказчик +
		" / " + Шапка.Получатель.DescriptionFull + ", " + СтрЗаменить(ФормированиеПечатныхФорм.ОписаниеОрганизации(КонтактнаяИнформация.СведенияОЮрФизЛице(Шапка.Получатель, Шапка.Дата), "АнглийскийАдрес,Телефоны,"),"тел","tel");
	КонецЕсли;  
	
	ОбластьМакета.Параметры.ДатаДень = День(ДатаКонтракта);
	ОбластьМакета.Параметры.ДатаМесяц = Месяц(ДатаКонтракта);
	ОбластьМакета.Параметры.ДатаГод = Год(ДатаКонтракта);
	ТабДокумент.Вывести(ОбластьМакета);
	
	Валюта = ?(ВРуб,Справочники.Валюты.НайтиПоКоду("643"),Шапка.ВалютаДокумента );
	Пересчет = ?(Валюта = Шапка.ВалютаДокумента, Ложь, Истина );
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьМакета.Параметры.ОтчетныйПериодНачало = Формат(ОтчетныйПериод.ДатаНачала,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.ОтчетныйПериодКонец = Формат(ОтчетныйПериод.ДатаОкончания,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.НомерДокумента = Шапка.Номер;
	ОбластьМакета.Параметры.Валюта = Валюта;
	ОбластьМакета.Параметры.ДатаДокумента = Формат(Шапка.Дата,"ДФ=dd.MM.yyyy");
	ТабДокумент.Вывести(ОбластьМакета);
	
	
	НомерСтроки =0;
	Для каждого СтрокаТаб из ДокументОбъект.Товары Цикл
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаАкта");
		НомерСтроки = НомерСтроки+1;
		ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
		ОбластьМакета.Параметры.НаименованиеРабот = СтрокаТаб.Номенклатура.НаименованиеПолное;
		ОбластьМакета.Параметры.Количество = СтрокаТаб.Количество;
		
		Если не Пересчет Тогда
			ОбластьМакета.Параметры.ЦенаЗаЕдиницу = СтрокаТаб.Цена;
			ОбластьМакета.Параметры.Стоимость = СтрокаТаб.Сумма;
		Иначе
			ОбластьМакета.Параметры.ЦенаЗаЕдиницу = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(СтрокаТаб.Цена,Шапка.ВалютаДокумента, Валюта, ОбщегоНазначения.ПолучитьКурсВалюты(Шапка.ВалютаДокумента, Шапка.Дата).Курс, ОбщегоНазначения.ПолучитьКурсВалюты(Валюта, Шапка.Дата).Курс);
			ОбластьМакета.Параметры.Стоимость = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(СтрокаТаб.Сумма,Шапка.ВалютаДокумента, Валюта, ОбщегоНазначения.ПолучитьКурсВалюты(Шапка.ВалютаДокумента, Шапка.Дата).Курс, ОбщегоНазначения.ПолучитьКурсВалюты(Валюта, Шапка.Дата).Курс);
		конецЕсли;	
		ОбластьМакета.Параметры.ЕдИзм = СтрокаТаб.Номенклатура.БазоваяЕдиницаИзмерения;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	Для каждого СтрокаТаб из ДокументОбъект.Услуги Цикл
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаАкта");
		НомерСтроки = НомерСтроки+1;
		ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
		ОбластьМакета.Параметры.НаименованиеРабот = СтрокаТаб.Содержание;
		ОбластьМакета.Параметры.Количество = СтрокаТаб.Количество;
		Если не Пересчет Тогда
			ОбластьМакета.Параметры.ЦенаЗаЕдиницу = СтрокаТаб.Цена;
			ОбластьМакета.Параметры.Стоимость = СтрокаТаб.Сумма;
		Иначе
			ОбластьМакета.Параметры.ЦенаЗаЕдиницу = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(СтрокаТаб.Цена,Шапка.ВалютаДокумента, Валюта, ОбщегоНазначения.ПолучитьКурсВалюты(Шапка.ВалютаДокумента, Шапка.Дата).Курс, ОбщегоНазначения.ПолучитьКурсВалюты(Валюта, Шапка.Дата).Курс);
			ОбластьМакета.Параметры.Стоимость = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(СтрокаТаб.Сумма,Шапка.ВалютаДокумента, Валюта, ОбщегоНазначения.ПолучитьКурсВалюты(Шапка.ВалютаДокумента, Шапка.Дата).Курс, ОбщегоНазначения.ПолучитьКурсВалюты(Валюта, Шапка.Дата).Курс);
		конецЕсли;	
		ОбластьМакета.Параметры.ЕдИзм = СтрокаТаб.Номенклатура.БазоваяЕдиницаИзмерения;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалТаблицы");
	
	Если ФормаКС = "КС-2" Тогда
		Если не Пересчет Тогда   
			ОбластьМакета.Параметры.Итого = ДокументОбъект.Товары.Итог("Сумма")+ДокументОбъект.Услуги.Итог("Сумма");
			ОбластьМакета.Параметры.НДС = ДокументОбъект.Товары.Итог("СуммаНДС")+ДокументОбъект.Услуги.Итог("СуммаНДС");
			ОбластьМакета.Параметры.ИтогоСНДС = ДокументОбъект.СуммаДокумента;
			
		Иначе
			ОбластьМакета.Параметры.Итого = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(ДокументОбъект.Товары.Итог("Сумма")+ДокументОбъект.Услуги.Итог("Сумма"),Шапка.ВалютаДокумента, Валюта, ОбщегоНазначения.ПолучитьКурсВалюты(Шапка.ВалютаДокумента, Шапка.Дата).Курс, ОбщегоНазначения.ПолучитьКурсВалюты(Валюта, Шапка.Дата).Курс);
			ОбластьМакета.Параметры.НДС = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(ДокументОбъект.Товары.Итог("СуммаНДС")+ДокументОбъект.Услуги.Итог("СуммаНДС"),Шапка.ВалютаДокумента, Валюта, ОбщегоНазначения.ПолучитьКурсВалюты(Шапка.ВалютаДокумента, Шапка.Дата).Курс, ОбщегоНазначения.ПолучитьКурсВалюты(Валюта, Шапка.Дата).Курс);
			ОбластьМакета.Параметры.ИтогоСНДС = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(ДокументОбъект.СуммаДокумента,Шапка.ВалютаДокумента, Валюта, ОбщегоНазначения.ПолучитьКурсВалюты(Шапка.ВалютаДокумента, Шапка.Дата).Курс, ОбщегоНазначения.ПолучитьКурсВалюты(Валюта, Шапка.Дата).Курс);;
			
		конецЕсли;	
	Иначе
		
		Если не Пересчет Тогда   
			ОбластьМакета.Параметры.Итого = ДокументОбъект.Товары.Итог("Сумма")+ДокументОбъект.Услуги.Итог("Сумма");
			ОбластьМакета.Параметры.ВсегоПоАкту = ДокументОбъект.СуммаДокумента;
			
		Иначе
			ОбластьМакета.Параметры.Итого = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(ДокументОбъект.Товары.Итог("Сумма")+ДокументОбъект.Услуги.Итог("Сумма"),Шапка.ВалютаДокумента, Валюта, ОбщегоНазначения.ПолучитьКурсВалюты(Шапка.ВалютаДокумента, Шапка.Дата).Курс, ОбщегоНазначения.ПолучитьКурсВалюты(Валюта, Шапка.Дата).Курс);
			ОбластьМакета.Параметры.ВсегоПоАкту = ОбщегоНазначения.ПересчитатьИзВалютыВВалюту(ДокументОбъект.СуммаДокумента,Шапка.ВалютаДокумента, Валюта, ОбщегоНазначения.ПолучитьКурсВалюты(Шапка.ВалютаДокумента, Шапка.Дата).Курс, ОбщегоНазначения.ПолучитьКурсВалюты(Валюта, Шапка.Дата).Курс);;
			
		конецЕсли;
		
	КонецЕсли;	
	
	Если ФормаКС = "КС-2" Тогда
		
		ОбластьМакета.Параметры.Сдал = СдалДолжность;
		ОбластьМакета.Параметры.РасшифровкаСдал = СдалФИО;
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	Если ФормаКС = "КС-2" Тогда
		
		ОбластьМакета.Параметры.Принял = ПринялДолжность;
		ОбластьМакета.Параметры.РасшифровкаПринял = ПринялФИО;
	Иначе
		ОбластьМакета.Параметры.Сдал = СдалДолжность + " / " + СдалДолжностьEng;
		ОбластьМакета.Параметры.РасшифровкаСдал = СдалФИО + " / " + СдалФИОEng;
		ОбластьМакета.Параметры.Принял = ПринялДолжность + " / " + ПринялДолжностьEng;
		ОбластьМакета.Параметры.РасшифровкаПринял = ПринялФИО + " / " + ПринялФИОEng;
		ОбластьМакета.Параметры.Согласовал1 = СогласованоДолжность + " / " + СогласованоДолжностьEng;
		ОбластьМакета.Параметры.РасшифровкаСогласовал1 = СогласованоФИО + " / " + СогласованоФИОEng;
		ОбластьМакета.Параметры.Согласовал2 = СогласованоДолжность1 + " / " + СогласованоДолжностьEng1;
		ОбластьМакета.Параметры.РасшифровкаСогласовал2 = СогласованоФИО1 + " / " + СогласованоФИОEng1;
	КонецЕсли; 		
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьКСФормы()

