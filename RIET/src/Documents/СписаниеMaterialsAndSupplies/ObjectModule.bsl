
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	  		
	//проверяем был ли создан документ за выбранный период
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписаниеMaterialsAndSupplies.Представление
	               |ИЗ
	               |	Документ.СписаниеMaterialsAndSupplies КАК СписаниеMaterialsAndSupplies
	               |ГДЕ
	               |	СписаниеMaterialsAndSupplies.НачалоПериода = &НачалоПериода
	               |	И СписаниеMaterialsAndSupplies.КонецПериода = &КонецПериода
	               |	И СписаниеMaterialsAndSupplies.Ссылка <> &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"За указанный период уже создан документ: 
				| " + Выборка.Представление + "",
				,,, Отказ);		
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(CreationDate) Тогда
		CreationDate = ТекущаяДата();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Responsible) Тогда
		Responsible = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// регистр MaterialsAndSupplies
	ДвиженияПоMaterialsAndSupplies = Движения.MaterialsAndSupplies;
	ДвиженияПоMaterialsAndSupplies.Очистить();
	ДвиженияПоMaterialsAndSupplies.Записывать = Истина;
	ДвиженияПоMaterialsAndSupplies.Записать();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.Текст = "ВЫБРАТЬ
	               |	MaterialsAndSuppliesОстатки.ПроводкаДеталейСКП,
	               |	MaterialsAndSuppliesОстатки.FiscalSumОстаток КАК FiscalSum,
	               |	MaterialsAndSuppliesОстатки.ManagementSumОстаток КАК ManagementSum,
	               |	ВЫБОР
	               |		КОГДА MaterialsAndSuppliesОстатки.ДокументПоступления ССЫЛКА Документ.РаспределениеИмпортаПоЗакрытиюПоставки
	               |			ТОГДА ДОБАВИТЬКДАТЕ(MaterialsAndSuppliesОстатки.ДокументПоступления.Дата, МЕСЯЦ, 3)
	               |		ИНАЧЕ MaterialsAndSuppliesОстатки.ДокументПоступления.ДатаПроведения
	               |	КОНЕЦ КАК ПериодСписания,
	               |	MaterialsAndSuppliesОстатки.ДокументПоступления
	               |ИЗ
	               |	РегистрНакопления.MaterialsAndSupplies.Остатки(
	               |			&ТекДата,
	               |			ВЫБОР
	               |				КОГДА ДокументПоступления ССЫЛКА Документ.РаспределениеИмпортаПоЗакрытиюПоставки
	               |					ТОГДА ДокументПоступления.Дата >= &НачалоПериода
	               |							И ДокументПоступления.Дата <= &КонецПериода
	               |				ИНАЧЕ ДокументПоступления.ДатаПроведения >= &НачалоПериода
	               |						И ДокументПоступления.ДатаПроведения <= &КонецПериода
	               |			КОНЕЦ) КАК MaterialsAndSuppliesОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Движение = ДвиженияПоMaterialsAndSupplies.ДобавитьРасход();
		Движение.Период              = Выборка.ПериодСписания;
		Движение.ПроводкаДеталейСКП  = Выборка.ПроводкаДеталейСКП;
		Движение.ДокументПоступления = Выборка.ДокументПоступления;
		Движение.FiscalSum           = Выборка.FiscalSum;
		Движение.ManagementSum       = Выборка.ManagementSum;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СтарыйАлгоритмОбработкаПроведения(Отказ, Режим)
	
	// регистр MaterialsAndSupplies
	ДвиженияПоMaterialsAndSupplies = Движения.MaterialsAndSupplies;
	ДвиженияПоMaterialsAndSupplies.Очистить();
	ДвиженияПоMaterialsAndSupplies.Записывать = Истина;
	ДвиженияПоMaterialsAndSupplies.Записать();
	
	МассивДатКвартала = Новый Массив;
	МассивДатКвартала.Добавить(Дата);
	МассивДатКвартала.Добавить(КонецМесяца(ДобавитьМесяц(Дата, - 1)));
	МассивДатКвартала.Добавить(КонецМесяца(ДобавитьМесяц(Дата, - 2)));
                                      	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ДобавитьМесяц(Дата, - 1)));
	Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(НачалоКвартала(Дата), -3));
	Запрос.Текст = "ВЫБРАТЬ
	               |	MaterialsAndSuppliesОстатки.ПроводкаДеталейСКП,
	               |	MaterialsAndSuppliesОстатки.FiscalSumОстаток КАК FiscalSum,
	               |	MaterialsAndSuppliesОстатки.ManagementSumОстаток КАК ManagementSum,
	               |	КОНЕЦПЕРИОДА(MaterialsAndSuppliesОстатки.ПроводкаДеталейСКП.FiscalDate, День) КАК FiscalDate
	               |ИЗ
	               |	РегистрНакопления.MaterialsAndSupplies.Остатки(
	               |			&ТекДата,
	               |			ПроводкаДеталейСКП.FiscalDate >= &НачалоПериода
	               |				И ПроводкаДеталейСКП.FiscalDate <= &КонецПериода) КАК MaterialsAndSuppliesОстатки";
	
	Результат = Запрос.Выполнить().Выгрузить();
	  	               	
	РезультатДляРаспределения = Результат.СкопироватьКолонки("ПроводкаДеталейСКП,FiscalDate,FiscalSum,ManagementSum");
	
	//по каждой строке результата добавляем 3 строки, 
	//прибавляя по месяцу (т.к. списывается в течение 3х месяцев, начиная со следующего)
	Для Каждого СтрокаТЗ из Результат Цикл 
		НоваяСтрока = РезультатДляРаспределения.Добавить();
		НоваяСтрока.ПроводкаДеталейСКП = СтрокаТЗ.ПроводкаДеталейСКП;
		НоваяСтрока.FiscalDate = КонецМесяца(ДобавитьМесяц(СтрокаТЗ.FiscalDate, 1));
		
		СтрокаПлюсМесяц = РезультатДляРаспределения.Добавить();
		СтрокаПлюсМесяц.ПроводкаДеталейСКП = СтрокаТЗ.ПроводкаДеталейСКП;
		СтрокаПлюсМесяц.FiscalDate = КонецМесяца(ДобавитьМесяц(СтрокаТЗ.FiscalDate, 2));
		
		СтрокаПлюсДваМесяца = РезультатДляРаспределения.Добавить();
		СтрокаПлюсДваМесяца.ПроводкаДеталейСКП = СтрокаТЗ.ПроводкаДеталейСКП;
		СтрокаПлюсДваМесяца.FiscalDate = КонецМесяца(ДобавитьМесяц(СтрокаТЗ.FiscalDate, 3));
	КонецЦикла;
	
	CustomsСервер.РаспределитьСуммы(Результат, "ПроводкаДеталейСКП", "FiscalSum,ManagementSum", "Проводка Деталей СКП", , , РезультатДляРаспределения, Отказ);
		
	Если Не Отказ Тогда 
		
		РезультатДляРаспределения.Сортировать("FiscalDate Возр");
            				
		Для Каждого Стр из  РезультатДляРаспределения Цикл 
			//записываем только строки с PostingDate, входящей в текущий квартал
			Если МассивДатКвартала.Найти(Стр.FiscalDate) <> Неопределено Тогда 
				
				 Движение = ДвиженияПоMaterialsAndSupplies.ДобавитьРасход();
				 Движение.Период             = Стр.FiscalDate;
				 Движение.ПроводкаДеталейСКП = Стр.ПроводкаДеталейСКП;
				 Движение.FiscalSum          = Стр.FiscalSum;
				 Движение.ManagementSum      = Стр.ManagementSum;
				 
			КонецЕсли;	
				
		КонецЦикла;
		
		ДвиженияПоMaterialsAndSupplies.Записать();
		
	КонецЕсли;
	 	
КонецПроцедуры

