
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ЗначениеЗаполнено(Period) и ЗначениеЗаполнено(Дата) Тогда 
		ЧислоМесяца = Число(Формат(Дата,"ДФ=dd"));
		Period = ?(ЧислоМесяца > 25, КонецМесяца(Дата)+1, НачалоМесяца(Дата)); 
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(CreatedBy) Тогда 
		CreatedBy = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;

    ПроверитьЗаполнениеРеквизитов(Отказ);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеРеквизитов(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	 	
	Если НЕ ЗначениеЗаполнено(Segment) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Необходимо заполнить поле ""Segment""!",
			ЭтотОбъект, "Segment", , Отказ);
	КонецЕсли;
	
	//проверим что нет другого Job с таким же FTL номером за тот же период
	Если ЗначениеЗаполнено(FTLNumber) Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Job.Ссылка
		               |ИЗ
		               |	Документ.Job КАК Job
		               |ГДЕ
		               |	Job.FTLNumber = &FTLNumber
		               |	И Job.Period = &Period
		               |	И Job.Ссылка <> &Ссылка";
		
		Запрос.УстановитьПараметр("FTLNumber", FTLNumber);
		Запрос.УстановитьПараметр("Period", Period);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Job с FTL number "+FTLNumber+" за период "+Формат(Period, "ДФ=MM.yyyy")+" уже создан для сегмента "+СокрЛП(Segment)+"!",
				, , , Отказ);
		КонецЕсли;
		  		
	КонецЕсли;
	       		      		
КонецПроцедуры
