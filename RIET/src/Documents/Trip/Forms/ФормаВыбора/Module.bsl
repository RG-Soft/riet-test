
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отбор = Параметры.Отбор;
	Если НЕ Отбор.Свойство("ПометкаУдаления") Тогда
		Отбор.Вставить("ПометкаУдаления", Ложь);
	КонецЕсли;
	
	Если Параметры.Свойство("GrossWeightKG") Тогда
		
		СписокTrip = Новый СписокЗначений;
		СписокTrip.ЗагрузитьЗначения(ПолучитьTripsСДостаточнымОстаткомGrossWeightKG(Параметры.GrossWeightKG));
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор,
			"Ссылка",
			СписокTrip,
			ВидСравненияКомпоновкиДанных.ВСписке);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьTripsСДостаточнымОстаткомGrossWeightKG(GrossWeightKG)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("GrossWeightKG", GrossWeightKG);

	Запрос.Текст = "ВЫБРАТЬ
	|	Trip.Ссылка КАК Trip
	|ИЗ
	|	Документ.Trip КАК Trip
	|ГДЕ
	|	Trip.Final = ДАТАВРЕМЯ(1, 1, 1)
	|	И НЕ Trip.ПометкаУдаления
	|	И Trip.Equipment.EffectiveWeight - Trip.GrossWeightKG >= &GrossWeightKG";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Trip");
		
КонецФункции