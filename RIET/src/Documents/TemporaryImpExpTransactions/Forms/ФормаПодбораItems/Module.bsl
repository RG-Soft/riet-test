
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьДляВыбораИзTemporaryImportTransaction(Параметры.СтруктураНастройки);
	
	Отбор = Параметры.Отбор;
	
	Если НЕ Отбор.Свойство("ПометкаУдаления") Тогда
		Отбор.Вставить("ПометкаУдаления", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДляВыбораИзTemporaryImportTransaction(СтруктураНастройки)
	
	// Установим параметры
	ПараметрыСписка = Список.Параметры;
	Если ЗначениеЗаполнено(СтруктураНастройки.TemporaryImportTransaction) Тогда
		ПараметрыСписка.УстановитьЗначениеПараметра("Дата", СтруктураНастройки.TemporaryImportTransaction.МоментВремени());
	Иначе
		ПараметрыСписка.УстановитьЗначениеПараметра("Дата", СтруктураНастройки.TransactionDate);
	КонецЕсли;
	
	ПараметрыСписка.УстановитьЗначениеПараметра("Регистратор", СтруктураНастройки.TemporaryImportTransaction);
	
	// Установим отборы
	Отбор = Параметры.Отбор;
	
	ProcessLevelCountry = РГСофтСерверПовтИспСеанс.ПолучитьЗначениеРеквизита(СтруктураНастройки.ProcessLevel, "Country");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		Список.Отбор,
		"SoldTo.Country",
		ВидСравненияКомпоновкиДанных.Равно,
		ProcessLevelCountry);
	
	Если СтруктураНастройки.TypeOfTransaction = Перечисления.TypesOfTemporaryImpExpTransaction.TemporaryImport
		ИЛИ СтруктураНастройки.TypeOfTransaction = Перечисления.TypesOfTemporaryImpExpTransaction.TemporaryExport Тогда
		
		Отбор.Вставить("Invoice", Документы.Инвойс.ПустаяСсылка());
		Отбор.Вставить("ExportRequest", Документы.ExportRequest.ПустаяСсылка());
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			Список.Отбор,
			"QtyInTemporaryImport",
			ВидСравненияКомпоновкиДанных.Больше,
			0);
		
		Если СтруктураНастройки.TypeOfTransaction = Перечисления.TypesOfTemporaryImpExpTransaction.ResponsibleChange Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
				Список.Отбор,
				"Responsible",
				ВидСравненияКомпоновкиДанных.НеРавно,
				СтруктураНастройки.NewResponsible);
			
		Иначе
				
			Отбор.Вставить("CustomsFileNo", СтруктураНастройки.CustomsFileNo);
			
		КонецЕсли;
				
	КонецЕсли;
	
	Если СтруктураНастройки.AlreadySelectedItems.Количество() > 0 Тогда
		
		СписокAlreadySelectedItems = Новый СписокЗначений;
		СписокAlreadySelectedItems.ЗагрузитьЗначения(СтруктураНастройки.AlreadySelectedItems);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			Список.Отбор,
			"Ссылка",
			ВидСравненияКомпоновкиДанных.НеВСписке,
			СписокAlreadySelectedItems);
			
	КонецЕсли;
	
КонецПроцедуры

