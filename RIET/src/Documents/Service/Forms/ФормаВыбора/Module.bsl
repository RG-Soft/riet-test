
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СтруктураНастройки") Тогда
		
		СтруктураНастройки = Параметры.СтруктураНастройки;
		Если СтруктураНастройки.Имя = "ВыборИзServicesCostsAllocation" Тогда
			НастроитьДляВыбораИзServicesCostsAllocation(СтруктураНастройки);
		ИначеЕсли СтруктураНастройки.Имя = "ВыборИзAgentInvoice" Тогда
			НастроитьДляВыбораИзAgentInvoice(СтруктураНастройки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДляВыбораИзServicesCostsAllocation(СтруктураНастройки)
	
	МассивUnallocatedServices = CustomsСервер.ПолучитьUnallocatedServices(СтруктураНастройки.Agent, СтруктураНастройки.ServicesCostsAllocation,
											  							  СтруктураНастройки.МассивТекущихServices);
		
	Параметры.Отбор.Вставить("Ссылка", МассивUnallocatedServices);
	Параметры.Отбор.Вставить("ПометкаУдаления", Ложь);
	
КонецПроцедуры 
     
&НаСервере
Процедура НастроитьДляВыбораИзAgentInvoice(СтруктураНастройки)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Date", СтруктураНастройки.Date);
	Запрос.УстановитьПараметр("Agent", СтруктураНастройки.Agent);
	Запрос.УстановитьПараметр("SoldTo", СтруктураНастройки.SoldTo);
	Запрос.УстановитьПараметр("МассивТекущихServices", СтруктураНастройки.МассивТекущихServices);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	NotReceivedAgentInvoicesОстатки.Service
		|ИЗ
		|	РегистрНакопления.NotReceivedAgentInvoices.Остатки(
		|			&Date,
		|			SoldTo = &SoldTo
		|				И Service.Agent = &Agent
		|				И (НЕ Service В (&МассивТекущихServices))) КАК NotReceivedAgentInvoicesОстатки";
	МассивNotReceivedServices = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Service");
		
	Параметры.Отбор.Вставить("Ссылка", МассивNotReceivedServices);
	
КонецПроцедуры 

