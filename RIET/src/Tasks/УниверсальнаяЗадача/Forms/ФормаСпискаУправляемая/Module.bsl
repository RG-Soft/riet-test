
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	Заместитель = ПолучитьЗаместителя();
	Если Заместитель = ТекПользователь Тогда 
		Элементы.СнятьЗаместителя.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗаместителя()
	
	//Проверить принадлежность к исполнителям и ответственным лицам
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АдресацияУведомлений.Исполнитель
	|ИЗ
	|	РегистрСведений.АдресацияУведомлений КАК АдресацияУведомлений
	|ГДЕ
	|	АдресацияУведомлений.ОтветственноеЛицо = &ТекущийПользователь
	|	И АдресацияУведомлений.ОтветственноеЛицо <> АдресацияУведомлений.Исполнитель");
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекПользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Исполнитель;
	иначе
		Возврат ТекПользователь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ЗаместительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ИзменениеЗаместителяНаСервере(ТекПользователь, ВыбранноеЗначение) Тогда 
		Заместитель = ВыбранноеЗначение;
		Элементы.СнятьЗаместителя.Видимость = ?(Заместитель = ТекПользователь, Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////
// КОМАНДА - СНЯТЬ ЗАМЕСТИТЕЛЯ

&НаКлиенте
Процедура СнятьЗаместителя(Команда)
	
	Если ИзменениеЗаместителяНаСервере(ТекПользователь, ТекПользователь) Тогда 
		Заместитель = ТекПользователь;
		Элементы.СнятьЗаместителя.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменениеЗаместителяНаСервере(ТекПользователь, Исполнитель)
	 		
	Набор = РегистрыСведений.АдресацияУведомлений.СоздатьНаборЗаписей();
	Набор.Отбор.ОтветственноеЛицо.Установить(ТекПользователь);
	Набор.Прочитать();
	Для каждого СтрокаНабора из Набор Цикл
		СтрокаНабора.Исполнитель = Исполнитель;
	КонецЦикла;
	
	Попытка
		Набор.Записать();
		Возврат Истина;
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось снять заместителя: " + ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;	
	
КонецФункции   

